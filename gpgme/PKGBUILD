# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=gpgme
pkgname=('libgpgme' 'libgpgme-devel')
pkgver=1.5.3
pkgrel=1
pkgdesc="A C wrapper library for GnuPG"
arch=('i686' 'x86_64')
url="http://www.gnupg.org/related_software/gpgme/"
license=('LGPL')
depends=('libassuan' 'libgpg-error' 'gnupg')
makedepends=('libassuan-devel' 'libgpg-error-devel')
options=('!libtool' '!emptydirs')
source=(ftp://ftp.gnupg.org/gcrypt/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2{,.sig}
        1.4.2-without-libassuan.patch
        gpgme-1.4.2-msys2.patch)
sha1sums=('8dd7711a4de117994fe2d45879ef8a9900d50f6a'
          'SKIP'
          '84af2decd19ecd700835d64e9dc257f96cb6608a'
          'd630f3fab06afa15e700d6883e59798a3998b816')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  #patch -p2 -i ${srcdir}/1.4.2-without-libassuan.patch
  patch -p1 -i ${srcdir}/gpgme-1.4.2-msys2.patch

  autoreconf -ivf
}

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --build=${CHOST} \
    --disable-gpgsm-test \
    --disable-gpgconf-test \
    --disable-g13-test \
    --enable-shared \
    --enable-static \
    --enable-fixed-path="/usr/bin:/bin:/usr/sbin:/usr/local/bin"

  make no_undefined=-no-undefined
  make DESTDIR=${srcdir}/dest install
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  #make check
}

package_libgpgme() {
  groups=('libraries')
  install=${pkgname}.install

  mkdir -p $pkgdir/usr/{bin,share}
  cp -f ${srcdir}/dest/usr/bin/*.dll $pkgdir/usr/bin/
  #cp -rf ${srcdir}/dest/usr/share/doc $pkgdir/usr/share/
  cp -rf ${srcdir}/dest/usr/share/info $pkgdir/usr/share/
}

package_libgpgme-devel() {
  pkgdesc="Libgpgme headers and libraries"
  groups=('development')
  depends=("libgpgme=${pkgver}" 'libassuan-devel' 'libgpg-error-devel')
  options=('staticlibs')
  
  mkdir -p $pkgdir/usr/{bin,share}
  cp -f ${srcdir}/dest/usr/bin/*-config $pkgdir/usr/bin/
  cp -rf ${srcdir}/dest/usr/include $pkgdir/usr/
  cp -rf ${srcdir}/dest/usr/lib $pkgdir/usr/
  cp -rf ${srcdir}/dest/usr/share/aclocal $pkgdir/usr/share/
  cp -rf ${srcdir}/dest/usr/share/common-lisp $pkgdir/usr/share/
}

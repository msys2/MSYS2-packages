# Maintainer: Chris Zheng <chriszheng99@gmail.com>

pkgname=neomutt
pkgver=20180716
pkgrel=1
pkgdesc='a version of mutt with added features'
url='https://neomutt.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('libidn2')
makedepends=('libgpgme-devel' 'libsasl-devel' 'libgdbm-devel' 'ncurses-devel' 'libidn2-devel')
source=("https://github.com/neomutt/neomutt/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('bd89826980b493ba312228c9c14ffe2403e268571aea6008c6dc7ed3848de200')

build() {
  cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"	# I don't know why.
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --gpgme \
    --gss \
    --ssl \
    --sasl \
    --with-ui=ncurses \
    --with-idn2=/usr \
    --idn=0 \
    --idn2=1 \
    --gdbm \

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=PadWalker
pkgname=perl-${_realname}
pkgver=2.3
pkgrel=1
pkgdesc="PadWalker - play with other peoples' lexical variables"
arch=('i686' 'x86_64')
url="https://metacpan.org/release/PadWalker"
groups=('perl-modules')
license=('GPL')
depends=('perl>=5.8.1' 'perl-ExtUtils-MakeMaker')
makedepends=('libcrypt-devel')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/R/RO/ROBIN/PadWalker-${pkgver}.tar.gz")
sha256sums=('2a6c44fb600861e54568e74081a8d1f121f0060076069ceab34b1ae89d6588cf')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  make -C "${srcdir}/${_realname}-${pkgver}" test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  make install DESTDIR="${pkgdir}"

  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}

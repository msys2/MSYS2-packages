# Maintainer: Jeremy Drake <github@jdrake.com>
pkgname='etc-update'
pkgdesc="CLI to interactively merge .pacnew configuration files"
pkgver=2.3.99
pkgrel=1
arch=('any')
url="https://wiki.gentoo.org/wiki/Handbook:X86/Portage/Tools#etc-update"
license=('GPL')
depends=('bash' 'diffutils')
optdepends=('dialog: for the menu mode (not automode)')
backup=("etc/etc-update.conf")
source=("https://github.com/gentoo/portage/archive/portage-${pkgver}.tar.gz")
sha256sums=('fc5dcc37033d876360c8427d1b6afbdb29786a675d6741909fb341da54568a1d')
noextract=("portage-${pkgver}.tar.gz")

prepare() {
  plain "Extracting portage-${pkgver}.tar.gz due to symlink(s) without pre-existing target(s)"
  cd "${srcdir}"
  [[ -d portage-portage-${pkgver} ]] && rm -rf portage-portage-${pkgver}
  tar zxf "${srcdir}/portage-${pkgver}.tar.gz" || true
}

package() {
  install -Dm 0755 -t "${pkgdir}/usr/bin" "portage-portage-${pkgver}/bin/${pkgname}"
  install -Dm 0644 -t "${pkgdir}/etc" "portage-portage-${pkgver}/cnf/${pkgname}.conf"
}

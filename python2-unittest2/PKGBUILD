# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgname=python2-unittest2
_pkgname=unittest2
pkgver=1.1.0
pkgrel=5
pkgdesc="New features in unittest for Python 2"
url="http://www.voidspace.org.uk/python/articles/unittest2.shtml"
arch=('any')
license=('BSD')
depends=('python2-six' 'python2-traceback2')
makedepends=('python2-distribute')
source=(${pkgname}-${pkgver}.tar.gz::https://hg.python.org/unittest2/archive/${pkgver}.tar.gz
        LICENSE)
sha512sums=('ccd701194994623422d5c0bdd88d89b279cbbc67cd96b3cfee7f48e1c32f87c154a3378771e2909afa89d9c13692173ccbd692312b1ad7b9d05cf8c115fcc14b'
            '85242ed21eb191fa0bab16f3e390957f1c8b7bc3b79aefbea2d925feaa8248f33d6e22afaa7d1c07c5fcb5e800910aa2a1cb52d892c104337bbdb57a380373a2')

prepare() {
  sed -i "s/'argparse', //" ${_pkgname}-${pkgver}/setup.py
}

build() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py install --skip-build -O1 --root="${pkgdir}"
  install -Dm 644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:

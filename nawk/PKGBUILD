# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=nawk
pkgver=20251225
pkgrel=1
pkgdesc="The one, true implementation of AWK"
url="https://github.com/onetrueawk/awk"
license=('MIT')
arch=('i686' 'x86_64')
makedepends=('make' 'gcc' 'git' 'bison')
options=('!makeflags')
source=("git+https://github.com/onetrueawk/awk.git#tag=${pkgver}"
        'manpage-naming.patch')
sha256sums=('a6ab93f9c748343f0469f8bd0ccf45a1429489f32b087272806011bb086fb9c3'
            'ab40a56fc454ea0a03ed39d7f2e664ea900b5cb54ade53e20f54581326caf0e6')

prepare() {
  cd awk
  # awk -> nawk
  patch -Np1 <"${srcdir}"/manpage-naming.patch

  # create license
  sed -n '/Copyright/,/THIS SOFTWARE\./p' README.md > LICENSE

  # fix build
  sed -i '/YACC = yacc -d -S/ s|^|#|' makefile
}

build() {
  cd awk
  make "CPPFLAGS=-DHAS_ISBLANK ${CPPFLAGS}" \
    "LDFLAGS=${LDFLAGS}" \
    "CFLAGS=${CFLAGS}"
}

package() {
  cd awk
  install -Dm755 a.exe "${pkgdir}/usr/bin/nawk.exe"
  install -Dm644 awk.1 "${pkgdir}/usr/share/man/man1/nawk.1"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/nawk/LICENSE"
}

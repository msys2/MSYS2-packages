# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=TimeDate
pkgname="perl-${_realname}"
pkgver=2.30
pkgrel=2
pkgdesc="Date formating subroutines"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/${_realname}/"
depends=('perl')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/G/GB/GBARR/${_realname}-${pkgver}.tar.gz")
sha256sums=('75bd254871cb5853a6aa0403ac0be270cdd75c9d1b6639f18ecba63c15298e86')

build() {
  cd ${_realname}-${pkgver}

  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make install DESTDIR="${pkgdir}"
}

# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=lynx
pkgver=2.8.9
_relver=${pkgver}rel.1
pkgrel=1
pkgdesc="A text browser for the World Wide Web"
url="https://lynx.browser.org/"
arch=('x86_64')
license=('GPL')
depends=('libopenssl' 'libidn' 'libintl' 'libiconv' 'ncurses' 'zlib')
makedepends=('ncurses-devel' 'openssl-devel' 'gettext' 'libintl' 'libiconv' 'zlib-devel' )
optdepends=('bzip2: Decompression of some bzip2 files.')
backup=('etc/lynx.cfg')
_sha512sum='61edbe082684fcbd91bdbf4f4d27c3baf92358811aaffc2f8af46adf23ca7b48aede1520fc5f2a8fc974a2f4bbf4e57e7e6027a187bfc6101e56878c98178e6d'
source=("https://src.fedoraproject.org/repo/pkgs/lynx/lynx${pkgver}rel.1.tar.bz2/sha512/${_sha512sum}/lynx${pkgver}rel.1.tar.bz2"
         lynx-2.8.9-redhat.patch
         lynx-2.8.9-build.patch
         lynx-CVE-2008-4690.patch
         lynx-2.8.8-locale.patch
         lynx-2.8.9-static-analysis.patch
         lynx-2.8.9-CVE-2021-38165.patch
         msys-stdlib.patch       
         msysize-lynx.patch
        )
sha256sums=('387f193d7792f9cfada14c60b0e5c0bff18f227d9257a39483e14fa1aaf79595'
            'a635b9baf216ec6efbeb4980bd110de125cc2818964f64a49ef79f0830dda370'
            '9ee9e83accf4f73a7c5ded210409664c8d2a1ae74e227b00dd7f21aaa0f5da16'
            'bfbbd03edd4f6534a0d723f4a210b80e43a2c308536a532dcc310736eb4126d9'
            '84906a7d4e11bd15c523f652d10c32ccffde9dec9e8c58981123cb787aa86a59'
            'ded14958433bcf96049f0fda5265d48ec74c5a36c1c856f052ad6bf10f9e1aed'
            '868371c39d717b0448315d13e54d40eb29091a9eeea8a075e0d883ced37803ca'
            '772c32c1cfd119238f3b3634a1850b44147cc4312b8f8c34c3a4ea4006e1ae0e'
            '0a9cc031ea4618f8d2c70950a1cfd2f7b311d3271a58ac4f09398ed5f6000f14')
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB')

prepare() {
  cd "${srcdir}/${pkgname}${_relver}"
  patch --forward --strip=1 --input="${srcdir}/lynx-2.8.9-redhat.patch"
  patch --forward --strip=1 --input="${srcdir}/lynx-2.8.9-build.patch"
  patch --forward --strip=1 --input="${srcdir}/lynx-CVE-2008-4690.patch"
   patch --forward --strip=1 --input="${srcdir}/lynx-2.8.8-locale.patch"
  patch --forward --strip=1 --input="${srcdir}/lynx-2.8.9-static-analysis.patch"
  patch --forward --strip=1 --input="${srcdir}/lynx-2.8.9-CVE-2021-38165.patch"
  patch -Nbp1 -i "${srcdir}/msys-stdlib.patch"
  patch -Nbp1 -i "${srcdir}/msysize-lynx.patch"
#  patch --forward --strip=1 --input="${srcdir}/CVE-2021-38165.diff"
}

build() {
  cd "${srcdir}/${pkgname}${_relver}"
  CFLAGS="-std=gnu99 -D_GNU_SOURCE ${CFLAGS}" \
  LDFLAGS="${LDFLAGS} -lgcc -lstdc++" \
  ./configure --prefix=/usr \
    --disable-font-switch           \
    --enable-addrlist-page          \
    --enable-charset-choice         \
    --enable-cgi-links              \
    --enable-cjk                    \
    --enable-default-colors         \
    --enable-externs                \
    --enable-file-upload            \
    --enable-internal-links         \
    --enable-ipv6                   \
    --enable-japanese-utf8          \
    --enable-justify-elts           \
    --enable-locale-charset         \
    --enable-kbd-layout             \
    --enable-libjs                  \
    --enable-nls                    \
    --enable-nsl-fork               \
    --enable-persistent-cookies     \
    --enable-prettysrc              \
    --enable-read-eta               \
    --enable-scrollbar              \
    --enable-source-cache           \
    --enable-warnings               \
    --with-screen=ncursesw          \
    --with-ssl                      \
    --with-bzlib                    \
    --with-zlib
  make
}

package() {
  cd "${srcdir}/${pkgname}${_relver}"
  make DESTDIR="${pkgdir}" install
   mkdir ${pkgdir}/etc
  cp lynx.cfg ${pkgdir}/etc/

  # FS#20404 - points to local help
  sed -i -e "s|^HELPFILE.*$|HELPFILE:file:///usr/share/doc/lynx/lynx_help/lynx_help_main.html|" "${pkgdir}/etc/lynx.cfg"

  install -d "${pkgdir}/usr/share/doc/lynx"
  cp -rf lynx_help "${pkgdir}/usr/share/doc/lynx"
}

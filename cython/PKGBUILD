# Maintainer: Alexandre Ferreira < alex.jorge.m [at] gmail.com >

_realname=cython
pkgbase=${_realname}
pkgname=('cython')
pkgver=3.1.6
pkgrel=1
pkgdesc='C-Extensions for Python'
arch=('i686' 'x86_64')
url='https://cython.org/'
msys2_repository_url='https://github.com/cython/cython'
msys2_changelog_url='https://cython.readthedocs.io/en/latest/src/changes.html'
msys2_references=(
  "archlinux: cython"
  "cygwin: python-cython"
  "gentoo: dev-python/cython"
  "purl: pkg:pypi/cython"
)
license=('spdx:Apache-2.0')
depends=('python')
conflicts=('cython0')
makedepends=(
  'gcc'
  'python-devel'
  "python-setuptools"
  "python-build"
  "python-installer"
)
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/cython/cython/archive/${pkgver}.tar.gz")
sha256sums=('db7ca1e9d0ca58a8b5b236eef972113ae2c081bc4a755b399be6f0aad858bf11')

prepare() {
  cd "${_realname}-${pkgver}"
}

build() {
  cd "${_realname}-${pkgver}"

  # XXX: this changes the build path, and thus the DLL base address
  MSYSTEM=CYGWIN
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
}

# Maintainer: LoveSy <shana@zju.edu.cn>
pkgname=flexc++
pkgver=2.06.01
pkgrel=1
pkgdesc="C++ scanner generator"
arch=('i686' 'x86_64')
url="https://fbb-git.github.io/flexcpp/"
license=('GPL')
# Versions taken from the 'required' file in sources
# depends=('libbobcat>=4.01.00')
makedepends=('icmake>=8.00.04' 'yodl>=3.06.00')
optdepends=()
source=("https://github.com/yujincheng08/flexcpp/archive/${pkgver}-${pkgrel}.tar.gz")
sha256sums=('8086e4d29edd05a48f8ba4c5fdb2aafcbb6dfae908e20e11f8dd4231ef746d2f')

build() {
  cd "$srcdir/flexcpp-${pkgver}-${pkgrel}/flexc++"

  CXXFLAGS="$CXXFLAGS --std=c++14"
  # Add the -P option not to use precompiled headers, which can be useful since
  # they require a lot of free space, compared to a normal compilation:
  # ./build -P program
  ./build program
  ./build man
  ./build manual
}

package() {
  cd "$srcdir/flexcpp-${pkgver}-${pkgrel}/flexc++"

  # Since 2.03.02, first argument to install is <what to install> (x = all),
  # and second is the base directory
  ./build install x "$pkgdir"
}

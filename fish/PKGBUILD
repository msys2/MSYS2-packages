pkgname=fish
pkgver=3.6.0
pkgrel=1
epoch=
pkgdesc='Smart and user friendly shell intended mostly for interactive use'
arch=('i686' 'x86_64')
url="https://fishshell.com/"
license=('GPL2')
groups=()
depends=('bc' 'gcc-libs' 'gettext' 'libiconv' 'libpcre2_16' 'man-db' 'ncurses')
makedepends=('doxygen' 'gcc' 'gettext-devel' 'intltool' 'libiconv-devel' 'ncurses-devel' 'pcre2-devel' 'cmake')
optdepends=('python: for manual page completion parser and web configuration tool')
install=fish.install
backup=('etc/fish/config.fish' 'etc/fish/msys2.fish' 'etc/fish/perlbin.fish')
source=("https://github.com/fish-shell/fish-shell/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc}
        "001-use-variable-width-pcre2-type.patch::https://github.com/fish-shell/fish-shell/commit/52d2087dd370241cf10c4e41df77d38300e6a20c.patch"
        "002-fix-last-PCRE2_UCHAR32.patch::https://github.com/fish-shell/fish-shell/commit/bd871c5372ec3c249629c396c8c40bf68033bab2.patch"
        config.fish
        msys2.fish
        msystem.fish
        perlbin.fish)
sha512sums=('a11ea761adace02ef7f88b25893328005cef07f92d362fbc794540dca26e9cfc970878d0312caebd19d64483e9f3c3e24ad6c27dfce264d177f93d65c7ef6996'
            'SKIP'
            '83b552508c7cea2853e1bba36c3c2b245a2fdcd158036d602eae38f6e7dea745da6f098775c33412f4a9ca4f10e4e3b963a5339a9e3c27b1c4bfea7cb51eae72'
            'f59ca02f2143d270a9730e48fc76961c2137728ad7ff5ee9a16040b6e5ff86882ba7dd951a9ad91f0a79e5369629b6611593c9d0dcedb27adf41a2754c160340'
            '817195e2a6cdec1791b96666ee95f87f76569048268645a947f9283f711906fd3f2f5f1a4908b2b1edc0b9742215e0ac1c1cdaf10f9ec50d38b115fc05ae44b7'
            '81d0a0b436a5a9dd2973e0de932833605c9f3fc57cc71ba32a15856e29b5192fa8ab450bb17d6f53636c8724327901a2d8ecd72fecb4afe120c258dcc581129d'
            '64777bf41ba576506d45ffe4a5d7b5c47a026f519678f71ef1b08a7fad3d678faac23f3a695cea2869743bbbea85a26c97c11df5de6eb91c65304d5e43671201'
            'a675062ebaf7da53845ba3a005d9f4b90c514c7eea2ead0f16592246925a00862afe7fd7326a86006f8d6077e02c3c37a87358105d4bb661001161bfd1788724')
validpgpkeys=('003837986104878835FA516D7A67D962D88A709A') # David Adam <zanchey@gmail.com>

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/001-use-variable-width-pcre2-type.patch"
  patch -p1 -i "${srcdir}/002-fix-last-PCRE2_UCHAR32.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export CXXFLAGS+=" ${CPPFLAGS}"
  cmake \
    -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev
  make -C build VERBOSE=1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  rm -rf "${srcdir}/${pkgname}-${pkgver}/build/user_doc"
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/config.fish" "${pkgdir}/etc/fish/config.fish"
  install -D -m644 "${srcdir}/msys2.fish" "${pkgdir}/etc/fish/msys2.fish"
  install -D -m644 "${srcdir}/msystem.fish" "${pkgdir}/etc/fish/msystem.fish"
  install -D -m644 "${srcdir}/perlbin.fish" "${pkgdir}/etc/fish/perlbin.fish"
}

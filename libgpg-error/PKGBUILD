# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgbase=libgpg-error
pkgname=('libgpg-error' 'libgpg-error-devel') # 'gpg-error'
pkgver=1.49
pkgrel=1
pkgdesc="Support library for libgcrypt"
arch=('i686' 'x86_64')
url="https://gnupg.org"
msys2_repository_url="https://git.gnupg.org/cgi-bin/gitweb.cgi?p=libgpg-error.git"
msys2_references=(
  "cpe: cpe:/a:gnupg:libgpg-error"
)
license=('spdx:LGPL-2.1-or-later')
depends=('sh' 'libiconv' 'libintl')
makedepends=('libiconv-devel' 'gettext-devel' 'autotools' 'gcc')
source=(https://gnupg.org/ftp/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2{,.sig}
        gnulib-weak.patch
        gpg-error-static-linking.patch)
sha256sums=('8b79d54639dbf4abc08b5406fb2f37e669a2dec091dd024fb87dd367131c63a9'
            'SKIP'
            '0df2f92ab866e9d9824086cca89ff286cfdfad5e659cb2b655061e0c7e562e3a'
            '9780e3e397b983ef52d5dae22d54c4453a47bddf04eafa2f874c75038e3485eb')
#These might be signed by any of these keys https://gnupg.org/signature_key.html
validpgpkeys=('5B80C5754298F0CB55D8ED6ABCEF7E294B092E28'
              '6DAA6E64A76D2840571B4902528897B826403ADA'
              'AC8E115BF73E2D8D47FA9908E98E9B2D19C6C8BD'
              '02F38DFF731FF97CB039A1DA549E695E905BA208')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i ${srcdir}/gnulib-weak.patch
  patch -p1 -i ${srcdir}/gpg-error-static-linking.patch
  autoreconf -fi
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --prefix=/usr \
    --build=${CYGWIN_CHOST} \
    --without-libiconv-prefix \
    --without-libintl-prefix \
    --enable-install-gpg-error-config \
    --enable-shared \
    --enable-static

  make no_undefined=-no-undefined
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package_libgpg-error() {
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/share
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  rm -f ${pkgdir}/usr/bin/*-config
  cp -rf ${srcdir}/dest/usr/share/locale ${pkgdir}/usr/share/
}

package_libgpg-error-devel() {
  pkgdesc="libgpg-error headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=('libiconv-devel' 'gettext-devel')

  mkdir -p ${pkgdir}/usr/{bin,share}
  cp -f ${srcdir}/dest/usr/bin/*-config ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share/aclocal ${pkgdir}/usr/share/
  cp -rf ${srcdir}/dest/usr/share/common-lisp ${pkgdir}/usr/share/
}

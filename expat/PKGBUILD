# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('expat' 'libexpat' 'libexpat-devel')
pkgver=2.1.1
pkgrel=1
pkgdesc="An XML parser library"
arch=('i686' 'x86_64')
url="http://expat.sourceforge.net/"
license=('custom')
source=(https://downloads.sourceforge.net/sourceforge/expat/${pkgname}-${pkgver}.tar.bz2
        msys2-expat-2.1.1.patch)
sha256sums=('aff584e5a2f759dcfc6d48671e9529f6afe1e30b0cd6a4cec200cbe3f793de67'
            '11102bf0c6271e4d9667ed1b45e3a006e06cf074438a2a8b5e4d9316e0a34f13')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/msys2-expat-2.1.1.patch
  rm -f m4/*

  autoreconf -vfi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
  make DESTDIR=${srcdir}/dest install
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package_expat() {
  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/xml* ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

package_libexpat() {
  groups=('libraries')
  depends=('gcc-libs')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
}

package_libexpat-devel() {
  pkgdesc="Libexpat headers and libraries"
  options=('staticlibs')
  groups=('development')
  depends=("libexpat=${pkgver}")

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}

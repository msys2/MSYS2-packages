# Maintainer: LoveSy <shana@zju.edu.cn>
pkgname=libbobcat
pkgver=4.07.00
pkgrel=1
pkgdesc="Bobcat (Brokken's Own Base Classes And Templates) library"
arch=('i686' 'x86_64')
url="https://fbb-git.github.io/bobcat/"
license=('GPL')
# Versions taken from the 'required' file in sources
makedepends=('icmake>=8.01.00' 'yodl>=3.07.01')
optdepends=()
# Use my own repo temporarily to avoid untaring soft link.
source=("https://github.com/yujincheng08/bobcat/archive/${pkgver}-${pkgrel}.tar.gz"
        "fix-g_errno.patch")
sha256sums=('6c4386339bf5b31fa723bca820ea37282c5a066aea197dcb6eb84efe19da1a4d'
            'fe1bedf6bbe255128aeab37215164022a029f2343bc29229e03e943501f3f73a')

prepare() {
	cd "${srcdir}/bobcat-${pkgver}"
    patch -p1 -i ../fix-g_errno.patch
}
build() {
  cd "$srcdir/bobcat-${pkgver}/bobcat"
  CXXFLAGS="$CXXFLAGS -std=c++14"
  # Add the -P option not to use precompiled headers, which can be useful since
  # they require a lot of free space, compared to a normal compilation:
  # ./build -P libraries all
  ./build -p light
  ./build man
}

package() {
  cd "$srcdir/bobcat-${pkgver}/bobcat"

  # Since 4.01.02, first argument to install is <what to install> (x = all),
  # and second is the base directory (absolute path, unlike 4.01.00)
  ./build install x "$pkgdir"
}


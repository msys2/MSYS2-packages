# Maintainer: Peter Budai <peterbudai@hotmail.com>

_realname=Test-NoWarnings
pkgname=perl-${_realname}
pkgver=1.06
pkgrel=1
pkgdesc="Make sure you didn't emit any warnings while testing"
arch=('any')
url="https://metacpan.org/release/Test-NoWarnings"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-Test-Simple')
options=('!emptydirs')
groups=('perl-modules')
source=("https://cpan.metacpan.org/authors/id/H/HA/HAARG/${_realname}-${pkgver}.tar.gz")
sha256sums=('c2dc51143b7eb63231210e27df20d2c8393772e0a333547ec8b7a205ed62f737')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL installdirs=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

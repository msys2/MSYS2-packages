# Contributor: Dirk Stolle

_realname=snowballstemmer
pkgname=python-${_realname}
pkgver=3.0.1
pkgrel=1
pkgdesc='Snowball stemming library collection for Python'
arch=('any')
url='https://github.com/snowballstem/snowball'
msys2_references=(
  'cygwin: python-snowballstemmer'
  'purl: pkg:pypi/snowballstemmer'
)
license=('spdx:BSD-3-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('6d5eeeec8e9f84d4d56b847692bacf79bc2c8e90c7f80ca4444ff8b6f2e52895')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}"

  python -m installer --prefix=${MSYSTEM_PREFIX} --destdir="${pkgdir}" dist/*.whl

  install -Dm644 COPYING "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}

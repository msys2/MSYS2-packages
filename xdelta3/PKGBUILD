# Maintainer: David Macek <david.macek.0@gmail.com>

pkgname=xdelta3
pkgver=3.0.8
pkgrel=1
pkgdesc='Diff utility which works with binary files'
arch=('x86_64' 'i686')
url='http://xdelta.org/'
license=('GPL')
depends=('xz')
source=("https://${pkgname/3}.googlecode.com/files/${pkgname}-${pkgver}.tar.xz"
        '0001-regtest_size_t.patch'
        '0002-disable-permission-test.patch')
sha256sums=('3a86f29c95664fb44b8a40ff22d9bcc3e87aa8c01f0ff75931a7fa78ed3d2e55'
            'cd68457982aad262db74613b17887a04eabf8bf764f7d40c494a1b24e07c32ea'
            '4e78cbec5e93621eda5924c0202e8f0540ddc4c26296d01d0ac1d1327d4d5c68')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-regtest_size_t.patch"
  patch -Np1 -i "${srcdir}/0002-disable-permission-test.patch"
}

check() {
  cd "${pkgname}-${pkgver}"
  ./$pkgname test
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --build=${CHOST}
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgbase=libuv
pkgname=("${pkgbase}" "${pkgbase}-devel")
pkgver=1.50.0
pkgrel=1
pkgdesc="Multi-platform support library with a focus on asynchronous I/O"
arch=('i686' 'x86_64')
url="https://github.com/libuv/libuv"
msys2_references=(
  "cpe: cpe:/a:libuv:libuv"
  "cpe: cpe:/a:libuv_project:libuv"
)
license=('custom')
makedepends=('gcc' 'autotools')
depends=('gcc-libs')
source=("https://dist.libuv.org/dist/v${pkgver}/libuv-v${pkgver}.tar.gz"{,.sign})
sha512sums=('b153d019c630831819913ccd0615c22737df49125da533f86db27f24a519937ed64113ceb6445d731d133e2d97b43b6949877e6078c4459d50ea90d2af4a9da9'
            'SKIP')
# PGP key IDs are available from https://github.com/libuv/libuv/blob/v1.x/MAINTAINERS.md
validpgpkeys=(
  'CFBB9CA9A5BEAFD70E2B3C5A79A67C55A3679C8B' # Jameson Nash (@vtjnash)
  '612F0EAD9401622379DF4402F28C3C8DA33C03BE' # Santiago Gimeno
)

prepare() {
  cd "${srcdir}"/${pkgname}-v${pkgver}
  ./autogen.sh
}

build() {
  export lt_cv_deplibs_check_method='pass_all'

  export MSYSTEM=CYGWIN
  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  cd "${srcdir}"/${pkgbase}-v${pkgver}
  ./configure --host=${CYGWIN_CHOST} \
              --build=${CYGWIN_CHOST} \
              --prefix=/usr

  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "${srcdir}"/${pkgbase}-v${pkgver}
  # a few of these tests are known to fail.
  make check || true
}

package_libuv() {
  groups=('libraries')

  cd "${srcdir}"/${pkgbase}-v${pkgver}
  install -Dm644 LICENSE \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 AUTHORS \
    "${pkgdir}"/usr/share/doc/${pkgname}/AUTHORS
  install -Dm644 README.md \
    "${pkgdir}"/usr/share/doc/${pkgname}/README.md
  install -Dm644 ChangeLog \
    "${pkgdir}"/usr/share/doc/${pkgname}/ChangeLog
 
  cd "${srcdir}/dest"
  mkdir -p ${pkgdir}/usr/bin
  cp -f usr/bin/*.dll ${pkgdir}/usr/bin
}

package_libuv-devel() {
  groups=('development')
  options=('staticlibs')
  depends=(${pkgbase}=${pkgver})
  pkgdesc="libuv headers and libraries"

  cd "${srcdir}/dest"
  mkdir -p ${pkgdir}/usr/lib
  mkdir -p ${pkgdir}/usr/include

  cp -rf usr/lib/* ${pkgdir}/usr/lib
  cp -rf usr/include/* ${pkgdir}/usr/include
  #This is required for cmake.  
  cp "${srcdir}"/${pkgbase}-v${pkgver}/include/uv/posix.h ${pkgdir}/usr/include/uv
}

# Maintainer Equim <sayaka@ekyu.moe>

pkgname=iperf
pkgver=2.0.10
pkgrel=1
pkgdesc='A tool to measure maximum TCP bandwidth'
arch=('i686' 'x86_64')
license=('custom')
url='https://sourceforge.net/projects/iperf2/'
depends=('gcc-libs')
source=("http://downloads.sourceforge.net/iperf2/iperf-$pkgver.tar.gz")
sha256sums=('7fe4348dcca313b74e0aa9c34a8ccd713b84a5615b8578f4aa94cedce9891ef2')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --build=${CHOST} \
    --host=${CHOST} \
    --prefix=/usr \
    --enable-ipv6 \
    --enable-multicast \
    --enable-threads
  make
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/iperf/LICENSE"
  make DESTDIR="$pkgdir" install
}

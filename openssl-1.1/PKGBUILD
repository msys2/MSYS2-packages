# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=openssl
pkgname=('openssl-1.1' 'libopenssl-1.1' 'openssl-1.1-devel')
_ver=1.1.1s
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('i686' 'x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=('zlib')
makedepends=('gcc' 'tar' 'perl' 'zlib-devel' 'diffutils' 'nasm')
noextract=(${_realname}-${_ver}.tar.gz)
source=("https://www.openssl.org/source/${_realname}-${_ver}.tar.gz"{,.asc}
        '0001-Use-usr-ssl-as-ca-dir-instead-of-.-demoCA.patch'
        '0002-Support-MSYS2.patch'
        '0003-tls13secretstest-work-around-multiple-definition-of.patch'
        '0004-Override-engines-directory.patch')
sha256sums=('c5ac01e760ee6ff0dab61d6b2bbd30146724d063eb322180c6f18a6f74e4b6aa'
            'SKIP'
            '35f5508bc3e651bb2ef1bf9f4e052f531aeaefbbdeff95b9746fff71137fff31'
            '066a51e209a1afdcae19f8a8b72edc63468227a128583b2b86618065c8e8dece'
            'a83dcfeec646724f081f8ff89beccee83c962758aa74d724f263138aa21b2438'
            '79765cc10799691ae47880556255287c64b8c1f26ea3907ad2e0add97bc9f1fc')
# https://www.openssl.org/community/otc.html
validpgpkeys=(
  '8657ABB260F056B1E5190839D9C4D26D0E604491' # Matt Caswell <matt@openssl.org>
  '7953AC1FBC3DC8B3B292393ED5E9E43F7DF9EE8C' # Richard Levitte <richard@levitte.org>
  'A21FAB74B0088AA361152586B8EF1A6BA9DA2D5C' # Tomáš Mráz <tm@t8m.info>
  'B7C1C14360F353A36862E4D5231C84CDDCC69C45' # Paul Dale <pauli@openssl.org>
)

prepare() {
  [[ -d ${srcdir}/${pkgname}-${_ver} ]] || tar -xzvf ${srcdir}/${_realname}-${_ver}.tar.gz -C ${srcdir}
  cd ${srcdir}/${_realname}-${_ver}

  patch -p1 -i ${srcdir}/0001-Use-usr-ssl-as-ca-dir-instead-of-.-demoCA.patch
  patch -p1 -i ${srcdir}/0002-Support-MSYS2.patch
  patch -p1 -i ${srcdir}/0003-tls13secretstest-work-around-multiple-definition-of.patch
  patch -p1 -i ${srcdir}/0004-Override-engines-directory.patch
}

build() {
  cd ${srcdir}/${_realname}-${_ver}

  if [ "${CARCH}" == 'x86_64' ]; then
    openssltarget='Cygwin-x86_64'
  elif [ "${CARCH}" == 'i686' ]; then
    openssltarget='Cygwin'
  fi

  export MSYSTEM=CYGWIN
  ./Configure \
    --prefix=/usr \
    --openssldir=/usr/ssl \
    --libdir=lib/openssl-1.1 \
    shared \
    zlib \
    no-idea \
    no-rc5 \
    ${openssltarget}

  make depend
  make all
  make DESTDIR=${srcdir}/dest MANDIR=/usr/share/man MANSUFFIX=ssl install_sw
}

check() {
  cd ${srcdir}/${pkgname}-${_ver}

  # the test would write test data into /usr/ssl, which we do not want
  patch -R -p1 -i ${srcdir}/0001-Use-usr-ssl-as-ca-dir-instead-of-.-demoCA.patch
  make test
  patch -p1 -i ${srcdir}/0001-Use-usr-ssl-as-ca-dir-instead-of-.-demoCA.patch
}

package_openssl-1.1() {
  depends=('libopenssl-1.1' 'zlib')
  optdepends=('ca-certificates')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/openssl.exe ${pkgdir}/usr/bin/openssl-1.1.exe
}

package_libopenssl-1.1() {
  depends=('zlib')
  conflicts=('libopenssl<3')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/

  mkdir -p ${pkgdir}/usr/lib/openssl-1.1/openssl
  cp -rf ${srcdir}/dest/usr/lib/openssl-1.1/openssl/engines-1.1 ${pkgdir}/usr/lib/openssl-1.1/openssl
  chmod -R 755 ${pkgdir}/usr/lib/openssl-1.1/openssl/engines-1.1

  install -D -m644 ${srcdir}/openssl-${_ver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

package_openssl-1.1-devel() {
  pkgdesc="Openssl headers and libraries"
  depends=("libopenssl-1.1=${pkgver}" 'zlib-devel')

  mkdir -p ${pkgdir}/usr/lib/openssl-1.1
  mkdir -p ${pkgdir}/usr/include
  cp -rf ${srcdir}/dest/usr/include/openssl/ ${pkgdir}/usr/include/openssl-1.1
  cp -rf ${srcdir}/dest/usr/lib/openssl-1.1/pkgconfig ${pkgdir}/usr/lib/openssl-1.1
  cp -f ${srcdir}/dest/usr/lib/openssl-1.1/*.a ${pkgdir}/usr/lib/openssl-1.1

  # Update includedir in .pc files
  sed -e 's|/include$|/include/openssl-1.1|' -i "$pkgdir"/usr/lib/openssl-1.1/pkgconfig/*.pc
}

# This was copied from the arch linux PKGBUILD.
# GNU Enscript is more or less feature complete, so it's a drop in file.

pkgname=enscript
pkgver=1.6.6
pkgrel=6
pkgdesc="Convert ASCII files to PostScript suitable for printing"
arch=('x86_64')
url="https://git.savannah.gnu.org/cgit/enscript.git"
license=('sdpx:GPL-3.0-or-later')
depends=('perl' 'gcc-libs' 'cygutils')

makedepends=('autotools' 'gcc')
optdepends=('cygutils: Send the output to a physical printer instead of stdout.')
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('6d56bada6934d055b34b6c90399aa85975e66457ac5bf513427ae7fc77f5c0bb')

# To avoid a mess with lpr, the config file will be patched to send to stdout by default.
prepare() {
  cd $pkgname-$pkgver
  sed -i '52s/DefaultOutputMethod: printer/DefaultOutputMethod: stdout/' lib/enscript.cfg.in
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc/enscript
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

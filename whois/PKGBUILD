# Maintainer: Felix Huettner <huettner94@gmx.de>

pkgname=whois
pkgver=5.6.5
pkgrel=1
pkgdesc="The whois client by Marco d'Itri"
arch=('i686' 'x86_64')
url="https://www.linux.it/~md/software/"
msys2_repository_url='https://github.com/rfc1036/whois'
license=('spdx:GPL-2.0-or-later')
depends=('libxcrypt' 'libidn2' 'libiconv' 'libunistring')
makedepends=('perl' 'libxcrypt-devel' 'libidn2-devel' 'libiconv-devel' 'libunistring-devel' 'make' 'gcc')
backup=('etc/whois.conf')
source=("http://ftp.debian.org/debian/pool/main/w/whois/${pkgname}_${pkgver}.tar.xz"
        "whois-libiconv.patch")
sha256sums=('3ec4168da3ff61e5e426964428202bc0573ad6ecc1f3baf6f43daea6c748aa7d'
            '15635fe62da7eed833ad0b1c124fc8e4647f06c7e5b6b5b3314b32a663899ce6')

prepare() {
  mv work "${pkgname}-${pkgver}"
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -i ${srcdir}/whois-libiconv.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix=/usr \
       CFLAGS="$CFLAGS $CPPFLAGS" \
       CONFIG_FILE=/etc/whois.conf \
       HAVE_LIBIDN2=1 \
       HAVE_ICONV=1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix=/usr BASEDIR="${pkgdir}" install-whois
  install -D -m644 whois.conf "${pkgdir}/etc/whois.conf"
}

# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=('zlib' 'zlib-devel')
pkgver=1.2.8
pkgrel=3
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=('i686' 'x86_64')
groups=('libraries')
license=('custom')
url="http://www.zlib.net/"
makedepends=('gcc' 'make')
source=(#"http://zlib.net/current/zlib-${pkgver}.tar.gz"
        "https://sourceforge.net/projects/libpng/files/zlib/${pkgver}/zlib-${pkgver}.tar.gz"
        1.2.7-minizip-cygwin.patch
        1.2.7-zlib-symbols.patch
        zlib-1.2.8-msys2.patch)
sha256sums=('36658cb768a54c1d4dec43c3116c27ed893e88b02ecfcb44f2166f9c0b7f2a0d'
            '425e2e36e8c788f0fa725c9b713642d5ae2f6629eab8613a2c2886eed2de5c21'
            '16e9ce18df14a700dda0572843cdb42b5cb3d398c64706af0acc7cd77c181b7e'
            'ecb05d16232b1026b77d01e99145f1a88b9635028e7b8fe447353e65c805f84e')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  grep -A 24 '^  Copyright' zlib.h > LICENSE

  patch -p2 -i ${srcdir}/1.2.7-minizip-cygwin.patch
  patch -p2 -i ${srcdir}/1.2.7-zlib-symbols.patch
  patch -p1 -i ${srcdir}/zlib-1.2.8-msys2.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
  make install DESTDIR="$srcdir/dest"
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make test
}

package_zlib() {
  depends=('gcc-libs')

  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/bin ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/share ${pkgdir}/usr/
  install -D -m644 ${srcdir}/${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/zlib/LICENSE
}

package_zlib-devel() {
  pkgdesc="zlib headers and libraries"
  groups=('development')
  options=('staticlibs')  # needed by binutils testsuite
  depends=("zlib=${pkgver}")

  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/include ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/lib ${pkgdir}/usr/
  sed -e "s| -L\${sharedlibdir}||g" -i ${pkgdir}/usr/lib/pkgconfig/zlib.pc
}

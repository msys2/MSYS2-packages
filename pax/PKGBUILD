# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=pax
pkgver=20240817
pkgrel=2
pkgdesc="Portable Archive Interchange - the POSIX standard archive tool for cpio and tar formats"
arch=('i686' 'x86_64')
url="https://www.mirbsd.org/pax.htm"
license=('BSD')
msys2_references=(
  'archlinux: pax'
  'cpe: cpe:2.3:a:pax_project:pax'
  'gentoo: app-arch/pax'
)
provides=("${pkgname}-git")
conflicts=("${pkgname}-git")
replaces=("${pkgname}-git")
makedepends=('gcc')
source=("http://www.mirbsd.org/MirOS/dist/mir/cpio/paxmirabilis-${pkgver}.cpio.gz")
noextract=("paxmirabilis-${pkgver}.cpio.gz")
sha256sums=('55eb24cef45fe2c7d72410a2c97ad12722e2d857142ba9fe65653dba53a58611')

prepare() {
  mkdir -p "${pkgname}-${pkgver}/build"

  bsdtar -x -f "paxmirabilis-${pkgver}.cpio.gz" -C "$pkgname-${pkgver}" --strip-components='1'
}

build() {
  cd "${srcdir}/$pkgname-${pkgver}"

  cd build
  CPPFLAGS="-DPAX_SAFE_PATH=\\\"/bin:/usr/bin:/usr/local/bin\\\"" \
  sh ../Build.sh -r -tpax

  # license
  sed -n '5,36p' ../pax.h > LICENSE # create file
  sed -i '1,32s/^.\{,3\}//' LICENSE # erase C comments
}

package() {
  # executables
  install -D -m755 "${pkgname}-${pkgver}/build/pax" -t "${pkgdir}/usr/bin"
  install -D -m755 "${pkgname}-${pkgver}/build/paxcpio" -t "${pkgdir}/usr/bin"
  install -D -m755 "${pkgname}-${pkgver}/build/paxtar" -t "${pkgdir}/usr/bin"

  # man pages
  install -D -m644 "${pkgname}-${pkgver}/build/mans/"*.1 -t "${pkgdir}/usr/share/man/man1"

  # license
  install -D -m644 "${pkgname}-${pkgver}/build/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

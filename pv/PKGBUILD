# Maintainer: poxu <poxvuibr@gmail.com>

pkgname=pv
pkgver=1.10.3
pkgrel=1
pkgdesc="Pipe viewer"
arch=('i686' 'x86_64')
url="https://www.ivarch.com/programs/pv.shtml"
msys2_repository_url="https://codeberg.org/a-j-wood/pv"
msys2_changelog_url='https://codeberg.org/ivarch/pv/src/branch/main/docs/NEWS.md'
license=('spdx:GPL-3.0-or-later')
depends=('libintl')
makedepends=('autotools' 'gcc' 'gettext-devel')
source=(https://www.ivarch.com/programs/sources/pv-${pkgver}.tar.gz)
sha256sums=('aa1630c79af6960a8922ffa64d2c3e7f87486da21fcb57e277824294fd266742')

build () {
  [ -d ${srcdir}/build-${CHOST} ] && rm -rf ${srcdir}/build-${CHOST}
  mkdir ${srcdir}/build-${CHOST} && cd ${srcdir}/build-${CHOST}
  
  ../${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --host=${CHOST} \

  make
}

check () {
    cd ${srcdir}/build-${CHOST}
    make test
}

package () {
  cd build-${CHOST}
  make prefix="${pkgdir}/usr" install
}

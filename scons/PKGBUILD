# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

pkgname=scons
pkgver=3.0.4
pkgrel=1
pkgdesc="Extensible Python-based build utility"
arch=('any')
groups=('base-devel')
url="http://scons.org"
license=('MIT')
depends=('python2')
source=("https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        scons-2.3.4-cyglink-msys2.patch
        scons-2.3.4-msys2-plat-mingw-env.patch
        mingw_w64.py)
sha256sums=('e2b8b36e25492720a05c0f0a92a219b42d11ce0c51e3397a1e8296dfea1d9b1a'
            'a8dd14aea1c09733c9eccf119652dbcfa6f7cad22c4ff387232b00bf6bdabf25'
            'c024560dc93636d92614c17d7519080dd931f6c1471088616692a443d3cfc064'
            '984c55d0ca18ac594941bb6336fe994efe2f5bba20225e8cb1f5d42c80eaf651')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cp ../mingw_w64.py engine/SCons/Tool/

  # avoid errors when re-running makepkg
  rm -f engine/SCons/Platform/msys.py engine/SCons/Tool/msyslink.py
  patch -p1 -i ${srcdir}/scons-2.3.4-cyglink-msys2.patch
  patch -p0 -i ${srcdir}/scons-2.3.4-msys2-plat-mingw-env.patch
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  /bin/python2 setup.py install \
      --standard-lib \
      --prefix=/usr \
      --install-data=/usr/share \
      --optimize=1 \
      --root="$pkgdir"

  # fix for python 2.7
  sed -i 's:^#!.*bin/env python:#!/usr/bin/python2:' \
    "$pkgdir"/usr/bin/*

  install -Dm644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

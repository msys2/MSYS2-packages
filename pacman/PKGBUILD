# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=pacman
pkgver=6.0.1
pkgrel=17
pkgdesc="A library-based package manager with dependency support (MSYS2 port)"
arch=('i686' 'x86_64')
url="https://www.archlinux.org/pacman/"
license=('GPL')
depends=('bash>=4.2.045'
         'gettext'
         'gnupg'
         'curl'
         'pacman-mirrors'
         'msys2-keyring'
         'which'
         'bzip2'
         'xz'
         'zstd')
checkdepends=('python')
makedepends=('asciidoc'
             'doxygen'
             'git'
             'gcc'
             'meson'
             'ninja'
             'gettext-devel'
             'heimdal-devel'
             'libarchive-devel'
             'libcurl-devel'
             'libgpgme-devel'
             'libsqlite-devel'
             'libunistring-devel')
backup=("etc/pacman.conf"
        "etc/makepkg.conf"
        "etc/makepkg_mingw.conf")
_commit="0a6fecd07271a54d9009ea7204c0e6288a44212b"
source=(pacman-${pkgver}::git+https://gitlab.archlinux.org/pacman/pacman.git#commit=${_commit}
        "pacman.conf"
        "makepkg.conf"
        "makepkg_mingw.conf"
        "makepkg-mingw"
        0001-Msysize.patch
        0002-More-debugging-info.patch
        0003-Core-update.patch
        0004-Remove-ldconfig.patch
        0005-Change-the-epoch-separator.patch
        0006-makepkg-avoid-creating-.tar-files-with-extended-attr.patch
        0007-pacman-libalpm-ignore-file-conflicts-for-foo.exe-foo.patch
        0008-Change-default-answer-of-all-queries-to-yes.patch
        0009-Use-pipe-instead-of-socket.patch
        0010-pacman-make-file-list-comparisons-between-packages-c.patch
        0011-Translate-MSYS2-specific-messages-into-Japanese.patch
        0012-Workaround-compressing-packages-with-bsdtar-under-Ac.patch
        0013-Fix-asciidoc-argparse.patch
        0014-Remove-fakeroot.patch
        0015-Remove-sudo.patch
        0016-Use-msys-tools.patch
        0017-Export-CC-and-CXX-variables-explicitly.patch
        0018-pacman.c-handle-cr-on-stdin-as-well.patch
        0019-Add-a-CI-job.patch
        0020-Strip-static-libraries-as-well.patch
        0021-Fix-building-staticlibs-with-LTO.patch
        0022-fixup-Export-CC-and-CXX-variables-explicitly.patch
        0023-Make-lint_pkgbuild-opt-in-via-MAKEPKG_LINT_PKGBUILD-.patch)
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
sha256sums=('SKIP'
            '26d141ead0b586e29ab6c49ffa45cf60eb2689f53f8e90c885ccd6d117e9ab67'
            'c12da01ede663a4924d0817a0d1bd6082b1380383cfb74cc1cea08f9d73e4902'
            '1b8033d17f23eb00c17b6303d854c4c6eae17cb2f59542f37ddfb83528437d6b'
            '98198e1f0f252eae0560d271bee4b9149e127399dd0d3fd5d8d24579d9e0550f'
            '04ac67a8f458a9daea532f87cedbab76f028ccf437069e3d24a583457603e61d'
            '6904ea154e451115c14246ded57737a5ab6c1151d1ac818c37dc849b70a5275f'
            '1c3a8b2ec84a277b13eceb7302aec6ea72b9d70a3d917b6d239160d95cde54c6'
            '4ba24fbaded92796e66833b19cf829048bb9e8fb2db80344c1e09d24d0f2f080'
            '633cfba88f1d598e62bae02d7451eedda874b75982934ddadd49b332021b3bad'
            '79f41bb7e68b811b481343f8b885104cb621174bce585442d8e8ef95975c9b7e'
            '478d9dfa62603fbf87c2a0dff299a5d0208ec7b29efc7352565e5a2445c0860b'
            'bdd40b16c963329598e2bd49722192136c71b4b17fcac9ac083ed28fd1f19b21'
            '05c99713437ecbc30772896faf2166f9927cde4d45737bdc1eab31eaf92315ca'
            'ac9cf7eeb5195ae7966cc253dca29e89b0928825fccf76735325942a9538add6'
            '511855147397af6f20db683129bea2bcb981a67dc4164843fda623a9b6fff49a'
            '643d1f972f58f5b33e865ad87d705d2f8420d7f923ef0bda6c49d2f3c427e08c'
            'ee8223a73ac00dc48ca811d0d96ccdabf4cdbc849fb820a8fb4124492763930e'
            'dc2cae6c2f1b317759e1c6e139e2c5ac2b65aaab76541a709791755839d444f8'
            '2fe08d76fc7ef47e1fc0b7ee6e3bc6961e3317c1da88da8778dbc844be219261'
            '32da0f0542425f279da01bae0c32b49361951a6d4c4f45666a6799260027409d'
            '6262bbe809ef52b0e1571a7a8c6a47d72379c698024365e0a1746e793f966ffd'
            '64e3888e79e023df0140721b67f57068200c423681ed7505f47faad105168ee8'
            'da6c02fddfe1acacca3ee4570d78a21d1b2af57ac4bd184ac66dff04cce5af3c'
            'fb6fa9c13f85bdd47dfa2c003904deb552887a785aa6743be9baa667f47824d6'
            '3997cf1d3999f25b1ac6ffae72ab2f96769da4ae38c8eb09fac5eb714fb8e543'
            '64a00e148071332c08ced252c7e4cba48afd29c286b73b8256588106ac3ef0aa'
            'b1a7813dc01b852e0e639e4cf473a3314a15ffd3582e236c3cf622e578a0e0e6')

apply_git_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fixup symlinks
  git config core.symlinks true
  git reset HEAD --hard

 apply_git_with_msg 0001-Msysize.patch \
  0002-More-debugging-info.patch \
  0003-Core-update.patch \
  0004-Remove-ldconfig.patch \
  0005-Change-the-epoch-separator.patch \
  0006-makepkg-avoid-creating-.tar-files-with-extended-attr.patch \
  0007-pacman-libalpm-ignore-file-conflicts-for-foo.exe-foo.patch \
  0008-Change-default-answer-of-all-queries-to-yes.patch \
  0009-Use-pipe-instead-of-socket.patch \
  0010-pacman-make-file-list-comparisons-between-packages-c.patch \
  0011-Translate-MSYS2-specific-messages-into-Japanese.patch \
  0012-Workaround-compressing-packages-with-bsdtar-under-Ac.patch \
  0013-Fix-asciidoc-argparse.patch \
  0014-Remove-fakeroot.patch \
  0015-Remove-sudo.patch \
  0016-Use-msys-tools.patch \
  0017-Export-CC-and-CXX-variables-explicitly.patch \
  0018-pacman.c-handle-cr-on-stdin-as-well.patch \
  0019-Add-a-CI-job.patch \
  0020-Strip-static-libraries-as-well.patch \
  0021-Fix-building-staticlibs-with-LTO.patch \
  0022-fixup-Export-CC-and-CXX-variables-explicitly.patch \
  0023-Make-lint_pkgbuild-opt-in-via-MAKEPKG_LINT_PKGBUILD-.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  LDFLAGS+=" -static-libgcc" \
  meson build-${CARCH} \
    --buildtype=plain \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --default-library=static \
    -Dbuildstatic=true \
    -Ddoc=enabled \
    -Ddoxygen=enabled \
    -Dgpgme=enabled \
    -Dcurl=enabled \
    -Duse-git-version=false \
    -Dpkg-ext=.pkg.tar.xz \
    -Dscriptlet-shell=/usr/bin/bash

  meson compile -C build-${CARCH}
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  meson test -C build-${CARCH}
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  DESTDIR=${pkgdir} meson install -C build-${CARCH}

  # install Arch specific stuff
  install -dm755 ${pkgdir}/etc
  install -m644 ${srcdir}/pacman.conf ${pkgdir}/etc/pacman.conf

  # Fixup .pc file
  sed -i "s|/usr/.*libintl.a|-lintl|g" ${pkgdir}/usr/lib/pkgconfig/libalpm.pc

  case "${CARCH}" in
  i686)
    mycarch="i686"
    mychost="i686-pc-msys"
    myflags="-march=i686"
  ;;
  x86_64)
    mycarch="x86_64"
    mychost="x86_64-pc-msys"
    myflags="-march=x86-64"
  ;;
  esac

  install -m644 ${srcdir}/makepkg.conf ${pkgdir}/etc/
  install -m644 ${srcdir}/makepkg_mingw.conf ${pkgdir}/etc/
  install -m755 ${srcdir}/makepkg-mingw ${pkgdir}/usr/bin/

  # set things correctly in the default conf file
  sed -i ${pkgdir}/etc/makepkg.conf \
    -e "s|@CARCH[@]|${mycarch}|g" \
    -e "s|@CHOST[@]|${mychost}|g" \
    -e "s|@CARCHFLAGS[@]|${myflags}|g"
}

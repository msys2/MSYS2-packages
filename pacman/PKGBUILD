# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

PKGEXT='.pkg.tar.xz'
pkgname=pacman
pkgver=5.2.2
pkgrel=3
pkgdesc="A library-based package manager with dependency support (MSYS2 port)"
arch=('i686' 'x86_64')
url="https://www.archlinux.org/pacman/"
license=('GPL')
groups=('base-devel')
depends=('bash>=4.2.045'
         #'dash'
         #'busybox'
         'gettext'
         'gnupg'
         #'libarchive'
         'curl'
         #'libgpgme'
         'pacman-mirrors'
         'msys2-keyring'
         'which'
         'bzip2'
         'xz'
         'zstd')
checkdepends=('python')
makedepends=('asciidoc'
             'doxygen'
             'git'
             'meson'
             'gettext-devel'
             'heimdal-devel'
             'libarchive-devel'
             'libcurl-devel'
             'libgpgme-devel'
             'libsqlite-devel'
             'libunistring-devel')
# options=('!strip' 'debug')
backup=("etc/pacman.conf"
        "etc/makepkg.conf"
        "etc/makepkg_mingw32.conf"
        "etc/makepkg_mingw64.conf")
source=(https://sources.archlinux.org/other/pacman/${pkgname}-${pkgver}.tar.gz{,.sig}
        "pacman.conf"
        "makepkg.conf"
        "makepkg_clang32.conf"
        "makepkg_clang64.conf"
        "makepkg_mingw32.conf"
        "makepkg_mingw64.conf"
        "makepkg-clang"
        "makepkg-mingw"
        "0000-pacman-msysize.patch"
        "0001-more-debugging-info.patch"
        "0002-Add-util-msys2.-c-h-and-rebase-db-msys2.-c.patch"
        "0003-use-busybox-for-msys2-post-installs.patch"
        "0004-Link-pacman-with-static-libraries.patch"
        "0006-makepkg-avoid-creating-.tar-files-with-extended-attr.patch"
        "0007-exe-interp-ignore-file-conflict.patch"
        "0008-answer-yes-by-default.patch"
        "0009-msys-use-pipe-instead-socket.patch"
        "0010-filelist-strcasecmp.patch"
        "0011-update-ja-po-for-msys2.patch"
        "0012-workaround-bsdtar-AD-account.patch"
        "0013-use-libintl.patch"
        "0014-fix-asciidoc-argparse.patch"
        "0015-excise-ldconfig.patch"
        "0016-excise-fakeroot.patch"
        "0017-excise-sudo.patch"
        "0018-use-msys-tools.patch"
        Doxyfile.in)
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
sha256sums=('bb201a9f2fb53c28d011f661d50028efce6eef2c1d2a36728bdd0130189349a0'
            'SKIP'
            '7849538ac2b89f4e9881ad5db78a2aa37a2ddb936f87a0ede1c2322e515918fe'
            '822af13248f04690377cd193370f33ac2f00a41234ead9cf3b7e5d5aba8bd0c5'
            '4469ac36c628d123c46feacc34db7ab763a7bfcfb860461846200c68a7d62e2e'
            '13b20f6833df76a34ca38376469e275f93552ef9063d9ca06fc3c9ef5e87aa0b'
            '0e484f00a427f030c95f7ebcab3e0de1e637e55d01531438de9165d1f520cf81'
            '560b03d9a2b7b969bd90dfcc65c6fb87a25704241c69e8a4d7c8ba7b89e7ef0d'
            'b50166ba89277459dcf4c18603e57b387b931e5252068fefcb3d2579ebe2dfa4'
            '501c38b95fcb6938c79a4cff11913fa257d1751d1f6ea6c482ce95999c3fd3b3'
            'cf6d18d4ba5cfa78837dae2a949c794c78d67a7bd321f49b02d32b6ef9d955cf'
            '24ea2c8dca37847e04894ebfd05d1cf5df49dc0c8089f5581c99caa19b77a7ef'
            '870b197b7d6379a9c1ebb5c449c902b21d75ec21e966a2e54af82501465180f7'
            '23132552a388b238acf8bf650b5c2aa08cf3de63c647e84ad551807c4edfeb1e'
            '65906109a7b71a9b836566ea888532b41f4ba6ea5996f1f3303c5789e03960cb'
            'e2731c4007a7c78848679e2203c35d1e56d3ef15a7586aeac9cc9e6e97824cfb'
            '52343bbf99da6408950e34dd84319e34724040f866c497c25e36b4ee54030e82'
            'e4f6e17af19e17e745a9f1c6b8402f5896229062c82167cb61f8e7d29eda716c'
            '9e8fe5ee78192b0407e80ad2e52cb27569c35974b6c26e465e3d55e19c03d108'
            '93be4523fb8c3dd6b56eddfe0b09e666725a62eb43392fee336ba1a328f9ffdd'
            '558919a5e4632512fa42690bd5ea96e72766213ae6943408f7010daa486de929'
            'c242202f3526d5eaa21d321a1d0fa472697c50e8288175ebe93212356c78d1f1'
            '7cec4688b83df9f8ab56f5ab5e162383fe8928ff88b76d753a2cea935f30ec93'
            'b84310fd8fb9a98258c55ed9628226b14f55d6e42304df4d2a5bd8d8445c03b3'
            '72195958662ebe3da2c4df5293caa130c19caad8042b6169d8aa3080287cfe42'
            'd6b6accddc890aff38b5ded3300e9dde35e1d7ed3f767e1655772e2cc7871739'
            '8346a5799be41bd3524fa6fdc57b6175d9d6a00b366f99bd5cd7fa3d43d0ae98'
            '7f60108a372718cfec5d883167a33983be7c5df33fc48bfc21f664449ac7a0a4'
            '8cc0a7b1a3390a8990cef242ac4a84d4eb03f367bc2a01c883908ff7c8d59427')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/0000-pacman-msysize.patch
  patch -p1 -i ${srcdir}/0001-more-debugging-info.patch
  # patch -p1 -i "${srcdir}"/0002-Add-util-msys2.-c-h-and-rebase-db-msys2.-c.patch
  # patch -p1 -i "${srcdir}"/0003-use-busybox-for-msys2-post-installs.patch
  patch -p1 -i ${srcdir}/0004-Link-pacman-with-static-libraries.patch
  patch -p1 -i ${srcdir}/0006-makepkg-avoid-creating-.tar-files-with-extended-attr.patch
  patch -p1 -i ${srcdir}/0007-exe-interp-ignore-file-conflict.patch
  patch -p1 -i ${srcdir}/0008-answer-yes-by-default.patch
  patch -p1 -i ${srcdir}/0009-msys-use-pipe-instead-socket.patch
  patch -p1 -i ${srcdir}/0010-filelist-strcasecmp.patch
  patch -p1 -i ${srcdir}/0011-update-ja-po-for-msys2.patch
  patch -p1 -i ${srcdir}/0012-workaround-bsdtar-AD-account.patch
  patch -p1 -i ${srcdir}/0013-use-libintl.patch
  patch -p1 -i ${srcdir}/0014-fix-asciidoc-argparse.patch
  patch -p1 -i ${srcdir}/0015-excise-ldconfig.patch
  patch -p1 -i ${srcdir}/0016-excise-fakeroot.patch
  patch -p1 -i ${srcdir}/0017-excise-sudo.patch
  patch -p1 -i ${srcdir}/0018-use-msys-tools.patch

  cp ${srcdir}/Doxyfile.in doc/
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  LDFLAGS+=" -static-libgcc" \
  PKG_CONFIG="/usr/bin/pkg-config --static" \
  meson build-${CARCH} \
    --buildtype=plain \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --default-library=static \
    -Dbuildstatic=true \
    -Ddoc=enabled \
    -Ddoxygen=enabled \
    -Dgpgme=enabled \
    -Dcurl=enabled \
    -Duse-git-version=false \
    -Dpkg-ext=.pkg.tar.xz \
    -Dscriptlet-shell=/usr/bin/bash
  ninja -v -C build-${CARCH}
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ninja -C build-${CARCH} test
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  DESTDIR=${pkgdir} ninja -C build-${CARCH} install

  # install Arch specific stuff
  install -dm755 ${pkgdir}/etc
  install -m644 ${srcdir}/pacman.conf ${pkgdir}/etc/pacman.conf

  case "${CARCH}" in
  i686)
    mycarch="i686"
    mychost="i686-pc-msys"
    myflags="-march=i686"
  ;;
  x86_64)
    mycarch="x86_64"
    mychost="x86_64-pc-msys"
    myflags="-march=x86-64"
  ;;
  esac

  install -m644 ${srcdir}/makepkg.conf ${pkgdir}/etc/
  install -m644 ${srcdir}/makepkg_clang32.conf ${pkgdir}/etc/
  install -m644 ${srcdir}/makepkg_clang64.conf ${pkgdir}/etc/
  install -m644 ${srcdir}/makepkg_mingw32.conf ${pkgdir}/etc/
  install -m644 ${srcdir}/makepkg_mingw64.conf ${pkgdir}/etc/
  install -m755 ${srcdir}/makepkg-clang ${pkgdir}/usr/bin/
  install -m755 ${srcdir}/makepkg-mingw ${pkgdir}/usr/bin/

  # set things correctly in the default conf file
  sed -i ${pkgdir}/etc/makepkg.conf \
    -e "s|@CARCH[@]|${mycarch}|g" \
    -e "s|@CHOST[@]|${mychost}|g" \
    -e "s|@CARCHFLAGS[@]|${myflags}|g"
}

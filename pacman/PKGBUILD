# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=pacman
_base_ver=4.2.0
pkgver=4.2.0.5981.0afb3f9
pkgrel=1
pkgdesc="A library-based package manager with dependency support (MSYS2 port)"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
depends=('bash>=4.2.045'
         #'dash'
         #'busybox'
         'gettext'
         'gnupg'
         'msys2-runtime'
         #'libarchive'
         'curl'
         #'libgpgme'
         'pacman-mirrors'
         'msys2-keyring'
         'which'
         'bzip2'
         'xz')
optdepends=('diffutils' 'vim')
checkdepends=('python2')
makedepends=('asciidoc'
             'autoconf'
             'automake-wrapper'
             'doxygen'
             'libtool'
             'git'
             'gettext-devel'
             'libarchive-devel'
             'libcurl-devel'
             'libgpgme-devel')
provides=('pacman-contrib')
conflicts=('pacman-contrib')
replaces=('pacman-contrib')
# options=('!strip' 'debug')
backup=("etc/pacman.conf"
        "etc/makepkg.conf"
        "etc/makepkg_mingw32.conf"
        "etc/makepkg_mingw64.conf")
source=("$pkgname"::'git://github.com/Alexpux/MSYS2-pacman.git'
        "pacman.conf.i686"
        "pacman.conf.x86_64"
        "makepkg.conf"
        "makepkg_mingw32.conf"
        "makepkg_mingw64.conf"
        "makepkg-mingw"
        "0001-more-debugging-info.patch"
        "0002-Add-util-msys2.-c-h-and-rebase-db-msys2.-c.patch"
        "0003-use-busybox-for-msys2-post-installs.patch"
        "0004-Link-pacman-with-static-libraries.patch")
md5sums=('SKIP'
         '8fa93febeebaeb62dfa7b63d2846ce52'
         '8fa93febeebaeb62dfa7b63d2846ce52'
         '26a559467194b3f436829d2573e7fbdb'
         'f540a6f024f37586c011b6632c8051e8'
         '73dec0eea10f743ba9c2ae73e2660e18'
         '6d09ad2af496334e3f9f546c1e586676'
         '3478ac5ab27b8cdb289d1ac3792b16ec'
         'feb2bcc62db05f7bc6ac97d43dd0643a'
         '9ee3ca289031ca9c92db2805b75c1944'
         '99f8ee6e6df141c9fa98f186ba8fa79c')

pkgver() {
  cd "$srcdir/$pkgname"
  #printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  printf "%s.%s.%s" "${_base_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $srcdir/$pkgname
  git am "$srcdir"/0001-more-debugging-info.patch
  #git am "$srcdir"/0002-Add-util-msys2.-c-h-and-rebase-db-msys2.-c.patch
  #git am "$srcdir"/0003-use-busybox-for-msys2-post-installs.patch
  git am "$srcdir"/0004-Link-pacman-with-static-libraries.patch

  autoreconf -fi
}

build() {
  cd $srcdir/$pkgname
  ./autogen.sh

  export PKG_CONFIG="/usr/bin/pkg-config --static"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-static \
    --disable-shared \
    --enable-doc \
    --enable-doxygen \
    --enable-git-version \
    --with-scriptlet-shell=/usr/bin/bash \
    --with-pkg-ext=.pkg.tar.xz
  make -j1
  make -C contrib
}

check() {
  make -C "$pkgname" check
}

package() {
  cd $srcdir/$pkgname
  make -j1 DESTDIR=$pkgdir install
  make -j1 DESTDIR=$pkgdir -C contrib install

  # install Arch specific stuff
  install -dm755 $pkgdir/etc
  install -m644 $srcdir/pacman.conf.$CARCH $pkgdir/etc/pacman.conf

  case "$CARCH" in
  i686)
    mycarch="i686"
    mychost="i686-pc-msys"
    myflags="-march=i686"
  ;;
  x86_64)
    mycarch="x86_64"
    mychost="x86_64-pc-msys"
    myflags="-march=x86-64"
  ;;
  esac

  install -m644 $srcdir/makepkg.conf $pkgdir/etc/
  install -m644 $srcdir/makepkg_mingw32.conf $pkgdir/etc/
  install -m644 $srcdir/makepkg_mingw64.conf $pkgdir/etc/
  install -m755 $srcdir/makepkg-mingw $pkgdir/usr/bin/

  # set things correctly in the default conf file
  sed -i $pkgdir/etc/makepkg.conf \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"

  # put bash_completion in the right location
  install -dm755 ${pkgdir}/usr/share/bash-completion/completions
  mv ${pkgdir}/etc/bash_completion.d/pacman \
    ${pkgdir}/usr/share/bash-completion/completions
  rmdir ${pkgdir}/etc/bash_completion.d

  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
  done
}

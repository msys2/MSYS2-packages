# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=pacman
pkgver=6.0.1
pkgrel=28
pkgdesc="A library-based package manager with dependency support (MSYS2 port)"
arch=('i686' 'x86_64')
url="https://www.archlinux.org/pacman/"
license=('GPL')
depends=('bash>=4.2.045'
         'gettext'
         'gnupg'
         'curl'
         'pacman-mirrors'
         'msys2-keyring'
         'which'
         'bzip2'
         'xz'
         'zstd')
checkdepends=('python')
makedepends=('asciidoc'
             'doxygen'
             'git'
             'gcc'
             'meson'
             'ninja'
             'gettext-devel'
             'heimdal-devel'
             'libarchive-devel'
             'libcurl-devel'
             'libgpgme-devel'
             'libsqlite-devel'
             'libunistring-devel')
backup=("etc/pacman.conf"
        "etc/makepkg.conf"
        "etc/makepkg_mingw.conf")
_commit="0a6fecd07271a54d9009ea7204c0e6288a44212b"
source=(pacman-${pkgver}::git+https://gitlab.archlinux.org/pacman/pacman.git#commit=${_commit}
        "pacman.conf"
        "makepkg.conf"
        "makepkg_mingw.conf"
        "makepkg-mingw"
        0001-Msysize.patch
        0002-More-debugging-info.patch
        0003-Core-update.patch
        0004-Remove-ldconfig.patch
        0005-Change-the-epoch-separator.patch
        0006-makepkg-avoid-creating-.tar-files-with-extended-attr.patch
        0007-pacman-libalpm-ignore-file-conflicts-for-foo.exe-foo.patch
        0008-Change-default-answer-of-all-queries-to-yes.patch
        0009-Use-pipe-instead-of-socket.patch
        0010-pacman-make-file-list-comparisons-between-packages-c.patch
        0011-Translate-MSYS2-specific-messages-into-Japanese.patch
        0012-Workaround-compressing-packages-with-bsdtar-under-Ac.patch
        0013-Fix-asciidoc-argparse.patch
        0014-Remove-fakeroot.patch
        0015-Remove-sudo.patch
        0016-Use-msys-tools.patch
        0017-Export-CC-and-CXX-variables-explicitly.patch
        0018-pacman.c-handle-cr-on-stdin-as-well.patch
        0019-Add-a-CI-job.patch
        0020-Strip-static-libraries-as-well.patch
        0021-Fix-building-staticlibs-with-LTO.patch
        0022-fixup-Export-CC-and-CXX-variables-explicitly.patch
        0023-Make-lint_pkgbuild-opt-in-via-MAKEPKG_LINT_PKGBUILD-.patch
        0024-libmakepkg-fix-compatibility-with-bash-5.2-globskipd.patch)
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
sha256sums=('SKIP'
            '53c0c2d42bc10f265aa41bc412a6ebc2d98177d9356b0fa9a2a130caec46ac2d'
            'c12da01ede663a4924d0817a0d1bd6082b1380383cfb74cc1cea08f9d73e4902'
            '8cb6b244d39107afc6cff74d919708ffc58b903c42f82d050d1d49bbf31208ab'
            '98198e1f0f252eae0560d271bee4b9149e127399dd0d3fd5d8d24579d9e0550f'
            '5951434296d1ea551941db408bac367e4b014add1b77ab5eb5d249deb81f777b'
            '1975fb6d1a0d4cc3c54caefb80b719b471b8cf797d4850a082a8d03aa2ba6278'
            '0cfc5332c3033321ff840b2399d80087416201a3100ea4ff868d3fdb29e35420'
            '97818a36b5a687626c70b782beda024761511c9c95b3ab829acecae1835107eb'
            '4b35438da5967cb4556e08509f35f1fc30b4b63d808b583f28ce2ac6caf15c57'
            '4dc0d8fee098933e92ae0b1fa4ea79cf755665929d4491ca6aa19b8d72475d8e'
            '725a128e9182253b886323ab4cf4557e09a22a3b7e7df6864709c2684b576990'
            '3c3bd339c540e5a56bb5109c7ebadfe95f59f8d2c81082655ca929788db62320'
            '160d132fa9f1b92f0e47bc5b0144b53c7a4c8b3a95b0d72906ec37e2dd638272'
            'c0daf020a2cfbaeedf1a97ebfc65a8ee6299fb44867b3ee42fd60fe66199ec57'
            'd0921fba5290fa1a7ffeff541b5c24a231c343a8b52c7bf10010598527265d47'
            '4a6efe93984db6148a47d7d4a2256a2ce2bd64822c9f3e6a6d29a8c035107ef8'
            '95d53e8f91060676b5b2aaae3e20ec0da76412997de6b8cda60e2cdbf4118757'
            '67ba3c03714bece0b1a5df395c29e8dfe7a1b8dee96c3e3272b08dabd7b362f5'
            '9073851459696a62a494f91a2421fa87f8e6554a953962db16cb87de9204c66e'
            'a9b4897445e94e30ca6a1f3e47b061983e14505d1e8fe8d85944310e3f316731'
            '7b86f89d9619258f4dfbdcf2b431dbc96a2a84628dc8ebbbe0356dec31b8f67f'
            '4caa52fa3f806ca3ad0b0f3b3f497a3fd3910b2c1224ea0a39b8a21df8740f95'
            '2480fcee75268d2f952c05e3fb46c0b7e9e9f533e7bc5a5bb53ec7536293e924'
            '534c0a48e18e6fb935b19bbea64e44f531d9fa49e9634c3717810b76061ba498'
            '4f8db01c932f592479dc0019eeb494bee8afc4a46e068de372df43de6e63e65c'
            '2debaf890678f07503dca1368869ef975ab53099b682232661e202b66d55ccd1'
            '335f3d23cdac35a203044e594053a84f9fbad3fb6ae3c6977aab9b06cf34a3e3'
            '0da1d2c6670ce1475af6ac81005285d7854f48b03c0a01e1ee6c96ad76f12b93')

apply_git_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fixup symlinks
  git config core.symlinks true
  git reset HEAD --hard

 apply_git_with_msg 0001-Msysize.patch \
  0002-More-debugging-info.patch \
  0003-Core-update.patch \
  0004-Remove-ldconfig.patch \
  0005-Change-the-epoch-separator.patch \
  0006-makepkg-avoid-creating-.tar-files-with-extended-attr.patch \
  0007-pacman-libalpm-ignore-file-conflicts-for-foo.exe-foo.patch \
  0008-Change-default-answer-of-all-queries-to-yes.patch \
  0009-Use-pipe-instead-of-socket.patch \
  0010-pacman-make-file-list-comparisons-between-packages-c.patch \
  0011-Translate-MSYS2-specific-messages-into-Japanese.patch \
  0012-Workaround-compressing-packages-with-bsdtar-under-Ac.patch \
  0013-Fix-asciidoc-argparse.patch \
  0014-Remove-fakeroot.patch \
  0015-Remove-sudo.patch \
  0016-Use-msys-tools.patch \
  0017-Export-CC-and-CXX-variables-explicitly.patch \
  0018-pacman.c-handle-cr-on-stdin-as-well.patch \
  0019-Add-a-CI-job.patch \
  0020-Strip-static-libraries-as-well.patch \
  0021-Fix-building-staticlibs-with-LTO.patch \
  0022-fixup-Export-CC-and-CXX-variables-explicitly.patch \
  0023-Make-lint_pkgbuild-opt-in-via-MAKEPKG_LINT_PKGBUILD-.patch \
  0024-libmakepkg-fix-compatibility-with-bash-5.2-globskipd.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  LDFLAGS+=" -static-libgcc" \
  meson build-${CARCH} \
    --buildtype=plain \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --default-library=static \
    -Dbuildstatic=true \
    -Ddoc=enabled \
    -Ddoxygen=enabled \
    -Dgpgme=enabled \
    -Dcurl=enabled \
    -Duse-git-version=false \
    -Dpkg-ext=.pkg.tar.xz \
    -Dscriptlet-shell=/usr/bin/bash

  meson compile -C build-${CARCH}
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  meson test -C build-${CARCH}
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  DESTDIR=${pkgdir} meson install -C build-${CARCH}

  # install Arch specific stuff
  install -dm755 ${pkgdir}/etc
  install -m644 ${srcdir}/pacman.conf ${pkgdir}/etc/pacman.conf

  # Fixup .pc file
  sed -i "s|/usr/.*libintl.a|-lintl|g" ${pkgdir}/usr/lib/pkgconfig/libalpm.pc

  case "${CARCH}" in
  i686)
    mycarch="i686"
    mychost="i686-pc-msys"
    myflags="-march=i686"
  ;;
  x86_64)
    mycarch="x86_64"
    mychost="x86_64-pc-msys"
    myflags="-march=x86-64"
  ;;
  esac

  install -m644 ${srcdir}/makepkg.conf ${pkgdir}/etc/
  install -m644 ${srcdir}/makepkg_mingw.conf ${pkgdir}/etc/
  install -m755 ${srcdir}/makepkg-mingw ${pkgdir}/usr/bin/

  # set things correctly in the default conf file
  sed -i ${pkgdir}/etc/makepkg.conf \
    -e "s|@CARCH[@]|${mycarch}|g" \
    -e "s|@CHOST[@]|${mychost}|g" \
    -e "s|@CARCHFLAGS[@]|${myflags}|g"
}

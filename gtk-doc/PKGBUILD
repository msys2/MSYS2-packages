# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gtk-doc
pkgver=1.35.1
pkgrel=1
pkgdesc="Documentation tool for public library API"
arch=('i686' 'x86_64')
url="https://gitlab.gnome.org/GNOME/gtk-doc"
msys2_repository_url='https://gitlab.gnome.org/GNOME/gtk-doc'
license=("spdx:GFDL-1.1-or-later AND GPL-2.0-or-later")
depends=("docbook-xsl"
         "docbook-xml"
         "glib2"
         "python"
         "python-pygments"
         "python-lxml")
makedepends=("pkgconf"
             "glib2-devel"
             "itstool"
             "meson"
             "ninja"
             "gcc"
             "yelp-tools")
source=(https://gitlab.gnome.org/GNOME/gtk-doc/-/archive/${pkgver}/gtk-doc-${pkgver}.tar.gz)
sha256sums=('f40f6e79d555c00bc0aa9f5cc8e7bee5545758965bc2f6f23812a442d6cd0996')

build() {
  meson setup "build-${CHOST}" "${srcdir}/${pkgname}-${pkgver}" \
    --prefix=/usr \
    --buildtype=plain \
    --auto-features=enabled \
    --wrap-mode=nodownload

  meson compile -C "build-${CHOST}"
}

package() {
  DESTDIR="${pkgdir}" meson install -C "build-${CHOST}"
}

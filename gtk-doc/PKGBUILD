# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gtk-doc
pkgver=1.35.0
pkgrel=1
pkgdesc="Documentation tool for public library API"
arch=('i686' 'x86_64')
url="https://gitlab.gnome.org/GNOME/gtk-doc"
license=("GPL" "FDL")
options=(staticlibs strip)
depends=("docbook-xsl"
         "glib2"
         "python"
         "python-pygments"
         "python-lxml"
         "vim"
         "yelp-tools"
        )
makedepends=("pkgconf"
             "glib2-devel"
             'itstool'
             'autotools'
             'gcc'
            )
source=(https://gitlab.gnome.org/GNOME/gtk-doc/-/archive/${pkgver}/gtk-doc-${pkgver}.tar.gz)
sha256sums=('4049cdfe80dca1db4e44dc198d43ccb7e8745a22fe77feb329900285c02e1994')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  libtoolize --force --copy
  autoreconf -fi
}

build() {
  mkdir -p "${srcdir}/build-${CHOST}"
  cd "${srcdir}/build-${CHOST}"
  export PYTHON=/usr/bin/python
  ../${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --host=${CHOST} \
    --enable-shared \
    --enable-static \
    --with-xml-catalog=/etc/xml/catalog
  make
}

check() {
  cd "${srcdir}/build-${CHOST}"
  make check || true
}

package() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR="${pkgdir}" install
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gtk-doc
pkgver=1.34.0
pkgrel=2
pkgdesc="Documentation tool for public library API"
arch=('i686' 'x86_64')
url="https://gitlab.gnome.org/GNOME/gtk-doc"
license=("GPL" "FDL")
options=(staticlibs strip)
depends=("docbook-xsl"
         "glib2"
         "libxml2-python"
         "python"
         "python-pygments"
         "vim"
         "yelp-tools"
        )
makedepends=("pkgconf"
             "glib2-devel"
             'itstool'
             'autotools'
             'gcc'
            )
source=(https://download.gnome.org/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('b20b72b32a80bc18c7f975c9d4c16460c2276566a0b50f87d6852dff3aa7861c')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  libtoolize --force --copy
  autoreconf -fi
}

build() {
  mkdir -p "${srcdir}/build-${CHOST}"
  cd "${srcdir}/build-${CHOST}"
  export PYTHON=/usr/bin/python
  ../${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --host=${CHOST} \
    --enable-shared \
    --enable-static \
    --with-xml-catalog=/etc/xml/catalog
  make
}

check() {
  cd "${srcdir}/build-${CHOST}"
  make check || true
}

package() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR="${pkgdir}" install
}

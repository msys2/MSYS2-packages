# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=markupsafe
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=2.1.1
pkgrel=2
pkgdesc="Implements a XML/HTML/XHTML Markup safe string for Python"
arch=('i686' 'x86_64')
license=('spdx:BSD-3-Clause')
url="https://pypi.python.org/pypi/MarkupSafe"
depends=("python")
makedepends=(
  "gcc"
  "python-setuptools"
  "python-wheel"
  "python-build"
  "python-installer"
)
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
source=("https://pypi.python.org/packages/source/M/MarkupSafe/MarkupSafe-${pkgver}.tar.gz")
sha256sums=('7f91197cc9e48f989d12e4e6fbc46495c446636dfc81b9ccf50bb0ec74b91d4b')

build() {
  cd "${srcdir}/MarkupSafe-${pkgver}"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/MarkupSafe-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.rst "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"
}

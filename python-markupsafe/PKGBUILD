# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=markupsafe
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=3.0.3
pkgrel=1
pkgdesc="Implements a XML/HTML/XHTML Markup safe string for Python"
arch=('i686' 'x86_64')
license=('spdx:BSD-3-Clause')
url="https://pypi.python.org/pypi/MarkupSafe"
msys2_references=(
 'archlinux: python-markupsafe'
 'gentoo: dev-python/markupsafe'
  "purl: pkg:pypi/markupsafe"
)
depends=("python")
makedepends=(
  "gcc"
  "python-setuptools"
  "python-build"
  "python-installer"
)
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
source=("https://pypi.python.org/packages/source/m/markupsafe/markupsafe-${pkgver}.tar.gz")
sha256sums=('722695808f4b6457b320fdc131280796bdceb04ab50fe1795cd540799ebe1698')

build() {
  cd "${srcdir}/markupsafe-${pkgver}"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/markupsafe-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"
}

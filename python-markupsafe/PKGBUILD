# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=markupsafe
pkgname=("python3-${pkgbase}" "python2-${pkgbase}")
pkgver=0.23
pkgrel=2
pkgdesc="Implements a XML/HTML/XHTML Markup safe string for Python"
arch=('i686' 'x86_64')
license=('custom')
url="https://pypi.python.org/pypi/MarkupSafe"
makedepends=("python2-setuptools" "python3-setuptools")
source=("https://pypi.python.org/packages/source/M/MarkupSafe/MarkupSafe-${pkgver}.tar.gz")
md5sums=('f5ab3deee4c37cd6a922fb81e730da6e')

build() {
  cd "$srcdir"

  rm -rf python{2,3}-build
  for builddir in python{2,3}-build; do
    cp -r MarkupSafe-$pkgver $builddir
    pushd $builddir
    /usr/bin/${builddir%-build} setup.py build
    popd
  done
}

package_python3-markupsafe() {
  depends=("python3")

  cd "$srcdir/python3-build"
  /usr/bin/python3 setup.py install --prefix=/usr --root="$pkgdir" -O1

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python3-${pkgbase}/LICENSE"
}

package_python2-markupsafe() {
  depends=("python2")

  cd "$srcdir/python2-build"
  /usr/bin/python2 setup.py install --prefix=/usr --root="$pkgdir" -O1

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python2-${pkgbase}/LICENSE"
}

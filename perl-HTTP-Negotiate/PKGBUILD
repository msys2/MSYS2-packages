# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=HTTP-Negotiate
pkgname=perl-${_realname}
pkgver=6.01
pkgrel=2
pkgdesc="choose a variant to serve"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.8.8' 'perl-HTTP-Message')
makedepends=()
checkdepends=()
conflicts=('perl-libwww<6')
url="https://metacpan.org/release/HTTP-Negotiate"
groups=('perl-modules')
source=("https://www.cpan.org/authors/id/G/GA/GAAS/${_realname}-${pkgver}.tar.gz")
sha256sums=('1c729c1ea63100e878405cda7d66f9adfd3ed4f1d6cacaca0ee9152df728e016')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_realname}-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "${srcdir}/${_realname}-${pkgver}"
    make test
  )
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}

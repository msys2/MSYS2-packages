# Maintainer: David Mott <mott.david.j@gmail.com>

pkgname=dialog
pkgver=1.3_20250817
pkgrel=1
pkgdesc="A tool to display dialog boxes from shell scripts"
arch=('i686' 'x86_64')
url="https://invisible-island.net/dialog/"
msys2_changelog_url='https://invisible-island.net/dialog/CHANGES.html'
license=('spdx:LGPL-2.1-or-later')
makedepends=('ncurses-devel' 'autotools' 'gcc' 'gettext-devel')
depends=('ncurses' 'libintl')
source=(https://invisible-mirror.net/archives/$pkgname/$pkgname-${pkgver/_/-}.tgz)
sha256sums=('6c59b4671616041dcd75d0d4a1d8646e8ca6b10a1ae534d3b9368c4c4ba29aa7')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  ./configure \
    --build=${CHOST} \
    --host=${CHOST} \
    --prefix=/usr \
    --enable-nls \
    --with-ncursesw
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  make DESTDIR="$pkgdir" install-full
}

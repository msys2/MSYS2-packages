# Maintainer: David Mott <mott.david.j@gmail.com>

pkgname=dialog
pkgver=1.3_20240619
pkgrel=1
pkgdesc="A tool to display dialog boxes from shell scripts"
arch=('i686' 'x86_64')
url="https://invisible-island.net/dialog/"
license=('spdx:LGPL-2.1-or-later')
makedepends=('ncurses-devel' 'autotools' 'gcc' 'gettext-devel')
depends=('ncurses' 'libintl')
source=(https://invisible-mirror.net/archives/$pkgname/$pkgname-${pkgver/_/-}.tgz)
sha256sums=('5d8c4318963db3fd383525340276e0e05ee3dea9a6686c20779f5433b199547d')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --build=${CYGWIN_CHOST} \
    --host=${CYGWIN_CHOST} \
    --prefix=/usr \
    --enable-nls \
    --with-ncursesw
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  make DESTDIR="$pkgdir" install-full
}

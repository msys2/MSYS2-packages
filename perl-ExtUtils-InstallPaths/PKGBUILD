# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ExtUtils-InstallPaths
pkgname=perl-${_realname}
pkgver=0.015
pkgrel=1
pkgdesc="ExtUtils::InstallPaths - Build.PL install path logic made easy"
arch=('any')
url="https://metacpan.org/dist/ExtUtils-InstallPaths"
msys2_references=(
  "purl: pkg:cpan/LEONT/ExtUtils-InstallPaths"
)
groups=('perl-modules')
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-ExtUtils-Config')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/L/LE/LEONT/${_realname}-${pkgver}.tar.gz")
sha256sums=('7d64eb2dfa87ead010cdf55c8a1bdfde50b7b5852d7cb8cf2304f55bea2eb007')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_realname}-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

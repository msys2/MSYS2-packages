# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libssh2' 'libssh2-devel')
pkgver=1.6.0
pkgrel=1
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts"
url="https://libssh2.org/"
groups=('libraries')
arch=('i686' 'x86_64')
license=('BSD')
makedepends=('openssl-devel' 'zlib-devel')
options=('!libtool')
source=("https://libssh2.org/download/${pkgname}-${pkgver}.tar.gz"{,.asc}
        1.2.7-cygwin.patch
        libssh2-1.4.3-msys2.patch)
md5sums=('00aabd6e714a5f42a4fb82ace20db1dd'
         'SKIP'
         '1e64f6f1034004d67e95167fd63da411'
         '3ade60167d8d6b0a54e6235f5b010385')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p2 -i ${srcdir}/1.2.7-cygwin.patch
  patch -p1 -i ${srcdir}/libssh2-1.4.3-msys2.patch

  autoreconf -ivf
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --build=${CHOST} \
    --enable-shared \
    --enable-static \
    --with-openssl \
    --with-libz \
    --without-libz-prefix \
    --without-libssl-prefix \
    --without-libgcrypt-prefix
  make
  make DESTDIR="${srcdir}/dest" install
}

package_libssh2() {
  pkgdesc="Multi-protocol file transfer library (runtime)"
  depends=('ca-certificates' 'openssl' 'zlib')
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_libssh2-devel() {
  pkgdesc="Libssh2 headers and libraries"
  options=('staticlibs')
  depends=("libssh2=${pkgver}" 'openssl-devel' 'zlib-devel')
  groups=('development')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libssh2' 'libssh2-devel')
pkgver=1.10.0
pkgrel=1
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts"
url="https://libssh2.org/"
groups=('libraries')
arch=('i686' 'x86_64')
license=('BSD')
makedepends=('openssl-devel' 'zlib-devel')
options=('!libtool')
source=("https://libssh2.org/download/${pkgname}-${pkgver}.tar.gz"{,.asc}
        libssh2-1.4.3-msys2.patch)
sha256sums=('2d64e90f3ded394b91d3a2e774ca203a4179f69aebee03003e5a6fa621e41d51'
            'SKIP'
            'bbfe1e132e824ac50aaf9364a82625bb75f7672ec52f041314cc539b4587bc6c')
validpgpkeys=('914C533DF9B2ADA2204F586D78E11C6B279D5C91'
              '27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/libssh2-1.4.3-msys2.patch

  autoreconf -ivf
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --build=${CHOST} \
    --enable-shared \
    --enable-static \
    --with-openssl \
    --with-libz \
    --without-libz-prefix \
    --without-libssl-prefix \
    --without-libgcrypt-prefix
  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "${pkgname}-${pkgver}"
  make check || true
}

package_libssh2() {
  pkgdesc="Multi-protocol file transfer library (runtime)"
  depends=('ca-certificates' 'openssl' 'zlib')
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_libssh2-devel() {
  pkgdesc="Libssh2 headers and libraries"
  options=('staticlibs')
  depends=("libssh2=${pkgver}" 'openssl-devel' 'zlib-devel')
  groups=('development')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}

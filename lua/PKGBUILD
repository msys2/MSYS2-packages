# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgname=lua
pkgver=5.4.7
_majorver=${pkgver%.*}
pkgrel=1
pkgdesc='Powerful lightweight programming language designed for extending applications'
arch=('x86_64')
url='https://www.lua.org/'
makedepends=('make' 'gcc')
license=('spdx:MIT')
source=(https://www.lua.org/ftp/lua-$pkgver.tar.gz)
sha256sums=('9fbf5e28ef86c69858f6d3d34eccc32e911c1a28b4120ff3e84aaa70cfbf1e30')

build() {
  cd "lua-$pkgver"

  make \
    CFLAGS="${CFLAGS}" \
    AR="${CC} -shared -Wl,--out-implib,liblua${_majorver}.dll.a -o" \
    RANLIB=: \
    LUA_A="msys-lua-${_majorver}.dll" \
    linux
}

package() {
  cd "lua-$pkgver"

  make \
    TO_BIN="msys-lua-${_majorver}.dll lua.exe luac.exe" \
    TO_LIB="liblua${_majorver}.dll.a" \
    INSTALL_TOP="$pkgdir"/usr \
    INSTALL_INC="$pkgdir"/usr/include \
    INSTALL_LIB="$pkgdir"/usr/lib \
    INSTALL_MAN="$pkgdir"/usr/share/man/man1 \
    install

  mkdir -p "$pkgdir/usr/lib/pkgconfig"
  make pc \
    INSTALL_TOP=/usr \
    INSTALL_INC=/usr/include \
    INSTALL_LIB=/usr/lib > "$pkgdir"/usr/lib/pkgconfig/lua.pc
}

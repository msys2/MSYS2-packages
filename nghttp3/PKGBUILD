# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

pkgbase=nghttp3
pkgname=("libnghttp3" "libnghttp3-devel")
pkgver=1.3.0
pkgrel=1
pkgdesc="HTTP/3 library written in C"
arch=('x86_64')
url='https://github.com/ngtcp2/nghttp3'
msys2_references=(
  'archlinux: libnghttp3'
)
license=('spdx:MIT')
depends=(gcc-libs)
makedepends=(gcc cmake ninja)
validpgpkeys=('F4F3B91474D1EB29889BD0EF7E8403D5D673C366') # Tatsuhiro Tsujikawa <tatsuhiro.t@gmail.com>
source=("https://github.com/ngtcp2/nghttp3/releases/download/v${pkgver}/nghttp3-${pkgver}.tar.xz"{,.asc})
sha256sums=('450525981d302f23832b18edd1a62cf58019392ca6402408d0eb1a7f3fd92ecf'
            'SKIP')

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  /usr/bin/cmake \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "${_extra_config[@]}" \
    -DENABLE_SHARED_LIB=ON \
    -DENABLE_STATIC_LIB=ON \
    -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
    -DENABLE_LIB_ONLY=ON \
    -DBUILD_TESTING=OFF \
    -B build-${MSYSTEM} \
    -S nghttp3-${pkgver}

  /usr/bin/cmake --build build-${MSYSTEM}
  DESTDIR="${srcdir}/dest" /usr/bin/cmake --install build-${MSYSTEM}
}

package_libnghttp3() {
  pkgdesc+=' (runtime)'
  depends=('gcc-libs' )
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
  install -Dm644 ${srcdir}/nghttp3-${pkgver}/COPYING "${pkgdir}/usr/share/licenses/libnghttp3/COPYING"
}

package_libnghttp3-devel() {
  pkgdesc+=" (devel)"
  depends=("libnghttp3=${pkgver}")

  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}


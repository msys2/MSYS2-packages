_realname=fibers
pkgname=guile-${_realname}
pkgver=1.3.1
pkgrel=2
pkgdesc="Concurrent ML-like concurrency for Guile"
url="https://github.com/wingo/fibers"
msys2_references=(
  "aur: guile-fibers"
)
arch=(i686 x86_64)
license=('GPL-3.0')
depends=('guile')
makedepends=("gcc"
             "autotools"
             "libevent-devel"
             "libguile-devel"
             'git')
source=("git+https://github.com/wingo/${_realname}.git#tag=v${pkgver}")
sha256sums=('SKIP')

prepare() {
  cd "${_realname}"
  autoreconf -vif
}

build() {
  cd "${_realname}"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --disable-rpath
  make
}

package() {
  cd "${_realname}"
  make DESTDIR="${pkgdir}" install
}

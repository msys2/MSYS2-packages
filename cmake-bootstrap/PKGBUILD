# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=cmake
pkgname="${_realname}-bootstrap"
pkgver=4.1.0
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('i686' 'x86_64')
url="https://www.cmake.org/"
msys2_repository_url="https://gitlab.kitware.com/cmake/cmake"
msys2_references=(
  "anitya: 306"
  "cpe: cpe:2.3:a:cmake_project:cmake"
)
license=(spdx:BSD-3-Clause)
makedepends=('gcc'
             'make')
depends=('gcc-libs')
conflicts=('cmake' 'cmake-emacs' 'cmake-vim')
provides=("cmake=$pkgver")
source=(https://www.cmake.org/files/v${pkgver%.*}/${_realname}-${pkgver}.tar.gz)
sha256sums=('81ee8170028865581a8e10eaf055afb620fa4baa0beb6387241241a975033508')

build() {
  [[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
  mkdir -p ${srcdir}/build-${CARCH}
  cd ${srcdir}/build-${CARCH}

  "${srcdir}/${_realname}-${pkgver}/configure" \
    --prefix=/usr \
    --no-system-libs \
    --no-qt-gui \
    --parallel="$(nproc)" \
    --mandir=share/man \
    --docdir="share/doc/${_realname}" \
    --datadir="share/${_realname}" \
    -- -DCMAKE_USE_OPENSSL=OFF

  make
}

package() {
  cd ${srcdir}/build-${CARCH}

  make DESTDIR="${pkgdir}" install

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE.rst \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

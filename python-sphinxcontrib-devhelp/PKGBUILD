# Contributor: Dirk Stolle

_realname=sphinxcontrib-devhelp
pkgname=python-${_realname}
pkgver=2.0.0
pkgrel=1
pkgdesc='Sphinx extension which outputs Devhelp documents'
arch=('any')
url='https://github.com/sphinx-doc/sphinxcontrib-devhelp'
msys2_references=(
  'cygwin: python-sphinxcontrib-devhelp'
  'purl: pkg:pypi/sphinxcontrib-devhelp'
)
license=('spdx:BSD-2-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-flit-core'
  'python-installer'
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname/-/_}-${pkgver}.tar.gz")
sha256sums=('411f5d96d445d1d73bb5d52133377b4248ec79db5c793ce7dbe59e074b4dd1ad')

build() {
  cp -r "${_realname/-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}"

  python -m installer --prefix=${MSYSTEM_PREFIX} --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENCE.rst "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}

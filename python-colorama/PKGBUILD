# Contributor: Dirk Stolle

_realname=colorama
pkgname=python-${_realname}
pkgver=0.4.6
pkgrel=1
pkgdesc='Python API for cross-platform colored terminal text'
arch=('any')
url='https://github.com/tartley/colorama'
msys2_references=(
  'cygwin: python-colorama'
  'purl: pkg:pypi/colorama'
)
license=('spdx:BSD-3-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-hatchling'
  'python-installer'
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('08695f5cb7ed6e0531a20572697297273c47b8cae5a63ffc6d6ed5c201be6e44')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}"

  python -m installer --prefix=${MSYSTEM_PREFIX} --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.txt "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}

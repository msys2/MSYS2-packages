# ArchLinux:
# Maintainer: Alexander Duscheleit <jinks@archlinux.us>
# MSYS2:
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_pyname=colorama
pkgbase="python-${_pyname}"
pkgname=("python-${_pyname}")
pkgver=0.4.3
pkgrel=2
pkgdesc="Python API for cross-platform colored terminal text."
arch=('any')
url="https://pypi.python.org/pypi/colorama"
license=('BSD')
provides=("python3-${_pyname}")
conflicts=("python3-${_pyname}")
replaces=("python3-${_pyname}")
depends=('python')
checkdepends=('python-mock')
source=(colorama-${pkgver}.tar.gz::https://github.com/tartley/colorama/archive/${pkgver}.tar.gz)
sha256sums=('0d93fc83f70c926cd8437b488ea9c8741f9df2ac2d2d681c40783d5ceef98e2b')

build() {
  cd "$srcdir/${_pyname}-${pkgver}"

  /usr/bin/python setup.py build
}

check() {
  cd "$srcdir/${_pyname}-${pkgver}"

  /usr/bin/python -m unittest discover -p *_test.py || warning "test failed"
}

package() {
  cd "$srcdir/${_pyname}-${pkgver}"

  /usr/bin/python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=tmux
_base_ver=3.6
_bugfix=
pkgver=${_base_ver}${_bugfix}
pkgrel=1
pkgdesc='A terminal multiplexer'
url='https://tmux.github.io/'
msys2_repository_url="https://github.com/tmux/tmux"
msys2_references=(
  "anitya: 4980"
  "cpe: cpe:/a:nicholas_marriott:tmux"
  "cpe: cpe:/a:tmux_project:tmux"
)
arch=('i686' 'x86_64')
license=('BSD')
conflict=('tmux-git')
replaces=('tmux-git')
depends=('ncurses' 'libevent')
makedepends=('ncurses-devel' 'libevent-devel' 'autotools' 'gcc')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/tmux/tmux/archive/${_base_ver}${_bugfix/./}.tar.gz)

sha256sums=('bb8e96da3809845c72b53589cd7f4c00d5e3cbbb88d3284e15e5a8c2220d471d')

prepare() {
  cd "${srcdir}/${pkgname}-${_base_ver}${_bugfix/./}"

  ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}-${_base_ver}${_bugfix/./}"

  ./configure \
    --build=${CHOST} \
    --enable-sixel \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    CPPFLAGS="${CPPFLAGS} -I/usr/include/ncursesw -U_XOPEN_SOURCE"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_base_ver}${_bugfix/./}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/licenses/tmux
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/tmux

  #install -d "${pkgdir}"/usr/share/tmux
  #install -m 644 examples/* "${pkgdir}"/usr/share/tmux
  #install -d "${pkgdir}"/usr/share/vim/vimfiles/syntax
  #install -m 644 examples/tmux.vim "${pkgdir}"/usr/share/vim/vimfiles/syntax

  #install -d "${pkgdir}"/usr/share/bash-completion/completions/tmux
  #mv "${pkgdir}"/usr/share/tmux/bash_completion_tmux.sh "${pkgdir}"/usr/share/bash-completion/completions/tmux
}

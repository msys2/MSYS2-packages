# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=XML-Parser
pkgname=perl-${_realname}
pkgver=2.43
pkgrel=1
pkgdesc="Expat-based XML parser module for perl"
arch=('i686' 'x86_64')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
groups=('perl-modules')
depends=('perl' 'libexpat' 'libcrypt')
makedepends=('libexpat-devel' 'libcrypt-devel')
replaces=('perlxml')
provides=("perlxml=${pkgver}")
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/T/TO/TODDR/${_realname}-${pkgver}.tar.gz"
        'XML-Parser-2.41.patch')
md5sums=('2c9ca46832d8e7578bcda99eba3a47f1'
         'c99cf18eca562cf584cb2c770304782e')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i ${srcdir}/XML-Parser-2.41.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"

  # remove perllocal.pod and .packlist.
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete

  # fix permissions
  find ${pkgdir}/usr -type f -name *.dll | xargs chmod 0755
}

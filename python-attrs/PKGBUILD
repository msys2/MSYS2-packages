# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=attrs
pkgbase=python-attrs
pkgname=("python-${_realname}")
pkgver=22.2.0
pkgrel=2
pkgdesc="Attributes without boilerplate."
arch=('any')
license=('spdx:MIT')
url="https://attrs.readthedocs.org/"
depends=('python')
makedepends=(
  'python-setuptools'
  'python-wheel'
  'python-build'
  'python-installer'
)
provides=("python3-${_realname}")
replaces=("python3-${_realname}")
conflicts=("python3-${_realname}")
source=("https://pypi.io/packages/source/a/attrs/attrs-$pkgver.tar.gz")
sha512sums=('a7707fb11e21cddd2b25c94c9859dc8306745f0256237493a4ad818ffaf005d1c1e84d55d07fce14eaea18fde4994363227286df2751523e1fe4ef6623562a20')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

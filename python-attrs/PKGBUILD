# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=attrs
pkgbase=python-attrs
pkgname=("python-${_realname}")
pkgver=25.4.0
pkgrel=1
pkgdesc="Attributes without boilerplate."
arch=('any')
license=('spdx:MIT')
url='https://www.attrs.org/'
msys2_repository_url='https://github.com/python-attrs/attrs'
msys2_references=(
  'archlinux: python-attrs'
  'gentoo: dev-python/attrs'
  "purl: pkg:pypi/attrs"
)
depends=('python')
makedepends=(
  'python-hatchling'
  'python-hatch-vcs'
  'python-hatch-fancy-pypi-readme'
  'python-build'
  'python-installer'
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha512sums=('945ea1766adf7e8c8cc8b8f8c63c3dca652553522cede6a09d479f9919a41d43539819da2fe6fdc0e0b42822e353c7cb04b79fb9cecfd54bde5e301574a82341')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=libatomic_ops
pkgname=("${pkgbase}" "${pkgbase}-devel")
pkgver=7.10.0
pkgrel=1
pkgdesc="Provides semi-portable access to hardware provided atomic memory operations"
arch=('any')
url="https://github.com/bdwgc/libatomic_ops"
msys2_references=(
  "anitya: 1561"
  "cpe: cpe:2.3:a:libatomic_ops_project:libatomic_ops"
)
makedepends=('autotools' 'gcc')
license=('spdx:GPL-2.0-or-later AND MIT')
source=("https://github.com/bdwgc/${pkgbase}/releases/download/v${pkgver}/${pkgbase}-${pkgver}.tar.gz")
sha256sums=('0db3ebff755db170f65e74a64ec4511812e9ee3185c232eeffeacd274190dfb0')

prepare(){
  cd ${srcdir}/${pkgbase}-${pkgver}

  autoreconf -fiv
}

build() {
  [[ -d ${srcdir}/build-${CHOST} ]] && rm -rf ${srcdir}/build-${CHOST}
  mkdir -p ${srcdir}/build-${CHOST} && cd ${srcdir}/build-${CHOST}

  export MSYSTEM=CYGWIN
  ../${pkgbase}-${pkgver}/configure \
    --build=${CHOST} \
    --host=${CHOST} \
    --target=${CHOST} \
    --prefix=/usr \
    --enable-shared \
    --enable-static
  make
}

check() {
  cd ${srcdir}/build-${CHOST}
  PATH="${srcdir}/build-${CHOST}/src/.libs:$PATH" make check
}

package_libatomic_ops() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/{include,lib,share}
  install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_libatomic_ops-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("libatomic_ops=${pkgver}")

  cd "${srcdir}/build-${CHOST}"

  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/bin
}

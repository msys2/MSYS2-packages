# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=libatomic_ops
pkgname=("${pkgbase}" "${pkgbase}-devel")
pkgver=7.6.14
pkgrel=2
pkgdesc="Provides semi-portable access to hardware provided atomic memory operations"
arch=('any')
url="https://github.com/ivmai/libatomic_ops"
makedepends=('autotools' 'gcc')
license=('spdx:GPL-2.0-or-later AND MIT')
source=("https://github.com/ivmai/${pkgbase}/releases/download/v${pkgver}/${pkgbase}-${pkgver}.tar.gz")
sha256sums=('390f244d424714735b7050d056567615b3b8f29008a663c262fb548f1802d292')

prepare(){
  cd ${srcdir}/${pkgbase}-${pkgver}

  autoreconf -fiv
}

build() {
  [[ -d ${srcdir}/build-${CHOST} ]] && rm -rf ${srcdir}/build-${CHOST}
  mkdir -p ${srcdir}/build-${CHOST} && cd ${srcdir}/build-${CHOST}

  export MSYSTEM=CYGWIN
  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ../${pkgbase}-${pkgver}/configure \
    --build=${CYGWIN_CHOST} \
    --host=${CYGWIN_CHOST} \
    --target=${CYGWIN_CHOST} \
    --prefix=/usr \
    --enable-shared \
    --enable-static
  make
}

check() {
  cd ${srcdir}/build-${CHOST}
  PATH="${srcdir}/build-${CHOST}/src/.libs:$PATH" make check
}

package_libatomic_ops() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/{include,lib,share}
  install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/doc/LICENSING.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_libatomic_ops-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("libatomic_ops=${pkgver}")

  cd "${srcdir}/build-${CHOST}"

  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/bin
}

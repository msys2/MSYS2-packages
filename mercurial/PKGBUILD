# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=mercurial
pkgver=5.9.1
pkgrel=1
pkgdesc="A scalable distributed SCM tool"
arch=('i686' 'x86_64')
url="https://www.mercurial-scm.org/"
license=('GPL')
groups=('VCS')
depends=('python')
makedepends=('python-devel')
#optdepends=('tk: for the hgk GUI')
backup=('etc/mercurial/hgrc')
source=("https://mercurial-scm.org/release/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'mercurial.profile'
        'mercurial-4.3.1-msys2.patch')
sha256sums=('32eff9433c62d781dc93e5a6103327264d897ad2bbce3ecb5d2d78a93cf49f27'
            'SKIP'
            '87427151713e689cd87dc50d50c048e0e58285815e4eb61962b50583532cbde5'
            '1a35962837bb1eb8893e4ab8aa3a49bb18023b632d4c3a2fa903ebfdd7e598ce')
validpgpkeys=(2BCCE14F5C6725AA2EA8AEB7B9C9DC824AA5BDD5
              3A8155163D0E20A530FCB78647A67FFAA346AACE
              EB851395B4223EE2F7BA0B28DA54740BF08732BA
              818D87CD1AC180C394C86E633A33DE460D9EC39F) # Pulkit Goyal <7895pulkit@gmail.com>

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/mercurial-4.3.1-msys2.patch
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  python3 setup.py install --root="${pkgdir}/" --prefix=/usr --optimize=1

  install -d ${pkgdir}/usr/share/man/{man1,man5}
  install -m644 doc/hg.1 "${pkgdir}/usr/share/man/man1"
  install -m644 doc/{hgrc.5,hgignore.5} "${pkgdir}/usr/share/man/man5"
  install -m755 contrib/hgk "${pkgdir}/usr/bin"
  #install -m644 -D contrib/zsh_completion "${pkgdir}/usr/share/zsh/site-functions/_hg"
  install -m644 -D contrib/bash_completion "${pkgdir}/usr/share/bash-completion/completions/hg"
  install -d "${pkgdir}/usr/share/emacs/site-lisp"
  install -m644 contrib/{mq.el,mercurial.el} "${pkgdir}/usr/share/emacs/site-lisp"

  vimpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm644 contrib/vim/HGAnnotate.vim "${vimpath}/syntax/HGAnnotate.vim"

  # set some variables
  install -m755 -d ${pkgdir}/etc/profile.d
  install -m755 ${srcdir}/mercurial.profile "${pkgdir}/etc/profile.d/mercurial.sh"

  # install configuration file
  install -m755 -d ${pkgdir}/etc/mercurial
  #install -m644 contrib/sample.hgrc "${pkgdir}/etc/mercurial/hgrc"
  
  # FS#38825 - Add certs config to package
  echo -e "\n[web]\ncacerts = /usr/ssl/certs/ca-bundle.crt\n" >> "${pkgdir}/etc/mercurial/hgrc"
}

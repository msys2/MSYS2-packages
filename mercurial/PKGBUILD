# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=mercurial
pkgver=4.9
pkgrel=1
pkgdesc="A scalable distributed SCM tool"
arch=('i686' 'x86_64')
url="https://www.mercurial-scm.org/"
license=('GPL')
groups=('VCS')
depends=('python2')
makedepends=('libcrypt-devel')
#optdepends=('tk: for the hgk GUI')
backup=('etc/mercurial/hgrc')
source=("https://mercurial-scm.org/release/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'mercurial.profile'
        'mercurial-4.3.1-msys2.patch')
sha256sums=('0f600c5c7e44d4318bedc1754a70b920f7ecd278e4089b0f6ac96f460c012f06'
            'SKIP'
            '87427151713e689cd87dc50d50c048e0e58285815e4eb61962b50583532cbde5'
            '656f76a407860dce525136e798281c8032c6b05783bc72cf27cf72d668b75fb9')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/mercurial-4.3.1-msys2.patch
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  python2 setup.py install --root="${pkgdir}/" --prefix=/usr --optimize=1

  sed -i -e 's#env python#env python2#' \
    "${pkgdir}"/usr/lib/python2.7/site-packages/mercurial/lsprof.py

  install -d ${pkgdir}/usr/share/man/{man1,man5}
  install -m644 doc/hg.1 "${pkgdir}/usr/share/man/man1"
  install -m644 doc/{hgrc.5,hgignore.5} "${pkgdir}/usr/share/man/man5"
  install -m755 contrib/hgk "${pkgdir}/usr/bin"
  #install -m644 -D contrib/zsh_completion "${pkgdir}/usr/share/zsh/site-functions/_hg"
  install -m644 -D contrib/bash_completion "${pkgdir}/usr/share/bash-completion/completions/hg"
  install -d "${pkgdir}/usr/share/emacs/site-lisp"
  install -m644 contrib/{mq.el,mercurial.el} "${pkgdir}/usr/share/emacs/site-lisp"

  vimpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm644 contrib/vim/HGAnnotate.vim "${vimpath}/syntax/HGAnnotate.vim"

  # set some variables
  install -m755 -d ${pkgdir}/etc/profile.d
  install -m755 ${srcdir}/mercurial.profile "${pkgdir}/etc/profile.d/mercurial.sh"

  # install configuration file
  install -m755 -d ${pkgdir}/etc/mercurial
  #install -m644 contrib/sample.hgrc "${pkgdir}/etc/mercurial/hgrc"
  
  # FS#38825 - Add certs config to package
  echo -e "\n[web]\ncacerts = /usr/ssl/certs/ca-bundle.crt\n" >> "${pkgdir}/etc/mercurial/hgrc"
}

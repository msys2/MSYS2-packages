# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=lxml
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=5.4.0
pkgrel=2
pkgdesc="Python binding for the libxml2 and libxslt libraries"
arch=('i686' 'x86_64')
url="https://lxml.de/"
msys2_repository_url="https://github.com/lxml/lxml"
msys2_references=(
  "purl: pkg:pypi/lxml"
  "cpe: cpe:/a:lxml:lxml"
)
license=('spdx:BSD-3-Clause')
depends=("python" "libxslt" "libxml2")
makedepends=(
  "gcc"
  "cython"
  "libxslt-devel"
  "libxml2-devel"
  "pkgconf"
  "python-setuptools"
  "python-build"
  "python-installer"
)
source=("https://github.com/lxml/lxml/archive/${_realname}-${pkgver}.tar.gz"
        "https://github.com/lxml/lxml/commit/6d5d6aed2e38e1abc625f29c0b3e97fc8c60ae3b.patch")
sha256sums=('12ed971a99bd07977e236bf59314cfd53fbf5d59f39d4a13caad901c7fceb0a2'
            'd7899ff7ba0df318824859dbf474d2bfe44c07ddfaa81a132223450737a756a9')

prepare() {
  mv "lxml-lxml-${pkgver}" "lxml-${pkgver}"

  cd "${_realname}-${pkgver}"
  # Fix build with Cython 3.1.0
  patch -p1 -i "${srcdir}/6d5d6aed2e38e1abc625f29c0b3e97fc8c60ae3b.patch"
}

build() {
  cd "${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSES.txt "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSES.txt"
}

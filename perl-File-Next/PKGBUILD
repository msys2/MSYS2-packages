# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=File-Next
pkgname=perl-${_realname}
pkgver=1.16
pkgrel=1
pkgdesc="File-finding iterator"
arch=('any')
license=('PerlArtistic' 'GPL')
url="https://metacpan.org/${_realname}"
groups=('perl-modules')
depends=('perl')
options=(!emptydirs)
source=("https://www.cpan.org/authors/id/P/PE/PETDANCE/${_realname}-${pkgver}.tar.gz")
sha256sums=('6965f25c2c132d0ba7a6f72b57b8bc6d25cf8c1b7032caa3a9bda8612e41d759')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
  PERL_AUTOINSTALL=--skipdeps \
  PERL_MM_OPT="INSTALLDIRS=vendor" \
  PERL_MB_OPT="--installdirs vendor" \
  MODULEBUILDRC=/dev/null \
    /usr/bin/perl Makefile.PL

  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 PERL5LIB="" make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_bootstrapping=no

rust_dist_server=https://static.rust-lang.org/dist
#rust_dist_server=https://dev-static.rust-lang.org/dist/2025-10-27

pkgname=rust
pkgver=1.91.1
pkgrel=3
pkgdesc="Systems programming language focused on safety, speed and concurrency"
arch=('x86_64')
url="https://www.rust-lang.org/"
msys2_repository_url="https://github.com/rust-lang/rust"
msys2_references=(
  'anitya: 7635'
  'archlinux: rust'
  "cpe: cpe:/a:rust-lang:rust"
  'gentoo: dev-lang/rust'
)
license=('spdx:Apache-2.0 AND MIT')
makedepends=('gcc'
             'llvm'
             'python'
             'libgit2-devel'
             'pcre2-devel'
             'libssh2-devel'
             'openssl-devel'
             'pkgconf'
             'libsqlite-devel'
             'liblzma-devel'
             'libzstd-devel'
             'zlib-devel'
             'libcurl-devel'
             $([[ "$_bootstrapping" == "no" ]] && echo 'rust'))
source=("${rust_dist_server}/${pkgname}c-${pkgver}-src.tar.xz"{,.asc}
        "bootstrap.toml")
noextract=(${pkgname}c-${pkgver}-src.tar.xz)
sha256sums=('66401bb815e236cc6b2aacbbe23b61b286c1fe27a67902e7c0222cfe77b3dbab'
            'SKIP'
            'c24975dfa00b1b8e133c5940790640a5327f683ff22ad123d096a8fb37f0f397')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE'  # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>
              '474E22316ABF4785A88C6E8EA2C794A986419D8A'  # Tom Stellard <tstellar@redhat.com>
              'B6C8F98282B944E3B0D5C2530FC3042E345AD05D') # Hans Wennborg <hans@chromium.org>

prepare() {
  plain "Extracting ${pkgname}c-${pkgver}-src.tar.xz"
  # workaround strange symlinking error during a build
  export MSYS=winsymlinks:nativestrict
  tar -xJf ${pkgname}c-${pkgver}-src.tar.xz || true
}

build() {
  export RUST_BACKTRACE=1
  # force some system libraries
  export LIBSQLITE3_SYS_USE_PKG_CONFIG=1
  export LIBSSH2_SYS_USE_PKG_CONFIG=1

  cd "${pkgname}c-${pkgver}-src"
  # substitute the values in bootstrap.toml
  export PKGREL="${pkgrel}"
  envsubst < ../bootstrap.toml > bootstrap.toml

  if [ "${_bootstrapping}" = "no" ]; then
    sed -i '/^\[build\]/,/^$/ s|^#||g' bootstrap.toml
  fi
  if check_option "debug" "y"; then
    sed -i 's/^#debug/debug/g' bootstrap.toml
  fi

  DESTDIR="$PWD/dest-rust" python x.py install --stage 2
}

check() {
  cd "${pkgname}c-${pkgver}-src"
  python x.py test --stage 2 --exclude src/test/debuginfo
}

package_rust() {
  depends=('gcc'
           'zlib'
           'libzstd'
           'libsqlite'
           'libssh2'
           'libgit2'
           'libopenssl'
           'libcurl'
           'llvm-libs')
  optdepends=("gdb: for rust-gdb script"
              "lldb: for rust-lldb script")

  cd "${pkgname}c-${pkgver}-src"

  cp -a dest-rust/* "${pkgdir}"

  # delete unnecessary files, e.g. components and manifest files only used for the uninstall script
  cd "${pkgdir}/usr/lib/rustlib"
  rm components install.log manifest-* rust-installer-version uninstall.sh
  rm "${pkgdir}/etc/target-spec-json-schema.json"

  # workaround for https://github.com/msys2/MSYS2-packages/issues/5784
  cp "${pkgdir}"/usr/lib/rustlib/x86_64-pc-cygwin/lib/std-*.dll -t "${pkgdir}/usr/bin"

  install -d "${pkgdir}/usr/share/bash-completion/completions"
  mv "${pkgdir}/etc/bash_completion.d/cargo" \
     "${pkgdir}/usr/share/bash-completion/completions/cargo"
}

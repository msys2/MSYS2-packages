# Maintainer: Norbert Pfeiler <norbert.pfeiler+aur ät gmail.com>

pkgname=grml-zsh-config
pkgver=0.19.9
pkgrel=1
pkgdesc="grml's zsh setup"
arch=('any')
url='https://grml.org/zsh/'
license=('GPL2')
provides=('grmlzshrc')
depends=('zsh' 'coreutils' 'grep' 'sed' 'procps-ng')
makedepends=('txt2tags')
source=("http://deb.grml.org/pool/main/g/grml-etc-core/grml-etc-core_${pkgver}.tar.xz")
sha256sums=('caf5c3406a364f39d3a62767537493cfd69242cb47b5e7fcfe61a460605edf45')

build() {
  cd ${srcdir}/grml-etc-core-${pkgver}/doc
  make
}

package() {
  cd ${srcdir}/grml-etc-core-${pkgver}
  install -D -m644 etc/skel/.zshrc ${pkgdir}/etc/skel/.zshrc
  install -D -m644 etc/zsh/keephack ${pkgdir}/etc/zsh/keephack
  install -D -m644 etc/zsh/zshrc ${pkgdir}/etc/zsh/zshrc

  install -D -m644 doc/grmlzshrc.5 ${pkgdir}/usr/share/man/man5/grmlzshrc.5
  # the symlinking from the original pkg doesn’t work
  # because the target doesn’t exist yet (manpage compression)
  # and msys2’s symlink simply copies
  install -D -m644 doc/grmlzshrc.5 ${pkgdir}/usr/share/man/man5/grml-zsh-config.5
}

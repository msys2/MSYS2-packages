# Maintainer: Norbert Pfeiler <norbert.pfeiler+aur ät gmail.com>

pkgname=grml-zsh-config
pkgver=0.15.0
pkgrel=1
pkgdesc="grml's zsh setup"
arch=('any')
url='https://grml.org/zsh/'
license=('GPL2')
provides=('grmlzshrc')
depends=('zsh' 'coreutils' 'inetutils' 'grep' 'sed' 'procps')
makedepends=('txt2tags')
source=("http://deb.grml.org/pool/main/g/grml-etc-core/grml-etc-core_${pkgver}.tar.gz")
sha256sums=('ffaf302e3b9a21e5e86985ecdf7cfe430027b19cab79bc6f16da83ad32e5b485')

build() {
  cd ${srcdir}/grml-etc-core-${pkgver}/doc
  make
}

package() {
  cd ${srcdir}/grml-etc-core-${pkgver}
  install -D -m644 etc/skel/.zshrc ${pkgdir}/etc/skel/.zshrc
  install -D -m644 etc/zsh/keephack ${pkgdir}/etc/zsh/keephack
  install -D -m644 etc/zsh/zshrc ${pkgdir}/etc/zsh/zshrc

  install -D -m644 doc/grmlzshrc.5 ${pkgdir}/usr/share/man/man5/grmlzshrc.5
  # the symlinking from the original pkg doesn’t work
  # because the target doesn’t exist yet (manpage compression)
  # and msys2’s symlink simply copies
  install -D -m644 doc/grmlzshrc.5 ${pkgdir}/usr/share/man/man5/grml-zsh-config.5
}

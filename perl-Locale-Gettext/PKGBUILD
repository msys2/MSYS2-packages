# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=perl-Locale-Gettext
_realname=gettext
pkgver=1.05
pkgrel=3
groups=('perl-modules')
pkgdesc="Permits access from Perl to the gettext() family of functions"
arch=('i686' 'x86_64')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('gettext' 'perl')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/P/PV/PVANDRY/${_realname}-${pkgver}.tar.gz"
        'compatibility-with-POSIX-module.patch'
        '1.05-as-needed.patch'
        'doc-update.diff')
md5sums=('f3d3f474a1458f37174c410dfef61a46'
         '854302f34d01a2f8ae739187788973dd'
         '5373a6d1547e4938c29e383ceb0fcb03'
         '165914085731872a0a4b96774925531e')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np1 -i ${srcdir}/compatibility-with-POSIX-module.patch
  patch -Np1 -i ${srcdir}/doc-update.diff
  patch -Np2 -i ${srcdir}/1.05-as-needed.patch
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"

  # fix permissons
  find ${pkgdir}/usr -type f \( -name *.dll -o -name *.exe \) | xargs chmod 0755
  
  # remove perllocal.pod and .packlist
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}

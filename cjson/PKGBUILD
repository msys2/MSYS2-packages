pkgname=cjson
pkgver=1.7.19
pkgrel=1
pkgdesc="Ultralightweight JSON parser in ANSI C"
arch=(x86_64)
url="https://github.com/DaveGamble/cJSON"
msys2_repository_url="https://github.com/DaveGamble/cJSON"
msys2_references=(
  "cpe: cpe:/a:cjson_project:cjson"
)
license=('spdx:MIT')
depends=(
)
makedepends=(
  cmake
  gcc
)
source=(
  https://github.com/DaveGamble/cJSON/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
)
sha512sums=(
  7b22bdd05b8e0bf8b24ab79db128dbab9f8c2c167f4b1d3073922b8ff1e0f0e8917322283d6d4091002f014f42692984f4f641761f05965d3b66fb3eca14dc46
)

build() {
  local cmake_options=(
    -B build
    -D ENABLE_CJSON_UTILS=ON
    -D ENABLE_CJSON_TEST=OFF
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_INSTALL_LIBDIR=lib
    -D CMAKE_POLICY_VERSION_MINIMUM=3.5
    -S cJSON-$pkgver
  )
  cmake "${cmake_options[@]}"
  make -C build -j$(nproc)
}

package() {
  DESTDIR="${pkgdir}" make -C build install
  install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

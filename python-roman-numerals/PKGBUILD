# Contributor: Dirk Stolle

_realname=roman-numerals
pkgname=python-${_realname}
pkgver=4.1.0
pkgrel=1
pkgdesc='Manipulate well-formed Roman numerals'
arch=('any')
url='https://github.com/AA-Turner/roman-numerals/tree/master/python'
msys2_references=(
  'gentoo: dev-python/roman-numerals'
  'purl: pkg:pypi/roman-numerals'
)
license=('spdx:0BSD OR CC0-1.0')
depends=('python')
makedepends=(
  'python-build'
  'python-flit-core'
  'python-installer'
)
checkdepends=('python-pytest')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname/-/_}-${pkgver}.tar.gz")
sha256sums=('1af8b147eb1405d5839e78aeb93131690495fe9da5c91856cb33ad55a7f1e5b2')

build() {
  cp -r "${_realname/-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "python-build-${MSYSTEM}"
  python -m pytest
}

package() {
  cd "python-build-${MSYSTEM}"

  python -m installer --prefix=${MSYSTEM_PREFIX} --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENCE.rst "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}

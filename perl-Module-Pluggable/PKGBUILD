# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Module-Pluggable
pkgname=perl-${_realname}
pkgver=6.2
pkgrel=1
pkgdesc="automatically give your module the ability to have plugins"
arch=('any')
url="https://metacpan.org/release/Module-Pluggable"
groups=('perl-modules')
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/S/SI/SIMONW/${_realname}-${pkgver}.tar.gz")
sha256sums=('2743f1cb59c2cac21bf14f0f71beab5ced75a589394a1fd8ac09ecc71599299e')

build() {
  cd  "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd  "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd  "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Module-Pluggable
pkgname=perl-${_realname}
pkgver=5.2
pkgrel=1
pkgdesc="automatically give your module the ability to have plugins"
arch=('any')
url="https://metacpan.org/release/Module-Pluggable"
groups=('perl-modules')
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/S/SI/SIMONW/${_realname}-${pkgver}.tar.gz")
sha256sums=('b3f2ad45e4fd10b3fb90d912d78d8b795ab295480db56dc64e86b9fa75c5a6df')

build() {
  cd  "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd  "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd  "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}

# Maintainer: Orgad Shaneh <orgad.shaneh@audiocodes.com>

pkgname=sipp
pkgver=3.7.1
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A SIP protocol test tool'
url='https://github.com/SIPp/sipp'
license=('spdx:GPL-2.0-or-later')
depends=('gcc-libs' 'libpcap' 'ncurses' 'openssl')
makedepends=('cmake' 'gcc' 'libpcap-devel' 'ncurses-devel' 'ninja' 'openssl-devel')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/SIPp/sipp/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('7d285de2158bbdf3d04c5a45b5638a8b6c1e160f5c1d5f6e9775fbda910a4c0b')

build() {
  cd ${pkgname}-${pkgver}
  cmake -B build -G Ninja -DCMAKE_INSTALL_PREFIX=/usr -DUSE_GSL=1 -DUSE_PCAP=1 -DUSE_SSL=1
  cmake --build build
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

_realname=poetry-core
pkgname="python-${_realname}"
pkgver=1.9.0
pkgrel=1
pkgdesc='Poetry PEP 517 Build Backend'
arch=(any)
url='https://github.com/python-poetry/poetry-core'
msys2_references=(
  "pypi:poetry-core"
)
license=('spdx:MIT')
depends=("python")
makedepends=(
  "python-setuptools"
  "python-wheel"
  "python-build"
  "python-installer"
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname/-/_}-${pkgver}.tar.gz")
sha256sums=('fa7a4001eae8aa572ee84f35feb510b321bd652e5cf9293249d62853e1f935a2')

build() {
  cd "${_realname/-/_}-${pkgver}"

  # https://github.com/python-poetry/poetry/issues/5547
  export GIT_DIR="$(pwd)"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${_realname/-/_}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

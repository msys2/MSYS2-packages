# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

pkgname=lftp
pkgver=4.7.6
pkgrel=1
pkgdesc="Sophisticated command line based FTP client (net-utils)"
arch=('i686' 'x86_64')
license=('GPL3')
groups=('net-utils')
depends=('gcc-libs'
         'ca-certificates'
         'expat'
         'gettext'
         'libexpat'
         'libgnutls'
         'libiconv'
         'libidn'
         'libintl'
         'libhogweed'
         'libnettle'
         'libp11-kit'
         'libreadline'
         'libtasn1'
         'openssh'
         'zlib')
makedepends=('gcc'
             'gettext-devel'
             'libexpat-devel'
             'libgnutls-devel'
             'libiconv-devel'
             'libidn-devel'
             'libnettle-devel'
             'libp11-kit-devel'
             'libreadline-devel'
             'libtasn1-devel'
             'zlib-devel')
optdepends=('perl: needed for convert-netscape-cookies and verify-file')
url="http://lftp.yar.ru/"
backup=('etc/lftp.conf')
source=(http://lftp.yar.ru/ftp/${pkgname}-${pkgver}.tar.xz{,.asc}
        lftp-configure-needtrio.patch
        lftp-configure-readline.patch
        lftp-4.4.15-msysize.patch
        lftp-4.7.6-no-undefined.patch
        lftp-correct-gettext-macros.patch)
options=('debug' '!strip')
sha256sums=('6e26254fdc7e1b92ceb31116a2c7bdcd891a07c72a7823144aaaf147a1841b7b'
            'SKIP'
            'a3781d946913a0d39a41d5b86390f255b36f67e9aea502e0117e701e295c7ea0'
            '45f817bda0f57b5c239b6133d8b7028811aff673783cb09c1ad568ae8f984688'
            'c81742dad375326b1f595784976cb20f83591b4df4309abf87d664325e91cd10'
            '46819c3e6bb66571721770368695e152e2e57ee00183ea893ce7bbc7cdbf5277'
            'd6d74b134385e1c3f824b9a960376a2bcb7c0e39acab2ae1c3c764f9d338e650')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/lftp-configure-needtrio.patch
  patch -p1 -i ${srcdir}/lftp-configure-readline.patch
  patch -p1 -i ${srcdir}/lftp-4.4.15-msysize.patch
  patch -p1 -i ${srcdir}/lftp-4.7.6-no-undefined.patch
  patch -p1 -i ${srcdir}/lftp-correct-gettext-macros.patch

  autoreconf -fiv
}

build() {
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  ../${pkgname}-${pkgver}/configure \
    --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-gnutls \
    --without-openssl \
    --without-included-regex \
    --disable-rpath \
    --disable-static \
    --with-debug
  make
}

check() {
  cd ${srcdir}/build-${CARCH}
  make -k check
}

package() {

  cd ${srcdir}/build-${CARCH}
  make DESTDIR=${pkgdir} install
  
  grep -q 'set ssl:ca-file' ${pkgdir}/etc/lftp.conf || echo 'set ssl:ca-file /etc/pki/ca-trust/extracted/pem/tls-ca-bundle.pem' >> ${pkgdir}/etc/lftp.conf

}

# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=Module-Build
pkgname=perl-${_realname}
pkgver=0.4229
pkgrel=1
pkgdesc="Build, test, and install Perl modules"
arch=('any')
url="https://metacpan.org/release/Module-Build"
license=('PerlArtistic' 'GPL')
depends=('perl>=5.8.0' 'perl-CPAN-Meta>=2.142060' 'perl-inc-latest')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/L/LE/LEONT/${_realname}-${pkgver}.tar.gz
        module-build-0.4224-msysize.patch)
sha512sums=('51e0d73cecf450062121c3223b1a4c6f8a8c260fc2345d1c1a5e13ddcbd2b7bc2578411ac254420e27720b9e00a8efc9a9daa3159e9cba5338a188734b35c738'
            '33d5e45a51583f5c9513506882485194aeb798408035f718716fe241696181dffdc939270db46b50d7e60e9ecb8b51b3b53368d8eb0dda95dab4d05ab2896481')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/module-build-0.4224-msysize.patch
}

build() {
  cd ${_realname}-${pkgver}
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd ${_realname}-${pkgver}
  perl Build test
}

package() {
  cd ${_realname}-${pkgver}
  perl Build install destdir="${pkgdir}"
}

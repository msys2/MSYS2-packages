# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=Module-Build
pkgname=perl-${_realname}
pkgver=0.4212
pkgrel=1
pkgdesc="Build, test, and install Perl modules"
arch=('any')
url="http://search.cpan.org/dist/Module-Build"
license=('PerlArtistic' 'GPL')
depends=('perl>=5.8.0' 'perl-CPAN-Meta>=2.142060')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/L/LE/LEONT/${_realname}-$pkgver.tar.gz
        module-build-0.4212-msysize.patch)
sha512sums=('5677decbb11194f5657e7436abb24afd9697021e924cd33db92da04be4a467d99fd9d587e62ccd865ef4f2120f7dad5e5ee38a7ae14f65d73e080f1bf6bc6147'
            '9483ac219e01d773db58a80e65111f47872d3009c67801b1b7caada50042b39f942ec2fbe6fa1d1269fc3e8b878ca87e02e9c1fa23746fdc2a48e6bf84dceb25')

prepare() {
  cd ${_realname}-$pkgver
  patch -p1 -i ${srcdir}/module-build-0.4212-msysize.patch
}

build() {
  cd ${_realname}-$pkgver
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd ${_realname}-$pkgver
  perl Build test
}

package() {
  cd ${_realname}-$pkgver
  perl Build install destdir="$pkgdir"
}

pkgname=guile-mqtt
pkgver=1.0.0
pkgrel=1
pkgdesc="Guile MQTT"
arch=(x86_64)
url="https://github.com/mdjurfeldt/guile-mqtt"
msys2_repository_url="https://github.com/mdjurfeldt/guile-mqtt"
license=('spdx:LGPL-3.0-only')
depends=(
  guile
  mosquitto
)
makedepends=(
  autotools
  gcc
  libguile-devel
  nyacc
)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/mdjurfeldt/guile-mqtt/archive/refs/tags/v$pkgver.tar.gz
  0001-Fix-some-issues.patch
  0002-module-ffi-mosquitto-nyacc.ffi-add-cpp-defs.patch
  0003-module-ffi-mosquitto-nyacc.ffi-add-include-helper.patch
)
sha512sums=(
  f4399da42e1f83e028fe262ef1c7d4738dcfbdb347b92d2330b13bd11cd544fe91dd6906e9070ae0c2d2dcc54d594dabbf9e843fb9ec2e666235e9231f4909d0
  53fcbf4814cc0e036a34ba00dc7536cc9f03b6864c6f25fc6beaed9f002b101e2075a0558ff601c1f49fd1fc1e05af6b7c475c3c0d13ed28f136e85d79783524
  77171524a110ef0b08035e2541440eaaf8c08162bb7a2bfe51e3f023dca10c826d6ac8210bc347d548c80df9b72866a44870efab6028e31e7fa74624f29dc652
  2167c207456102c48680f115fced9ae128b639022a41b91f24ec16199dc3d6840c40c4c5831bfeb1e6791a5246938eb4bf35c95b3e154b5cfd4d6edb9f46cf09
)
prepare() {
  cd    $pkgname-$pkgver
  patch -p1 -i "${srcdir}/0001-Fix-some-issues.patch"
  patch -p1 -i "${srcdir}/0002-module-ffi-mosquitto-nyacc.ffi-add-cpp-defs.patch"
  patch -p1 -i "${srcdir}/0003-module-ffi-mosquitto-nyacc.ffi-add-include-helper.patch"

  ./bootstrap.sh
  ./configure
}

build() {
  make -C $pkgname-$pkgver -j1  #No parallel execution
}

package() {
  DESTDIR="$pkgdir" make -C $pkgname-$pkgver install

  install -Dm644 "$pkgname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

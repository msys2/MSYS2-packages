# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=man-db
pkgver=2.7.6
pkgrel=1
pkgdesc="A utility for reading man pages"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/man-db/"
license=('GPL' 'LGPL')
groups=('base-devel')
depends=( 'bash' 'gdbm' 'zlib' 'groff' 'libpipeline' 'less')
optdepends=('gzip')
makedepends=('libgdbm-devel' 'libpipeline-devel' 'zlib-devel' 'pkg-config')
backup=('etc/man_db.conf')
conflicts=('man')
provides=('man')
replaces=('man')
install=${pkgname}.install
source=(https://download-mirror.savannah.gnu.org/releases/man-db/${pkgname}-${pkgver}.tar.xz{,.sig}
        convert-mans
		man-db-2.7.6-update-gnulib.patch
        man-db-2.7.6-msysize.patch)
sha256sums=('c68cffa6b93f6362beb1d1259f9ad5b65af2aee9a7d9910086082ea4b75f5da2'
            'SKIP'
            '15c9452984c06335543d9692e25d7825063d39e4d7897a224a36bbbd5e0ffaa8'
			'4931ce4f6ac8e4697ff51d5d5ea5dde2dcde4f0d6e6dc6e02b0e5cedb477647c'
            'c43501b7ddf75bceabdb7b8b20a84e52d4caed8e44c09f5b827794e562d78012')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/man-db-2.7.6-update-gnulib.patch
  patch -p1 -i ${srcdir}/man-db-2.7.6-msysize.patch

  autoreconf -fi
  
  # fix error: 
  #		gettext infrastructure mismatch: using a Makefile.in.in from gettext version 0.18 but the autoconf macros are from gettext version 0.19

  local f
  for f in .{,/gnulib}/po/Makefile.in{,.in}; do
	if [ -f ${f} ]; then
		sed -i -r "s/^(GETTEXT_MACRO_VERSION[[:space:]]*=[[:space:]]*).*/\1$(grep -Po '(?<=GETTEXT_MACRO_VERSION=)[\d.]+' ./configure)/g" ${f}
	fi
  done
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --with-db=gdbm \
    --disable-setuid \
    --enable-mandirs=GNU \
    --enable-static \
    --disable-shared \
    --with-sections="1 n l 8 3 0 2 5 4 9 6 7"
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # part of groff pkg
  rm -f ${pkgdir}/usr/bin/zsoelim

  # script from LFS to convert manpages, see
  # http://www.linuxfromscratch.org/lfs/view/6.4/chapter06/man-db.html
  install -D -m755 ${srcdir}/convert-mans  ${pkgdir}/usr/bin/convert-mans
}

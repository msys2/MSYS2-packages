# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=dulwich
pkgname="python-${_realname}"
pkgver=1.0.0
pkgrel=1
pkgdesc='Pure-Python implementation of the Git file formats and protocols'
arch=('any')
url='https://www.dulwich.io'
msys2_repository_url='https://github.com/jelmer/dulwich'
msys2_changelog_url='https://github.com/jelmer/dulwich/blob/master/NEWS'
msys2_references=(
  'archlinux: python-dulwich'
  'gentoo: dev-python/dulwich'
  'purl: pkg:pypi/dulwich'
  "cpe: cpe:/a:dulwich_project:dulwich"
)
license=('spdx:Apache-2.0 OR GPL-2.0-or-later')
depends=(
  'python'
  'python-urllib3'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-rust'
  'python-devel'
  'rust'
  'gcc'
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('3d07104735525f22bfec35514ac611cf328c89b7acb059316a4f6e583c8f09bc')

prepare(){
  cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  local link_arg=$(python -c "import sysconfig; print(sysconfig.get_config_var('LIBPYTHON'))")
  export RUSTFLAGS="-C link-arg=$link_arg"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

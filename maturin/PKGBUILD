# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgbase=maturin
pkgname=("python-maturin" "maturin")
pkgver=1.9.3
pkgrel=1
pkgdesc='Build and publish crates with pyo3, rust-cpython and cffi bindings'
arch=('x86_64' 'i686')
url='https://www.maturin.rs/'
msys2_repository_url='https://github.com/pyo3/maturin'
msys2_references=(
  'purl: pkg:pypi/maturin'
)
license=('spdx:MIT OR Apache-2.0')
makedepends=(
  "python-build"
  "python-installer"
  "python-setuptools-rust")
options=('!strip')
source=("https://pypi.org/packages/source/${pkgbase::1}/${pkgbase}/${pkgbase}-${pkgver}.tar.gz")
sha256sums=('267ac8d0471d1ee2320b8b2ee36f400a32cd2492d7becbd0d976bd3503c2f69b')

prepare() {
  cp -r "${pkgbase}-${pkgver}" "python-build" && cd "python-build"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "python-build"

  export MATURIN_SETUP_ARGS="--features full,rustls"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package_python-maturin() {
  pkgdesc+=' (Python bindings)'
  depends=("python" "maturin")

  cd "python-build"

  python -m installer --prefix=/usr \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 license-{apache,mit} -t "${pkgdir}/usr/share/licenses/python-${pkgbase}/"

  # split maturin executable
  mkdir -p "dest/usr/bin"
  mv "${pkgdir}/usr"/bin/* "dest/usr/bin/"
}

package_maturin() {
  cd "python-build"

  mv dest/* "${pkgdir}"

  local _complete="${pkgdir}/usr/bin/maturin completions"
  $_complete bash | install -Dm644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/maturin"
  $_complete zsh | install -Dm644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_maturin"
  $_complete fish | install -Dm644 /dev/stdin "${pkgdir}/usr/share/fish/vendor_completions.d/maturin.fish"
  install -Dm644 license-{apache,mit} -t "${pkgdir}/usr/share/licenses/${pkgbase}/"
}

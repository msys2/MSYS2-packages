# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=gc
pkgname=('libgc' 'libgc-devel')
pkgver=8.0.6
pkgrel=1
pkgdesc="A garbage collector for C and C++"
arch=('i686' 'x86_64')
groups=('libraries')
url="https://www.hboehm.info/gc/"
license=('GPL')
makedepends=('autotools' 'gcc')
source=("https://github.com/ivmai/bdwgc/releases/download/v${pkgver}/${pkgbase}-${pkgver}.tar.gz")
makedepends=(libatomic_ops-devel)
options=('!libtool')
sha256sums=('3b4914abc9fa76593596773e4da671d7ed4d5390e3d46fbf2e5f155e121bea11')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  ../${pkgbase}-${pkgver}/configure \
    --build=${CHOST} \
    --prefix=/usr \
    --enable-cplusplus \
    --enable-threads=pthreads \
    --with-libatomic-ops=yes

  make
}

check() {
  cd "${srcdir}/build-${CARCH}"

  make check
}

package_libgc() {
  depends=('libatomic_ops' 'gcc-libs')
  cd "${srcdir}/build-${CARCH}"

  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/{include,lib}
}

package_libgc-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("libgc=${pkgver}" "libatomic_ops-devel")

  cd "${srcdir}/build-${CARCH}"

  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/{bin,share}
}

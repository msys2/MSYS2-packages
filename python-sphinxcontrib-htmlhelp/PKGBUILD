# Contributor: Dirk Stolle

_realname=sphinxcontrib-htmlhelp
pkgname=python-${_realname}
pkgver=2.1.0
pkgrel=1
pkgdesc='Sphinx extension which renders HTML help files'
arch=('any')
url='https://github.com/sphinx-doc/sphinxcontrib-htmlhelp'
msys2_references=(
  'cygwin: python-sphinxcontrib-htmlhelp'
  'purl: pkg:pypi/sphinxcontrib-htmlhelp'
)
license=('spdx:BSD-2-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-flit-core'
  'python-installer'
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname/-/_}-${pkgver}.tar.gz")
sha256sums=('c9e2916ace8aad64cc13a0d233ee22317f2b9025b9cf3295249fa985cc7082e9')

build() {
  cp -r "${_realname/-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}"

  python -m installer --prefix=${MSYSTEM_PREFIX} --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENCE.rst "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}

# Maintainer: Bin Meng <bmeng.cn@gmail.com>

pkgname=u-boot-tools
pkgver=2021.10
pkgrel=1
pkgdesc="U-Boot Tools"
arch=('i686' 'x86_64')
url="https://github.com/u-boot/u-boot"
license=('GPL2')
depends=('dtc' 'openssl')
makedepends=('openssl-devel' 'gcc' 'make')
source=(https://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2{,.sig})
noextract=("u-boot-${pkgver}.tar.bz2")
sha256sums=('cde723e19262e646f2670d25e5ec4b1b368490de950d4e26275a988c36df0bd4'
            'SKIP')
validpgpkeys=('1A3C7F70E08FAB1707809BBF147C39FF9634B72C')

prepare() {
  # workaround an issue of bsdtar for extracting symbolic links in the tarball
  bsdtar -xzf ${srcdir}/u-boot-${pkgver}.tar.bz2 -C $srcdir || true
  bsdtar -xzf ${srcdir}/u-boot-${pkgver}.tar.bz2 -C $srcdir || true
}

build() {
  cd ${srcdir}/u-boot-${pkgver}
  make tools-only_defconfig tools-only NO_SDL=1
}

package() {
  mkdir -p ${pkgdir}/usr/bin
  cd ${srcdir}/u-boot-${pkgver}
  cp -f tools/dumpimage.exe ${pkgdir}/usr/bin/
  cp -f tools/fdtgrep.exe ${pkgdir}/usr/bin/
  cp -f tools/fit_info.exe ${pkgdir}/usr/bin/
  cp -f tools/fit_check_sign.exe ${pkgdir}/usr/bin/
  cp -f tools/mkimage.exe ${pkgdir}/usr/bin/
}

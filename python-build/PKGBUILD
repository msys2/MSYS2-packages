# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=build
pkgname="python-${_realname}"
pkgver=1.0.3
pkgrel=2
pkgdesc="A simple, correct Python build frontend"
arch=('any')
url='https://github.com/pypa/build'
license=('spdx:MIT')
depends=("python-packaging"
         "python-pyproject-hooks")
makedepends=(
  "python-flit-core"
  "python-installer"
)
source=("${_realname}-$pkgver.tar.gz"::"https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('538aab1b64f9828977f84bc63ae570b060a8ed1be419e7870b8b4fc5e6ea553b')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m flit_core.wheel
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/python-${_realname}/LICENSE
}

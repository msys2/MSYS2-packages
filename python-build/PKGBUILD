# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=build
pkgname="python-${_realname}"
pkgver=1.4.0
pkgrel=1
pkgdesc="A simple, correct Python build frontend"
arch=('any')
url='https://github.com/pypa/build'
msys2_repository_url='https://github.com/pypa/build'
msys2_references=(
  "purl: pkg:pypi/build"
)
license=('spdx:MIT')
depends=("python-packaging"
         "python-pyproject-hooks")
makedepends=(
  "python-flit-core"
  "python-installer"
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('f1b91b925aa322be454f8330c6fb48b465da993d1e7e7e6fa35027ec49f3c936')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m flit_core.wheel
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/python-${_realname}/LICENSE
}

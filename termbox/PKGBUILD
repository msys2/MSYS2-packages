# Maintainer: Yusuke Sasaki <yusuke dot sasaki dot nuem at gmail dot com>

pkgname=termbox
pkgver=1.1.0
pkgrel=1
pkgdesc="Library for writing text-based user interfaces"
_wafver=1.9.3
arch=('i686' 'x86_64')
license=('MIT')
url="https://github.com/nsf/termbox"
makedepends=('python3')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/nsf/termbox/archive/v${pkgver}.tar.gz
        https://waf.io/waf-${_wafver}
        0010-msysize.patch)
sha256sums=('2743ee4aeb0ff39fadbaf945b76c43e6f6bba544156f2576282b775a3067d748'
            '5a189d5058dbac302a3ffccde4b8eb1b3eafaf3b8dad777bfe7c811549156a22'
            'ad0550bd4e66daee3f6c3cfcc01c3f12e8b2e415f87deae910df8cfbe0f4ee35')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  cp -f ${srcdir}/waf-${_wafver} ./waf

  patch -p1 -i ${srcdir}/0010-msysize.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  /usr/bin/python waf configure --prefix=/usr
  /usr/bin/python waf build -v
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  /usr/bin/python waf install --destdir="${pkgdir}"
}


# Maintainer: atom2013 <atom.long@hotmail.com>

_realname=pip
pkgbase="python2-${_realname}"
pkgname=("python2-${_realname}")
pkgver=19.3.1
pkgrel=2
pkgdesc="The PyPA recommended tool for installing Python packages"
url="https://pip.pypa.io/"
arch=('any')
license=('MIT')
depends=('python2' 'python2-setuptools')
conflicts=('python-pyinstall')
replaces=('python-pyinstall')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/pypa/${_realname}/archive/${pkgver}.tar.gz)
sha256sums=('f12b7a6be2dbbfeefae5f14992c89175ef72ce0fe96452b4f66be855a12841ff')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python2 setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python2 setup.py install --prefix=/usr --root="${pkgdir}"

  mv "${pkgdir}/usr/bin/pip" "${pkgdir}/usr/bin/pip2"
  sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" \
    ${pkgdir}/usr/lib/python2.7/site-packages/pip/__init__.py
  python2 -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/pip/__init__.py

  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# Maintainer: Jeremy Drake <github@jdrake.com>

pkgname=cygutils
pkgver=1.4.16.7
pkgrel=1
pkgdesc="${pkgname} is a collection of simple utilities"
arch=('i686' 'x86_64')
url="https://cygwin.org/"
license=('GPL')
#options=('debug' '!strip')
# Nothing in this package directly depends on libintl/libiconv, only indirectly
# via popt
depends=('gcc-libs' 'popt')
makedepends=('gcc'
             'gettext-devel'
             'git'
             'libiconv-devel'
             'autotools'
             'popt-devel')
conflicts=('winln')
replaces=('winln')
provides=('winln')
_commit='936dce44b5799e3fd6926b9faacd9046cf5f2aaa'
source=("${pkgname}-${pkgver}::git+https://sourceware.org/git/cygwin-apps/${pkgname}.git#commit=${_commit}"
        msysize.patch)
sha256sums=(SKIP
            '814b5a1df4f5a5071387feb948a951ba3756e3be13d405bf8180a80cc6443ac0')

prepare(){
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/msysize.patch

  autoreconf -fi
}

build() {
  mkdir -p "${srcdir}/build-${CHOST}"
  cd "${srcdir}/build-${CHOST}"

  ../${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --host=${CHOST} \
    --target=${CHOST}

  make
}

package() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR="${pkgdir}" install
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

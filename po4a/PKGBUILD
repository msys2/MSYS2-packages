# Maintainer: Peter Budai <peterbudai at hotmail dot com>
# Contributor: Biswapriyo Nath <nathbappai@gmail.com>

pkgname=po4a
pkgver=0.74
pkgrel=1
pkgdesc="Tools for helping translation of documentation"
arch=('any')
url="https://po4a.org/"
msys2_repository_url="https://github.com/mquinson/po4a"
license=('GPL')
depends=("perl" "gettext" 'perl-YAML-Tiny' 'perl-Text-WrapI18N'
         'perl-Locale-Gettext' 'perl-TermReadKey' 'perl-sgmls'
         'perl-Unicode-LineBreak' 'perl-Pod-Parser')
makedepends=('perl-Module-Build' 'docbook-xsl')
options=('!emptydirs')
source=(https://github.com/mquinson/po4a/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        001-fix-man-names.patch)
sha256sums=('25fc323f2ba37bbd48c3af0ebf49952644b0e468261f98633e91219a838fe7c2'
            '7b8aa572c3f6f6626786b2ee26c516b98e2906a7fa864a130f4d0ac86788bf0e')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/001-fix-man-names.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  perl Build.PL INSTALLDIRS=vendor create_packlist=0
  # The script expects a UTF-8 locale
  LC_ALL=en_US.UTF-8 perl Build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  perl Build destdir="${pkgdir}" install

  # remove perllocal.pod and .packlist
  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}

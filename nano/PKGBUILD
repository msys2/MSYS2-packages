# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=nano
pkgver=2.5.2
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.nano-editor.org"
groups=('editors')
depends=('ncurses' 'sh')
makedepends=('make'
             'automake-wrapper'
             'ncurses-devel'
             'libiconv-devel'
             'gettext-devel')
backup=('etc/nanorc')
install=nano.install
source=(http://www.nano-editor.org/dist/v${pkgver%.*}/${pkgname}-${pkgver}.tar.gz
        2.2.4-wchar.patch
        nano-2.3.4-fix-ncurses-location.patch
        nano-2.3.2-msys2.patch)
sha256sums=('89bd6ac5120880d2a95431d8bad22bf92c7ccbf10b8f1716d4ea062f3faeeb41'
            '60ea17cdaf6ce0dbcf7e21b592e54aa829b34d9aab95a7ad20ddce569605c3d4'
            '576d365d7e51fc0fd6268a45023de1ea14a9a4d30df9d6c7bd2188f426422bea'
            '47c88125d8a81628208a327296b5033b264b2a98c97b5012545a2e6617c7417b')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p2 -i ${srcdir}/2.2.4-wchar.patch
  patch -p1 -i ${srcdir}/nano-2.3.4-fix-ncurses-location.patch
  patch -p1 -i ${srcdir}/nano-2.3.2-msys2.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-color \
    --without-slang \
    --enable-nanorc \
    CFLAGS="${CFLAGS} -I/usr/include/ncursesw" \
    CPPFLAGS="${CPPFLAGS} -I/usr/include/ncursesw"

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -DTm644 ${srcdir}/${pkgname}-${pkgver}/doc/nanorc.sample ${pkgdir}/etc/nanorc
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=nano
pkgver=5.9
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=('i686' 'x86_64')
license=('GPL')
url="https://www.nano-editor.org"
groups=('editors')
depends=('file' 'libintl' 'ncurses' 'sh')
makedepends=('make'
             'automake-wrapper'
             'ncurses-devel'
             'libiconv-devel'
             'gettext-devel')
backup=('etc/nanorc')
install=nano.install
source=(https://www.nano-editor.org/dist/v5/${pkgname}-${pkgver}.tar.xz{,.asc}
        2.2.4-wchar.patch
        nano-2.3.4-fix-ncurses-location.patch
        nano-2.8.0-msys2.patch)
sha256sums=('757db8cda4bb2873599e47783af463e3b547a627b0cabb30ea7bf71fb4c24937'
            'SKIP'
            '021d6bba4d45068b6d24aefdaed5044675f05b01430b68d27721f90d5ea6f5d3'
            '62b0e50470254ed09e55c068b29b72fe784d0552f97fbc5a15b6ee791c9cbd72'
            '5566700bbd3e8e157869d76086c731169d3352adb345fe9c3be48762b36a174e')
validpgpkeys=('8DA6FE7BFA7A418AB3CB2354BCB356DF91009FA7' # "Chris Allegretta <chrisa@asty.org>"
              'A7F6A64A67DA09EF92782DD79DF4862AF1175C5B' # "Benno Schulenberg <bensberg@justemail.net>"
              'BFD009061E535052AD0DF2150D28D4D2A0ACE884' # "Benno Schulenberg <bensberg@telfort.nl>"
)

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  msg2 "2.2.4-wchar.patch"
  patch -p2 -i ${srcdir}/2.2.4-wchar.patch
  msg2 "nano-2.3.4-fix-ncurses-location.patch"
  patch -p1 -i ${srcdir}/nano-2.3.4-fix-ncurses-location.patch
  msg2 "nano-2.8.0-msys2.patch"
  patch -p1 -i ${srcdir}/nano-2.8.0-msys2.patch

  autoreconf -fiv
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-color \
    --enable-nanorc \
    --enable-utf8 \
    CFLAGS="${CFLAGS} -I/usr/include/ncursesw" \
    CPPFLAGS="${CPPFLAGS} -I/usr/include/ncursesw"

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -DTm644 ${srcdir}/${pkgname}-${pkgver}/doc/sample.nanorc ${pkgdir}/etc/nanorc
}

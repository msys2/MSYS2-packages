# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=nano
pkgver=8.3
pkgrel=2
pkgdesc="Pico editor clone with enhancements"
arch=('i686' 'x86_64')
license=('spdx:GPL-3.0-or-later')
url="https://www.nano-editor.org"
msys2_repository_url="https://git.savannah.gnu.org/cgit/nano.git"
msys2_references=(
  "cpe: cpe:/a:gnu:nano"
)
groups=('editors')
depends=('file' 'libintl' 'ncurses' 'sh')
makedepends=('autotools'
             'gcc'
             'ncurses-devel'
             'libiconv-devel'
             'gettext-devel')
backup=('etc/nanorc')
source=(https://www.nano-editor.org/dist/v8/${pkgname}-${pkgver}.tar.xz{,.asc}
        "0001-fix-link-order.patch"
        "0002-paint-interface-elements.patch")
sha256sums=('551b717b2e28f7e90f749323686a1b5bbbd84cfa1390604d854a3ca3778f111e'
            'SKIP'
            'fd89f31bd8813db14587241ef4526f929dcee17b97572b70e92f2db40b53c859'
            '9df1a720de5cbcd6fa93e257afbc0ccf92d5c841c24a0a9c666df3b1d63399f2')
validpgpkeys=(
  '168E6F4297BFD7A79AFD4496514BBE2EB8E1961F' # Benno Schulenberg <bensberg@telfort.nl>
)

prepare() {
  cd "${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/0001-fix-link-order.patch"
  patch -Np1 -i "${srcdir}/0002-paint-interface-elements.patch"

  autoreconf -vfi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure --build=${CYGWIN_CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-color \
    --enable-nanorc \
    --enable-utf8

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -DTm644 ${srcdir}/${pkgname}-${pkgver}/doc/sample.nanorc ${pkgdir}/etc/nanorc
}

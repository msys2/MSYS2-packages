# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=make
pkgver=4.3
pkgrel=3
pkgdesc="GNU make utility to maintain groups of programs"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/make"
license=('GPL3')
groups=('base-devel')
depends=('libintl' 'sh')
makedepends=('gettext' 'procps' 'autotools' 'gcc' 'gettext-devel')
options=() # 'debug' '!strip')
source=("https://ftp.gnu.org/gnu/make/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('e05fdde47c5f7ca45cb697e973894ff4f5d79e13b750ed57d7b66d8defc78e19'
            'SKIP')
validpgpkeys=('6D4EEB02AD834703510B117680CB727A20C79BB2')  # Paul D. Smith <psmith@gnu.org>


prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  #autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # 32bit msys make is broken without --disable-posix-spawn somehow.
  # posix_spawn in cygwin likely isn't faster anway so just disable
  # it everywhere: https://github.com/msys2/MSYS2-packages/issues/2801
  # edit: it even seems to be slower with posix_spawn:
  # https://github.com/msys2/MSYS2-packages/pull/2803#issuecomment-1010253935

  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --without-libintl-prefix \
    --without-libiconv-prefix \
    --without-guile \
    --disable-posix-spawn \
    ac_cv_dos_paths=yes

  make -j1 all
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}

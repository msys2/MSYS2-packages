# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.5.5
pkgrel=3
pkgdesc="Cygwin POSIX emulation engine"
arch=('x86_64')
url="https://www.cygwin.com/"
license=('GPL')
msys2_references=(
  'cygwin: cygwin'
  "cpe: cpe:/a:cygwin:cygwin"
)
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'mingw-w64-cross-crt'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'autotools'
             'xmlto'
             'docbook-xsl')
# re zipman: https://github.com/msys2/MSYS2-packages/pull/2687#issuecomment-965714874
options=('!zipman')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver}
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-strace.cc-Don-t-set-MSYS-noglob.patch
        0011-Add-debugging-for-strace-make_command_line.patch
        0012-strace-quiet-be-really-quiet.patch
        0013-path_conv-special-case-root-directory-to-have-traili.patch
        0014-When-converting-to-a-Unix-path-avoid-double-trailing.patch
        0015-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch
        0016-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0019-Add-debugging-for-build_argv.patch
        0020-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0021-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0022-Introduce-the-enable_pcon-value-for-MSYS.patch
        0023-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0024-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0025-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0026-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0027-kill-kill-Win32-processes-more-gently.patch
        0028-Cygwin-make-option-for-native-inner-link-handling.patch
        0029-docs-skip-building-texinfo-and-PDF-files.patch
        0030-install-libs-depend-on-the-toollibs.patch
        0031-POSIX-ify-the-SHELL-variable.patch
        0032-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0033-uname-allow-setting-the-system-name-to-CYGWIN.patch
        0034-Pass-environment-variables-with-empty-values.patch
        0035-Optionally-disallow-empty-environment-values-again.patch
        0036-build_env-respect-the-MSYS-environment-variable.patch
        0037-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch
        0038-Avoid-sharing-cygheaps-across-Cygwin-versions.patch
        0039-uname-report-msys2-runtime-commit-hash-too.patch
        0040-Cygwin-find_fast_cwd-don-t-run-assembler-checking-co.patch
        0041-cygthread-suspend-thread-before-terminating.patch
        0042-Cygwin-revert-use-of-CancelSyncronousIo-on-wait_thre.patch
        0043-Cygwin-cache-IsWow64Process2-host-arch-in-wincap.patch
        0044-Cygwin-uname-add-host-machine-tag-to-sysname.patch
        0045-Cygwin-signal-Do-not-handle-signal-when-__SIGFLUSHFA.patch
        1001-Revert-Cygwin-signal-Do-not-handle-signal-when-__SIG.patch
        1002-Cygwin-cygwait-Make-cygwait-reentrant.patch
        1003-Cygwin-signal-Do-not-handle-signal-when-__SIGFLUSHFA.patch
        1004-Cygwin-signal-Avoid-frequent-TLS-lock-unlock-for-SIG.patch)
sha256sums=('0c6fcf91be78369e753deb1963b4a04e0db613194e70a7ec653774b028adf509'
            '76e37d572d2aba473aab8f5a1984af2084e6069f9195795c71bc45778edbd1eb'
            '5c79b09f9337cc8a5f993db6dd1f54df269f8390ab3348a94e5a139a5d060e39'
            'ad84aab9a9f88b5f7510f04b33b77bbe380ce48e29a9a4feb54b485a3def3114'
            '89efe4ed3c93ae1508376d33cd775bd4275b2c02c51b754d4f29f6f9bbeeb1c5'
            'ea9df2911864c80cc70ff1c8026b22d47cbabcdcbad82ff072188b0d651eeb34'
            'a5984a95c83c782e02f1db14ff0789c79474a6197d33ecf849fab5dda95e581a'
            'c0172da962a94e5d2f1ba325967a91c5f037cc00f6aeffaeeebec7858931c2a9'
            'cd600a8b45ee7fe14c932f81b27c4e051b1ea8244258bf0172788df863011963'
            '1668b401e4ca68257663623fa7de7994b4499b0c183c495a6b07a71c81d4ff64'
            '6d03e2d13e2e5b87eb9d58fdcc9b61917da170a5052e1cba048e484f736b8604'
            'e2f2bcb5c3ea0d46e33b4eb938f858e7e302e7d3d45d5bf638a3522db57c3dc1'
            '4e24df7714b810c7451a90eb27f8d7c94eeefa42e314019517445f2963672d62'
            '77fd82e00ece7cced83494ca92b78b1c2df4fd903f4b5263e2d3bc3de557c03d'
            'ef149a162cdb6cca098640aaae73b270e0df08e016a8f66c5c666d7e69f1495e'
            '2a12a835c30ea729830df30e45135ef83476e2fb9cf5ba3969d65a9ba7a901ca'
            '0dcff7c8a47d79f6a7b432623475f5cf241221176a58acbc9438c5d0f6dd0640'
            '3c8baa45e81b329ac2737dd09861910cd42435b05ae49c2b343c62564c6f1b6d'
            'ead4e40ad7a5efe0ff4f2c0ece682ea8bbc0e5b2013c7c1bbc41c1160a79818d'
            '6149aa6901bad4c4b62ebb1b270ca13506491e299d60ae5f327a81882f791a4f'
            '3a686b4baae983fed1996109ec86f78091bbd65a3163ffe0a73e3e283004baf7'
            'ffd7319e19839cabe6d048775d258818b82631eeaa62475850220ba63062447a'
            '020ef4fd46dc3c48a9c3d7f4914860363d8d1012c27fc935cae0d2ac638cb211'
            '3b473683e9f1de616f41542e2e46dbf3212d45a62f1cca34daba6a618d235756'
            '91b46b4f1b79024b50e75113089612248336e07733930960fff5e6cddb3bbff0'
            '6e4e2e0ed960fd014d11f92a8a4b8ca96bf1a47249f104809fd549b7ee1b7129'
            '6c5bdca8f8c3655db90216a13405bf38c4bc504ccf6bc60ab277fc859d99776d'
            'a992b1fb7243c5b3e0cf6a7848dd5d4b6a5fa6bfd85ac13b744a09b812770c1f'
            '08540ea1bf7d23cff0d239c99771ab231927576845187f3b55ebbae3abc2e1a4'
            'd163f7e899099964b0c67af7597bc08d4e887813ccba1a08610bbcb06beacda1'
            '6c958541e116200d5cc2d407bdabf14825a66d11e80f8937ec4dbdcb7f9bc570'
            'd0b701bf91d8cd70f4e8ef2f753f03668d176ae5b5b41b7304d666d9a4b09cb4'
            'd2be4b5d9449711b1b52fe81aa8b41f71e1071cff19e5db43a6ed9e7cd6b2e19'
            '0752d0df9c493be8eabcfb6bd3fd49e3cddb3828b38c3ae828945dcb8681744e'
            'a6660ecb7a7f02a3e23ef0bfef234d477412da54e56446c24db39dc1faf50c88'
            'd499d11b94c71201945915107c2ed55384dae60a5c6284a5bb685798fe58276c'
            '08207bdb9af01cffacb859be228dfa9a7574deb26a7d08fcf7e86a0c551db059'
            '4ce0b8c8e05d52030a1411f36b5b4075db90d3450380663c4f46ffbc28553b9e'
            '7bb4f2d31cc77492c0df7ea16114edefbb8c79e305eae22610c0298759704626'
            'e8f0340048346be4ef0e49f24433087c4ff2eff0aac4803cb42dc85a76ddaf1c'
            'a7dc3d4b8a170338e2fea44b3efb7f9b299d1846edb8f6a6ba9375a6d53e88f1'
            'eac80fb3b54dced1e982c01a755c1914eccd1f8d74b7b88a0af43819ab317a2f'
            '29c3412a1c7b0e4e719b64337ba5508b141037884ba96e9bee5f8ea253811aa3'
            '7064362256cb558fe443469b5f9988ca927667b7cf13f1c1020aca98e616f900'
            'fb6c38381eb4f36338a5184f79c98e6046c9ff741da37f2f38f4757c8714ca92'
            '84bbe9320fe9fdc8bf6af88c9ae68eaff4bc8e162ba58ba7422bbd053e88a986'
            'd967076779bd2c658eecba139ca281f9a0e5aefbcea5bca3ed95f335775a9121'
            '3ff2d2263ef462a380aa4adf93ed107ecc334c904a60376842d69665ecd54e46'
            '7d166a00cc728c5b750205c3d1c0ae4d379a1abcb0cefd10be693e226db3a77a'
            '5e5b15c83b7fecd3afb51cca2df9009e76f616a9d2b166d2ef0682d414282647')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
 apply_git_am_with_msg 0001-Add-MSYS2-triplet.patch \
  0002-Fix-msys-library-name-in-import-libraries.patch \
  0003-Rename-dll-from-cygwin-to-msys.patch \
  0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch \
  0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch \
  0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch \
  0009-Do-not-convert-environment-for-strace.patch \
  0010-strace.cc-Don-t-set-MSYS-noglob.patch \
  0011-Add-debugging-for-strace-make_command_line.patch \
  0012-strace-quiet-be-really-quiet.patch \
  0013-path_conv-special-case-root-directory-to-have-traili.patch \
  0014-When-converting-to-a-Unix-path-avoid-double-trailing.patch \
  0015-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch \
  0016-path-conversion-Introduce-ability-to-switch-off-conv.patch \
  0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0019-Add-debugging-for-build_argv.patch \
  0020-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0021-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch \
  0022-Introduce-the-enable_pcon-value-for-MSYS.patch \
  0023-popen-call-usr-bin-sh-instead-of-bin-sh.patch \
  0024-Expose-full-command-lines-to-other-Win32-processes-b.patch \
  0025-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch \
  0026-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch \
  0027-kill-kill-Win32-processes-more-gently.patch \
  0028-Cygwin-make-option-for-native-inner-link-handling.patch \
  0029-docs-skip-building-texinfo-and-PDF-files.patch \
  0030-install-libs-depend-on-the-toollibs.patch \
  0031-POSIX-ify-the-SHELL-variable.patch \
  0032-Handle-ORIGINAL_PATH-just-like-PATH.patch \
  0033-uname-allow-setting-the-system-name-to-CYGWIN.patch \
  0034-Pass-environment-variables-with-empty-values.patch \
  0035-Optionally-disallow-empty-environment-values-again.patch \
  0036-build_env-respect-the-MSYS-environment-variable.patch \
  0037-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch \
  0038-Avoid-sharing-cygheaps-across-Cygwin-versions.patch \
  0039-uname-report-msys2-runtime-commit-hash-too.patch \
  0040-Cygwin-find_fast_cwd-don-t-run-assembler-checking-co.patch \
  0041-cygthread-suspend-thread-before-terminating.patch \
  0042-Cygwin-revert-use-of-CancelSyncronousIo-on-wait_thre.patch \
  0043-Cygwin-cache-IsWow64Process2-host-arch-in-wincap.patch \
  0044-Cygwin-uname-add-host-machine-tag-to-sysname.patch \
  0045-Cygwin-signal-Do-not-handle-signal-when-__SIGFLUSHFA.patch

 # test patches for @tyan0
 apply_patch_with_msg \
   1001-Revert-Cygwin-signal-Do-not-handle-signal-when-__SIG.patch \
   1002-Cygwin-cygwait-Make-cygwait-reentrant.patch \
   1003-Cygwin-signal-Do-not-handle-signal-when-__SIGFLUSHFA.patch \
   1004-Cygwin-signal-Avoid-frequent-TLS-lock-unlock-for-SIG.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=stringop-truncation -Wno-error=array-bounds -Wno-error=overloaded-virtual -Wno-narrowing -Wno-use-after-free -Wno-error=maybe-uninitialized"

  # otherwise it asks git which appends "-dirty" because of our uncommited patches
  CFLAGS+=" -DCYGPORT_RELEASE_INFO=${pkgver}"

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  rm -rf "${srcdir}"/dest/etc

  # split debug info from msys-2.0.dll
  cd "${srcdir}"/dest
  objcopy --add-gnu-debuglink=/dev/null --only-keep-debug usr/bin/msys-2.0.dll usr/bin/msys-2.0.dbg
  objcopy -g --add-gnu-debuglink=usr/bin/msys-2.0.dbg usr/bin/msys-2.0.dll usr/bin/msys-2.0.dll.new
  mv -f usr/bin/msys-2.0.dll.new usr/bin/msys-2.0.dll
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  conflicts=('catgets' 'libcatgets' 'msys2-runtime-3.4' 'msys2-runtime-3.5')
  replaces=('catgets' 'libcatgets' 'msys2-runtime-3.5')

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  depends=("msys2-runtime=${pkgver}")
  conflicts=('libcatgets-devel' 'msys2-runtime-3.4-devel' 'msys2-runtime-3.5-devel')
  replaces=('libcatgets-devel' 'msys2-runtime-3.5-devel')
  # strip breaks the split debug info.  msys2/msys2-pacman#52
  options=('!strip')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

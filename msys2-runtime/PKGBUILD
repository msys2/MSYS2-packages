# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=2.5.2
pkgrel=2
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS-triplets.patch
        0002-Rename-DLL-from-cygwin-to-msys.patch
        0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
        0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
        0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0008-Do-not-convert-environment-for-strace.patch
        0009-convert-be-more-careful-to-stay-within-the-buffer.patch
        0010-convert-check-safely-whether-we-could-allocate-the-b.patch
        0011-convert-use-the-elegant-realloc-call.patch
        0012-convert-plug-potential-memory-leak.patch
        0013-convert-warn-when-we-cut-off-a-path.patch
        0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
        0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
        0016-Special-case-for-converting-root-directory-to-have-t.patch
        0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0019-strace.cc-Don-t-set-MSYS-noglob.patch
        0020-Add-debugging-for-build_argv.patch
        0021-Add-debugging-for-strace-make_command_line.patch
        0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0023-path.cc-Ignore-zero-length-exclusions.patch
        0024-machine-_types.h-__blkcnt_t-should-be-signed.patch)
sha256sums=('SKIP'
            '7b24f901bddfb4b92ed2982d8608e8a9fdd1d2e523ae028115b5f5e8d710fea4'
            '2683c45d3d964c55738dc65c9897fe6b8344ba20ead4102c432c46e382923625'
            'c335d965acc3d632f95ebabd68b1ccd29f09bc4de0be7b9eb5e8f51092a7512c'
            'aee9c77565bb076d8641f2bbf83020ead6dc5a5407489592718ea1bff9c17dba'
            '58ca67dc53b65075679f518915854590b5c14572b7b27824251d06675fd434fe'
            '0fc81a9ebb06f1b445d1feaab5cad47f8d2dc9194cc4e8a089a995b00cfdc24b'
            '7b4f1f59a1c872b453234320d1d4e4d9f232530c2c97e41016fdcd294f5cc1ae'
            'd874063ea8aa51fb63f3329daa101f16b388f9efa64495375aa353bbd6a51106'
            '769399c63146d0d49ffbf8e6fbd6b79cea9e82f9268cf134ce6599819821f424'
            '2ca7d7269f07722a631ddea0f60146fb93c1003449f0287f73ecaa34572f3c81'
            'c441aaac174f5f1511381aac687cc70d5923ad5e78f98951c010d1afef1cc630'
            'f8d7975f56d1c1ca25492ba2c3283c225d7df4508d508f776463080f0d4a084d'
            'd3ab995ac5141f38fc228800b0ef867428775df789cd6ea242ef790ccabe6f0e'
            '05e5305a4562ade558efa572176fa2632ac8a0b8c1680c904f3fcc4e900f6604'
            '0995138d7311f5515fcb33524747960624cadb2f22f8059b99f3ad5b95e0917c'
            '4ed24fcd8a4d0ac4961da8707a357ec7021d8c0c0638c83fa2734b764c897d28'
            '43a02d7c85c55e4b9abbfb0e3ffe7512eefacd4420e42686efa5ba6705a53d7b'
            'acb7bb895dc93eb4cbec81d9026b63f35dbf954093f9507629a5a8e0274aae63'
            '90d9f051cd13d1ec9c6d23adda9eed28f3219da59378d98fc9e2f0cb8de7ad48'
            'fc067fe6bb2d7d15909e41c1982fed7ebb1d2e3622acd277dbdadfc64beaa4ad'
            'fd2731afc3b6ff3d9e870e5b0a582280eeb1a1ed7bbfe944c15299112bd2eb21'
            '673680ee81c15a5750c3c56bf8803cfa4f4bd698d0b875d8e1a5d9362e4ce865'
            '8e3fdcd9c6914e5edbe415b164321ff588ec2f1259772e633629314c30eac6b7'
            '671401f0a26c53edc2b72dc9e87cf72780f64bffe3a3465f8c7c4b6760dab192')
prepare() {
  cd "${srcdir}"/msys2-runtime
  git am --committer-date-is-author-date "${srcdir}"/0001-Add-MSYS-triplets.patch
  git am --committer-date-is-author-date "${srcdir}"/0002-Rename-DLL-from-cygwin-to-msys.patch
  git am --committer-date-is-author-date "${srcdir}"/0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
  git am --committer-date-is-author-date "${srcdir}"/0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
  git am --committer-date-is-author-date "${srcdir}"/0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
  git am --committer-date-is-author-date "${srcdir}"/0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
  git am --committer-date-is-author-date "${srcdir}"/0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
  git am --committer-date-is-author-date "${srcdir}"/0008-Do-not-convert-environment-for-strace.patch
  git am --committer-date-is-author-date "${srcdir}"/0009-convert-be-more-careful-to-stay-within-the-buffer.patch
  git am --committer-date-is-author-date "${srcdir}"/0010-convert-check-safely-whether-we-could-allocate-the-b.patch
  git am --committer-date-is-author-date "${srcdir}"/0011-convert-use-the-elegant-realloc-call.patch
  git am --committer-date-is-author-date "${srcdir}"/0012-convert-plug-potential-memory-leak.patch
  git am --committer-date-is-author-date "${srcdir}"/0013-convert-warn-when-we-cut-off-a-path.patch
  git am --committer-date-is-author-date "${srcdir}"/0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
  git am --committer-date-is-author-date "${srcdir}"/0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
  git am --committer-date-is-author-date "${srcdir}"/0016-Special-case-for-converting-root-directory-to-have-t.patch
  git am --committer-date-is-author-date "${srcdir}"/0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
  git am --committer-date-is-author-date "${srcdir}"/0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
  git am --committer-date-is-author-date "${srcdir}"/0019-strace.cc-Don-t-set-MSYS-noglob.patch
  git am --committer-date-is-author-date "${srcdir}"/0020-Add-debugging-for-build_argv.patch
  git am --committer-date-is-author-date "${srcdir}"/0021-Add-debugging-for-strace-make_command_line.patch
  git am --committer-date-is-author-date "${srcdir}"/0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
  git am --committer-date-is-author-date "${srcdir}"/0023-path.cc-Ignore-zero-length-exclusions.patch
  git am --committer-date-is-author-date "${srcdir}"/0024-machine-_types.h-__blkcnt_t-should-be-signed.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb"

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('MSYS2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#

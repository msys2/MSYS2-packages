# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.3.3
pkgrel=5
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'mingw-w64-cross-crt-git'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'autotools'
             'xmlto'
             'docbook-xsl')
# re zipman: https://github.com/msys2/MSYS2-packages/pull/2687#issuecomment-965714874
options=('!zipman')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-Special-case-for-converting-root-directory-to-have-t.patch
        0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0013-strace.cc-Don-t-set-MSYS-noglob.patch
        0014-Add-debugging-for-build_argv.patch
        0015-Add-debugging-for-strace-make_command_line.patch
        0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0018-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch
        0019-strace-quiet-be-really-quiet.patch
        0020-Default-to-disable_pcon.patch
        0021-Introduce-the-enable_pcon-value-for-MSYS.patch
        0022-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0023-CI-add-a-GHA-for-doing-a-basic-build-test.patch
        0024-Set-up-a-GitHub-Action-to-keep-in-sync-with-Cygwin.patch
        0025-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0026-Disable-the-cygwin-GitHub-workflow.patch
        0027-Do-not-show-Error-dialogs-by-default.patch
        0028-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0029-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0030-kill-kill-Win32-processes-more-gently.patch
        0031-Cygwin-make-option-for-native-inner-link-handling.patch
        0032-docs-skip-building-texinfo-and-PDF-files.patch
        0033-install-libs-depend-on-the-toollibs.patch
        0034-fixup-Rename-dll-from-cygwin-to-msys.patch
        0035-Cygwin-pty-fix-bug-in-reduced-input-transfer.patch
        0036-Cygwin-path-Convert-UNC-path-prefix-back-to-drive-le.patch
        0037-Cygwin-path-Fix-path-conversion-of-virtual-drive.patch
        0038-fixup-CI-add-a-GHA-for-doing-a-basic-build-test.patch
        0039-Cygwin-fhandler_pipe-get_query_hdl_per_process-avoid.patch
        0040-POSIX-ify-the-SHELL-variable.patch
        0041-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0042-fixup-CI-add-a-GHA-for-doing-a-basic-build-test.patch)
sha256sums=('SKIP'
            'c933090fc69948f47dc6452fb9913750de9e341fd80534e7099c7a16f8391167'
            'c25197a13a4df28bd34283d830d14d1c091b6517a5f298ce0f50b14fd31b7e8f'
            '3a3da05259b0f472d47489c34c445e45458a7fe3da376853a67ba36fe08ec475'
            '24a403ee90c1545c9ff6c2a514165e528b41172375efcf0a52c11cb0881047f9'
            '0b161362d679bee3a815c59dce92181a9d8dac65fa2e538e6bd2f8c919160947'
            '008c5d1b55229edbc630ed886466d0392b48a94ece7af5f88b622a81f8635296'
            '0763ac24d51479df033dcfea9e9d35e4e3effd57e21c8c9891c2e42840e2d980'
            'c197fb040eab7f5952c979ecad28a6b77d90d3940e0e82b25c3de62ab7780f21'
            'fe757631d03c53424795f883bfba5d6a92dcb248d1f0ff4e2eb6ad3897cdbaca'
            '86a313ec911adf513574d2bdb2eb4e1be057cdc3d0dd7a66f1bd2f6307af8eb3'
            '24cb6a85e50379ab9206d29fe0c61305897b84b944391394db64e3623d682adf'
            '99b07e4665952fffae070c7247458f2b8033f4dc03d0c57d19beedd62d361f11'
            'ee03d19bba78cf4f27bc6a3f02cad1e7df232fd4638e1426c01d236cfa5d234d'
            'a0c2c18b7e95de1449acb7d6cc5b75ba59492bbf3a0d685b15b331bd2a43e120'
            'c0921f539cea440960d6b4c5f37d7074f945109ad20dfb375b9ff6f938340483'
            '7651671b39eac52b11e6ab15d4bcdec44c106b222042603351cc658a46b89699'
            '41cd66f80653c6cb54b4962d472504e9e333f92a2cf1a70e596cbb9bfaa62d36'
            'd1b8f1c0f6fff5a224458655da5a1fc755ce5406368cd4e98356a162b309cd64'
            '3682e76d282a4c5b66424636a5ace3a3b62816c22ad7c7b25b9b24f55a226fee'
            '45a9149ca2e81d6f93abaf3d077285e3565ea7b46a0ec812170708ef88c07fe4'
            '1dfb9aaafb2b6606e024ca8ebc320897d84113d5e54210a358d803ebbf537975'
            '3ae6028f7d7571fe2a8d547472cb31ca3a671cc12e1150542f6a08d7f7e572a9'
            'f15cefe3d27cab7af7d0ed454a35f42e4e18a0c6cb03aff5480d47bd58e460f7'
            'bf9bef389d3dca6f925ee07fee171fc1af81ad4c8f1451973906866ed4626a68'
            'f8feea53ad3144715b43dd5674d43f8752b5aee6ab024e1446027e97ddd44733'
            'eb0d8a021d62ab7e3d8b021548765c860a0f9cae3953d9147134b9d021bc3701'
            '488fcbb7111592810399ef3a794d6806c0073ea29a632b17c0dbcffe59f59d73'
            '14deab2049942144413c1499b1d4cfdab4bb8707ad247102ec13e349c8e57779'
            'c04dd4fe9cb9ef9bbfc3cfd0a903b3f59205824fb41fbebf51d113775dbe189b'
            '289e90d99d31dffca18dd1bf989198dad9a2dd43c8b7b9621df8a057236212f6'
            'e2cc8ad38c62f83cfa63f1935b4aae9454953078e07bd7d5718f775dd398ae46'
            '4c3fcaa7f63581488504d321bca7a475153b5a1327e913ddbeb2f80443f16757'
            'db27ac00a4f1f5621cfcbbb215cd2b8af629d347b7d1de56145f18360fb90a58'
            '8257cf1beeb00b95eb1ac0a913de3aa1844ee7ea1b16cee59ecd205cf7deab9f'
            '8545ac529b2fa7d33b92735c3371a16b0377b5860c94254b78882da70af143f0'
            '8f0932a0595d54ab3ba50bc105c595f0083bb22eac4524fd69d624efce79f666'
            '3dae0b2dbbf30c0b6c3ba55a64a897d46cb7ddd27fb8f4f25306159edbef0feb'
            'bf719010c001455812dca85f54917668feaae88d2c3ff339754e6dc8b5cb0aa5'
            '2d1f19222e917caae30f5640e18e812b92995533b181b0d1e315cec5bfc8feb1'
            '4db81cd6dcabb23eb7e4b14781ce368021156b9dfc02397dc824407187687fd6'
            '5517535ed08b2d28144311eb871c3d451635788bf7dc0b5ac0ebb66d44939ea9'
            'c20f5238987778a6a46051e6b0c489de8fde7f7428b15bb8254f358b4b70f383')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git am --committer-date-is-author-date "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
 apply_git_am_with_msg 0001-Add-MSYS2-triplet.patch \
  0002-Fix-msys-library-name-in-import-libraries.patch \
  0003-Rename-dll-from-cygwin-to-msys.patch \
  0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch \
  0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch \
  0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch \
  0009-Do-not-convert-environment-for-strace.patch \
  0010-Special-case-for-converting-root-directory-to-have-t.patch \
  0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0013-strace.cc-Don-t-set-MSYS-noglob.patch \
  0014-Add-debugging-for-build_argv.patch \
  0015-Add-debugging-for-strace-make_command_line.patch \
  0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch \
  0018-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch \
  0019-strace-quiet-be-really-quiet.patch \
  0020-Default-to-disable_pcon.patch \
  0021-Introduce-the-enable_pcon-value-for-MSYS.patch \
  0022-popen-call-usr-bin-sh-instead-of-bin-sh.patch \
  0023-CI-add-a-GHA-for-doing-a-basic-build-test.patch \
  0024-Set-up-a-GitHub-Action-to-keep-in-sync-with-Cygwin.patch \
  0025-Expose-full-command-lines-to-other-Win32-processes-b.patch \
  0026-Disable-the-cygwin-GitHub-workflow.patch \
  0027-Do-not-show-Error-dialogs-by-default.patch \
  0028-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch \
  0029-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch \
  0030-kill-kill-Win32-processes-more-gently.patch \
  0031-Cygwin-make-option-for-native-inner-link-handling.patch \
  0032-docs-skip-building-texinfo-and-PDF-files.patch \
  0033-install-libs-depend-on-the-toollibs.patch \
  0034-fixup-Rename-dll-from-cygwin-to-msys.patch \
  0035-Cygwin-pty-fix-bug-in-reduced-input-transfer.patch \
  0036-Cygwin-path-Convert-UNC-path-prefix-back-to-drive-le.patch \
  0037-Cygwin-path-Fix-path-conversion-of-virtual-drive.patch \
  0038-fixup-CI-add-a-GHA-for-doing-a-basic-build-test.patch \
  0039-Cygwin-fhandler_pipe-get_query_hdl_per_process-avoid.patch \
  0040-POSIX-ify-the-SHELL-variable.patch \
  0041-Handle-ORIGINAL_PATH-just-like-PATH.patch \
  0042-fixup-CI-add-a-GHA-for-doing-a-basic-build-test.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  options=('!strip')
  conflicts=('catgets' 'libcatgets')
  replaces=('catgets' 'libcatgets')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')
  conflicts=('libcatgets-devel')
  replaces=('libcatgets-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#

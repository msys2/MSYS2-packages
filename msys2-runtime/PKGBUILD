# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=2.11.1
pkgrel=2
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-crt-git'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS-triplets.patch
        0002-Rename-DLL-from-cygwin-to-msys.patch
        0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
        0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
        0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0008-Do-not-convert-environment-for-strace.patch
        0009-convert-be-more-careful-to-stay-within-the-buffer.patch
        0010-convert-check-safely-whether-we-could-allocate-the-b.patch
        0011-convert-use-the-elegant-realloc-call.patch
        0012-convert-plug-potential-memory-leak.patch
        0013-convert-warn-when-we-cut-off-a-path.patch
        0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
        0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
        0016-Special-case-for-converting-root-directory-to-have-t.patch
        0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0019-strace.cc-Don-t-set-MSYS-noglob.patch
        0020-Add-debugging-for-build_argv.patch
        0021-Add-debugging-for-strace-make_command_line.patch
        0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0023-path.cc-Ignore-zero-length-exclusions.patch
        0024-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
)
sha256sums=('SKIP'
            '3b16cf413fe49254902d18faf96787f206c1dbc992ffff9c4e4d4230485c1bf9'
            '45484264acedba213c14a230137a10a937d4476e1930f66a6531c8fec9f2be37'
            '57cfab9f43b12a7dc0fe120032d9c811ee0db6efdc6c88dc08c72d5f278a1d09'
            'a35b0bd423a516a915651ec6474565eb70c8f52c04f6bbcfe63ad6f2e68a52d4'
            'aeb7f3185277c81d8b0a7481588a531dcf3b04ae4336855fb7c692ca4b657d70'
            'c05bf13f03f88c1bbaa25451319c7e81a74fb4f487deac337af6e12febc9d91d'
            'd98ac11f1e483d2ed9adc30e6e9f4f70f2d8544f24ec79da0e15cb6a9fff272c'
            '6c736e1efe35423bc6449374e2d33fac666ffa43ea2c8f5920c34e6fd74595a9'
            'ac92806d9e413ad1bbcedbb2eb701067765413cd528107e90d070ed640af0a7e'
            'e78cdb2410ed5c04c51fb28b4a0760ffb840f0ddf60b933de91d1da7dd5514e7'
            '85e6eaf55fe052960913293e4a901812b9dddece2a398e97dcc4e2e64ea5be4b'
            '80e4240155b5b93ffa87edde9eaffa2a47e6c0021d03858f99206aaa43403de0'
            'b975234d6aedb25f61f7a27c4953b96fd12412e1b4e8c4bd5c6a9aaf1c24ae29'
            '6a35ee32f7dd30553f735606d8efc9080a6d89f5972534545dd3e7097163f6b2'
            '22a6b289fe7b2cc79254c08eb6a436e402ae67d7c0561ee790b73634ea22a8bb'
            'a75f07bb64328a2ae814b17d3dbc894f164a9169d70a402aaf6499604bc5b466'
            '2672c1abe34f43523d609cffce8db09c3e57dce15379acee70368421cc386f14'
            'def566a9cf65b77cd017603aaa1f20e0969ee0cbdcfd5908bcbb44b4cd5d2474'
            'bab265d3a67d38cac15b9cb260dfaf48778388a879cba21744de7900b406151e'
            '4edfc95f0a8b2be0981cfbfea6be662312331915899734a19de96703f4c2b4c7'
            '65cc92f23946f76552be855f60b00cc518f5abc387cb71bd180a6a1e96719fbd'
            '16a87c39f043da71b5f7095eaa697a3631782957ccb034cbf1d1d6d53a6ec9b2'
            'a3dfc8f8320bcd224072ec2901cc8eaaff2b756ef9d038d387ec1316bc77832e'
            '50d8a52cc33501898e7a01b00e8e5965c1ef7f37f26039a4380788ce7639a04c')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git am --committer-date-is-author-date "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h 
 apply_git_am_with_msg 0001-Add-MSYS-triplets.patch \
  0002-Rename-DLL-from-cygwin-to-msys.patch \
  0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch \
  0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch \
  0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch \
  0008-Do-not-convert-environment-for-strace.patch \
  0009-convert-be-more-careful-to-stay-within-the-buffer.patch \
  0010-convert-check-safely-whether-we-could-allocate-the-b.patch \
  0011-convert-use-the-elegant-realloc-call.patch \
  0012-convert-plug-potential-memory-leak.patch \
  0013-convert-warn-when-we-cut-off-a-path.patch \
  0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch \
  0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch \
  0016-Special-case-for-converting-root-directory-to-have-t.patch \
  0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0019-strace.cc-Don-t-set-MSYS-noglob.patch \
  0020-Add-debugging-for-build_argv.patch \
  0021-Add-debugging-for-strace-make_command_line.patch \
  0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0023-path.cc-Ignore-zero-length-exclusions.patch \
  0024-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch

}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb"

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  conflicts=('catgets' 'libcatgets')
  replaces=('catgets' 'libcatgets')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('msys2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')
  conflicts=('libcatgets-devel')
  replaces=('libcatgets-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#

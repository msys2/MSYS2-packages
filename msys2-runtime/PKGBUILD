# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.5.6
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('x86_64')
url="https://www.cygwin.com/"
license=('GPL')
msys2_references=(
  'cygwin: cygwin'
  "cpe: cpe:/a:cygwin:cygwin"
)
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'mingw-w64-cross-crt'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'autotools'
             'xmlto'
             'docbook-xsl')
# re zipman: https://github.com/msys2/MSYS2-packages/pull/2687#issuecomment-965714874
options=('!zipman')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver}
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-strace.cc-Don-t-set-MSYS-noglob.patch
        0011-Add-debugging-for-strace-make_command_line.patch
        0012-strace-quiet-be-really-quiet.patch
        0013-path_conv-special-case-root-directory-to-have-traili.patch
        0014-When-converting-to-a-Unix-path-avoid-double-trailing.patch
        0015-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch
        0016-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0019-Add-debugging-for-build_argv.patch
        0020-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0021-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0022-Introduce-the-enable_pcon-value-for-MSYS.patch
        0023-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0024-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0025-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0026-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0027-kill-kill-Win32-processes-more-gently.patch
        0028-Cygwin-make-option-for-native-inner-link-handling.patch
        0029-docs-skip-building-texinfo-and-PDF-files.patch
        0030-install-libs-depend-on-the-toollibs.patch
        0031-POSIX-ify-the-SHELL-variable.patch
        0032-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0033-uname-allow-setting-the-system-name-to-CYGWIN.patch
        0034-Pass-environment-variables-with-empty-values.patch
        0035-Optionally-disallow-empty-environment-values-again.patch
        0036-build_env-respect-the-MSYS-environment-variable.patch
        0037-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch
        0038-Avoid-sharing-cygheaps-across-Cygwin-versions.patch
        0039-uname-report-msys2-runtime-commit-hash-too.patch
        0040-Cygwin-find_fast_cwd-don-t-run-assembler-checking-co.patch
        0041-cygthread-suspend-thread-before-terminating.patch
        0042-Cygwin-revert-use-of-CancelSyncronousIo-on-wait_thre.patch
        0043-Cygwin-cache-IsWow64Process2-host-arch-in-wincap.patch
        0044-Cygwin-uname-add-host-machine-tag-to-sysname.patch
        0045-fixup-Instead-of-creating-Cygwin-symlinks-use-deep-c.patch)
sha256sums=('1035636f3caa8539553c24ed0a090fa4bcb8cf76c78e30d768fa1021161113c7'
            '4d9d6129996fc892423dd60fd514fc2cecc60f93d2fed1b8d72fd2a82a5afc16'
            'e55d880a60e0892e53d78b26527e127f58e4fac810b207e190047ac80fe0ca79'
            '2495f387d6ad710ee8b332fb8314b82a9b4385a5e5db370bcd2921e712317866'
            '3c5e89ed37babacfed6a3c97d2ceeb8c2ad82690fadce9d16715829e11d4efc1'
            '2ad9cc463746f4721fc9f484b30ee538e9500183a18ffd3ce76683965da7cbf0'
            'f764ef5b1e2f17078962d71940f8864ae42fc383f05d8d774e74c2368a32da21'
            'a1cf49a723674218d79bbc48eaf0d8597545f59975870afcf9a26c70b8a37764'
            'ead7ea131ea4302e891fee321aff226c2f588fe6be9e9b7f41ddb75f6da9041c'
            '9f0c9d14ffa56f38912ac7fb4231bf42566f406a96af09c8e386199d35221456'
            '2d92d1f6627e233fa8609b394ddb69c1c728ffbb6140bc1edce99c5bbdf01ba2'
            '712723b6064340db9e6d24ac13bedf4910c8d73c772a65d28c19b342b3015762'
            '570e64abea846f178d67684e32351b91e26486cc0b65ea306f10bc5ac44a326c'
            '465593bbb17728269af88b2b02a26e93925637a20672486ba933771e455948e2'
            '3a1e9ea0788715df94e23f9840236d5868f64f608fd1e9eeafcf4bacaf32301e'
            '3f4bc4edcbfe3daf0fbd3532fb061ece7bba793dd933cc439a0a3ab8f032f217'
            '95c6c9db4f6cb34a3d8bfaabec72ba8304a6aa422bddeb86ce4af5aa247ff568'
            '674a02bba18d551a79be65b57b64f301d04e477e6d1c8328e123ae9d755f5ac1'
            '5aea7b8233767d28beb429cd421cc36a5f60922c2a47e98a48a7dda3470abd3e'
            'bd2e42a471eefc6a5f183c3702fe7b445b2f856e9dcc88c5d64c1bdefce6f7f0'
            'a6308256684ed7d5870524c4510cd56107f7a8d1c04760ad2eceed474ba49197'
            'abf962400c92812e8bfa532e580598183d5862b2c7567ef1abf29f9d6dcacf42'
            '3d9d64b1cb418593f993b40733f8ae185348ed58bfc2334f36f65433f8a43a35'
            '0dd22ac8d64ec4b03c50d2711fb00c79229cc32dbf1dae480f70dffe5f22e55d'
            '5ed0f39dbcee791fda02ebbded283b7499783dd01fcd1553e807638bbe044beb'
            'ececbb13e83615c3f9976b96ecc0995ea7f56afee54a2cd0789d4e6212020b63'
            'e3354337b0ae053d0dc0d36a8574142f2a38242508bc6278f41041bb32271c63'
            '49ddf1d0643e07a269f02b097f8485b2a015314984603d0086a2993fc5dd11b7'
            '4e75f48b41bd408a210100a8d8e5c156ccdba961e2e0a618976cef0570944449'
            'f92b9e718294c7de4a25d1068060c99823d8085e134a7f01bacfb5425c6b2a3b'
            'fabe5d1cdec4a2061fb3f39f4192c8d981fb6af6d457f449b8d103441d09488c'
            '94011b74f84f4cb0c0b532258bfdf98e98db2352fd753a116b800c866e14cf71'
            'f2ea4c8201dad51ccd6e3e4555efb11bf88c67d964e47f5dd6b022bee4f7545f'
            '789934f015312e8a37bdce2fafb3942a47fec6d71745173d250d94465c56a9d1'
            '030466b160394aa46cad53ff5173c533328de53df00b7556942ea27aeae33c61'
            '5ce12bf75970111f7444ddbb3f7830665eb255c7db983d79d2e9a714c5833365'
            'd1464b20b494685e7a614a35299c0c56f9841e95d6ef5ac4d6f76eaf62297c41'
            '2fbf033ae55d9455e8d274ae64dcbec6275c4451ab16e28165a979ab6a5c2cbf'
            '388b3a6b7220431e815c1a0ff9c93a80cfc6cefe6e113f02ab8dd40ffa6da583'
            'db575e96a72489d4d0f210d1c19e0f3a985ad7395384dd492900c4a0fff9db9c'
            '5cb4b6241697d96d32f1f201fb0d3e862525ed4673d9c5f1ceb755ce4c4e136e'
            '0c080dfa9f97e176fbbb0f6d0c534a43c3f04578710d8db3be52326df0615c39'
            'fb75643be37fbfc13c502d5a00374aba51bec31159281fd7dc46b35d4194b14c'
            '02b61d28f0bd16269ec40db99a3d959b76f2127c26b460203015052be50e6d9a'
            'b6349ec52a0756e301dd89e9004091ec8cbdb28186ef7e31eaf362fd0512659d'
            '37f67039c055f232645190fd1bf4feb023bf5bec0a45475dc5aab3224b63ce0f')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
 apply_git_am_with_msg 0001-Add-MSYS2-triplet.patch \
  0002-Fix-msys-library-name-in-import-libraries.patch \
  0003-Rename-dll-from-cygwin-to-msys.patch \
  0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch \
  0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch \
  0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch \
  0009-Do-not-convert-environment-for-strace.patch \
  0010-strace.cc-Don-t-set-MSYS-noglob.patch \
  0011-Add-debugging-for-strace-make_command_line.patch \
  0012-strace-quiet-be-really-quiet.patch \
  0013-path_conv-special-case-root-directory-to-have-traili.patch \
  0014-When-converting-to-a-Unix-path-avoid-double-trailing.patch \
  0015-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch \
  0016-path-conversion-Introduce-ability-to-switch-off-conv.patch \
  0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0019-Add-debugging-for-build_argv.patch \
  0020-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0021-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch \
  0022-Introduce-the-enable_pcon-value-for-MSYS.patch \
  0023-popen-call-usr-bin-sh-instead-of-bin-sh.patch \
  0024-Expose-full-command-lines-to-other-Win32-processes-b.patch \
  0025-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch \
  0026-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch \
  0027-kill-kill-Win32-processes-more-gently.patch \
  0028-Cygwin-make-option-for-native-inner-link-handling.patch \
  0029-docs-skip-building-texinfo-and-PDF-files.patch \
  0030-install-libs-depend-on-the-toollibs.patch \
  0031-POSIX-ify-the-SHELL-variable.patch \
  0032-Handle-ORIGINAL_PATH-just-like-PATH.patch \
  0033-uname-allow-setting-the-system-name-to-CYGWIN.patch \
  0034-Pass-environment-variables-with-empty-values.patch \
  0035-Optionally-disallow-empty-environment-values-again.patch \
  0036-build_env-respect-the-MSYS-environment-variable.patch \
  0037-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch \
  0038-Avoid-sharing-cygheaps-across-Cygwin-versions.patch \
  0039-uname-report-msys2-runtime-commit-hash-too.patch \
  0040-Cygwin-find_fast_cwd-don-t-run-assembler-checking-co.patch \
  0041-cygthread-suspend-thread-before-terminating.patch \
  0042-Cygwin-revert-use-of-CancelSyncronousIo-on-wait_thre.patch \
  0043-Cygwin-cache-IsWow64Process2-host-arch-in-wincap.patch \
  0044-Cygwin-uname-add-host-machine-tag-to-sysname.patch \
  0045-fixup-Instead-of-creating-Cygwin-symlinks-use-deep-c.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=stringop-truncation -Wno-error=array-bounds -Wno-error=overloaded-virtual -Wno-narrowing -Wno-use-after-free -Wno-error=maybe-uninitialized"

  # otherwise it asks git which appends "-dirty" because of our uncommited patches
  CFLAGS+=" -DCYGPORT_RELEASE_INFO=${pkgver}"

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  rm -rf "${srcdir}"/dest/etc

  # split debug info from msys-2.0.dll
  cd "${srcdir}"/dest
  objcopy --add-gnu-debuglink=/dev/null --only-keep-debug usr/bin/msys-2.0.dll usr/bin/msys-2.0.dbg
  objcopy -g --add-gnu-debuglink=usr/bin/msys-2.0.dbg usr/bin/msys-2.0.dll usr/bin/msys-2.0.dll.new
  mv -f usr/bin/msys-2.0.dll.new usr/bin/msys-2.0.dll
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  conflicts=('catgets' 'libcatgets' 'msys2-runtime-3.4' 'msys2-runtime-3.5')
  replaces=('catgets' 'libcatgets' 'msys2-runtime-3.5')

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  depends=("msys2-runtime=${pkgver}")
  conflicts=('libcatgets-devel' 'msys2-runtime-3.4-devel' 'msys2-runtime-3.5-devel')
  replaces=('libcatgets-devel' 'msys2-runtime-3.5-devel')
  # strip breaks the split debug info.  msys2/msys2-pacman#52
  options=('!strip')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

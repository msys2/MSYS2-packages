# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgname=python-fastbencode
pkgver=0.3.1
pkgrel=1
pkgdesc='Implementation of bencode with optional fast C extensions'
arch=('i686' 'x86_64')
url='https://github.com/breezy-team/fastbencode'
msys2_references=(
  "purl: pkg:pypi/fastbencode"
)
license=(spdx:GPL-2.0-or-later)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-devel
  cython
  gcc
)
checkdepends=(python-pytest)
source=(https://pypi.io/packages/source/f/fastbencode/fastbencode-$pkgver.tar.gz
        "https://github.com/breezy-team/fastbencode/commit/287a8ae762fd3e6d532cda7b899d08ec9859f84a.patch")
sha256sums=('5fe0cb7d1736891af61d2ade40ce948941d46e908b16f5383f55f127848da197'
            '9b5e6689f9d81cc92a32cfc760bf37c1403f93e0718a9625cc963e652c7a2a4b')

prepare() {
  cd fastbencode-$pkgver

  # Fix build with Cython 3.1.0
  patch -p1 < ../287a8ae762fd3e6d532cda7b899d08ec9859f84a.patch
}

build() {
  cd fastbencode-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd fastbencode-$pkgver
  python -m pytest
}

package() {
  cd fastbencode-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

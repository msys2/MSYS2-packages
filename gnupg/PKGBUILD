# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gnupg
pkgver=2.2.28
pkgrel=2
pkgdesc='Complete and free implementation of the OpenPGP standard'
provides=('dirmngr' "gnupg2=${pkgver}")
url='https://gnupg.org/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('gettext-devel'
             'libassuan-devel'
             'libbz2-devel'
             'libcurl-devel'
             'libgcrypt-devel'
             'libgnutls-devel'
             'libgpg-error-devel'
             'libiconv-devel'
             'libksba-devel'
             'libnettle-devel'
             'libnpth-devel'
             'libp11-kit-devel'
             'libreadline-devel'
             'libsqlite-devel'
             'libtasn1-devel'
             'libunistring-devel'
             'nettle'
             'tar'
             'zlib-devel')
optdepends=('curl: gpg2keys_curl')
depends=('bzip2'
         'libassuan'
         'libbz2'
         'libcurl'
         'libgcrypt'
         'libgpg-error'
         'libgnutls'
         'libiconv'
         'libintl'
         'libksba'
         'libnpth'
         'libreadline'
         'libsqlite'
         'nettle'
         'pinentry'
         'zlib'
        )
source=("https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        '0001-gnupg-2.2.8-msys2.patch'
        '0002-dirmngr-Only-use-SKS-pool-CA-for-SKS-pool.patch'
        '0003-Use-keyserver.ubuntu.com-as-the-default-keyserver.patch'
        "c8b2162.patch::https://git.gnupg.org/cgi-bin/gitweb.cgi?p=gnupg.git;a=patch;h=c8b2162")
sha256sums=('6ff891fc7583a9c3fb9f097ee0d1de0a12469d4b53997e7ba5064950637dfaec'
            'SKIP'
            'cb23f1a61fd213c25e85b6ba8afb190b7da14a8cfa59cc56ce82df941db8c3c9'
            '33b6b7c9f71232794326780c55450079147f65f61c8152930b2e51366ca3e843'
            '25caf05126b89f76d6849d475e0eb2d50a79ea2771250246b3c1b5daf3284995'
            '5a4e9dbc453d472eab0d0b450d2ce259038871940461a9feb07b6f0a2032550c')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'
              '46CC730865BB5C78EBABADCF04376F3EE0856959'
              '031EC2536E580D8EA286A9F22071B08A33BD3F06'
              '5B80C5754298F0CB55D8ED6ABCEF7E294B092E28'
              '6DAA6E64A76D2840571B4902528897B826403ADA')
install=${pkgname}.install

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed '/noinst_SCRIPTS = gpg-zip/c sbin_SCRIPTS += gpg-zip' -i tools/Makefile.in

  patch -p1 -i ${srcdir}/0001-gnupg-2.2.8-msys2.patch

  # https://bugs.archlinux.org/task/71078#comment200369
  patch -p1 -i ${srcdir}/0002-dirmngr-Only-use-SKS-pool-CA-for-SKS-pool.patch
  patch -p1 -i ${srcdir}/0003-Use-keyserver.ubuntu.com-as-the-default-keyserver.patch

  # Upstream commit dirmngir: Fix build with --disable-ldap.
  patch -Np1 -i "${srcdir}/c8b2162.patch"

  ./autogen.sh --force

  sed 's/development_version=yes/development_version=no/' -i configure
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/gnupg \
    --disable-libdns \
    --enable-maintainer-mode

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

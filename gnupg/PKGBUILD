# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gnupg
pkgver=2.5.5
pkgrel=1
pkgdesc='Complete and free implementation of the OpenPGP standard'
provides=('dirmngr' "gnupg2=${pkgver}")
url='https://gnupg.org/'
license=('GPL')
arch=('i686' 'x86_64')
msys2_changelog_url='https://git.gnupg.org/cgi-bin/gitweb.cgi?p=gnupg.git;a=blob;f=NEWS;hb=HEAD'
msys2_documentation_url='https://www.gnupg.org/documentation'
msys2_repository_url='https://git.gnupg.org/cgi-bin/gitweb.cgi?p=gnupg.git'
msys2_issue_tracker_url='https://dev.gnupg.org/maniphest'
msys2_pgp_keys_url='https://gnupg.org/signature_key.html'
msys2_references=(
  'cygwin: gnupg2'
  'cpe: cpe:/a:gnupg:gnupg'
)
makedepends=('gettext-devel'
             'libassuan-devel'
             'libbz2-devel'
             'libcurl-devel'
             'libgcrypt-devel'
             'libgnutls-devel'
             'libgpg-error-devel'
             'libiconv-devel'
             'libksba-devel'
             'libnettle-devel'
             'libnpth-devel'
             'libp11-kit-devel'
             'libreadline-devel'
             'libsqlite-devel'
             'libtasn1-devel'
             'libunistring-devel'
             'gcc'
             'nettle'
             'tar'
             'autotools'
             'zlib-devel')
optdepends=('curl: gpg2keys_curl')
depends=('bzip2'
         'libassuan'
         'libbz2'
         'libcurl'
         'libgcrypt'
         'libgpg-error'
         'libgnutls'
         'libiconv'
         'libintl'
         'libksba'
         'libnpth'
         'libreadline'
         'libsqlite'
         'nettle'
         'pinentry'
         'zlib'
        )
source=("https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        '0001-gnupg-2.2.8-msys2.patch'
        'gnupg-2.4-avoid_beta_warning.patch'
        'gnupg-2.4-drop_import_clean.patch'
        'gnupg-2.4-revert_default_rfc4880bis.patch'
        'gnupg-2.5-Avoid-looking-for-SOCKET.patch')
sha256sums=('7afa71d72ff9aaff75a6810b87b486bc492fd752e4f77b07c41759ce4ef36b31'
            'SKIP'
            '1cf11adb8d6942a0fc0cc53b690167162976fd03e529a94c70b3310ebd62812a'
            '599f19263d56855f50ec0913a3bde589b17e9828960e6f813aac4c7b16f79c40'
            '3da55ad697f1eab22353dfe2c6c72f0a38b0b3f48945db11f7daf00db39facee'
            '2c84b6106c835d9274e2dc4e6848f74095db2487643e0d5cf15c62c8d120843a'
            '7266098ffe88d61e06affdac52821eb62c14e9da27452bc1e33e7abc55e88c98')
validpgpkeys=(
  '5B80C5754298F0CB55D8ED6ABCEF7E294B092E28' # Andre Heinecke (Release Signing Key)
  '6DAA6E64A76D2840571B4902528897B826403ADA' # Werner Koch (dist signing 2020)
  'AC8E115BF73E2D8D47FA9908E98E9B2D19C6C8BD' # Niibe Yutaka (GnuPG Release Key)
  '02F38DFF731FF97CB039A1DA549E695E905BA208' # GnuPG.com (Release Signing Key 2021)
)
install=${pkgname}.install

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # MSYS2 patches
  patch -p1 -i ${srcdir}/0001-gnupg-2.2.8-msys2.patch
  patch -p1 -i ${srcdir}/gnupg-2.5-Avoid-looking-for-SOCKET.patch

  # Arch Linux patches
  patch -p1 -i ${srcdir}/gnupg-2.4-avoid_beta_warning.patch
  patch -p1 -i ${srcdir}/gnupg-2.4-drop_import_clean.patch
  patch -p1 -i ${srcdir}/gnupg-2.4-revert_default_rfc4880bis.patch

  ./autogen.sh --force
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --build=${CYGWIN_CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/gnupg \
    --disable-libdns \
    --enable-maintainer-mode

  make MAKEINFO=true
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make MAKEINFO=true check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make MAKEINFO=true DESTDIR="${pkgdir}" install
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gnupg
pkgver=2.4.8
pkgrel=2
pkgdesc='Complete and free implementation of the OpenPGP standard'
provides=('dirmngr' "gnupg2=${pkgver}")
url='https://gnupg.org/'
license=('GPL')
arch=('i686' 'x86_64')
msys2_changelog_url='https://git.gnupg.org/cgi-bin/gitweb.cgi?p=gnupg.git;a=blob;f=NEWS;hb=HEAD'
msys2_documentation_url='https://www.gnupg.org/documentation'
msys2_repository_url='https://git.gnupg.org/cgi-bin/gitweb.cgi?p=gnupg.git'
msys2_issue_tracker_url='https://dev.gnupg.org/maniphest'
msys2_pgp_keys_url='https://gnupg.org/signature_key.html'
msys2_references=(
  'cygwin: gnupg2'
  'cpe: cpe:/a:gnupg:gnupg'
)
makedepends=('gettext-devel'
             'libassuan-devel'
             'libbz2-devel'
             'libcurl-devel'
             'libgcrypt-devel'
             'libgnutls-devel'
             'libgpg-error-devel'
             'libiconv-devel'
             'libksba-devel'
             'libnettle-devel'
             'libnpth-devel'
             'libp11-kit-devel'
             'libreadline-devel'
             'libsqlite-devel'
             'libtasn1-devel'
             'libunistring-devel'
             'gcc'
             'nettle'
             'tar'
             'autotools'
             'zlib-devel')
optdepends=('curl: gpg2keys_curl')
depends=('bzip2'
         'libassuan'
         'libbz2'
         'libcurl'
         'libgcrypt'
         'libgpg-error'
         'libgnutls'
         'libiconv'
         'libintl'
         'libksba'
         'libnpth'
         'libreadline'
         'libsqlite'
         'nettle'
         'pinentry'
         'zlib'
        )
source=("https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        '0001-gnupg-2.2.8-msys2.patch'
        gnupg-2.4-avoid_beta_warning.patch
        # patches maintained by freepg project: https://gitlab.com/freepg/gnupg/-/commits/gnupg-2.4.8-freepg
        0001-gpg-accept-subkeys-with-a-good-revocation-but-no-sel.patch
        0002-gpg-allow-import-of-previously-known-keys-even-witho.patch
        0003-tests-add-test-cases-for-import-without-uid.patch
        0004-gpg-drop-import-clean-from-default-keyserver-import-.patch
        0005-avoid-systemd-deprecation-warning.patch
        0006-Add-systemd-support-for-keyboxd.patch
        0007-Ship-sample-systemd-unit-files.patch
        0008-gpg-default-El-Gamal-to-3072-bit-keys.patch
        0009-gpg-Always-support-and-default-to-using-SHA-512.patch
        0010-gpg-Prefer-SHA-512-and-SHA-384-in-personal-digest-pr.patch
        0011-Avoid-simple-memory-dumps-via-ptrace.patch
        0013-ssh-agent-emulation-under-systemd-inject-SSH_AUTH_SO.patch
        0014-gpg-Sync-compliance-mode-cleanup-with-master.patch
        0015-gpg-emit-RSA-pubkey-algorithm-when-in-compatibility-.patch
        0016-gpg-Reintroduce-openpgp-as-distinct-from-rfc4880.patch
        0017-gpg-Emit-LibrePGP-material-only-in-compliance-gnupg.patch
        0018-gpg-gpgconf-list-report-actual-compliance-mode.patch
        0019-gpg-Default-to-compliance-openpgp.patch
        0020-gpg-Fix-newlines-in-Cleartext-Signature-Framework-CS.patch
        0021-Add-keyboxd-systemd-support.patch
        0022-Support-large-RSA-keygen-in-non-batch-mode.patch
        0023-gpg-Verify-Text-mode-Signatures-over-binary-Literal-.patch
        gnupg-2.4.8-fix-gpg-agent-memory-leak.patch)
sha256sums=('b58c80d79b04d3243ff49c1c3fc6b5f83138eb3784689563bcdd060595318616'
            'SKIP'
            '902563c91c72ed9222343de3482f4ca7b141775235625af5ad790f3d86419370'
            '243c3a79295519b3931f9d846cf2af5caa064a78de812ee336dc786c1567b4d0'
            'e07bd246e13d88df72506ef1f98aee416473c1c6674de6628ad79cc39fa999b8'
            '978a57dd8b73939f176f4eec5ca66c5f49ce639fd7aabd2a6890ec03b6a90d2b'
            '69d19d59d6b76ce454e0c3c0f297de153c9a644d6be89dbff8aefbd9b26529a2'
            '0123d0ebac003fc10f0ac5d2c26a1277966580e26b70cdd1c7233543d6b4f465'
            '2decf2d841e7e2b510de1782d2ecb9be3399c0dabed8e9a15e0c1d1d8d6e5a43'
            'c7608ba9c93cd8bab43fbee268b084beac9487a9ee487c99525cc7cf64da77a8'
            '6d64b9e872e30c33858573f07be8efa33f4c875c4b5c34fe31bf0957960022b0'
            '7c268e00d30214c8ab20c3e3e2629f17613d4470da545f8e76e2600ef80a51a4'
            'd63d6ed550628f7ad8d6e8487528aaab2093781fe9c22b077aac91580ddcae08'
            '992d8982b3d48a0549736ba52eef7651d91c569921d022fc448b38dbddb652c3'
            'd231aafd106f5443ac05a27518b7f837e3c4a22b299ae847127a87df82e139d3'
            '1670ce1d008f6dd7b5f6ffd04fba37be5a030d4113f60ff37e4dc22f28614a67'
            '6a64e9fc621b310bdd1401a8bc44aad05151a9d320b5e60143e6a813151a21bd'
            'a9990b88a2d3f36605191fee63ad713dbe21e4a2b685dbd4d170d086103e85dc'
            '5d4455cdce11a086e3bde813be7228eb34166a5e3875942bc8fb9946102ad48f'
            '52b527a0d752335902766473fa0085939e6a293cdadad8ba21586fbfe95a5524'
            'db6de8d5543e6b04578b01062b17715b796306b58bc91e471aaa001034c1dbb7'
            'd7e55304b5769fd210dbf4ac0b56f1d80090d0d5ce1f8695c670d28efd687344'
            'cc574dfdffa3c64241e56b819a2626ee80d2cd5b19c3b6794d55162c4e286e77'
            '5ad3252991b975080154971b235c493c91f21242ccd47aa8a43f57d62eac79e6'
            '9bb1ea61b1f2885e4e82cf743e065c67ee60ce8a8c135ae6b4a56536a49bcec6'
            'fe879074384037f818c5287c668d2e655bc3628299ce1ab33f8999967c88321d'
            '848266985543a65c0beec1bac16db7e0b7eb6b494a2759c690ece57a1d579511')
validpgpkeys=(
  '5B80C5754298F0CB55D8ED6ABCEF7E294B092E28' # Andre Heinecke (Release Signing Key)
  '6DAA6E64A76D2840571B4902528897B826403ADA' # Werner Koch (dist signing 2020)
  'AC8E115BF73E2D8D47FA9908E98E9B2D19C6C8BD' # Niibe Yutaka (GnuPG Release Key)
  '02F38DFF731FF97CB039A1DA549E695E905BA208' # GnuPG.com (Release Signing Key 2021)
)
install=${pkgname}.install

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # MSYS2 patches
  patch -p1 -i ${srcdir}/0001-gnupg-2.2.8-msys2.patch

  # Arch Linux/freepg patches
  patch -p1 -i ${srcdir}/gnupg-2.4-avoid_beta_warning.patch
  patch -p1 -i ${srcdir}/0001-gpg-accept-subkeys-with-a-good-revocation-but-no-sel.patch
  patch -p1 -i ${srcdir}/0002-gpg-allow-import-of-previously-known-keys-even-witho.patch
  patch -p1 -i ${srcdir}/0003-tests-add-test-cases-for-import-without-uid.patch
  patch -p1 -i ${srcdir}/0004-gpg-drop-import-clean-from-default-keyserver-import-.patch
  patch -p1 -i ${srcdir}/0005-avoid-systemd-deprecation-warning.patch
  patch -p1 -i ${srcdir}/0006-Add-systemd-support-for-keyboxd.patch
  patch -p1 -i ${srcdir}/0007-Ship-sample-systemd-unit-files.patch
  patch -p1 -i ${srcdir}/0008-gpg-default-El-Gamal-to-3072-bit-keys.patch
  patch -p1 -i ${srcdir}/0009-gpg-Always-support-and-default-to-using-SHA-512.patch
  patch -p1 -i ${srcdir}/0010-gpg-Prefer-SHA-512-and-SHA-384-in-personal-digest-pr.patch
  patch -p1 -i ${srcdir}/0011-Avoid-simple-memory-dumps-via-ptrace.patch
  patch -p1 -i ${srcdir}/0013-ssh-agent-emulation-under-systemd-inject-SSH_AUTH_SO.patch
  patch -p1 -i ${srcdir}/0014-gpg-Sync-compliance-mode-cleanup-with-master.patch
  patch -p1 -i ${srcdir}/0015-gpg-emit-RSA-pubkey-algorithm-when-in-compatibility-.patch
  patch -p1 -i ${srcdir}/0016-gpg-Reintroduce-openpgp-as-distinct-from-rfc4880.patch
  patch -p1 -i ${srcdir}/0017-gpg-Emit-LibrePGP-material-only-in-compliance-gnupg.patch
  patch -p1 -i ${srcdir}/0018-gpg-gpgconf-list-report-actual-compliance-mode.patch
  patch -p1 -i ${srcdir}/0019-gpg-Default-to-compliance-openpgp.patch
  patch -p1 -i ${srcdir}/0020-gpg-Fix-newlines-in-Cleartext-Signature-Framework-CS.patch
  patch -p1 -i ${srcdir}/0021-Add-keyboxd-systemd-support.patch
  patch -p1 -i ${srcdir}/0022-Support-large-RSA-keygen-in-non-batch-mode.patch
  patch -p1 -i ${srcdir}/0023-gpg-Verify-Text-mode-Signatures-over-binary-Literal-.patch
  patch -p1 -i ${srcdir}/gnupg-2.4.8-fix-gpg-agent-memory-leak.patch

  ./autogen.sh --force
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/gnupg \
    --disable-libdns \
    --enable-large-secmem \
    --enable-maintainer-mode

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

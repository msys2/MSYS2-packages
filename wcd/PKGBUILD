#Maintainer: Erwin Waterlander <waterlan@xs4all.nl>

pkgname=wcd
pkgver=6.0.6
pkgrel=1
pkgdesc='Wherever Change Directory'
arch=('i686' 'x86_64')
url='https://waterlan.home.xs4all.nl/'
msys2_references=(
  "anitya: 341689"
)
license=('GPL')
depends=('libintl' 'libunistring' 'ncurses')
groups=('sys-utils')
makedepends=('perl' 'gettext-devel' 'libunistring-devel' 'ncurses-devel' 'make' 'gcc')
source=("https://waterlan.home.xs4all.nl/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        querycp.patch)
sha256sums=('c5d1b22aefbb0f142e27b1cddad17021c43034c76103830fb2757387ac593eb1'
            '3c7f4d0c586f2c5bdf5b5ea02e6d1c04c631cf60619612f45373ef6f22c4f3d5')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p2  -i "${srcdir}/querycp.patch"
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}/src
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}/src
  make DESTDIR=${pkgdir} install
  make DESTDIR=${pkgdir} install-profile
  cd ${srcdir}/${pkgname}-${pkgver}/doc
  install -D -m644 copying.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

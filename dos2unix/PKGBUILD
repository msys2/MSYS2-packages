# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=dos2unix
pkgver=7.1
pkgrel=1
pkgdesc='Text file format converter'
arch=('i686' 'x86_64')
url='http://waterlan.home.xs4all.nl/dos2unix.html'
license=('BSD')
depends=('libintl')
groups=('base-devel')
makedepends=('perl')
conflicts=('hd2u')
source=("http://waterlan.home.xs4all.nl/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        0001-msysize.msys2.patch)
sha256sums=('46f93e792f7dfde06e9d8f347099eb4c4db2167b728fafcafb5ca6b5503740c5'
            '8e2daf6d51f0d2a0a2e4321de10de7da970c81444ded944ec40bf0d0cf7206cb')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 -i ${srcdir}/0001-msysize.msys2.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm755 $pkgdir/usr/bin/dos2unix.exe $pkgdir/usr/bin/d2u.exe
  install -Dm755 $pkgdir/usr/bin/unix2dos.exe $pkgdir/usr/bin/u2d.exe
  install -D -m644 COPYING.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

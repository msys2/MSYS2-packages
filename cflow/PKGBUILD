#! /usr/bin/bash

pkgname=cflow
pkgver=1.6
pkgrel=1
pkgdesc="GNU cflow"
arch=('i686' 'x86_64')
license=('GPL3')
url="https://www.gnu.org/software/cflow/"
depends=('libintl' 'libiconv')
makedepends=('flex' 'gettext-devel' 'libiconv-devel')
source=(https://ftp.gnu.org/gnu/cflow/${pkgname}-${pkgver}.tar.xz)
sha256sums=('d5392a6d8a63ba2f41df9f88b51f97bc0452c5262762a3c27e0412a6c03c06db')


prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  autoreconf -vfi
  cd ..
}

build() {
  mkdir -p build-${pkgname}-${pkgver}-${MSYSTEM_CHOST}
  cd build-${pkgname}-${pkgver}-${MSYSTEM_CHOST}
  ../${pkgname}-${pkgver}/configure -C \
    --prefix=/usr
  make
  cd ..
}

check() {
  cd build-${pkgname}-${pkgver}-${MSYSTEM_CHOST}
  make check
}

package() {
  make -C build-${pkgname}-${pkgver}-${MSYSTEM_CHOST} DESTDIR=${pkgdir} install

  # license
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}/
  cp ${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}

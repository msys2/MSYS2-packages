# Maintainer: Christoph Reiter <reiter.christoph@mail.com>

_pyname=merge3
pkgname=python-merge3
pkgver=0.0.16
pkgrel=1
pkgdesc='Python implementation of 3-way merge'
arch=(any)
url=https://github.com/breezy-team/merge3
msys2_repository_url="https://github.com/breezy-team/merge3"
msys2_references=(
  "purl: pkg:pypi/merge3"
)
license=("spdx:GPL-2.0-or-later")
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
)
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('0852de4381cb46be5ef4ed49e3ac20c5a4a0cd46a8ff4bbb870bc27aab543306')

build() {
  cd "${_pyname}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${_pyname}-${pkgver}"
  python -m unittest discover
}

package() {
  python -m installer --destdir="${pkgdir}" "${_pyname}-${pkgver}"/dist/*.whl
}

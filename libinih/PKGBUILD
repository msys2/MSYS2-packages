# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

pkgbase=libinih
pkgname=(libinih libinih-devel)
pkgver=56
pkgrel=2
pkgdesc='A simple .INI file parser written in C'
license=('spdx:BSD-3-Clause')
url='https://github.com/benhoyt/inih'
arch=('i686' 'x86_64')
makedepends=('gcc' 'pkgconf' 'python' 'meson' 'ninja')
source=(https://github.com/benhoyt/inih/archive/refs/tags/r${pkgver}.tar.gz)
sha256sums=('4f2ba6bd122d30281a8c7a4d5723b7af90b56aa828c0e88256d7fceda03a491a')

build() {
  mkdir "${srcdir}"/build && cd "${srcdir}"/build

  /usr/bin/meson \
    --wrap-mode=nodownload \
    --auto-features=enabled \
    --buildtype=plain \
    --prefix=/usr \
    -Ddefault_library=shared \
    -Ddistro_install=true \
    -Dwith_INIReader=true \
    "../inih-r${pkgver}"

  /usr/bin/meson compile

  DESTDIR="${srcdir}/dest" /usr/bin/meson install
}

package_libinih() {
  depends=('gcc-libs')
  groups=('libraries')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
}

package_libinih-devel() {
  depends=("libinih=${pkgver}")
  groups=('development')
  options=('staticlibs')
  pkgdesc="libinih headers and libraries"

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}

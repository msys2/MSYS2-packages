# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Net-SSLeay
pkgname=perl-${_realname}
pkgver=1.89_01
pkgrel=1
pkgdesc="Perl extension for using OpenSSL"
arch=('i686' 'x86_64')
license=('custom:BSD')
url="https://metacpan.org/${_realname}/"
groups=('perl-modules')
makedepends=('openssl-devel')
depends=('openssl')
options=(!emptydirs)
replaces=('net-ssleay')
provides=('net-ssleay')
source=("https://cpan.metacpan.org/authors/id/C/CH/CHRISN/${_realname}-${pkgver}.tar.gz"
        'Net-SSLeay-1.55.patch')
sha256sums=('9fa58a740565bf6032188fee57bec38f441c1b359f8927834ab4ea860767581e'
            'a81e528372aeae3d47585260e317f40f80d786fe56d295a1b0c63e90275159d3')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/Net-SSLeay-1.55.patch
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}

  export OPTIMIZE="$CFLAGS"
  export OPENSSL_PREFIX=/usr

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL -- INSTALLDIRS=vendor
  make
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}

  make install DESTDIR=${pkgdir}
  install -D -m644 README "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=crypt
pkgname=('crypt' 'libcrypt' 'libcrypt-devel')
pkgver=1.4
pkgrel=1
pkgdesc="Encryption/Decryption utility and library"
groups=('base')
arch=('i686' 'x86_64')
license=('custom')
url="https://cygwin.com/"
makedepends=('gcc' 'git' 'make')
source=(${pkgbase}-${pkgver}::git://sourceware.org/git/cygwin-apps/crypt.git#tag=${pkgbase}-${pkgver//./_}-release
        msysize.patch)
sha256sums=('SKIP'
            '13746ef5eb4d37e74b4a8ae85430e5f80836e3f1fae72cb0b5f5bd7c5c398240')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  patch -p1 -i ${srcdir}/msysize.patch
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make CC=gcc AR=ar CFLAGS="${CFLAGS}"

  make DESTDIR=${srcdir}/dest install
}

package_crypt() {
  pkgdesc="Encryption/Decryption utility"
  depends=('libcrypt')

  mkdir -p ${pkgdir}/usr/bin

  cp -f ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libcrypt() {
  pkgesc="Encryption/Decryption library"
  groups=('libraries')
  depends=('gcc-libs')
  mkdir -p ${pkgdir}/usr/bin

  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
}

package_libcrypt-devel() {
  pkgdesc="Libcrypt headers and libraries"
  groups=('development')
  depends=("libcrypt=${pkgver}")
  options=('staticlibs')

  mkdir -p ${pkgdir}/usr

  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}

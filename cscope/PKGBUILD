# Maintainer: Joï¿½o Guerra <joca.bt@gmail.com>

pkgname=cscope
pkgver=15.9
pkgrel=3
pkgdesc="A tool for browsing source code"
arch=("i686" "x86_64")
url="https://cscope.sourceforge.io/"
msys2_references=(
  "cpe: cpe:/a:cscope:cscope"
)
license=('spdx:BSD-3-Clause')
depends=('ncurses')
makedepends=("ncurses-devel" 'autotools' 'gcc')
source=("https://sourceforge.net/projects/cscope/files/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c5505ae075a871a9cd8d9801859b0ff1c09782075df281c72c23e72115d9f159')

prepare () {
  cd "${pkgname}-${pkgver}"
  sed -i 's|/usr/local/lib/cs|/usr/lib/cs|' contrib/ocs
}

build () {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CHOST/-msys/-cygwin}
  make
}

package () {
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# Maintainer: Alexandre Ferreira <contact@alexjorgef.com>

pkgname=ansible-core
pkgver=2.20.3
pkgrel=1
pkgdesc='Radically simple IT automation platform'
arch=('any')
url='https://pypi.org/project/ansible-core'
msys2_repository_url='https://github.com/ansible/ansible'
msys2_references=(
  "cpe: cpe:/a:ansible:ansible"
  "cpe: cpe:/a:redhat:ansible"
  "cpe: cpe:/a:redhat:ansible_engine"
  "purl: pkg:pypi/ansible-core"
)
license=('spdx:GPL-3.0-or-later')
depends=('python' 'python-yaml' 'python-jinja' 'python-packaging' 'python-cryptography' 'python-resolvelib'
         # msys2-runtime-devel is needed for ctypes.util.find_library("c") as it provides libc.a
         'msys2-runtime-devel')
provides=('python-ansible' 'ansible-base')
optdepends=(
	'sshpass: for ssh connections with password'
)
makedepends=('tar' 'python-build' 'python-installer' 'python-setuptools')
replaces=('ansible-base')
backup=('etc/ansible/ansible.cfg')
source=("https://pypi.python.org/packages/source/a/ansible-core/ansible_core-${pkgver}.tar.gz")
sha512sums=('f9281aa0d7a32ed81a548e26d035877efffe847673df4f8293f2ef101881a9eb64ff116a90d7291b627e0eeb488abdf297add67fccaaaa801c339dbc9218284a')
noextract=("${pkgname/-/_}-${pkgver}.tar.gz")

prepare() {
  tar zxf "${srcdir}/${pkgname/-/_}-${pkgver}.tar.gz" || tar zxf "${srcdir}/${pkgname/-/_}-${pkgver}.tar.gz"

  cd "${srcdir}/${pkgname/-/_}-${pkgver}"
}

build() {
  cd "${srcdir}/${pkgname/-/_}-${pkgver}"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd ${pkgname/-/_}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 COPYING "${pkgdir}"/usr/share/doc/${pkgname}/COPYING
}

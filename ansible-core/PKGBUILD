# Maintainer: Alexandre Ferreira <contact@alexjorgef.com>

pkgname=ansible-core
pkgver=2.15.0
pkgrel=1
pkgdesc='Radically simple IT automation platform'
arch=('any')
url='https://pypi.org/project/ansible-core'
license=('spdx:GPL-3.0-or-later')
depends=('python' 'python-yaml' 'python-jinja' 'python-packaging')
provides=('python-ansible' 'ansible-base')
optdepends=(
	'sshpass: for ssh connections with password'
)
makedepends=('tar' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
replaces=('ansible-base')
backup=('etc/ansible/ansible.cfg')
source=("https://pypi.python.org/packages/source/a/ansible-core/ansible-core-${pkgver}.tar.gz"
        "0001-ctypes-cdll-loadlibrary-msys2.patch")
sha512sums=('aebb43840380854e7759656a0b08579a8fbdddbf2d204c47fb16cdb73ffd540df5ad56e5bf4a78a338804a3d40f9b042ea50b2b1853aaef6b7c2f0aab7a95432'
            'f129850ecf75b48dd89b2f43e7f09a2585d63959be807299f5c1ddcdb12ecafafa4a230dc0d8a9c284d5d6fec21eae22eb4c0c8c5fde37a09d7bdac7b8503b98')
noextract=("${pkgname}-${pkgver}.tar.gz")

apply_patch_with_msg() {
  for _fname in "$@"
  do
    msg2 "Applying ${_fname}"
    patch -Nbp1 -i "${srcdir}"/${_fname}
  done
}

prepare() {
  tar zxf "${srcdir}/${pkgname}-${pkgver}.tar.gz" || tar zxf "${srcdir}/${pkgname}-${pkgver}.tar.gz"

  cd "${srcdir}/${pkgname}-${pkgver}"

  # Patches
  apply_patch_with_msg \
    0001-ctypes-cdll-loadlibrary-msys2.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 COPYING "${pkgdir}"/usr/share/doc/${pkgname}/COPYING

  install -d "${pkgdir}"/usr/share/ansible/doc
  cp -dpr --no-preserve=ownership ./examples "${pkgdir}"/usr/share/ansible/doc/
  install -Dm644 examples/ansible.cfg "${pkgdir}"/etc/ansible/ansible.cfg

  install -d "${pkgdir}"/usr/share/man/man1
  cp -dpr --no-preserve=ownership docs/man/man1/*.1 "${pkgdir}"/usr/share/man/man1
}

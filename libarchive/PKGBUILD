# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libarchive' 'libarchive-devel' 'bsdcpio' 'bsdtar')
pkgver=3.5.1
pkgrel=2
pkgdesc="library that can create and read several streaming archive formats"
arch=('i686' 'x86_64')
url="https://libarchive.org/"
license=('BSD')
groups=('libraries')
depends=('gcc-libs' 'libbz2' 'libiconv' 'libexpat' 'liblzma' 'liblz4' 'libnettle' 'libxml2' 'libzstd' 'zlib')
makedepends=('libbz2-devel' 'libiconv-devel' 'libexpat-devel' 'liblzma-devel' 'liblz4-devel' 'libnettle-devel'
             'libxml2-devel' 'libzstd-devel' 'zlib-devel' 'cmake' 'ninja')
source=("https://github.com/libarchive/libarchive/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        'libarchive-3.3.2-bcrypt-fix.patch'
        'libarchive-3.3.1-msys2.patch')
sha256sums=('0e17d3a8d0b206018693b27f08029b598f6ef03600c2b5d10c94ce58692e299b'
            'ba26625c5ab8bcd4cf1b671a44f2b2f9241d4a43ed3223455e168672fe97ff95'
            '239a135427e0e4ceaa992033e8b4825c69131acc919ac0f5578134c96a1bd718')

prepare() {
  cd "${pkgname}-${pkgver}"

  # msysize patch
  #patch -Np1 -i "${srcdir}/libarchive-3.3.1-msys2.patch"
  patch -Np1 -i "${srcdir}/libarchive-3.3.2-bcrypt-fix.patch"
}

build() {
  local build_type=Release
  if check_option "debug" "y"; then
    build_type=Debug
  fi

  #export lt_cv_deplibs_check_method='pass_all'

  mkdir -p "${pkgname}-${pkgver}/build-${CHOST}"
  cd "${pkgname}-${pkgver}/build-${CHOST}"

  cmake \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=$build_type \
    -DENABLE_TAR_SHARED=ON \
    -DENABLE_CPIO_SHARED=ON \
    -DENABLE_CAT_SHARED=ON \
    ..

  cmake --build .
  DESTDIR="${srcdir}/dest" cmake --install .
}

check() {
  cd "${pkgname}-${pkgver}/build-${CHOST}"
}

package_libarchive() {
  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/

  install -D -m644 ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/libarchive/COPYING
}

package_libarchive-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("libarchive=${pkgver}" 'libbz2-devel' 'libiconv-devel' 'libexpat-devel' 'liblzma-devel' 'liblz4-devel' 'libnettle-devel' 'libxml2-devel' 'libzstd-devel' 'zlib-devel')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}

package_bsdcpio() {
  depends=("libarchive=${pkgver}")
  mkdir -p ${pkgdir}/usr/bin
  mkdir -p ${pkgdir}/usr/share/man/man1/
  cp -f ${srcdir}/dest/usr/bin/bsdcpio.exe ${pkgdir}/usr/bin/
  cp -f ${srcdir}/dest/usr/share/man/man1/bsdcpio.* ${pkgdir}/usr/share/man/man1/
}

package_bsdtar() {
  depends=("libarchive=${pkgver}")
  mkdir -p ${pkgdir}/usr/bin
  mkdir -p ${pkgdir}/usr/share/man/man1/
  cp -f ${srcdir}/dest/usr/bin/bsdtar.exe ${pkgdir}/usr/bin/
  cp -f ${srcdir}/dest/usr/share/man/man1/bsdtar.* ${pkgdir}/usr/share/man/man1/
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=rebase
pkgver=4.4.1
pkgrel=6
pkgdesc="The Cygwin rebase distribution contains four utilities, rebase, rebaseall, peflags, and peflagsall."
groups=('base')
arch=('i686' 'x86_64')
license=('custom')
url="https://www.cygwin.com/"
depends=('msys2-runtime' 'dash')
makedepends=('coreutils' 'grep' 'gzip' 'sed')
# options=('debug' '!strip')
source=('rebase-4.4.1.tar.bz2'
        'rebase-4.4.1-msys2.patch'
        'autorebase.bat'
        'autorebasebase1st.bat'
        'rebaseall-add-python-exts.patch'
        'pacman-rec-filename-grep'
        'allow-non-database-mode-when-__CYGWIN__-__MSYS__.patch'
        'msys2-usr.patch')
sha256sums=('4a9288049839e89a631dab33d19edad27131e0a017f904cbf955f05e5ab56139'
            'e1e8695ebea08b1b6b1b7d746730cedff92eb15b707f67dadb2086c01e887b55'
            '639db08ad3f8027c50376a2c7aab1d4e1e12e1ff6a893aeb6c07005de426f40b'
            '207dd9880255ed2fc8864452619f7c1a3ba74c6508dfcfca475af8a6caab412a'
            'b06d561d82e7c32573082cf0207ca80275bab241c61debf2d3d8cc10cf79e31b'
            '2ca1e58fb1d2625e93224d0d1ca3b86944030ef2572289222b42ccc2387033fd'
            'b4823bfdc169744e05b9c71d513ef570b580ffc9dded4fa482ec644a557de56b'
            '825b24888bdf0e5e51691cda8a160905c6e40e991a6098664e2f892e56f87742')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i $srcdir/rebase-4.4.1-msys2.patch
  patch -p1 -i $srcdir/rebaseall-add-python-exts.patch
  patch -p1 -i $srcdir/allow-non-database-mode-when-__CYGWIN__-__MSYS__.patch
  patch -p1 -i $srcdir/msys2-usr.patch

  if check_option "strip" "n"; then
    sed -i "s/ -s//g" Makefile.in
  fi
  autoreconf -fi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    CXXFLAGS="${CXXFLAGS} -static -static-libgcc -static-libstdc++" \
    CFLAGS="${CFLAGS} -static -static-libgcc"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR=${pkgdir} install
  cp -f ${srcdir}/autorebase.bat ${pkgdir}
  cp -f ${srcdir}/autorebasebase1st.bat ${pkgdir}
  cp -f ${srcdir}/pacman-rec-filename-grep ${pkgdir}/usr/bin
}

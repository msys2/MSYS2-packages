# Contributor: Dirk Stolle

_realname=idna
pkgname=python-${_realname}
pkgver=3.11
pkgrel=1
pkgdesc='Internationalized Domain Names in Applications'
arch=('any')
url='https://github.com/kjd/idna'
msys2_repository_url='https://github.com/kjd/idna'
msys2_references=(
  'archlinux: python-idna'
  'cygwin: python-idna'
  'gentoo: dev-python/idna'
  'purl: pkg:pypi/idna'
)
license=('spdx:BSD-3-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-flit-core'
  'python-installer'
)
checkdepends=('python-pytest')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('795dafcc9c04ed0c1fb032c2aa73654d8e8c5023a7df64a53f39190ada629902')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "python-build-${MSYSTEM}"

  python -m pytest
}

package() {
  cd "python-build-${MSYSTEM}"

  python -m installer --prefix=${MSYSTEM_PREFIX} --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.md "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}

# Maintainer: Simon Kissane <skissane@gmail.com>

pkgname=mtools
pkgver=4.0.43
pkgrel=1
pkgdesc="collection of utilities to access FAT filesystems without mounting them"
arch=('i686' 'x86_64')
license=('spdx:GPL-2.0-or-later')
url="https://www.gnu.org/software/mtools"
depends=('libiconv')
makedepends=('autotools' 'gcc' 'libiconv-devel')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.lz{,.sig})
sha256sums=('997ffe4125a19de1fd433ed63f128f7d54bc1a5915f3cdb36da6491ef917f217'
            'SKIP')
validpgpkeys=('51A23D9D7C4DF41AA48FA893E99CF5537790B839')

build() {
  [[ -d ${srcdir}/build-${CHOST} ]] && rm -rf ${srcdir}/build-${CHOST}
  mkdir -p ${srcdir}/build-${CHOST} && cd ${srcdir}/build-${CHOST}

  ../${pkgname}-${pkgver}/configure \
    --build=${CHOST} \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --localstatedir=/var
  make USERLDLIBS=-"liconv"
}

package() {
  cd ${srcdir}/build-${CHOST}
  make DESTDIR=${pkgdir} install
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/README "${pkgdir}/usr/share/licenses/${pkgname}/README"
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

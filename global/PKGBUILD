# Maintainer: Jo√£o Guerra <joca.bt@gmail.com>

pkgname=global
pkgver=6.6.7
pkgrel="1"
pkgdesc="A source code tagging system"
arch=("i686" "x86_64")
url="https://www.gnu.org/software/global/"
license=('GPL3')
depends=("libltdl")
optdepends=("ctags" "python3-pygments")
makedepends=("ncurses-devel" 'autotools' 'gperf')
options=(libtool !emptydirs)
source=("https://ftp.gnu.org/gnu/global/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('69a0f77f53827c5568176c1d382166df361e74263a047f0b3058aa2f2ad58a3c'
            'SKIP')
validpgpkeys=("26F631B43D624A927E6F1C33969C3BE389DDA6EB") # Shigio YAMAGUCHI <shigio@gnu.org>

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -fiv
}

build () {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST} \
              --enable-shared \
              --disable-static \
              --with-exuberant-ctags=ctags
  make
}

package () {
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

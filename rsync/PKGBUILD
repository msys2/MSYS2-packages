# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=rsync
pkgver=3.2.7
pkgrel=2
pkgdesc="A file transfer program to keep remote files in sync"
arch=('i686' 'x86_64')
url="https://rsync.samba.org"
groups=('net-utils')
license=('GPL3')
depends=('perl' 'libiconv' 'liblz4' 'libopenssl' 'libxxhash' 'libzstd')
makedepends=('libiconv-devel' 'liblz4-devel' 'libxxhash-devel' 'libzstd-devel' 'openssl-devel' 'autotools' 'gcc')
conflicts=('rsync2')
replaces=('rsync2')
source=("https://download.samba.org/pub/rsync/src/${pkgname}-${pkgver}.tar.gz")
sha256sums=('4e7d9d3f6ed10878c58c5fb724a67dacf4b6aac7340b13e488fb2dc41346f2bb')
noextract=("${pkgname}-${pkgver}.tar.gz")

prepare() {
  tar zxf "${pkgname}-${pkgver}.tar.gz" || tar zxf "${pkgname}-${pkgver}.tar.gz"
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
      --build=${CYGWIN_CHOST} \
      --prefix=/usr
  
  make reconfigure
  make man all
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm755 support/rrsync "${pkgdir}/usr/lib/rsync/rrsync"

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

# Maintainer (Arch Linux): Florian Pritz <flo@xinu.at>

pkgname=perl-rename
pkgver=1.16
pkgrel=1
pkgdesc='Rename multiple files using Perl regular expressions'
arch=(any)
url='https://search.cpan.org/~pederst/rename/'
msys2_references=(
  'archlinux: perl-rename'
  'purl: pkg:cpan/PEDERST/rename'
)
license=('spdx:GPL-1.0-or-later', 'spdx:Artistic-1.0-Perl')
depends=(perl)
options=(!emptydirs)
source=("https://search.cpan.org/CPAN/authors/id/P/PE/PEDERST/rename-$pkgver.tar.gz")
sha256sums=('3bc985310cdd3387c50c64f30e5f30e22928cdaf66233f0a1cf43b6a7056afc7')

build() {
    cd "$srcdir/rename-$pkgver"

    perl Makefile.PL PREFIX=/usr INSTALLDIRS=vendor
    make
}

package() {
    cd "$srcdir/rename-$pkgver"

    make DESTDIR="$pkgdir"/ install
    mv "$pkgdir"/usr/bin/vendor_perl/{rename,perl-rename}
    mv "$pkgdir"/usr/share/man/man1/{rename,perl-rename}.1p
}

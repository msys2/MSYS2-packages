# Maintainer: atom2013 <atom.long@hotmail.com>

_realname=pyparsing
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=3.1.1
pkgrel=1
pkgdesc='General parsing module for Python'
arch=('any')
url='https://github.com/pyparsing/pyparsing/'
license=('spdx:MIT')
depends=('python')
makedepends=(
  'python-flit-core'
  'python-installer'
)
provides=("python3-${_realname}")
replaces=("python3-${_realname}")
conflicts=("python3-${_realname}")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('ede28a1a32462f5a9705e07aea48001a08f7cf81a021585011deba701581a0db')

build() {
  cd "${srcdir}"/pyparsing-${pkgver}

  python -m flit_core.wheel
}

package() {
  cd pyparsing-${pkgver}

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

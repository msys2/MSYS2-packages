# Maintainer: atom2013 <atom.long@hotmail.com>

_realname=pyparsing
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=3.2.1
pkgrel=1
pkgdesc='General parsing module for Python'
arch=('any')
url='https://github.com/pyparsing/pyparsing/'
msys2_references=(
  "anitya: 3756"
  "purl: pkg:pypi/pyparsing"
)
license=('spdx:MIT')
depends=('python')
makedepends=(
  'python-flit-core'
  'python-installer'
)
provides=("python3-${_realname}")
replaces=("python3-${_realname}")
conflicts=("python3-${_realname}")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('61980854fd66de3a90028d679a954d5f2623e83144b5afe5ee86f43d762e5f0a')

build() {
  cd "${srcdir}"/pyparsing-${pkgver}

  python -m flit_core.wheel
}

package() {
  cd pyparsing-${pkgver}

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

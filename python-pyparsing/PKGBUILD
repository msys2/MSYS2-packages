# Maintainer: atom2013 <atom.long@hotmail.com>

_realname=pyparsing
pkgbase="python-${_realname}"
pkgname=("python3-${_realname}" "python2-${_realname}")
pkgver=2.3.0
pkgrel=1
pkgdesc='General parsing module for Python'
arch=('any')
url='https://github.com/pyparsing/pyparsing/'
makedepends=('python3-setuptools' 'python2-setuptools' 'subversion')
license=('MIT')
source=("https://github.com/pyparsing/pyparsing/archive/pyparsing_$pkgver.tar.gz")
sha256sums=('1c43865daf37c14b183c8c46be537a31797973ec4db79a8b9f83dd1a4adf23cc')

prepare() {
  cp -a pyparsing-pyparsing_${pkgver}{,-py2}
}

build() {
  # Build python 3 module
  cd "${srcdir}"/pyparsing-pyparsing_${pkgver}
  python3 setup.py build

  # Build python 2 module
  cd "${srcdir}"/pyparsing-pyparsing_${pkgver}-py2
  python2 setup.py build
}

package_python3-pyparsing() {
  depends=('python3')

  cd pyparsing-pyparsing_${pkgver}

  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-pyparsing() {
  depends=('python2')

  cd pyparsing-pyparsing_${pkgver}-py2

  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:

# Maintainer: atom2013 <atom.long@hotmail.com>

_realname=pyparsing
pkgbase="python-${_realname}"
pkgname=("python3-${_realname}" "python2-${_realname}")
pkgver=2.4.4
pkgrel=1
pkgdesc='General parsing module for Python'
arch=('any')
url='https://github.com/pyparsing/pyparsing/'
makedepends=('python3-setuptools' 'python2-setuptools' 'subversion')
license=('MIT')
source=("https://github.com/pyparsing/pyparsing/archive/pyparsing_$pkgver.tar.gz")
sha256sums=('44cb13c3c953b2cf004637f9ccb0f77cd799174c11539f6b65b3cd76c6de8970')

prepare() {
  cp -a pyparsing-pyparsing_${pkgver}{,-py2}
}

build() {
  # Build python 3 module
  cd "${srcdir}"/pyparsing-pyparsing_${pkgver}
  python3 setup.py build

  # Build python 2 module
  cd "${srcdir}"/pyparsing-pyparsing_${pkgver}-py2
  python2 setup.py build
}

package_python3-pyparsing() {
  depends=('python3')

  cd pyparsing-pyparsing_${pkgver}

  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-pyparsing() {
  depends=('python2')

  cd pyparsing-pyparsing_${pkgver}-py2

  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=HTTP-Message
pkgname=perl-${_realname}
pkgver=7.01
pkgrel=1
pkgdesc="HTTP style messages"
arch=(any)
url="https://metacpan.org/release/HTTP-Message"
msys2_references=(
  'archlinux: perl-http-message'
  'cygwin: perl-HTTP-Message'
  'purl: pkg:cpan/OALDERS/HTTP-Message'
)
groups=('perl-modules')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.8.8' 'perl-Clone' 'perl-Encode-Locale>=1'
         'perl-IO-HTML' 'perl-HTTP-Date>=6'
         'perl-LWP-MediaTypes>=6' 'perl-URI>=1.10')
makedepends=('perl-Try-Tiny')
#checkdepends=('perl-IO-Compress-Brotli')
conflicts=('perl-libwww<6')
source=("https://search.cpan.org/CPAN/authors/id/O/OA/OALDERS/${_realname}-${pkgver}.tar.gz")
sha256sums=('82b79ce680251045c244ee059626fecbf98270bed1467f0175ff5ea91071437e')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd ${_realname}-${pkgver}
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd ${_realname}-${pkgver}
    make test
  )
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="$pkgdir" install
}

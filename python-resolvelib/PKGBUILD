# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_name=resolvelib
pkgname=python-resolvelib
pkgver=1.2.1
pkgrel=1
pkgdesc="Resolve abstract dependencies into concrete ones"
url="https://github.com/sarugaku/resolvelib"
msys2_repository_url="https://github.com/sarugaku/resolvelib"
msys2_references=(
  'archlinux: python-resolvelib'
  'gentoo: dev-python/resolvelib'
  'purl: pkg:pypi/resolvelib'
)
license=(spdx:ISC)
arch=(any)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha256sums=('4d4c4137d7b8f8f27f298ff1af2faa767d790f271928506358d25e4252ed749a')

build() {
  cd "$_name-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_name-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

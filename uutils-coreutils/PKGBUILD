# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>
# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=coreutils
pkgname="uutils-${_realname}"
pkgver=0.2.0
pkgrel=1
pkgdesc="Cross-platform Rust rewrite of the GNU coreutils"
arch=('any')
url='https://github.com/uutils/coreutils'
msys2_references=(
  'archlinux: uutils-coreutils'
  'purl: pkg:cargo/coreutils'
)
license=('spdx:MIT')
depends=("oniguruma" "gcc-libs")
makedepends=("rust" "pkgconf")
source=("https://github.com/uutils/coreutils/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('185be1670bb5091f48d29524c6f81326f12aef5e599fcdb122967a95d017f32a')

prepare() {
  cd "${_realname}-${pkgver}"

  cat >> Cargo.toml <<END

[patch.crates-io]
mio = { git = "https://github.com/tokio-rs/mio", rev = "f352b81d27e39ca3ee1da08c12d8f5795a9dca12" }
END

  cargo update -p mio
  cargo fetch --locked --target 'x86_64-pc-cygwin'
}

build() {
  cd "${_realname}-${pkgver}"

  export RUSTONIG_DYNAMIC_LIBONIG=1
  make PROFILE=release
}

package() {
  cd "${_realname}-${pkgver}"

  make install \
    DESTDIR="${pkgdir}" \
    PREFIX="/usr" \
    PROG_PREFIX=uu- \
    PROFILE=release

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/uutils-${_realname}/LICENSE"
}

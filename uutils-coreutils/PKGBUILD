# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>
# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=coreutils
pkgname="uutils-${_realname}"
pkgver=0.5.0
pkgrel=5
pkgdesc="Cross-platform Rust rewrite of the GNU coreutils"
arch=('any')
url='https://github.com/uutils/coreutils'
msys2_references=(
  'archlinux: uutils-coreutils'
  'purl: pkg:cargo/coreutils'
)
license=('spdx:MIT')
depends=("gcc-libs" "oniguruma")
makedepends=("git" "rust" "pkgconf")
conflicts=(coreutils)
provides=(coreutils)
source=("${pkgname}::git+${url}#tag=$pkgver"
        "git+https://github.com/nix-rust/nix#tag=v0.30.1"
        "ioctl.patch::https://patch-diff.githubusercontent.com/raw/nix-rust/nix/pull/2715.patch") # unstable csum. should cherry-pick
sha256sums=('6ed67de40fe5950b7bc4263e96674adaeccc347a4bd368c9d8eee8c6cd0a0d84'
            'aebec7d9c51d26d003333bc53158ac75b8e960ca5cb75d24fe8a5cb027c59842'
            '3b2a744cba48a350eed165c84971c83fb1becab74c5764c5a90894ade6ffafd2')

prepare() {
  cd nix
  git cherry-pick -n 71f6ee9f4380d5caad3c049dbd352627b9d6eb0a # add sync
  git apply -v -p1 ../ioctl.patch

  cd ../"${pkgname}"
  git cherry-pick -n 54ba74bb7e72addeb1967c2b04da475315f24f96 # add UNIX_PROGS
  git cherry-pick -n f3f4993b11bbbc2f71686ccf0892da8fc0bbf408 # bump mio

  cat >> Cargo.toml <<END

[patch.crates-io]
nix.path = "../nix"
END

  cargo update -p nix
  cargo update -p libc --precise 0.2.178
  cargo fetch --locked --target "${RUST_CHOST}"
}

# spliting build() sometimes cause building twice at make build
package() {
  cd "$pkgname"
  bsdtar -a -cf docs/tldr.zip --files-from /dev/null # Mask many warnings
  export CARGO_BUILD_BARGET="$pkgname/cargo_build_target" && cargo clean # base image issue?

  export RUSTONIG_DYNAMIC_LIBONIG=1
  export MSYSTEM=CYGWIN
  make install \
    DESTDIR="${pkgdir}" \
    PREFIX="/usr" \
    PROFILE=release-fast \
    SKIP_UTILS="kill more uptime" \
    LIBSTDBUF_DIR="/usr/lib/coreutils" \
    MULTICALL=y

  # Should be part of uutils/util-linux...
  make install \
    DESTDIR="${pkgdir}" \
    PREFIX="/usr" \
    PROFILE=release-fast \
    PROG_PREFIX="uu-" UTILS="kill more uptime" \
    MULTICALL=y

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

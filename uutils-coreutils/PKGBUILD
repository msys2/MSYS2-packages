# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>
# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=coreutils
pkgname="uutils-${_realname}"
pkgver=0.4.0
pkgrel=1
pkgdesc="Cross-platform Rust rewrite of the GNU coreutils"
arch=('any')
url='https://github.com/uutils/coreutils'
msys2_references=(
  'archlinux: uutils-coreutils'
  'purl: pkg:cargo/coreutils'
)
license=('spdx:MIT')
depends=("oniguruma" "gcc-libs")
makedepends=("rust" "pkgconf")
source=("https://github.com/uutils/coreutils/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
        "nix-0.30.1.tar.gz::https://crates.io/api/v1/crates/nix/0.30.1/download"
        "nix-add-sync.patch"
        "uucore-support-cygwin.patch")
sha256sums=('5f0c3f97b807e72edccc844c6a685ec9862199f16a665df07de5b1d20ec21233'
            '37c93d8daa9d8a012fd8ab92f088405fb202ea0b6ab73ee2482ae66af4f42091'
            '74523f3a35e05aba87a1d978330aef40f67b0304ac79c1c00b294c9830543db6'
            '14f430635fb87d0bc12d1b6870365614f7ae259f7c235c420454303250aef7dd'
            '8cf8d3b83fb1f66b7d1fd8892368f755216cdfffc8a8687a33aa6b79fb5d183e')

prepare() {
  cd "${_realname}-${pkgver}"

  patch -p1 -i ../uucore-support-cygwin.patch
  patch -d ../nix-0.30.1 -p1 -i ../nix-add-sync.patch
  cat >> Cargo.toml <<END

[patch.crates-io]
mio = { git = "https://github.com/tokio-rs/mio", rev = "f352b81d27e39ca3ee1da08c12d8f5795a9dca12" }
nix.path = "../nix-0.30.1"
END

  cargo update -p mio -p nix
  cargo update -p libc --precise 0.2.178
  cargo fetch --locked --target "${RUST_CHOST}"
}

# spliting build() sometimes cause building twice at make build
package() {
  cd "${_realname}-${pkgver}"

  # Remove utils if MinGW version does not have cygwin path issue
  # PROFILE=release-fast is replaced by release at 0.4.0 yet...
  export SKIP_UTILS="arch date factor false sleep true uname yes"
  export RUSTONIG_DYNAMIC_LIBONIG=1
  make -O install \
    DESTDIR="${pkgdir}" \
    PREFIX="/usr" \
    PROFILE=release-fast \
    PROG_PREFIX="uu-" \
    MULTICALL=y

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/uutils-${_realname}/LICENSE"
}

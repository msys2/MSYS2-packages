# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=curl
pkgname=('curl' 'libcurl' 'libcurl-devel')
pkgver=7.57.0
pkgrel=2
pkgdesc="Multi-protocol file transfer utility"
arch=('i686' 'x86_64')
url="https://curl.haxx.se"
license=('MIT')
depends=('ca-certificates')
makedepends=('heimdal-devel' 'libmetalink-devel' 'libcrypt-devel' 'libidn2-devel' 'libnghttp2-devel' 'libssh2-devel' 'openssl-devel' 'zlib-devel') #  'libcares-devel'
options=('!libtool' 'strip' '!debug')
source=("https://curl.haxx.se/download/${pkgname}-${pkgver}.tar.bz2"{,.asc}
        curl-7.32.0-msys2.patch)
sha256sums=('c92fe31a348eae079121b73884065e600c533493eb50f1f6cee9c48a3f454826'
            'SKIP'
            '703963690bfa95b92e8281b7d9070a12c18bada044c3a7c9995fe9ae5adc5a8f')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2'   # Daniel Stenberg
              '914C533DF9B2ADA2204F586D78E11C6B279D5C91')  # Daniel Stenberg (old key)

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/curl-7.32.0-msys2.patch

  autoreconf -fi
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --enable-shared \
    --enable-static \
    --enable-debug \
    --enable-optimize \
    --enable-ipv6 \
    --disable-hidden-symbols \
    --disable-ares \
    --with-gssapi \
    --with-libidn \
    --with-libmetalink \
    --without-librtmp \
    --with-libssh2 \
    --without-spnego \
    --with-ssl \
    --without-gnutls \
    --with-zlib \
    --with-ca-bundle=/usr/ssl/certs/ca-bundle.crt \
    ac_cv_func_setmode=yes

  make
  make DESTDIR="${srcdir}/dest" install
}

package_curl() {
  depends=('ca-certificates' 'libcurl' 'libcrypt' 'libmetalink' 'openssl' 'zlib' 'libnghttp2')
  groups=('base')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/

  # license
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_libcurl() {
  pkgdesc="Multi-protocol file transfer library (runtime)"
  depends=('ca-certificates' 'heimdal-libs' 'libcrypt' 'libidn2' 'libmetalink' 'libnghttp2' 'libssh2' 'openssl' 'zlib') #'libcares'
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
}

package_libcurl-devel() {
  pkgdesc="Libcurl headers and libraries"
  depends=("libcurl=${pkgver}" 'heimdal-devel' 'libcrypt-devel' 'libidn2-devel' 'libiconv-devel' 'libmetalink-devel' 'libnghttp2-devel' 'libssh2-devel' 'openssl-devel' 'zlib-devel') #'libcares-devel'
  options=('staticlibs')
  groups=('development')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*-config ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}

# Maintainer: Andrea Dalle Vacche

_realname=Test-Harness
pkgname=perl-${_realname}
pkgver=3.39
pkgrel=1
pkgdesc="Test::Harness - Run Perl standard test scripts with statistics"
arch=('any')
license=('GPL' 'PerlArtistic')
url="https://metacpan.org/release/Test-Harness"
depends=('perl')
groups=('perl-modules')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/L/LE/LEONT/${_realname}-${pkgver}.tar.gz)
sha256sums=('69bd44c81c6e1d2db18e298ecf631852608893ddfddb332a7d55285bbfc51132')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

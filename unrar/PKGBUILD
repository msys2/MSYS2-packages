# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=unrar
pkgname=('unrar' 'libunrar')
pkgver=5.5.3
pkgrel=1
arch=('i686' 'x86_64')
groups=('base-devel')
url="http://www.rarlab.com/rar_add.htm"
license=('custom')
options=('staticlibs' '!makeflags')
depends=('gcc-libs')
source=("http://www.rarlab.com/rar/unrarsrc-${pkgver}.tar.gz"
        'build-dll.patch')
sha256sums=('d1d9ef4a9247db088f825666de8f8bb69006d8d8b0e004ff366b3e04c103a2b3'
            'ab57259011a9b93f389fd646155b9ffb35f9290ee6ba2ffa5eef4d06bb061d45')

prepare() {
  cd ${pkgname}
  patch -p1 -i ${srcdir}/build-dll.patch

  #autoreconf -fi
}

build() {
  cd ${pkgname}
  cp -a ${srcdir}/unrar ${srcdir}/libunrar
  make -C "${srcdir}"/libunrar lib CXXFLAGS="${CXXFLAGS}"
  make CXXFLAGS="${CXXFLAGS}" STRIP="true"
}

package_unrar() {
  pkgdesc="The RAR uncompression program"
  depends=('gcc-libs')
  cd "${srcdir}/${pkgbase}"
  install -Dm755 unrar.exe "${pkgdir}/usr/bin/unrar.exe"
  # install license
  install -Dm644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_libunrar() {
  pkgdesc="Library and header file for applications that use libunrar"
  depends=('gcc-libs')
  cd "${srcdir}/libunrar"
  install -Dm755 libunrar.dll.a "${pkgdir}/usr/lib/libunrar.dll.a"
  install -Dm755 libunrar.dll "${pkgdir}/usr/bin/libunrar.dll.a"
  install -Dm644 dll.hpp "${pkgdir}/usr/include/unrar/dll.hpp"
  # install license
  install -Dm644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}


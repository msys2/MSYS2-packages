# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=binutils
_basever=2.26
pkgver=${_basever}
#pkgver=2.24.51
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/binutils/"
license=('GPL')
groups=('msys2-devel')
depends=('libiconv' 'libintl' 'zlib')
checkdepends=('dejagnu' 'bc')
makedepends=('libiconv-devel' 'gettext-devel' 'zlib-devel')
options=('staticlibs' '!distcc' '!ccache')
install=binutils.install
source=(#https://www.mirrorservice.org/sites/sourceware.org/pub/binutils/snapshots/binutils-${_basever}.tar.bz2
        https://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig}
        binutils-2.26-msys2.patch
        0005-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
        coff-linker-weak-nt-externals.patch
        gas-aux-nt-weak-externals.patch
        objcopy-weak-nt-externals2local.patch)
sha256sums=('c2ace41809542f5237afc7e3b8f32bb92bc7bc53c6232a84463c423b0714ecd9'
            'SKIP'
            '91dc8f8624301e1fc54f6c31b076ccdd7aff795b608f547b050bc6df281c7b43'
            'bd38317b28b894d5c0e0ba043fc2445af415f6e96bb9ac93c348748d21a43625'
            '76663a2493f81e2306140259a2e4306b7cc4592fc4381951df87b5aa52c0fb62'
            'a5764911df3bde0e51fe2c244cac38a2f4169081017796b8cc9f214d34c17930'
            '05933fb77e5577257fe1d7a6ff2e701e93fd43ce417a29165bc6a71da5326459')

prepare() {
  cd "${srcdir}"/binutils-$_basever
  patch -p1 -i "${srcdir}"/coff-linker-weak-nt-externals.patch
  patch -p1 -i "${srcdir}"/gas-aux-nt-weak-externals.patch
  patch -p1 -i "${srcdir}"/objcopy-weak-nt-externals2local.patch
  patch -p1 -i "${srcdir}"/binutils-2.26-msys2.patch
  patch -p1 -i "${srcdir}"/0005-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  mkdir "${srcdir}"/binutils-${CARCH}
}

build() {
  cd "${srcdir}"/binutils-${CARCH}

  if [ "${CARCH}" = "x86_64" ]; then
    local _conf='--enable-64-bit-bfd'
  else
    local _conf=''
  fi
  "${srcdir}"/binutils-${_basever}/configure \
    --prefix=/usr \
    --{build,host,target}=${CHOST} \
    --disable-werror \
    --disable-sim \
    $_conf \
    --without-libiconv-prefix \
    --without-libintl-prefix

  make
}

check() {
  cd "${srcdir}"/binutils-${CARCH}

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd "${srcdir}"/binutils-${CARCH}
  make DESTDIR=${pkgdir} install
  cp libiberty/libiberty.a ${pkgdir}/usr/lib

  # Remove unwanted files
  #rm ${pkgdir}/usr/share/info/{configure,standards}.info

  # Add some useful headers
  #install -m644 "${srcdir}"/binutils-${_basever}/include/libiberty.h ${pkgdir}/usr/include
  #install -m644 "${srcdir}"/binutils-${_basever}/include/demangle.h ${pkgdir}/usr/include
}

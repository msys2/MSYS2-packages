# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_pyname=trove_classifiers
pkgname=python-trove-classifiers
pkgver=2025.12.1.14
pkgrel=1
pkgdesc="Canonical source for classifiers on PyPI (pypi.org)"
arch=('any')
url="https://github.com/pypa/trove-classifiers"
msys2_references=(
  "anitya: 88298"
  "archlinux: python-trove-classifiers"
  "gentoo: dev-python/trove-classifiers"
  "purl: pkg:pypi/trove-classifiers"
)
license=('spdx:Apache-2.0')
depends=(
  'python'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-calver'
)
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('a74f0400524fc83620a9be74a07074b5cbe7594fd4d97fd4c2bfde625fdc1633')

build() {
  cd "${_pyname}-${pkgver}"

  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

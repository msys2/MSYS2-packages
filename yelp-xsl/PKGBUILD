# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=yelp-xsl
pkgver=42.1
pkgrel=1
pkgdesc="Stylesheets for Yelp"
arch=('any')
license=('GPL')
url="https://www.gnome.org"
options=('strip' '!libtool' 'staticlibs')
makedepends=("libxml2-python" "intltool" "itstool" 'autotools' 'gettext-devel' 'gcc')
source=(https://download.gnome.org/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('238be150b1653080ce139971330fd36d3a26595e0d6a040a2c030bf3d2005bcd')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  autoreconf -ivf
}

build() {
  mkdir -p ${srcdir}/build-${CHOST} && cd ${srcdir}/build-${CHOST}

  ${srcdir}/${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --host=${CHOST} \
    --sysconfdir=/etc \
    --localstatedir=/var

  make
}

check() {
  cd ${srcdir}/build-${CHOST}
  make -k check
}

package() {
  cd ${srcdir}/build-${CHOST}
  make DESTDIR="${pkgdir}" install
  mkdir -p ${pkgdir}/usr/lib
  mv ${pkgdir}/usr/share/pkgconfig ${pkgdir}/usr/lib/
}

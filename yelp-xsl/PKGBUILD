# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=yelp-xsl
pkgver=3.36.0
pkgrel=2
pkgdesc="Stylesheets for Yelp"
arch=('any')
license=('GPL')
url="https://www.gnome.org"
options=('strip' '!libtool' 'staticlibs')
makedepends=("libxml2-python" "intltool" "itstool")
source=(https://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('4fe51c0233b79a4c204c68498d45f09b342c30ed02c4e418506c0e35f0904ec3')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  autoreconf -ivf
}

build() {
  mkdir -p ${srcdir}/build-${CHOST} && cd ${srcdir}/build-${CHOST}
  export PYTHON=/usr/bin/python2
  ${srcdir}/${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --host=${CHOST} \
    --sysconfdir=/etc \
    --localstatedir=/var

  make
}

check() {
  cd ${srcdir}/build-${CHOST}
  make -k check
}

package() {
  cd ${srcdir}/build-${CHOST}
  make DESTDIR="${pkgdir}" install
  mkdir -p ${pkgdir}/usr/lib
  mv ${pkgdir}/usr/share/pkgconfig ${pkgdir}/usr/lib/
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime-3.4
pkgname=('msys2-runtime-3.4' 'msys2-runtime-3.4-devel')
pkgver=3.4.0
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('x86_64')
url="https://www.cygwin.com/"
license=('GPL')
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'mingw-w64-cross-crt-git'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'autotools'
             'xmlto'
             'docbook-xsl')
# re zipman: https://github.com/msys2/MSYS2-packages/pull/2687#issuecomment-965714874
options=('!zipman')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver}
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-path_conv-special-case-root-directory-to-have-traili.patch
        0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0013-strace.cc-Don-t-set-MSYS-noglob.patch
        0014-Add-debugging-for-build_argv.patch
        0015-Add-debugging-for-strace-make_command_line.patch
        0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0018-strace-quiet-be-really-quiet.patch
        0019-Introduce-the-enable_pcon-value-for-MSYS.patch
        0020-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0021-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0022-Do-not-show-Error-dialogs-by-default.patch
        0023-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0024-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0025-kill-kill-Win32-processes-more-gently.patch
        0026-Cygwin-make-option-for-native-inner-link-handling.patch
        0027-docs-skip-building-texinfo-and-PDF-files.patch
        0028-install-libs-depend-on-the-toollibs.patch
        0029-POSIX-ify-the-SHELL-variable.patch
        0030-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0031-uname-allow-setting-the-system-name-to-CYGWIN.patch
        0032-Pass-environment-variables-with-empty-values.patch
        0033-Optionally-disallow-empty-environment-values-again.patch
        0034-build_env-respect-the-MSYS-environment-variable.patch
        0035-When-converting-to-a-Unix-path-avoid-double-trailing.patch
        0036-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch
        0037-Revert-Cygwin-drop-requirement-to-build-newlib-s-std.patch
        0038-Revert-Revert-Cygwin-drop-requirement-to-build-newli.patch
        0039-fixup-Rename-dll-from-cygwin-to-msys.patch)
sha256sums=('SKIP'
            'cab8dcfded363f755be1cb64a89c0d9b6baa1e36cae676bc516d91384f8004cf'
            '1daf07bae4596211b2d8913bc8d7936017e3dc79fcdb4d4614c4f192b2f12225'
            'ceca1abc1cabf420f449287dd0e9de6338d0ce0895854a23a8b9d3d8e530387f'
            '732591b423767612bc20f88971835b65ef0ed265288184479436ea0beeca6e29'
            'd7a12cea1381947a6156f5faba8afd1bbdfe0d21594c4ef563be08010ba1170e'
            'd9f4e887f594bba0b2903ed1127d20a5a26bbd2771e3c9b939fd56b403d98594'
            '618d1332d9c91f8c855f407f20117a51579648ecb171ba252f77868a5a15a115'
            'aa2d11b4ce425427b6ba6a6c17aa3490f5bfff3c66e32967445ff48ead267dfe'
            'e6409b1de10ac5b68cff2315d8da539df065e2f66c7d89e3bb2cef8be40dfd40'
            'd23aed07798073dc6e5a350e2cd264ad86ed3b084f3dbd1a655e40ff218ac059'
            'e695d15c064fd473b3471341f8685271e68ae3208b8eb7368fbd5375bcc02d17'
            '8a64e6120a4ce466cda85429e34ea83e74207c678d17655dcd330b7f9396abf9'
            'feb0b57f94a728012365f9f77ea3b72cf8cbcbec3203263e07f2d8ea70b015e6'
            '51e848c7f7ce922b9321c7de8aa33ab254f9d2d7a2ee2a29f51561ceb10d7dfe'
            'dc611623c5cba33279b6021f979b1d3698a8ee77e6bd22505c197d5a121a45cb'
            '441e883db25fdbf32987a89cbb15eecdba9294d6c443fbd18d4841bc2473d304'
            'b34c4fc945a5c808af6b26ce10faac4624fe4c04d8dd146abc2d707aa58fdab9'
            '90f5593a6ed2d4d0d44f6d031cbb07d7c09917b66be119a7be6be790e4aba13a'
            'd589d950ea5e94324952666771a366517143e6bd03f586088b343ce27ce12fb5'
            'd4beea772795ff856033f5eb94d312b0607fc50c82055cf69d3afb1467467170'
            '58d30b3a325981bf53cc7da1dafe431d930ac5d0127c336bb8cd26459132fb73'
            '5edf1fcb84b622addcd51e653e28f61787f91d05aa2be40f8ce6e97513eee3c1'
            'd25ec7d1d5ff493f5c3c467bc2a8e9ce27fa63a580eea99e50562d6a713f56b7'
            '05cfa5e1d6a9c675c58068915d9971d9d3646cbf28d405bdf84ac8d8c4668ebc'
            '22f4c4f527bbaafe97883f34054bbb414f37bcce41c13386a20c2966c5e4b24f'
            'e70b23e90eb7af271d53e48a3777cfb94fee725e845b49c77f0971fbff1b25e0'
            '725c0e1f42ea5d8972db65c0f428a7cd2f393f55ec8e60e9a9dabc400f1bf70c'
            '4fcc272ba5f237daa1aa8f1769173293a909a7f9fc6d20b49cc0260cc931fb14'
            '112e89e2e72cee3046814439742ca55255fdc6eeed38dceb39a3100f9c9c0ed6'
            'a1d273d7e0d921d50aa9aa580ec6b7edcf843421cdc7fe2b4409183254e2a9ec'
            '5f45e670a42e5088b058be089901f678644750c4e4fe42d9f5433254843d5a7b'
            '78142c60b2d911bbdbd86a4a2a02aee3badf54f1fd158a53f69ca0e9655fb410'
            '556158f439db1de440db470f0682d0627df23969ac2b39eca9f9e821d31a1a49'
            '7b778576c81c677f6209e33bb4a00a51ba8d5c2f32e89752d3976337808af20f'
            '70003701ba0b837c8df31afca7471120b02504e005ddd18b3cf8a97ded495198'
            'beda69e05d7149b20c86799d5149f747b1367259029e93be83815eb97791c87a'
            '92dbb8c9ce9d6a7b9c21ea5a9a443d3925e9aca6084cfb53dd6e077cfde622af'
            '249153bc8cd047039910a53f93d14e86bdb81140e52971e0a7badb79a97175da'
            'a524ff063176c760005247d1c6abc6b0aafc5f73e3a70ce8f4b32b3743397614')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
 apply_git_am_with_msg 0001-Add-MSYS2-triplet.patch \
  0002-Fix-msys-library-name-in-import-libraries.patch \
  0003-Rename-dll-from-cygwin-to-msys.patch \
  0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch \
  0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch \
  0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch \
  0009-Do-not-convert-environment-for-strace.patch \
  0010-path_conv-special-case-root-directory-to-have-traili.patch \
  0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0013-strace.cc-Don-t-set-MSYS-noglob.patch \
  0014-Add-debugging-for-build_argv.patch \
  0015-Add-debugging-for-strace-make_command_line.patch \
  0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch \
  0018-strace-quiet-be-really-quiet.patch \
  0019-Introduce-the-enable_pcon-value-for-MSYS.patch \
  0020-popen-call-usr-bin-sh-instead-of-bin-sh.patch \
  0021-Expose-full-command-lines-to-other-Win32-processes-b.patch \
  0022-Do-not-show-Error-dialogs-by-default.patch \
  0023-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch \
  0024-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch \
  0025-kill-kill-Win32-processes-more-gently.patch \
  0026-Cygwin-make-option-for-native-inner-link-handling.patch \
  0027-docs-skip-building-texinfo-and-PDF-files.patch \
  0028-install-libs-depend-on-the-toollibs.patch \
  0029-POSIX-ify-the-SHELL-variable.patch \
  0030-Handle-ORIGINAL_PATH-just-like-PATH.patch \
  0031-uname-allow-setting-the-system-name-to-CYGWIN.patch \
  0032-Pass-environment-variables-with-empty-values.patch \
  0033-Optionally-disallow-empty-environment-values-again.patch \
  0034-build_env-respect-the-MSYS-environment-variable.patch \
  0035-When-converting-to-a-Unix-path-avoid-double-trailing.patch \
  0036-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch \
  0037-Revert-Cygwin-drop-requirement-to-build-newlib-s-std.patch \
  0038-Revert-Revert-Cygwin-drop-requirement-to-build-newli.patch \
  0039-fixup-Rename-dll-from-cygwin-to-msys.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime-3.4() {
  pkgdesc="Posix emulation engine for Windows"
  options=('!strip')
  provides=("msys2-runtime=${pkgver}")
  conflicts=('catgets' 'libcatgets' 'msys2-runtime')
  replaces=('catgets' 'libcatgets')

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-3.4-devel() {
  pkgdesc="MSYS2 headers and libraries"
  depends=("msys2-runtime-3.4=${pkgver}")
  provides=("msys2-runtime-devel=${pkgver}")
  options=('staticlibs' '!strip')
  conflicts=('libcatgets-devel' 'msys2-runtime-devel')
  replaces=('libcatgets-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#

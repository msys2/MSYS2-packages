# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Authen-SASL
pkgname=perl-${_realname}
pkgver=2.1900
pkgrel=1
pkgdesc="Perl/CPAN Module Authen::SASL : SASL authentication framework"
arch=('any')
license=('GPL' 'PerlArtistic')
url="https://metacpan.org/dist/Authen-SASL"
depends=('perl')
groups=('perl-modules')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/E/EH/EHUELS/${_realname}-${pkgver}.tar.gz)
sha256sums=('be3533a6891b2e677150b479c1a0d4bf11c8bbeebed3e7b8eba34053e93923b0')

prepare() {
  cd ${_realname}-${pkgver}
}

build() {
  cd ${_realname}-${pkgver}
  PERL_USE_UNSAFE_INC=1 \
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd ${_realname}-${pkgver}
  make pure_install doc_install DESTDIR=$pkgdir
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}

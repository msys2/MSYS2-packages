# This PKGBUILD is derived from one found on the AUR and includes patches contributed by gisburn.
pkgname=ksh93
pkgver=1.0.10
pkgrel=1
pkgdesc="The Original ATT Korn Shell, Now Maintained By The Community."
arch=('i686' 'x86_64')
url="https://www.github.com/ksh93/ksh"
license=('spdx:EPL-2.0')
makedepends=('gcc' 'binutils')
# You might be wondering why there is almost nothing for makedepends. It turns out that
# it has almost no dependencies beyond a C compiler and POSIX compliant shell.
# This is a curse and a blessing. It works literally anywhere, however,
# it's extremely stubborn in how it's built.

# There exists already a package that provides ksh.exe, mksh, so I will set this to be a conflict.
# Unless there can be a convincing reason to let both packages be installed.
conflicts=('mksh')

source=("${pkgname}-${pkgver}.zip"::https://github.com/ksh93/ksh/archive/refs/tags/v1.0.10.zip
  'ksh.kshrc'
  'ksh-unc-paths.patch')

sha256sums=('ab412118ce9527c6d805335080593fa874a0b859e9229c172f3fcf55d26f92e9'
  'SKIP'
  '4838b950e32fc61dbfbb5e446570f4f7503f302a26305dff1f9b0769585a94bc')

prepare() {
  cd "${srcdir}/ksh-${pkgver}"
  patch -p1 -i ../ksh-unc-paths.patch
}

# Things might get a bit messary, prepare yourselves.
# I really should use gisburn's kshrc default config.
build() {
	cd "${srcdir}/ksh-${pkgver}"
  
  	# For now, it literally fails if cc1.exe is not explicitly on the PATH.
  	local GCC_DIR="$(gcc -print-search-dirs | sed -ne '/^install: /s///p')"
  	export PATH="$PATH:$GCC_DIR"


	# Additional weird workaround.
	export CFLAGS="-I${GCC_DIR}/include -L${GCC_DIR} -L/usr/lib"
	export CCFLAGS="${CFLAGS} -fno-use-linker-plugin"
	export LIBRARY_PATH="${GCC_DIR}"

	# What the build system does by default is
	# entirely a shell script system. It sets 'chmod +x' to the script
	# but that does nothing with windows. Need to append
	# shebang at the start.
	sed -i '1s/^/#!\/usr\/bin\/bash\n/' src/cmd/INIT/mamprobe.sh
	sed -i '6s/^/#undef _lib_iconv_open\n/' src/lib/libast/features/iconv # Without this, fails with libiconv installed.

	# My previous attempt at patching the mamake.exe replacement failed. Reason being, is that
	# this build process is highly biased towards unix way of thinking, which doesn't work well with
	# windows. It might kinda work on cygwin, but that fails with msys2. The only successful way so far
	# it's been to build it, is to build it twice. Failure is expected.

	# First round. Note that '|| true' has to be there, it WILL bail the build process when the expected fork() issue
	# happens, so this tells bash to keep going anyways in spite of non zero return code.
	printf "If it reports back fork() error, this is to be expected.\n"
	printf "It will simply run the build command again to finish the job.\n"
	CC="/usr/bin/gcc" /usr/bin/bash bin/package make PACKAGEROOT="$PWD" || true

	sleep 2

	CC="/usr/bin/gcc" /usr/bin/bash bin/package make PACKAGEROOT="$PWD" || true


}

package() {
	cd "${srcdir}"
	install -dm0755 "${pkgdir}/etc/skel"
	install -dm0755 "${pkgdir}/usr/share/ksh"
	install -dm0755 "${pkgdir}/usr/share/doc/ksh"
	install -dm0755 "${pkgdir}/usr/share/licenses/ksh"

	# Install the core components of ksh93
	cd "${srcdir}/ksh-${pkgver}"
	CC="/usr/bin/gcc" /usr/bin/bash bin/package install "${pkgdir}/usr" PACKAGEROOT="$PWD"

	# install ksh sample rc file
	install -Dm644 "${srcdir}/ksh.kshrc" "${pkgdir}/etc/ksh.kshrc"

	# Install copyright and license files
	install -Dm0644 LICENSE.md "${pkgdir}/usr/share/licenses/ksh/LICENSE.md"
	install -Dm0644 COPYRIGHT "${pkgdir}/usr/share/licenses/ksh/COPYRIGHT"

	local _docs
	for _docs in 'builtins.mm' 'COMPATIBILITY' 'DESIGN' 'OBSOLETE' 'PROMO.mm' 'README' 'README-AUDIT.md' \
		'RELEASE' 'RELEASE88' 'RELEASE93' 'sh.memo' 'TYPES'; do
	install -Dm0644 "src/cmd/ksh93/${_docs}" "${pkgdir}/usr/share/doc/ksh/${_docs}"
done

	# Because ksh.exe, like mksh, provided by this package and often people want to specify ksh93,
 	# I will make a copy of of the ksh executable naming it ksh93.exe.
 	cp "${pkgdir}/usr/bin/ksh.exe" "${pkgdir}/usr/bin/ksh93.exe"
   
	# Lastly, move ksh-specific /usr/share/fun to /usr/share/ksh/functions
	mv "${pkgdir}/usr/share/fun" "${pkgdir}/usr/share/ksh/functions"

 	

}

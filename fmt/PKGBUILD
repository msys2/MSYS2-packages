# Maintainer : Gork             <bertsmithzp@gmail.com>
# Contributor: Christoph Reiter <reiter.christoph@gmail.com>
pkgname=fmt
pkgver=11.1.4
pkgrel=1
pkgdesc='Open-source formatting library for C++'
arch=(x86_64 i686)
url=https://fmt.dev
msys2_repository_url="https://github.com/fmtlib/fmt"
license=(spdx:MIT)
depends=(gcc-libs)
makedepends=(
  cmake
  ninja
  gcc
)
source=("https://github.com/fmtlib/fmt/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('ac366b7b4c2e9f0dde63a59b3feb5ee59b67974b14ee5dc9ea8ad78aa2c1ee1e')

build() {
  cmake -S "${pkgname}-${pkgver}" -B build \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON

  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install
  install -Dm 644 "${pkgname}-${pkgver}"/LICENSE -t "${pkgdir}"/usr/share/licenses/fmt/
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=intltool
pkgver=0.50.2
pkgrel=1
pkgdesc="The internationalization tool collection"
license=('GPL3')
arch=('i686' 'x86_64')
url="https://edge.launchpad.net/intltool"
groups=('base-devel')
makedepends=('perl')
depends=('perl-XML-Parser')
source=(http://edge.launchpad.net/intltool/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz{,.asc})
sha1sums=('7fddbd8e1bf94adbf1bc947cbf3b8ddc2453f8ad'
          '85c71e9651df41fc330c5f171fdfe1bf11e44f65')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --build=${CHOST} --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m644 doc/I18N-HOWTO "${pkgdir}/usr/share/doc/${pkgname}/I18N-HOWTO"
}

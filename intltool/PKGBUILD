# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=intltool
pkgver=0.51.0
pkgrel=2
pkgdesc="The internationalization tool collection"
license=('GPL3')
arch=('i686' 'x86_64')
url="https://edge.launchpad.net/intltool"
groups=('base-devel')
makedepends=('perl')
depends=('perl-XML-Parser')
source=(https://edge.launchpad.net/intltool/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz{,.asc}
        perl-5.22-compatibility.patch)
sha1sums=('a0c3bcb99d1bcfc5db70f8d848232a47c47da090'
          'SKIP'
          '9b09b206b3101b83eb91706c56d3b0df693fc9c8')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/perl-5.22-compatibility.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --build=${CHOST} --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m644 doc/I18N-HOWTO "${pkgdir}/usr/share/doc/${pkgname}/I18N-HOWTO"
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=ed
pkgver=1.21
pkgrel=1
pkgdesc="A POSIX-compliant line-oriented text editor"
arch=('i686' 'x86_64')
license=('GPL')
url="https://www.gnu.org/software/ed/ed.html"
depends=('sh')
makedepends=('autotools' 'gcc')
options=('!emptydirs')
source=("https://ftp.gnu.org/gnu/ed/$pkgname-$pkgver.tar.lz"{,.sig})
sha256sums=('60e24998727d453a5cf02c54664b97536de46a2b34cd1f4f67c1c1a61bbbad75'
            'SKIP')
validpgpkeys=('1D41C14B272A2219A739FA4F8FE99503132D7742') # Antonio Diaz Diaz <antonio@gnu.org>

build() {
  cd "$pkgname-$pkgver"

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure --prefix=/ \
              --build=${CYGWIN_CHOST} \
              --bindir=/usr/bin \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              "CFLAGS=$CFLAGS" \
              "LDFLAGS=$LDFLAGS"

  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" "DESTDIR=$pkgdir" install
}

# Maintainer: Sundaram Ramaswamy <legends2k@yahoo.com>

pkgname=bc
pkgver=1.08.1
pkgrel=2
pkgdesc="An arbitrary precision calculator language"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/bc"
msys2_references=(
  "anitya: 170"
  "cpe: cpe:2.3:a:gnu:bc"
)
license=('spdx:GPL-3.0-or-later')
depends=('libreadline' 'ncurses')
makedepends=('ed' 'gcc' 'libreadline-devel' 'ncurses-devel' 'autotools')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('b71457ffeb210d7ea61825ff72b3e49dc8f2c1a04102bbe23591d783d1bfe996')

prepare () {
  cd "${srcdir}"/${pkgname}-${pkgver}
  # patch -p1 -i "${srcdir}"/0001-Fix-readline-prototype.patch
  return 0
}

build () {
  mkdir ${srcdir}/build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}
  
  ../${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --build=${CHOST/-msys/-cygwin} \
    --host=${CHOST} \
    --with-readline

  make
}

check () {
  cd build-${MSYSTEM}
  echo "quit" | ./bc/bc -l "${srcdir}"/${pkgname}-${pkgver}/Test/checklib.b # 10 failures
}

package () {
  cd build-${MSYSTEM}
  make prefix="${pkgdir}/usr" install
}

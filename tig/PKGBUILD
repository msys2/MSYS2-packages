# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=tig
pkgver=2.4.1
pkgrel=1
pkgdesc='Text-mode interface for Git'
depends=('git' 'libreadline' 'ncurses')
makedepends=('asciidoc' 'xmlto' 'libreadline-devel' 'ncurses-devel' 'libiconv-devel')
url='https://jonas.github.io/tig/'
license=('GPL')
arch=('i686' 'x86_64')
source=("https://github.com/jonas/${pkgname}/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"
        001-fix-type.patch)
sha256sums=('b6b6aa183e571224d0e1fab3ec482542c1a97fa7a85b26352dc31dbafe8558b8'
            '2483512d95ef91077e5f3fb66f5f6df64603f48b85d77e0d4c60c272643e61a1')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/001-fix-type.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --build=${CHOST}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install install-doc-man
  install -Dm0644 contrib/tig-completion.bash "${pkgdir}/usr/share/bash-completion/completions/tig"
}

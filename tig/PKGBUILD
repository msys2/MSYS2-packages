# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=tig
pkgver=2.5.10
pkgrel=1
pkgdesc='Text-mode interface for Git'
depends=('git' 'libreadline' 'ncurses')
makedepends=('asciidoc' 'xmlto' 'libreadline-devel' 'ncurses-devel' 'libiconv-devel' 'autotools' 'gcc')
url='https://jonas.github.io/tig/'
msys2_repository_url="https://github.com/jonas/tig"
msys2_references=(
  "anitya: 4969"
)
license=('GPL')
arch=('i686' 'x86_64')
backup=('etc/tigrc')
source=("https://github.com/jonas/${pkgname}/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('f655cc1366fc10058a2bd505bb88ca78e653ff7526c1b81774c44b9d841210e3')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  CPPFLAGS="-D_GNU_SOURCE" \
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --build=${CHOST}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install install-doc-man
  install -Dm0644 contrib/tig-completion.bash "${pkgdir}/usr/share/bash-completion/completions/tig"
}

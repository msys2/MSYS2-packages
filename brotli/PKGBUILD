# Maintainer: Oleg Titov <oleg.titov@gmail.com>

pkgbase=brotli
pkgname=('brotli' 'brotli-devel' 'python-brotli' 'brotli-testdata')
pkgver=1.0.7
pkgrel=3
pkgdesc='Brotli compression library'
arch=('i686' 'x86_64')
license=('MIT')
url='https://github.com/google/brotli'
depends=('msys2-runtime' 'gcc-libs')
makedepends=('cmake' 'python' 'libcrypt-devel')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/google/$pkgbase/archive/v${pkgver}.tar.gz"
        brotli-rename-static-libs.patch)
sha256sums=('4c61bfb0faca87219ea587326c467b95acb25555b53d1a421ffa3c8a9296ee2c'
            '25c2e154e805f6bd2999343a3d38efefa213577e09f5ea932bfa7eb42cf97f35')

prepare() {
  cd brotli-${pkgver}
  patch -p1 -i ${srcdir}/brotli-rename-static-libs.patch
  cd ..
  mkdir -p build
}

build() {
  cd "${srcdir}"/brotli-${pkgver}
  python setup.py build

  cd "${srcdir}"/build
  cmake \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DCMAKE_INSTALL_LIBDIR="/usr/lib" \
    ../brotli-${pkgver}

  make
}

check() {
  cd brotli-${pkgver}
  make test
}

package_brotli() {
  cd build
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{include,lib}
  install -D -m644 "${srcdir}"/brotli-${pkgver}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_brotli-devel() {
  depends=('brotli')
  cd build
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/bin
}

package_python-brotli() {
  depends=('python')

  cd brotli-${pkgver}

  /usr/bin/python setup.py install --skip-build -O1 --root="${pkgdir}"
  install -D -m644 "${srcdir}"/brotli-${pkgver}/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_brotli-testdata() {
  depends=()

  cd brotli-${pkgver}
  install -dm755 "${pkgdir}"/usr/share/brotli
  cp -a tests/testdata "${pkgdir}"/usr/share/brotli/
  install -D -m644 "${srcdir}"/brotli-${pkgver}/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=rcs
pkgver=5.10.1
pkgrel=2
pkgdesc='Revision Control System: manages multiple revisions of files'
url='https://www.gnu.org/software/rcs/'
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('groff' 'ed' 'autotools' 'gcc')
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.lz"{,.sig}
        gnulib-weak.patch)
sha256sums=('43ddfe10724a8b85e2468f6403b6000737186f01e60e0bd62fde69d842234cc5'
            'SKIP'
            '873e5a94eb48c5aa49b60179e51ae306e9a6a788dc651286d8e4aedaf35eb2da')
validpgpkeys=('748EA0E81CB8A7489BFA6CE4670322244C807502') # Thien-Thi Nguyen (software signing) <ttn@gnuvola.org>

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/gnulib-weak.patch"
  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
  --prefix=/usr \
  --build=${CYGWIN_CHOST}

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check || true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
}

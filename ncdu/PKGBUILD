# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

pkgname=ncdu
pkgver=1.22
pkgrel=1
pkgdesc="Disk usage analyzer with an ncurses interface."
arch=('i686' 'x86_64')
url="https://dev.yorhel.nl/ncdu"
license=('custom:MIT')
depends=('ncurses')
makedepends=('ncurses-devel' 'autotools' 'gcc')
source=("https://dev.yorhel.nl/download/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('0ad6c096dc04d5120581104760c01b8f4e97d4191d6c9ef79654fa3c691a176b'
            'SKIP')
validpgpkeys=('74460D32B80810EBA9AFA2E962394C698C2739FA') # Yoran Heling <info@yorhel.nl>

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

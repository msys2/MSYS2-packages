# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgbase=neon
pkgname=("lib${pkgbase}" "lib${pkgbase}-devel")
pkgver=0.36.0
pkgrel=1
pkgdesc="HTTP and WebDAV client library with a C interface"
arch=('i686' 'x86_64')
url="https://notroj.github.io/neon/"
msys2_repository_url="https://github.com/notroj/neon"
msys2_references=(
  "cpe: cpe:/a:neon:neon"
  "cpe: cpe:/a:webdav:neon"
)
license=('GPL' 'LGPL')
depends=('libexpat' 'openssl' 'ca-certificates' 'libintl')
makedepends=('libexpat-devel' 'openssl-devel' 'autotools' 'gcc' 'gettext-devel')
options=('libtool') # FS#16067
source=(https://notroj.github.io/neon/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('70cc7f2aeebde263906e185b266e04e0de92b38e5f4ecccbf61e8b79177c2f07')

prepare() {
  cd "${srcdir}"/${pkgbase}-${pkgver}

  ./autogen.sh
}

build() {
  cd "${srcdir}"/${pkgbase}-${pkgver}

  export MSYSTEM=CYGWIN
  ./configure --build=${CHOST} \
    --prefix=/usr \
    --with-expat \
    --enable-shared \
    --disable-static \
    --with-ssl=openssl \
    --with-ca-bundle=/usr/ssl/certs/ca-bundle.crt
  make

  make DESTDIR="${srcdir}/dest" install
}

package_libneon() {
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libneon-devel() {
  pkgdesc="Libneon headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libneon=${pkgver}" 'libexpat-devel' 'openssl-devel')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*-config ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=make-git
pkgver=4.2.92
pkgrel=1
pkgdesc="GNU make utility to maintain groups of programs"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/make"
license=('GPL3')
provides=("make")
conflicts=("make")
depends=('msys2-runtime' 'guile')
options=() # 'debug' '!strip')
makedepends=('gcc' 'git' 'rsync' 'libguile-devel' 'gmp-devel' 'libunistring-devel' 'libgc-devel' 'libffi-devel' 'libcrypt-devel')
install=$pkgname.install
source=("$pkgname"::'git+https://git.savannah.gnu.org/git/make.git'
		0001-remove-Werror.patch
		0002-no-symlinks-test.patch
		)
sha256sums=('SKIP'
			'99b6f851491847f739a0817f3decf50ccbf1911a0e4f7a3cb15eed7cb511b258'
			'5a8a9b1a9665b6d1536a98d3f94e5b5c2a4ae9214fc6d2cd3efdcdf0f50b2a23'
			)

prepare() {
  cd ${srcdir}/${pkgname}
  patch -p1 -i ${srcdir}/0001-remove-Werror.patch
  patch -p1 -i ${srcdir}/0002-no-symlinks-test.patch
  ./bootstrap
}

build() {
  cd ${srcdir}/${pkgname}
  ./configure \
    --prefix=/usr \
    --build=${CHOST} \
    --without-libintl-prefix \
    --without-libiconv-prefix \
    ac_cv_dos_paths=yes
  make -j1 all
}

check() {
  cd ${srcdir}/${pkgname}
  make check
}

package() {
  cd ${srcdir}/${pkgname}
  make DESTDIR=${pkgdir} install
}

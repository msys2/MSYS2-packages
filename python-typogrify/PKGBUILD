# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgname=python-typogrify
_pyname=typogrify
pkgver=2.1.0
pkgrel=1
pkgdesc='filters to make caring about typography on the web a bit easier'
arch=('any')
url='https://github.com/justinmayer/typogrify'
msys2_references=(
  'purl: pkg:pypi/typogrify'
)
license=('spdx:BSD-3-Clause')
depends=('python' 'python-smartypants')
makedepends=(
  'python-hatchling'
  'python-build'
  'python-installer'
)
source=("https://pypi.python.org/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('f0aa004e98032a6e6be4c9da65e7eb7150e36ca3bf508adbcda82b4d003e61ee')

build() {
  cd "${_pyname}-${pkgver}"

  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime-3.5
pkgname=('msys2-runtime-3.5' 'msys2-runtime-3.5-devel')
pkgver=3.5.1
pkgrel=2
pkgdesc="Cygwin POSIX emulation engine"
arch=('x86_64')
url="https://www.cygwin.com/"
license=('GPL')
msys2_references=(
  'cygwin: cygwin'
)
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'mingw-w64-cross-crt'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'autotools'
             'xmlto'
             'docbook-xsl')
# re zipman: https://github.com/msys2/MSYS2-packages/pull/2687#issuecomment-965714874
options=('!zipman')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver}
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-path_conv-special-case-root-directory-to-have-traili.patch
        0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0013-strace.cc-Don-t-set-MSYS-noglob.patch
        0014-Add-debugging-for-build_argv.patch
        0015-Add-debugging-for-strace-make_command_line.patch
        0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0018-strace-quiet-be-really-quiet.patch
        0019-Introduce-the-enable_pcon-value-for-MSYS.patch
        0020-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0021-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0022-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0023-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0024-kill-kill-Win32-processes-more-gently.patch
        0025-Cygwin-make-option-for-native-inner-link-handling.patch
        0026-docs-skip-building-texinfo-and-PDF-files.patch
        0027-install-libs-depend-on-the-toollibs.patch
        0028-POSIX-ify-the-SHELL-variable.patch
        0029-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0030-uname-allow-setting-the-system-name-to-CYGWIN.patch
        0031-Pass-environment-variables-with-empty-values.patch
        0032-Optionally-disallow-empty-environment-values-again.patch
        0033-build_env-respect-the-MSYS-environment-variable.patch
        0034-When-converting-to-a-Unix-path-avoid-double-trailing.patch
        0035-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch
        0036-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch
        0037-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0038-proc-fix-error-the-address-of-iso15924-will-never-be.patch)
sha256sums=('SKIP'
            '3fe353105560908248719a77da7dbb929c870201bd4f7ce811b03900b33c9bd7'
            'b1f027063173ad78a2251251c5fc45022172d050bcf72081045d87fa69cd8652'
            'a7c6b6e58263011518d251e3679eac582709c578328bd4af92f1d20d8764f43c'
            'd6ae6e7aa1505342ee765b87bdda22a004d202ddce6d5eedf57408f1b2600ea5'
            '679c27eb83200a64445b67a7d9107f85fdcd75171be4c5a3d4959227744cc35a'
            'c14c38fae786e27c4fb82cf2b9d7c1c3e304e5cf8ffba85d3451ed9b780289c5'
            '05dc8e5603fe36cba87ef079f585067882af332834b0fef43860bf8d954efaa7'
            '962e192880ba4dbb2ef7e9139a49daf2f0bd3fe2f630ce83277237d056b847bd'
            '6a0fd8e6052605562033d8488100f0b09d2d2a4db1ffd7f77838d720baba9c23'
            '805677ccf31c238e0d7b59505d7d8503623c2398e73ce352bc5dc71d04e3e259'
            '1f9dc4dedc67484af412c290171da55f2f48b1fbcaf3c4503fa8ceee0c287d4c'
            '757de41fa6dddf384f63c35a3fffcef8da61641b179def1dce73042d11b95a8b'
            '28a9c03121755d446c30864cc153dcf2e7799b459d5b1f034cbf0f1556a3610d'
            '14bb01b9cb23a52a45c1a4316996cc8b89eec66f22e7aa52fe6b746e7e248143'
            '6ea4b7046f5b6e94ad1b541ee997a1ca3b2fe19217ed10f888955916187c9f9d'
            '4f68888a833e8876b80f051bae780baa4bf3eec08f1aa383a8050a5e5d61c1a9'
            '078316130362175a5dc4292743b68c55f68b28dbb1d230313a5d849ba0edcf87'
            '5eeae976220b82167f57cdb2f400ae7304abc78ed39b78a444ff7e3de23d6451'
            '67a16aa86c801b79938b5880d2294820c8ddec73156e880736774f3355ed07c6'
            '1cc4e89f76899cbb9b26b7dc98491cfb37264913652c5cce351f255477893987'
            '2c1d992b7068a5bb3b87ffe454ce89a0e8d37a930520b86dc3bf39cc2c0ceaa4'
            '5d5fceeae06efcf9ebf8a3a18cab002a439004ff3c75f5709ce71476f0768507'
            '2f48febc39e79f073c1efa516a3fbd03372149c58ed93668fd8fda9bcb049b6c'
            '2c9ad5ef3f336fb90475fc33d49af8e8049ea0b38655172e35ef493c3885a104'
            '62b11dd049b0bec0892a66068f178987fcd0df632e231f1506918a8f1c74ac8e'
            '09202f05941cad3d7894319e2624d7b054701b3cf91cec0dc6afd06e6199f0b6'
            '31b868ecec17d226900bdc397bc28067f53b0bb544ae53de1a93bbe27062ce61'
            'a08ed7293dbfd8bbbc10213fe1bf0d54e2babdf256248f7268279cd86e5da8c2'
            'e4cd14573ef56eb200364c5c5a80f816c675aaa510df964ba97e03d526d98c52'
            'a4d7711ccef5879cab4e83c7f614d06f9ecb1a1f8bc09e3a7c53674a6e0c6834'
            'bf5fd6a184368ccfc1d691a50b290aecf62b1543575aa056b9da712590d4290f'
            '07c09f069afe63699173076c40ab964712328d28d642a7689642b11940589e00'
            '342b04f45a0a9095c0162c9f36185e839a8b71a16907d2d45ae8400001666134'
            'db7167ceb761934e66238c5c66338534e73e1f7e38838235513611ce75b02c9b'
            '752edd9d25d8024be20c97fec5541b5c854eea7724862858633c6968acfde6f6'
            'cd403827179ff05c7c2a83a6b26e1bc6ea7e215ab1d2d94e855615c919756f13'
            '20cafb5e1e4044ec32de4b8292288cf4ed978b57e2b4e1d42a6579b517498db5'
            'f3f7d70a21dd9cd0026d5cbc825a19fe61636e3f492fbd5cceb03381467cb054')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
 apply_git_am_with_msg 0001-Add-MSYS2-triplet.patch \
  0002-Fix-msys-library-name-in-import-libraries.patch \
  0003-Rename-dll-from-cygwin-to-msys.patch \
  0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch \
  0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch \
  0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch \
  0009-Do-not-convert-environment-for-strace.patch \
  0010-path_conv-special-case-root-directory-to-have-traili.patch \
  0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0013-strace.cc-Don-t-set-MSYS-noglob.patch \
  0014-Add-debugging-for-build_argv.patch \
  0015-Add-debugging-for-strace-make_command_line.patch \
  0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch \
  0018-strace-quiet-be-really-quiet.patch \
  0019-Introduce-the-enable_pcon-value-for-MSYS.patch \
  0020-popen-call-usr-bin-sh-instead-of-bin-sh.patch \
  0021-Expose-full-command-lines-to-other-Win32-processes-b.patch \
  0022-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch \
  0023-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch \
  0024-kill-kill-Win32-processes-more-gently.patch \
  0025-Cygwin-make-option-for-native-inner-link-handling.patch \
  0026-docs-skip-building-texinfo-and-PDF-files.patch \
  0027-install-libs-depend-on-the-toollibs.patch \
  0028-POSIX-ify-the-SHELL-variable.patch \
  0029-Handle-ORIGINAL_PATH-just-like-PATH.patch \
  0030-uname-allow-setting-the-system-name-to-CYGWIN.patch \
  0031-Pass-environment-variables-with-empty-values.patch \
  0032-Optionally-disallow-empty-environment-values-again.patch \
  0033-build_env-respect-the-MSYS-environment-variable.patch \
  0034-When-converting-to-a-Unix-path-avoid-double-trailing.patch \
  0035-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch \
  0036-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch \
  0037-path-conversion-Introduce-ability-to-switch-off-conv.patch \
  0038-proc-fix-error-the-address-of-iso15924-will-never-be.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=stringop-truncation -Wno-error=array-bounds -Wno-error=overloaded-virtual -Wno-narrowing -Wno-use-after-free"

  # otherwise it asks git which appends "-dirty" because of our uncommited patches
  CFLAGS+=" -DCYGPORT_RELEASE_INFO=${pkgver}"

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime-3.5() {
  pkgdesc="Posix emulation engine for Windows"
  provides=("msys2-runtime=${pkgver}")
  conflicts=('catgets' 'libcatgets' 'msys2-runtime-3.3' 'msys2-runtime')
  replaces=('catgets' 'libcatgets')

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-3.5-devel() {
  pkgdesc="MSYS2 headers and libraries"
  depends=("msys2-runtime-3.5=${pkgver}")
  provides=("msys2-runtime-devel=${pkgver}")
  conflicts=('libcatgets-devel' 'msys2-runtime-3.3-devel' 'msys2-runtime-devel')
  replaces=('libcatgets-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

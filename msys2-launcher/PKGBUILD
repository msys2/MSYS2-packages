# Maintainer: David Macek <david.macek.0@gmail.com>

_realname="msys2-launcher"
pkgname=("${_realname}")
pkgver=1.5
pkgrel=3
pkgdesc="Helper for launching MSYS2 shells"
arch=('x86_64' 'i686')
license=('spdx:MIT')
backup=({mingw32,mingw64,msys2,ucrt64,clang64,clangarm64}.ini)
conflicts=("${_realname}-git")
replaces=("${_realname}-git")
depends=("mintty>=1~2.2.1")
if [[ "${CARCH}" == 'i686' ]]; then
  makedepends+=('mingw-w64-cross-mingw32-gcc')
else
  makedepends+=('mingw-w64-cross-mingw64-gcc')
fi
source=("https://github.com/msys2/msys2-launcher/archive/v${pkgver}.tar.gz")
sha256sums=('bd0552417efd04e5c46c2c7009181d72fac0380e99f43d4b2633a505f147f773')
msys2_references=(
  'internal'
)

build() {
  cd "msys2-launcher-${pkgver}"
  make PREFIX=/opt/bin/${CARCH}-w64-mingw32-
}

package() {
  cd "msys2-launcher-${pkgver}"
  cp {mingw32,mingw64,msys2,ucrt64,clang64,clangarm64}.{exe,ini} "${pkgdir}"
}

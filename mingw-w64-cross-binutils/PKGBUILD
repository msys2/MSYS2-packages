# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>

_realname=binutils
_mingw_suff=mingw-w64-cross
pkgname=("${_mingw_suff}-${_realname}")
#_basever=2.24.51
#pkgver=${_basever}.20131119
pkgver=2.25
pkgrel=2
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/binutils/"
license=('GPL')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
depends=("libiconv" "zlib")
checkdepends=('dejagnu' 'bc')
makedepends=("libiconv" "zlib")
options=('staticlibs' '!distcc' '!ccache' '!buildflags')
#install=binutils.install
source=(#"https://mirrors.kernel.org/sources.redhat.com/binutils/snapshots/binutils-${_basever}.tar.bz2"
        https://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig}
        0001-enable-gold-on.mingw32.patch
        0002-check-for-unusual-file-harder.patch
        0007-armv7-w64-mingw32.patch
        0008-fix-libiberty-makefile.mingw.patch
        0009-fix-libiberty-configure.mingw.patch
        0110-binutils-mingw-gnu-print.patch
        0900-binutils-trunk-msys2.patch
        27aaeda.diff
        a93d5cb.diff
        9ac47a4.diff)
sha256sums=('22defc65cfa3ef2a3395faaea75d6331c6e62ea5dfacfed3e2ec17b08c882923'
            'SKIP'
            '93296b909e1a4f9d8a4bbe2437aafa17ca565ef6642a9812b0360c05be228c9d'
            'd182569814dbcad46ba6f2b4d72f4f827cefa7d0bd7030c89379613a53ca9786'
            'fa634d3f1827051a3833fed8aed4bda8cb5d5ba46ba08829ebe97c4cceae624f'
            '7c4349bbd4ad57157c896917fb692b41eb6d1ce50197c58fecbc45e5501b0104'
            '4849fe969fc7abb6b6dfcef4d4c6506f23831d86ba22c6dbf8d3039212654837'
            'e33541d4fc7bbf96e41ad2636f57f521f73d364ef72968a3ea2fa6c8596c9ebb'
            '4661ea9608e0c00c95dff5a43e4db69917a16b2877f4fae2e4f4330a5ec592be'
            'ec32f5a63fe00315586d1502bd91fc0c700ce63174fc5eade3ae96420140b134'
            '2ed73d77ec8757cf0deef72aee44e55c0801ac3c75c79fdc727cf5fb7d9b983c'
            'de7f8850150921a321196822a18ddca8574e69e706323226c2156752318fb6d0')

_targets="i686-w64-mingw32 x86_64-w64-mingw32 armv7-w64-mingw32"

prepare() {
  cd ${srcdir}/binutils-${pkgver}

  patch -p1 -i ${srcdir}/0001-enable-gold-on.mingw32.patch
  patch -p1 -i ${srcdir}/0002-check-for-unusual-file-harder.patch
  patch -p1 -i ${srcdir}/0007-armv7-w64-mingw32.patch
  patch -p1 -i ${srcdir}/0008-fix-libiberty-makefile.mingw.patch
  patch -p1 -i ${srcdir}/0009-fix-libiberty-configure.mingw.patch
  patch -p1 -i ${srcdir}/0110-binutils-mingw-gnu-print.patch
  patch -p1 -i ${srcdir}/0900-binutils-trunk-msys2.patch

  #do not install libiberty
  #sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  # Upstream patches
  patch -p1 -i "${srcdir}"/27aaeda.diff
  patch -p1 -i "${srcdir}"/a93d5cb.diff
  patch -p1 -i "${srcdir}"/9ac47a4.diff
}

build() {
  for _target in ${_targets}; do
    mkdir -p ${srcdir}/binutils-build-${_target} && cd ${srcdir}/binutils-build-${_target}

    if [ "${_target}" != "armv7-w64-mingw32" ]; then
      local _conf='--enable-lto'
    else
      local _conf=''
    fi

    if [ "${_target}" = "x86_64-w64-mingw32" ]; then
     _conf+=' --enable-64-bit-bfd'
    fi

    ${srcdir}/binutils-${pkgver}/configure \
      --build=${CHOST} \
      --host=${CHOST} \
      --target=${_target} \
      --prefix=/opt \
      --disable-werror \
      --with-libiconv-prefix=/usr \
      $_conf

    make
  done
}

check() {
  for _target in ${_targets}; do
    cd ${srcdir}/binutils-build-${_target}

    # unset LDFLAGS as testsuite makes assumptions about which ones are active
    # do not abort on errors - manually check log files
    make LDFLAGS="" -k check || true
  done
}

package() {
  for _target in ${_targets}; do
    cd ${srcdir}/binutils-build-${_target}
    make DESTDIR=${pkgdir} install

    # Remove unwanted files
    #rm ${destdir}/usr/share/info/{configure,standards}.info

    # Add some useful headers
    #install -m644 ${srcdir}/binutils-${pkgver}/include/libiberty.h ${pkgdir}/opt/include
    #install -m644 ${srcdir}/binutils-${pkgver}/include/demangle.h ${pkgdir}/opt/include
  done
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Digest-HMAC
pkgname=perl-${_realname}
pkgver=1.03
pkgrel=2
pkgdesc="Perl Module: Keyed-Hashing for Message Authentication."
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/${_realname}/"
groups=('perl-modules')
depends=('perl>=5.10.0') # 'perl-Digest-SHA1' in perl core now
options=(!emptydirs)
replaces=('Digest-HMAC')
provides=('Digest-HMAC')
source=(https://www.cpan.org/authors/id/G/GA/GAAS/${_realname}-${pkgver}.tar.gz)
sha256sums=('3bc72c6d3ff144d73aefb90e9a78d33612d58cf1cd1631ecfb8985ba96da4a59')

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${srcdir}/${_realname}-${pkgver}
  make test
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make install DESTDIR=${pkgdir}
}

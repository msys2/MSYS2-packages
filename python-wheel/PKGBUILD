# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=wheel
pkgname="python-${_realname}"
pkgver=0.41.3
pkgrel=1
pkgdesc="A built-package format for Python"
arch=('any')
url="https://pypi.python.org/pypi/wheel"
license=('spdx:MIT')
depends=("python")
makedepends=(
  "python-flit-core"
  "python-installer"
  "python-build"
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('4d4987ce51a49370ea65c0bfd2234e8ce80a12780820d9dc462597a6e60d0841')

build() {
  cd "${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}


package() {
  cd "${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=wheel
pkgname="python-${_realname}"
pkgver=0.43.0
pkgrel=1
pkgdesc="A built-package format for Python"
arch=('any')
url="https://pypi.python.org/pypi/wheel"
msys2_references=(
  "anitya: 11428"
  "pypi:wheel"
  "cpe: cpe:/a:wheel_project:wheel"
)
license=('spdx:MIT')
depends=("python")
makedepends=(
  "python-flit-core"
  "python-installer"
  "python-build"
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('465ef92c69fa5c5da2d1cf8ac40559a8c940886afcef87dcf14b9470862f1d85')

build() {
  cd "${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}


package() {
  cd "${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

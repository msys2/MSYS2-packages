pkgbase=wiggle
pkgname=('wiggle' 'wiggle-devel')
pkgver=1.3
pkgrel=1
pkgdesc='A utility to apply rejected patches and perform word-wise diffs'
arch=('i686' 'x86_64')
url='http://neil.brown.name/wiggle'
license=('GPL2')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/neilbrown/${pkgbase}/archive/refs/tags/v${pkgver}.tar.gz")
depends=('ncurses')
makedepends=('gcc' 'ncurses-devel')
sha256sums=('ff92cf0133c1f4dce33563e263cb30e7ddb6f4abdf86d427b1ec1490bec25afa')


build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make ./${pkgbase} V=1 CWFLAGS='-Wall -Wstrict-prototypes -Wextra'
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make test
}

package_wiggle() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make install DESTDIR="${pkgdir}"

  # install license
  install -vDt "${pkgdir}/usr/share/licenses/${pkgname}/" COPYING
}

package_wiggle-devel() {
  pkgdesc+=' (devel)'
  depends+=('ncurses-devel')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  install -vDt "${pkgdir}/usr/include/" wiggle.h
  install -vDt "${pkgdir}/usr/lib/" 'o/libwiggle.a'

  # install license
  install -vDt "${pkgdir}/usr/share/licenses/${pkgname}/" COPYING
}

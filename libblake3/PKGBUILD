# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=BLAKE3
pkgname=libblake3
pkgver=1.6.1
pkgrel=2
pkgdesc='The official C implementation of BLAKE3'
arch=(x86_64 i686)
url=https://github.com/BLAKE3-team/BLAKE3
msys2_reference=(
  'archlinux: libblake3'
  'aur: libblake3'
  'gentoo: dev-libs/blake3'
)
license=('spdx:CC0-1.0 OR Apache-2.0 OR Apache-2.0 WITH LLVM-exception')
depends=(gcc-libs)
makedepends=(
  gcc
  cmake
  ninja
)
source=("https://github.com/BLAKE3-team/BLAKE3/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
        0001-use-windows-asm.patch)
sha256sums=('1f2fbd93790694f1ad66eef26e23c42260a1916927184d78d8395ff1a512d285'
            'ae47916bb5b5ae870718ffe09e58539513379ca2f1161d79f3cf28d4230d8e36')

prepare() {
  cd "${_realname}-${pkgver}"

  patch -Np1 -i ../0001-use-windows-asm.patch
}

build() {

  # FIXME: fails to link otherwise
  CFLAGS+=" -DBLAKE3_NO_AVX512"

  cmake \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -D BUILD_SHARED_LIBS=ON \
    -S "${_realname}-${pkgver}/c" \
    -B "build-${CARCH}"

  cmake --build "build-${CARCH}"
}

package() {
  DESTDIR="$pkgdir" cmake --install "build-${CARCH}"
}

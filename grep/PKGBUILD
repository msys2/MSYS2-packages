# Maintainer: Alexey Pavlov <alexpux@gmail.com>
#
# NOTE: Since grep 3.1 support for handling Windows line endings was removed by: 
# https://git.savannah.gnu.org/cgit/grep.git/commit/?h=v3.1&id=5ac3d87a69122d45a837840914c2f9e66b5bb8d4
# https://git.savannah.gnu.org/cgit/grep.git/commit/?h=v3.1&id=c4485ac49a977d666187db4c5c25045215976577
# Removing it lead for MSYS2 broken configure tests for MINGW packages that use autotools.
# See next issues before do grep update:
# https://github.com/msys2/MSYS2-packages/issues/1353
# https://github.com/msys2/MSYS2-packages/pull/2163
# https://github.com/msys2/MINGW-packages/issues/9771


pkgname=grep
pkgver=3.12
pkgrel=7
epoch=1
pkgdesc="A string search utility"
arch=('i686' 'x86_64')
license=('GPL3')
url="https://www.gnu.org/software/grep/"
msys2_references=(
  "cpe: cpe:/a:gnu:grep"
)
depends=('libiconv' 'libintl' 'libpcre' 'sh')
makedepends=('texinfo' 'gettext-devel' 'libiconv-devel' 'pcre-devel' 'autotools' 'gcc')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig})
sha256sums=('2649b27c0e90e632eadcd757be06c6e9a4f48d941de51e7c0f83ff76408a07b9'
            'SKIP')
validpgpkeys=('155D3FC500C834486D1EEA677FD9FCCB000BEEEE')  # Jim Meyering

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  export gl_cv_have_weak=no

  export MSYSTEM=CYGWIN
  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
      --build=${CYGWIN_CHOST} \
      --prefix=/usr \
      --without-libiconv-prefix \
      --without-libintl-prefix
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # Licenses
  install -Dm644 "${srcdir}"/${pkgname}-${pkgver}/README \
    "${pkgdir}"/usr/share/licenses/${_realname}/README
  install -Dm644 "${srcdir}"/${pkgname}-${pkgver}/COPYING \
    "${pkgdir}"/usr/share/licenses/${_realname}/COPYING
}

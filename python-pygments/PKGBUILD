# Maintainer: João Guerra <joca.bt@gmail.com>

_pyname=Pygments
pkgbase="python-pygments"
pkgname=("python2-pygments" "python3-pygments")
pkgver=2.2.0
pkgrel=2
pkgdesc="A syntax highlighting engine written in Python"
arch=("i686" "x86_64")
url="http://pygments.org/"
license=('BSD')
makedepends=("python2-setuptools" "python3-setuptools")
source=("https://pypi.python.org/packages/source/P/Pygments/Pygments-${pkgver}.tar.gz")
noextract=("Pygments-${pkgver}.tar.gz")
sha256sums=('dbae1046def0efb574852fab9e90209b23f556367b5a320c0bcb871c77c3e8cc')

prepare () {
  # workaround for symlink issue
  tar -xf ${_pyname}-${pkgver}.tar.gz || true
  cp ${_pyname}-${pkgver}/scripts/debug_lexer.py \
    ${_pyname}-${pkgver}/scripts/find_error.py

}

package_python2-pygments () {
  depends=("python2")
  install="python2-pygments.install"

  cd "${_pyname}-${pkgver}"
  /usr/bin/python2 setup.py install --root="${pkgdir}" -O1
  mv "${pkgdir}/usr/bin/pygmentize" "${pkgdir}/usr/bin/pygmentize2"
  install -Dm644 external/pygments.bashcomp "${pkgdir}/usr/share/bash-completion/completions/pygmentize2"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python3-pygments () {
  depends=("python3")

  cd "${_pyname}-${pkgver}"
  /usr/bin/python3 setup.py install --root="${pkgdir}" -O1
  install -Dm644 external/pygments.bashcomp "${pkgdir}/usr/share/bash-completion/completions/pygmentize"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# Maintainer: Jo√£o Guerra <joca.bt@gmail.com>

_pyname=Pygments
pkgbase="python-pygments"
pkgname=("python-pygments")
pkgver=2.13.0
pkgrel=1
pkgdesc="A syntax highlighting engine written in Python"
arch=("i686" "x86_64")
url="http://pygments.org/"
license=('BSD')
depends=("python")
makedepends=("python-setuptools")
provides=("python3-pygments")
conflicts=("python3-pygments")
replaces=("python3-pygments")
source=("https://pypi.python.org/packages/source/P/Pygments/Pygments-${pkgver}.tar.gz")
noextract=("Pygments-${pkgver}.tar.gz")
sha256sums=('56a8508ae95f98e2b9bdf93a6be5ae3f7d8af858b43e02c5a2ff083726be40c1')

prepare () {
  # workaround for symlink issue
  tar -xf ${_pyname}-${pkgver}.tar.gz || true
  cp ${_pyname}-${pkgver}/scripts/debug_lexer.py \
    ${_pyname}-${pkgver}/scripts/find_error.py

}

package () {
  cd "${_pyname}-${pkgver}"

  /usr/bin/python setup.py install --root="${pkgdir}" -O1
  install -Dm644 external/pygments.bashcomp "${pkgdir}/usr/share/bash-completion/completions/pygmentize"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pytest
pkgbase=python-pytest
pkgname=('python-pytest')
pkgver=7.4.3
pkgrel=1
provides=("python3-pytest")
replaces=("python3-pytest")
conflicts=("python3-pytest")
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('spdx:MIT')
url="https://pytest.org/"
depends=('python'
         'python-pluggy'
         'python-packaging'
         'python-iniconfig')
makedepends=('python-build'
             'python-installer'
             'python-setuptools'
             'python-setuptools-scm'
             'python-wheel')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('d989d136982de4e3b29dabcc838ad581c64e8ed52c11fbe86ddebd9da0818cd5')

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}

  cd "${srcdir}/pytest-${pkgver}"
  /usr/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd pytest-${pkgver}
  /usr/bin/python -m installer --destdir="${pkgdir}" dist/*.whl
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

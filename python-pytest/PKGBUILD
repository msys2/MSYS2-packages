# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgbase=python-pytest
pkgname=('python-pytest')
pkgver=7.2.0
pkgrel=1
provides=("python3-pytest")
replaces=("python3-pytest")
conflicts=("python3-pytest")
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="https://pytest.org/"
depends=('python'
         'python-attrs'
         'python-pluggy'
         'python-packaging'
         'python-iniconfig'
         'python-tomli')
makedepends=('python-build'
             'python-installer'
             'python-setuptools'
             'python-setuptools-scm'
             'python-wheel')
source=("pytest-${pkgver}.tar.gz::https://github.com/pytest-dev/pytest/archive/${pkgver}.tar.gz")
sha256sums=('cb62381a473a6cfce8e4b5d2fc873ec248e64f5a7a6ce06fa312e3023c65c25e')

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}

  cd "${srcdir}/pytest-${pkgver}"
  /usr/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd pytest-${pkgver}
  /usr/bin/python -m installer --destdir="${pkgdir}" dist/*.whl
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgbase=python-pytest
pkgname=('python-pytest')
pkgver=6.2.1
pkgrel=2
provides=("python3-pytest")
replaces=("python3-pytest")
conflicts=("python3-pytest")
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="https://pytest.org/"
depends=('python'
         'python-atomicwrites'
         'python-attrs'
         'python-more-itertools'
         'python-pluggy'
         'python-py'
         'python-setuptools'
         'python-packaging'
         'python-iniconfig'
         'python-toml'
         'python-six')
makedepends=('python-setuptools-scm')
source=("pytest-${pkgver}.tar.gz::https://github.com/pytest-dev/pytest/archive/${pkgver}.tar.gz")
sha256sums=('efc60ff9d50f63a6cc1571f5f121155cd1775905a29dbc58b55612ac5a81f530')

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
  
  cd "${srcdir}/pytest-${pkgver}"
  /usr/bin/python setup.py build
}

package() {
  cd pytest-${pkgver}
  /usr/bin/python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

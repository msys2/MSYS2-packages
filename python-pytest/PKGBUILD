# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgbase=python-pytest
pkgname=('python-pytest')
pkgver=8.3.5
pkgrel=1
provides=("python3-pytest")
replaces=("python3-pytest")
conflicts=("python3-pytest")
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('spdx:MIT')
url="https://pytest.org/"
msys2_repository_url="https://github.com/pytest-dev/pytest"
msys2_references=(
  "anitya: 3765"
  "purl: pkg:pypi/pytest"
)
depends=('python'
         'python-attrs'
         'python-pluggy'
         'python-packaging'
         'python-iniconfig')
makedepends=('python-build'
             'python-installer'
             'python-setuptools'
             'python-setuptools-scm')
source=("pytest-${pkgver}.tar.gz::https://github.com/pytest-dev/pytest/archive/${pkgver}.tar.gz")
sha256sums=('b58e6aa58a553a54950405ad6e3005459dd8c13ecf2e1735703a3cfa5c72b381')

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}

  cd "${srcdir}/pytest-${pkgver}"
  /usr/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd pytest-${pkgver}
  /usr/bin/python -m installer --destdir="${pkgdir}" dist/*.whl
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

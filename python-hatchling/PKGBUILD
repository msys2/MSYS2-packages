# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_pyname=hatchling
pkgname=python-hatchling
pkgver=1.28.0
pkgrel=1
pkgdesc="A modern project, package, and virtual env manager (backend)"
arch=('any')
url="https://github.com/pypa/hatch"
msys2_references=(
  "purl: pkg:pypi/hatchling"
)
license=('spdx:MIT')
depends=(
  'python'
  'python-packaging'
  'python-pathspec'
  'python-pluggy'
  'python-editables'
  'python-trove-classifiers'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
)
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('4d50b02aece6892b8cd0b3ce6c82cb218594d3ec5836dbde75bf41a21ab004c8')

build() {
  cd "${_pyname}-${pkgver}"

  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

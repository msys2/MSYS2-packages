# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=File-Listing
pkgname=perl-${_realname}
pkgver=6.04
pkgrel=2
pkgdesc="parse directory listing"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.8.8' 'perl-HTTP-Date>=6')
conflicts=('perl-libwww<6')
url="https://metacpan.org/release/File-Listing"
groups=('perl-modules')
source=("https://www.cpan.org/authors/id/G/GA/GAAS/${_realname}-${pkgver}.tar.gz")
sha256sums=('1e0050fcd6789a2179ec0db282bf1e90fb92be35d1171588bd9c47d52d959cf5')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd ${srcdir}/${_realname}-${pkgver}
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd ${srcdir}/${_realname}-${pkgver}
    make test
  )
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make DESTDIR="$pkgdir" install
}

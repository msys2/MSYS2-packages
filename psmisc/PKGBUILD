# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=psmisc
pkgver=23.3
pkgrel=1
pkgdesc="Miscellaneous procfs tools"
arch=('i686' 'x86_64')
url="https://psmisc.sourceforge.io/"
license=('GPL')
groups=('sys-utils')
depends=('msys2-runtime' 'gcc-libs' 'ncurses' 'libiconv' 'libintl')
makedepends=('gcc' 'ncurses-devel' 'libiconv-devel' 'gettext-devel')
source=(${pkgname}-${pkgver}.tar.gz::https://gitlab.com/psmisc/psmisc/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz)
sha256sums=('f23dfd7993cd16e7554ce46e4cb62dc210b4693d73792794f516b066ec700be3')

prepare() {
  cd ${pkgname}-v${pkgver}
  echo ${pkgver} > .tarball-version
  ./autogen.sh
}

build() {
  cd ${pkgname}-v${pkgver}
  ./configure \
    --prefix=/usr \
    --without-libiconv-prefix \
    --without-libintl-prefix \
    --disable-harden-flags \
    CPPFLAGS="${CPPFLAGS} -I/usr/include/ncursesw"
  make
}

package() {
  cd ${pkgname}-v${pkgver}
  make DESTDIR=${pkgdir} install
}

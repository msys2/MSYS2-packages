# Maintainer: Orgad Shaneh <orgads@gmail.com>

pkgname=gsl
pkgver=2.7.1
pkgrel=1
pkgdesc="The GNU Scientific Library (GSL) is a modern numerical library for C and C++ programmers (mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/gsl/gsl.html"
license=("GPL")
makedepends=("autotools" "gcc")
options=('staticlibs' 'strip')
source=("https://ftp.gnu.org/gnu/gsl/${pkgname}-${pkgver}.tar.gz")
sha256sums=('dcb0fbd43048832b757ff9942691a8dd70026d5da0ff85601e52687f6deeb34b')

build() {
  [[ -d "${srcdir}/build-${CHOST}" ]] && rm -rf "${srcdir}/build-${CHOST}"
  mkdir -p "${srcdir}/build-${CHOST}" && cd "${srcdir}/build-${CHOST}"

  ../${pkgname}-${pkgver}/configure \
    --build=${CHOST} \
    --host=${CHOST} \
    --prefix=/usr

  make
}

package() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR="${pkgdir}" install
}

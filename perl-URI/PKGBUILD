# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=URI
pkgname=perl-${_realname}
pkgver=5.34
pkgrel=1
pkgdesc="Uniform Resource Identifiers (absolute and relative)"
arch=('any')
url="https://metacpan.org/dist/URI"
msys2_references=(
  "purl: pkg:cpan/OALDERS/URI"
)
groups=('perl-modules')
license=('PerlArtistic')
depends=('perl>=5.10.0')
checkdepends=('perl-Test-Needs')
provides=('perl-URI-Escape=3.30')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/O/OA/OALDERS/${_realname}-${pkgver}.tar.gz")
sha256sums=('de64c779a212ff1821896c5ca2bb69e74767d2674cee411e777deea7a22604a8')

build() {
  cd "${srcdir}/${_realname}-$pkgver"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-$pkgver"
  make install DESTDIR="${pkgdir}"
}

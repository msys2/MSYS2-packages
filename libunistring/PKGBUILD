# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libunistring' 'libunistring-devel')
pkgver=1.4.1
pkgrel=1
pkgdesc="Library for manipulating Unicode strings and C strings."
url="https://www.gnu.org/software/libunistring/"
msys2_repository_url="https://git.savannah.gnu.org/gitweb/?p=libunistring.git"
msys2_references=(
  "cpe: cpe:/a:gnu:libunistring"
)
arch=('i686' 'x86_64')
license=('spdx:LGPL-3.0-or-later OR GPL-2.0-or-later')
depends=('libiconv')
makedepends=('libiconv-devel' 'autotools' 'gcc')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        libunistring-0.9.3-2.src.patch)
sha256sums=('67d88430892527861903788868c77802a217b0959990f7449f2976126a307763'
            'SKIP'
            '629fd3b0ddc7235ef0dc408c3d5ba7df5797348b056a0a6a3ba49a0ca486a280')
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871'  # Daiki Ueno <ueno@unixuser.org>
              'E0FFBD975397F77A32AB76ECB6301D9E1BBEAC08') # Bruno Haible (Free Software Development) <bruno@clisp.org>

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/libunistring-0.9.3-2.src.patch

  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export gl_cv_have_weak=no
  ./configure \
    --build=${CHOST} \
    --prefix=/usr

  make
  make DESTDIR="${srcdir}/dest" install
}

package_libunistring() {
  groups=('libraries')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libunistring-devel() {
  pkgdesc="libunistring headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libunistring=${pkgver}" "libiconv-devel")

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}

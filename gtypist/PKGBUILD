# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

pkgname=gtypist
pkgver=2.9.5
pkgrel=1
pkgdesc="Universal typing tutor"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gtypist/gtypist.html"
license=("GPL")
depends=('ncurses' 'perl')
makedepends=('gcc' 'ncurses-devel' 'libiconv-devel')
source=(ftp://ftp.gnu.org/gnu/gtypist/$pkgname-$pkgver.tar.gz)
md5sums=('6098c32890f2437384b5efe4e993fb32')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="$pkgdir"/usr install
  rm -f "${pkgdir}/usr/share/info/dir"
}

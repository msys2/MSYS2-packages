# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=DBI
pkgname=perl-${_realname}
pkgver=1.646
pkgrel=1
pkgdesc="Database independent interface for Perl"
arch=('i686' 'x86_64')
url="https://metacpan.org/dist/DBI"
msys2_references=(
  "cpe: cpe:/a:perl:database_interface"
  "cpe: cpe:/a:perl:dbi"
)
groups=('perl-modules')
license=('GPL' 'PerlArtistic')
depends=('perl')
makedepends=('perl-devel' 'gcc')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/H/HM/HMBRAND/${_realname}-${pkgver}.tar.gz"
        'DBI-1.627.patch')
sha256sums=('53ab32ac8c30295a776dde658df22be760936cdca5a3c003a23bda6d829fa184'
            '67efabf04c22dc7ce1ba4af141e8e35b8b6d42a2d043a2fab590e3f55a956761')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/DBI-1.627.patch
}

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=DBI
pkgname=perl-${_realname}
pkgver=1.642
pkgrel=2
pkgdesc="Database independent interface for Perl"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/DBI/"
groups=('perl-modules')
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/T/TI/TIMB/${_realname}-${pkgver}.tar.gz"
        'DBI-1.627.patch')
sha256sums=('3f2025023a56286cebd15cb495e36ccd9b456c3cc229bf2ce1f69e9ebfc27f5d'
            '67efabf04c22dc7ce1ba4af141e8e35b8b6d42a2d043a2fab590e3f55a956761')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/DBI-1.627.patch
}

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

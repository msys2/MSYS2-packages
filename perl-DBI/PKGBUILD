# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=DBI
pkgname=perl-${_realname}
pkgver=1.630
pkgrel=1
pkgdesc="Database independent interface for Perl"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/DBI/"
groups=('perl-modules')
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/T/TI/TIMB/${_realname}-${pkgver}.tar.gz"
        'DBI-1.627.patch')
md5sums=('306020fe7b54a53773f54ad581af8c54'
         '386f95bb6d8b383abbb745bbba348d2b')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/DBI-1.627.patch
}

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

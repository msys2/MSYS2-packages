# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=dejagnu
pkgver=1.5.3
pkgrel=1
pkgdesc="Framework for testing other programs"
arch=('any')
url="http://www.gnu.org/software/dejagnu/"
license=('GPL')
depends=('expect')
#install=dejagnu.install
source=(http://ftp.gnu.org/gnu/dejagnu/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('5bda2cdb1af51a80aecce58d6e42bd2f'
         'SKIP')
validpgpkeys=('CB0B31452BBE862933013D9176362888B55988D4')   # Ben Elliston

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}

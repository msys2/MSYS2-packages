# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=dejagnu
pkgver=1.6.3
pkgrel=2
pkgdesc="Framework for testing other programs"
arch=('any')
url="https://www.gnu.org/software/dejagnu/"
license=('GPL')
depends=('expect')
makedepends=('autotools' 'gcc')
#install=dejagnu.install
source=(https://ftp.gnu.org/gnu/dejagnu/${pkgname}-${pkgver}.tar.gz{,.sig}
        "0001-PATCH-Add-lregex-to-unit-target.patch"
        "0002-Remove-redundant-ESC-escape-characters.patch")
sha256sums=('87daefacd7958b4a69f88c6856dbd1634261963c414079d0c371f589cd66a2e3'
            'SKIP'
            'dc7e71110eb8b81c6f6666fb7f6b8aee6a4dd65f8537d7fb28d3a3e9ff8e11e8'
            '34824370953f9c8c1a55f5f9e6322dac78755666734d20fe212aab9e0c97bbb1')
validpgpkeys=('D231B45ECB0B7BD8F6E7A682CE9D6843AABACC90')   # Jacob Bachmeyer

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  apply_patch_with_msg "0001-PATCH-Add-lregex-to-unit-target.patch" \
    0002-Remove-redundant-ESC-escape-characters.patch

  autoreconf -fiv
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}

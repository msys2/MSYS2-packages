# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgname=meson
pkgver=1.6.1
pkgrel=1
pkgdesc='High-productivity build system'
arch=('any')
url="https://mesonbuild.com/"
msys2_repository_url="https://github.com/mesonbuild/meson"
msys2_references=(
  "pypi:meson"
)
license=('spdx:Apache-2.0')
depends=('python' 'ninja' 'pkgconf')
makedepends=(
  "python-setuptools"
  "python-build"
  "python-installer"
)
source=("https://github.com/mesonbuild/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        "0001-cygwin-disguise.patch"
        "https://patch-diff.githubusercontent.com/raw/mesonbuild/meson/pull/12861.patch")
sha256sums=('1eca49eb6c26d58bbee67fd3337d8ef557c0804e30a6d16bfdf269db997464de'
            'SKIP'
            '7025acbbf8bfd26cf806542eeedb4bfd072b5382db43c1f39e605ab5d155c45a'
            '999e0751148bd47c4496509210618cdba5e7baeb2601fbf810a88fc193bd0378')
validpgpkeys=("19E2D6D9B46D8DAA6288F877C24E631BABB1FE70") # Jussi Pakkanen <jpakkane@gmail.com>

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}"/0001-cygwin-disguise.patch

  # https://github.com/mesonbuild/meson/pull/12861
  patch -p1 -i "${srcdir}"/12861.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

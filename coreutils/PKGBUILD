# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=coreutils
pkgver=9.4
pkgrel=1
pkgdesc="The basic file, shell and text manipulation utilities of the GNU operating system"
arch=('i686' 'x86_64')
license=('spdx:GPL-3.0-or-later' AND 'spdx:GFDL-1.3-or-later')
url="https://www.gnu.org/software/coreutils"
depends=('gmp' 'libiconv' 'libintl')
makedepends=('groff' 'gmp-devel' 'libiconv-devel' 'gettext-devel' 'help2man' 'autotools' 'gcc')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        coreutils-8.32-configure.ac.patch
        coreutils-8.32-doc-coreutils.texi.patch
        coreutils-8.32-lib-freopen.c.patch
        coreutils-8.32-lib-hash-pjw.c.patch
        coreutils-8.32-lib-hash-pjw.h.patch
        coreutils-8.32-lib-local.mk.patch
        coreutils-9.1-lib-root-dev-ino.h.patch
        coreutils-9.4-lib-root-dev-ino.c.patch
        coreutils-8.32-lib-same.c.patch
        coreutils-9.4-src-chcon.c.patch
        coreutils-8.32-src-chgrp.c.patch
        coreutils-9.4-src-chmod.c.patch
        coreutils-8.32-src-chown.c.patch
        coreutils-9.4-src-chown-core.h.patch
        coreutils-8.32-src-chroot.c.patch
        coreutils-9.4-src-copy.c.patch
        coreutils-8.32-src-dd.c.patch
        coreutils-8.32-src-dircolors.c.patch
        coreutils-9.4-src-install.c.patch
        coreutils-9.4-src-ls.c.patch
        coreutils-9.1-src-mv.c.patch
        coreutils-8.32-src-pwd.c.patch
        coreutils-9.4-src-remove.h.patch
        coreutils-8.32-src-rm.c.patch
        coreutils-9.4-src-stat.c.patch
        coreutils-9-src-dircolors-hin.patch
        004-msystem-osname-cygwin.patch
        cygwin.h
        cygwin.c)
sha256sums=('ea613a4cf44612326e917201bbbcdfbd301de21ffc3b59b6e5c07e040b275e52'
            'SKIP'
            '40052d667830078c3fbdd62db99b92310d3f3be4674c50be1debcd11d8ec6b3c'
            'dee3138601ccc3183418bc571359d5ebd49c9a0678778432c8b71cbe79431213'
            '553a798fd493d7537dc93d2b18d9329ee7e964a330fc088f261b440049a5d284'
            '6326eba2c476001b4a07abfc5a59aac881e6349e269252d006a7c71ed2b289ac'
            '5ceffc6464e006e53888a599ca4a29b752e8c345b530e90b3a8a19a8f7c28743'
            'bc9b1f0434fa9ca3b98d2fe6fcd821c64fc9b358516f3fe8873de1080afc3749'
            '055f99c882c6fff3273c1fbfc2838729a9167fb3be437fb64caf06646ecbda0d'
            '8b511b9155928a7027f7ceb9fd8ece9d532f6c3ff77c75edf28a61c25c4b9ce7'
            '06b089c5225273684e420f8b6e486e55400ff20a240a7d78d2e6cc9cabdeae8b'
            '2b42f8ab3832cef7337f6eace7dfc8dce4fb04ed9ebbdda99b1189e89c2025c7'
            'e0ef98efbae37297109f4b50f3922c08c3abd7c20401aaa520122667da6c1590'
            '9d3ca2197ea04d12696534526e75f4f1f01450701cc1f192a5465075d82faab5'
            '7951ec47a030444b74542a712a70fd5d8d6cdcb5a5a85a1886b3f35b60042365'
            '467689d047ebfc1d6c78d3047b8e44f026aa546929501a16d66af675053450b5'
            'fdcb5871c63ba2e4ca6a6c23c357c4a10fba2ab6975ee4f528f1eca1c3619aa8'
            'a2165a1444aebbc90eecd7d632ef6901a03497bdefd96961f06c98c022f499dd'
            '0cbe20d9280be2aa9a055131ea8d2aff041fd5879b2ec80203a359e64d3d523a'
            '05dc41601dd7276fc38f57ca0eadc9fd060d6f2724c6e48382f71209bf30ef66'
            '1adbee994448de83f02f3c5401b0c89210e0668e5e76c94b8374f0a268a55001'
            '52a8ec786692a15ee79db100c25e781cf538b5f61e3cefc21af001b37f3753ed'
            'a2e4b54690958cbf86262930aac240d18f607bf6b34dd006b337a5d1afc015ec'
            '1057b97007e65fb597ad4be036977303ac9be2e2a46c44c30fce0537ac04e96a'
            'dd381cc10b944ae84b5d942543d86569f838167dc390e16e17262a27a6a3b7b3'
            'bc3fb774431005318cb89f723175c79e9f18808c7ef6010c410d7ea9495f854e'
            '07ddcca3f127e60631e63012aa1553721e939b44dafe0220b85ecd727445681f'
            'f08f69c6089192dcb295c3110c8b0d59af30438894bb3eb8a817d162b14ae3e1'
            '3922afd54b2323772f5cbd0638d0887ef858199bbaf4076a1db318173021c3dc'
            '602faa31be04e3cba6cf39921f4da397fbca4a66baa5a1186e070a16dc65391a'
            '8dce514ac47518d78a31fd07addc6300912cb2965915798ad1381208d36c887c')
validpgpkeys=('6C37DC12121A5006BC1DB804DF6FD971306037D9')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  apply_patch_with_msg \
    coreutils-8.32-configure.ac.patch \
    coreutils-8.32-doc-coreutils.texi.patch \
    coreutils-8.32-lib-freopen.c.patch \
    coreutils-8.32-lib-hash-pjw.c.patch \
    coreutils-8.32-lib-hash-pjw.h.patch \
    coreutils-8.32-lib-local.mk.patch \
    coreutils-9.1-lib-root-dev-ino.h.patch \
    coreutils-9.4-lib-root-dev-ino.c.patch \
    coreutils-8.32-lib-same.c.patch \
    coreutils-9.4-src-chcon.c.patch \
    coreutils-8.32-src-chgrp.c.patch \
    coreutils-9.4-src-chmod.c.patch \
    coreutils-8.32-src-chown.c.patch \
    coreutils-9.4-src-chown-core.h.patch \
    coreutils-8.32-src-chroot.c.patch \
    coreutils-9.4-src-copy.c.patch \
    coreutils-8.32-src-dd.c.patch \
    coreutils-8.32-src-dircolors.c.patch \
    coreutils-9.4-src-install.c.patch \
    coreutils-9.4-src-ls.c.patch \
    coreutils-9.1-src-mv.c.patch \
    coreutils-8.32-src-pwd.c.patch \
    coreutils-9.4-src-remove.h.patch \
    coreutils-8.32-src-rm.c.patch \
    coreutils-9.4-src-stat.c.patch \
    coreutils-9-src-dircolors-hin.patch

  # https://github.com/msys2/MSYS2-packages/issues/3012
  apply_patch_with_msg \
    004-msystem-osname-cygwin.patch

  cp "${srcdir}"/cygwin.c lib/
  cp "${srcdir}"/cygwin.h src/

  autoreconf -fi
  sed -i -e 's|\(libstdbuf\.so\)$(EXEEXT)|\1|g' Makefile.in
}

build() {
  mkdir -p ${srcdir}/build-${CHOST} && cd ${srcdir}/build-${CHOST}

  export CYGWIN_CHOST="${CHOST/-msys/-cygwin}"

  export gl_cv_have_weak=no

  ../${pkgname}-${pkgver}/configure \
    --build=${CYGWIN_CHOST} \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --program-transform-name=s/kill/gkill/ \
    --without-libintl-prefix \
    --without-libiconv-prefix \
    --enable-install-program=arch,hostname \
    --enable-no-install-program=uptime \
    gl_cv_func_strtod_works=yes \
    gl_cv_func_strtold_works=yes

  make
}

check() {
  cd ${srcdir}/build-${CHOST}
  make check
}

package() {
  cd ${srcdir}/build-${CHOST}
  make DESTDIR=${pkgdir} install

  test ! -f ${pkgdir}/usr/lib/coreutils/libstdbuf.so.exe ||
  mv ${pkgdir}/usr/lib/coreutils/libstdbuf.so.exe \
    ${pkgdir}/usr/lib/coreutils/libstdbuf.dll

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/dircolors.hin ${pkgdir}/etc/DIR_COLORS
}

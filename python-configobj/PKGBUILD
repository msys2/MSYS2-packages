# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=configobj
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=5.0.9
pkgrel=2
pkgdesc="Simple but powerful config file reader and writer for Python"
arch=('any')
license=('spdx:BSD-3-Clause')
url="https://github.com/DiffSK/configobj"
msys2_references=(
  "purl: pkg:pypi/configobj"
)
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('03c881bbf23aa07bccf1b837005975993c4ab4427ba57f959afdd9d1a2386848')

build() {
  cd "${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
}

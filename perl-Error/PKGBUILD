# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Error
pkgname=perl-Error
pkgver=0.17029
pkgrel=1
pkgdesc="Perl/CPAN Error module - Error/exception handling in an OO-ish way"
url="https://metacpan.org/release/Error"
arch=('any')
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-Test-Pod' 'perl-test-pod-coverage')
options=('!emptydirs')
groups=('perl-modules')
source=(https://www.cpan.org/authors/id/S/SH/SHLOMIF/${_realname}-${pkgver}.tar.gz)
sha256sums=('1a23f7913032aed6d4b68321373a3899ca66590f4727391a091ec19c95bf7adc')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}


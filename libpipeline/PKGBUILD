# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libpipeline' 'libpipeline-devel')
pkgver=1.5.4
pkgrel=1
pkgdesc="a C library for manipulating pipelines of subprocesses in a flexible and convenient way"
url="http://libpipeline.nongnu.org/"
groups=('libraries')
arch=('i686' 'x86_64')
license=('BSD')
makedepends=('openssl-devel' 'zlib-devel' 'autotools')
options=('!libtool')
source=(https://download.savannah.gnu.org/releases/libpipeline/libpipeline-$pkgver.tar.gz{,.asc}
        libpipeline-1.5.1-msys2.patch)
sha256sums=('db785bddba0a37ef14b4ef82ae2d18b8824e6983dfb9910319385e28df3f1a9c'
            'SKIP'
            '847f6626c782fd0139e6e7d5c9adf97811b0765dddac3e363360480f7e6d27a3')
validpgpkeys=('87317259431038049C92C139F6DD330210FA4CD1') # Colin Watson <cjwatson@flatline.org.uk>

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/libpipeline-1.5.1-msys2.patch

  autoreconf -fi
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --build=${CHOST} --prefix=/usr
  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package_libpipeline() {
  depends=('gcc-libs')
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libpipeline-devel() {
  pkgdesc="libpipeline headers and libraries"
  options=('staticlibs')
  depends=("libpipeline=${pkgver}")
  groups=('development')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}

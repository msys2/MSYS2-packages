# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=mintty
pkgver=2.0.1
pkgrel=1
pkgdesc="Terminal emulator with native Windows look and feel"
arch=('i686' 'x86_64')
license=('GPL3')
groups=('base')
depends=('sh')
url="https://mintty.github.io"
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/mintty/mintty/archive/${pkgver}.tar.gz
        02-mintty-add-msys-64bit.patch)
sha1sums=('18a28d22c7dabbb9bf5de54d3f7eb73bc4001281'
          'f3686ce7f28a954b058c155016a884480c06d6b0')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/02-mintty-add-msys-64bit.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p ${pkgdir}/usr/{bin,share}
  mkdir -p ${pkgdir}/usr/share/man/man1
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  

  install -m755 src/mintty.exe ${pkgdir}/usr/bin/mintty.exe
  install -m644 docs/mintty.1 ${pkgdir}/usr/share/man/man1/mintty.1
  
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 LICENSE.Oxygen ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 LICENSE.PuTTY ${pkgdir}/usr/share/licenses/${pkgname}/
}

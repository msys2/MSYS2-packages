# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=mintty
pkgver=2.1.5
pkgrel=1
epoch=1
pkgdesc="Terminal emulator with native Windows look and feel"
arch=('i686' 'x86_64')
license=('GPL3')
groups=('base')
depends=('sh')
url="https://mintty.github.io"
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/mintty/mintty/archive/${pkgver}.tar.gz
        02-mintty-add-msys-64bit.patch
        0001-Fix-compilation-when-WINVER-is-set-to-0x600-or-highe.patch
        0002-Set-the-app-ID-associated-with-the-window-explicitly.patch
        0003-Allow-overriding-the-command-line-when-pinning-to-th.patch)
sha1sums=('2ce5abdc203dc962aee61f2a1e8ab53a436ed83c'
          'f3686ce7f28a954b058c155016a884480c06d6b0'
          '325e27ecdb53b9a366ec0a25b1b02b259a7eed29'
          'adfae4e15f1b29dfca4e7f7d3404316501580475'
          '6f2ae8ba9691b1b99668c846c65948d2c90c1ccb')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/02-mintty-add-msys-64bit.patch
  patch -p1 -i ${srcdir}/0001-Fix-compilation-when-WINVER-is-set-to-0x600-or-highe.patch
  patch -p1 -i ${srcdir}/0002-Set-the-app-ID-associated-with-the-window-explicitly.patch
  patch -p1 -i ${srcdir}/0003-Allow-overriding-the-command-line-when-pinning-to-th.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p ${pkgdir}/usr/{bin,share}
  mkdir -p ${pkgdir}/usr/share/man/man1
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  

  install -m755 src/mintty.exe ${pkgdir}/usr/bin/mintty.exe
  install -m644 docs/mintty.1 ${pkgdir}/usr/share/man/man1/mintty.1
  
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 LICENSE.Oxygen ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 LICENSE.PuTTY ${pkgdir}/usr/share/licenses/${pkgname}/
}

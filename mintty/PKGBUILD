# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=mintty
pkgver=3.7.2
pkgrel=1
epoch=1
pkgdesc="Terminal emulator with native Windows look and feel"
arch=('i686' 'x86_64')
license=('spdx:GPL-3.0-or-later')
depends=('sh')
makedepends=('gcc')
url="https://mintty.github.io"
msys2_repository_url="https://github.com/mintty/mintty"
msys2_references=(
  "cpe: cpe:/a:mintty_project:mintty"
)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/mintty/mintty/archive/${pkgver}.tar.gz
        "001-default-size.patch")
sha256sums=('8cbcfd7665950591a1e30b90c5e81f73d1bbbc9f3e1f2621cbd0ad60fd451025'
            '2bcd93ed0e91c5dad900e3c15c7de33e1fb5388de227a24538fb4409d3363445')

prepare() {
  cd "${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/001-default-size.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p ${pkgdir}/usr/{bin,share}
  mkdir -p ${pkgdir}/usr/share/man/man1
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  mkdir -p ${pkgdir}/usr/share/${pkgname}/lang
  mkdir -p ${pkgdir}/usr/share/${pkgname}/themes
  mkdir -p ${pkgdir}/usr/share/${pkgname}/icon
  mkdir -p ${pkgdir}/usr/share/${pkgname}/sounds
  mkdir -p ${pkgdir}/usr/share/mintty/emojis

  install -m755 bin/mintty.exe ${pkgdir}/usr/bin/mintty.exe
  install -m755 tools/mintheme ${pkgdir}/usr/bin/
  install -m644 docs/mintty.1 ${pkgdir}/usr/share/man/man1/mintty.1

  install -m644 LICENSE* ${pkgdir}/usr/share/licenses/${pkgname}/

  install -m644 lang/messages.pot ${pkgdir}/usr/share/${pkgname}/lang/
  install -m644 lang/*.po ${pkgdir}/usr/share/${pkgname}/lang/

  install -m644 themes/* ${pkgdir}/usr/share/${pkgname}/themes/

  install -m644 tools/getemojis ${pkgdir}/usr/share/mintty/emojis

  install -m644 icon/wsl.ico ${pkgdir}//usr/share/${pkgname}/icon
  for i in 16 24 32 48 64 256
  do
    mkdir -p ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps
    install -m644 icon/hi${i}-apps-mintty.png \
                 ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/mintty.png
  done
  install -m644 sounds/*.wav ${pkgdir}/usr/share/${pkgname}/sounds/
  install -m644 sounds/*.WAV ${pkgdir}/usr/share/${pkgname}/sounds/
  install -m644 sounds/*.md ${pkgdir}/usr/share/${pkgname}/sounds/
}

# Maintainer: Masanori Shirayama <mashir43@gmail.com>

pkgname=mksh
_ver=59c
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
pkgrel=2
pkgdesc="The MirBSD Korn Shell"
arch=("i686" "x86_64")
url="http://www.mirbsd.org/mksh.htm"
license=('ISC')
depends=('gcc-libs')
makedepends=('gcc')
provides=('ksh')
conflicts=('ksh')
install=mksh.install
source=(
  "https://mbsd.evolvis.org/MirOS/dist/mir/${pkgname}/${pkgname}-R${_ver}.tgz" # Mirror of http://www.mirbsd.org/MirOS/dist/mir/
  "TaC-mksh.txt" # From http://www.mirbsd.org/TaC-mksh.txt
)
sha256sums=('77ae1665a337f1c48c61d6b961db3e52119b38e58884d1c89684af31f87bc506'
            '3a895b6fbe9c81061bfcee50c3791083a82a2151b97b319367d9cb036daebef1')

build() {
  cd "${srcdir}/${pkgname}"

  ./Build.sh
}

check() {
  cd "${srcdir}/${pkgname}"
  ./test.sh || true
}

package() {
  cd "${srcdir}/${pkgname}"

  mkdir -p "${pkgdir}/usr/share/licenses/mksh"

  install -Dm755 mksh.exe "${pkgdir}/usr/bin/mksh.exe"
  install -Dm755 mksh.exe "${pkgdir}/usr/bin/ksh.exe"
  install -Dm644 dot.mkshrc "${pkgdir}/etc/skel/.mkshrc"
  install -Dm644 mksh.1 "${pkgdir}/usr/share/man/man1/mksh.1"

  cd "${srcdir}"
  install -Dm644 TaC-mksh.txt "${pkgdir}/usr/share/licenses/mksh/TaC-mksh.txt"
}

# Maintainer: Eon Jeong <administrator@korea.ac.kr>

pkgname=cpio
pkgver=2.15
pkgrel=1
pkgdesc="cpio - copy files to/from archives"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/cpio/"
msys2_repository_url="https://git.savannah.gnu.org/cgit/cpio.git"
msys2_references=(
  "cpe: cpe:/a:gnu:cpio"
)
license=('GPLv3')
depends=('libintl')
makedepends=('gettext-devel' 'autotools' 'gcc')
options=('staticlibs')
source=(https://ftp.gnu.org/pub/gnu/cpio/cpio-${pkgver}.tar.gz)
sha256sums=('efa50ef983137eefc0a02fdb51509d624b5e3295c980aa127ceee4183455499e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --build=${CYGWIN_CHOST} \
    --host=${CYGWIN_CHOST} \
    --target=${CYGWIN_CHOST} \
    --prefix=/usr

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # remove rmt
  rm -rf ${pkgdir}/usr/libexec
  rm -rf ${pkgdir}/usr/share/man/man8
  rm -f ${pkgdir}/usr/lib/charset.alias
}

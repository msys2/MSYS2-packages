# Maintainer: Martell Malone <Martell Malone at g mail dot com>

_realname=ninja
pkgname=("${_realname}" "${_realname}-vim")
pkgver=1.13.2
pkgrel=1
_descr="Ninja is a small build system with a focus on speed"
pkgdesc="${_descr}"
arch=('i686' 'x86_64')
url="https://ninja-build.org"
msys2_repository_url="https://github.com/ninja-build/ninja"
license=('Apache')
depends=()
options=('strip' 'staticlibs')
makedepends=("re2c" "python3" 'gcc')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/ninja-build/ninja/archive/v${pkgver}.tar.gz")
sha256sums=('974d6b2f4eeefa25625d34da3cb36bdcebe7fbce40f4c16ac0835fd1c0cbae17')

prepare () {
  cd ${srcdir}/ninja-${pkgver}
}

build() {
  cd ${srcdir}/ninja-${pkgver}

  CXXFLAGS+=" -D_XOPEN_SOURCE=700"
  /usr/bin/python3 configure.py --bootstrap
}

package_ninja() {
  optdepends=("ninja-vim: ninja syntax support for vim")
  mkdir -p "${pkgdir}"/usr/bin
  mv "${srcdir}/ninja-${pkgver}/ninja.exe" "${pkgdir}"/usr/bin/
  mkdir -p "${pkgdir}"/usr/lib
  mv "${srcdir}/ninja-${pkgver}/build/libninja.a" "${pkgdir}"/usr/lib/
  mkdir -p "${pkgdir}"/usr/share/bash-completion/completions
  cp  "${srcdir}/ninja-${pkgver}/misc/bash-completion" "${pkgdir}"/usr/share/bash-completion/completions/ninja
  mkdir -p "${pkgdir}"/usr/share/zsh/site-functions
  cp  "${srcdir}/ninja-${pkgver}/misc/zsh-completion" "${pkgdir}"/usr/share/zsh/site-functions/_ninja

}

package_ninja-vim() {
  pkgdesc="${_descr} (vim mode)"
  depends=("ninja=$pkgver" 'vim')
 
  vimpath="${pkgdir}/usr/share/vim/vimfiles"
  install -d "${vimpath}"/syntax

  cp -r "${srcdir}/${_realname}-$pkgver/misc/ninja.vim" "${vimpath}/syntax/"
}


# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
# stolen from Archlinux

pkgname=re2c
pkgver=3.1
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc='A tool for generating C-based recognizers from regular expressions'
url='https://re2c.org/'
license=('public domain')
depends=('gcc-libs')
makedepends=('cmake' 'gcc' 'python')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/skvadrik/re2c/archive/${pkgver}.tar.gz)
sha256sums=('087c44de0400fb15caafde09fd72edc7381e688a35ef505ee65e0e3d2fac688b')

build() {
  cd ${pkgname}-${pkgver}
  cmake -B build -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

check() {
  cd ${pkgname}-${pkgver}
  cmake --build build --target check
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

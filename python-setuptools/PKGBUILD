# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=setuptools
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=65.6.3
pkgrel=2
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=('any')
license=('PSF')
url="https://pypi.python.org/pypi/setuptools"
depends=('python')
makedepends=(
  "python-wheel"
  "python-build"
  "python-installer"
)
provides=("python3-${_realname}" 'python3-distribute')
replaces=("python3-${_realname}" 'python3-distribute')
conflicts=("python3-${_realname}")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/pypa/setuptools/archive/v${pkgver}.tar.gz")
sha512sums=('cf6a020636060391c3e4ac5896bd3580b8a6c7671237faa48372b6a64d0785cbd12910bd3c9b458b2008ccafd1e122a249513d45c8c9bcd2637b6628d68a3210')

prepare() {
  export SETUPTOOLS_INSTALL_WINDOWS_SPECIFIC_FILES=0
}

build() {
  cd "${srcdir}/setuptools-${pkgver}"

  python setup.py egg_info
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() { (
  # Workaround UTF-8 tests by setting LC_CTYPE
  export LC_CTYPE=en_US.UTF-8

  # https://github.com/pypa/setuptools/pull/810
  export PYTHONDONTWRITEBYTECODE=1

  # Check python module
  # cd "${srcdir}/setuptools-${pkgver}"
  # /usr/bin/python setup.py pytest
)}

package() {
  cd "${srcdir}/setuptools-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=HTTP-Date
pkgname=perl-${_realname}
pkgver=6.02
pkgrel=1
pkgdesc="Date conversion routines"
arch=('any')
license=('PerlArtistic' 'GPL')
groups=('perl-modules')
options=('!emptydirs')
depends=('perl>=5.8.8')
conflicts=('perl-libwww<6')
url=http://search.cpan.org/dist/HTTP-Date
source=(http://search.cpan.org/CPAN/authors/id/G/GA/GAAS/${_realname}-$pkgver.tar.gz)
md5sums=('52b7a0d5982d61be1edb217751d7daba')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "${_realname}-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "${_realname}-${pkgver}"
    make test
  )
}

package() {
    cd "${_realname}-${pkgver}"
    make DESTDIR="$pkgdir" install
}

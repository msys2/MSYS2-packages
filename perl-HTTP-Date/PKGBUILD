# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=HTTP-Date
pkgname=perl-${_realname}
pkgver=6.02
pkgrel=2
pkgdesc="Date conversion routines"
arch=('any')
license=('PerlArtistic' 'GPL')
groups=('perl-modules')
options=('!emptydirs')
depends=('perl>=5.8.8')
conflicts=('perl-libwww<6')
url="https://metacpan.org/release/HTTP-Date"
source=(https://www.cpan.org/authors/id/G/GA/GAAS/${_realname}-$pkgver.tar.gz)
sha256sums=('e8b9941da0f9f0c9c01068401a5e81341f0e3707d1c754f8e11f42a7e629e333')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "${_realname}-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "${_realname}-${pkgver}"
    make test
  )
}

package() {
  cd "${_realname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}

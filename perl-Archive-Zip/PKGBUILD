# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Archive-Zip
pkgname=perl-${_realname}
pkgver=1.68
pkgrel=1
pkgdesc="Provide a perl interface to ZIP archive files"
arch=('any')
license=('GPL' 'PerlArtistic')
url="https://metacpan.org/release/Archive-Zip"
depends=('perl')
groups=('perl-modules')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/P/PH/PHRED/${_realname}-${pkgver}.tar.gz)
sha256sums=('984e185d785baf6129c6e75f8eb44411745ac00bf6122fb1c8e822a3861ec650')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

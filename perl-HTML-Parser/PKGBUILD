# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=HTML-Parser
pkgname=perl-${_realname}
pkgver=3.76
pkgrel=1
pkgdesc="Perl HTML parser class"
arch=('i686' 'x86_64')
license=('PerlArtistic')
url="https://metacpan.org/release/HTML-Parser"
groups=('perl-modules')
depends=('perl-HTML-Tagset' 'perl-HTTP-Message' 'perl-URI' 'perl')
makedepends=('perl-devel' 'make' 'gcc')
checkdepends=('perl-Test-Pod')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/O/OA/OALDERS/${_realname}-${pkgver}.tar.gz)
sha256sums=('64d9e2eb2b420f1492da01ec0e6976363245b4be9290f03f10b7d2cb63fa2f61')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make install DESTDIR="${pkgdir}"
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=HTTP-Cookies
pkgname=perl-${_realname}
pkgver=6.10
pkgrel=2
pkgdesc="HTTP cookie jars"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
groups=('perl-modules')
depends=('perl>=5.8.8' 'perl-HTTP-Date>=6' 'perl-HTTP-Message')
makedepends=()
checkdepends=()
conflicts=('perl-libwww<6')
url="https://metacpan.org/release/HTTP-Cookies"
source=(https://www.cpan.org/authors/id/O/OA/OALDERS/${_realname}-${pkgver}.tar.gz)
sha256sums=('e36f36633c5ce6b5e4b876ffcf74787cc5efe0736dd7f487bdd73c14f0bd7007')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_realname}-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "${srcdir}/${_realname}-${pkgver}"
    make test
  )
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

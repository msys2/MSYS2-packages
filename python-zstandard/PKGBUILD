# Maintainer: Jeremy Drake <github@jdrake.com>

pkgname=python-zstandard
pkgver=0.25.0
pkgrel=1
pkgdesc='Python bindings to the Zstandard (zstd) compression library'
arch=('i686' 'x86_64')
url="https://github.com/indygreg/python-zstandard"
msys2_references=(
  "anitya: 147738"
  'archlinux: python-zstandard'
  'cygwin: python-zstandard'
  'gentoo: dev-python/zstandard'
  "purl: pkg:pypi/zstandard"
)
license=('spdx:BSD-3-Clause')
depends=("python")
makedepends=(
  "python-devel"
  'gcc'
  "python-setuptools"
  "python-build"
  "python-installer"
)
options=('!emptydirs')
source=("https://github.com/indygreg/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('851846ffe25681f7936ab2fd89130acf62b214a77b8c54e4319824f3510ab395')

prepare() {
  cd "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${pkgname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

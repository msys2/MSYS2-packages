# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=bison
pkgver=3.2
pkgrel=1
pkgdesc="The GNU general-purpose parser generator"
arch=('i686' 'x86_64')
license=('GPL3')
url="https://www.gnu.org/software/bison/bison.html"
depends=('m4' 'sh')
groups=('base-devel')
options=('staticlibs')
makedepends=('flex')
install=bison.install
source=(https://ftp.gnu.org/gnu/bison/${pkgname}-${pkgver}.tar.xz{,.sig}
        bison-3.1-msys2.patch
        bison-3.0.5-gettext-0.19.patch)
sha256sums=('deec377b95aa72ec4e1a33fe2c938d2480749d740b5291a7cc1d77808d3710bf'
            'SKIP'
            'a246955dc355150a80228a556a6954e38c527e3cd6ab03b5df4e41df00a198b3'
            '100f3ff1cd68a080b803a4f4a86a0a11b660e2b3b89f7ec95afa0437ee2b2b5c')
validpgpkeys=('7DF84374B1EE1F9764BBE25D0DDCAA3278D5264E')  # Akim Demaille


prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/bison-3.1-msys2.patch
  patch -p1 -i ${srcdir}/bison-3.0.5-gettext-0.19.patch

  autoreconf -vfi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}

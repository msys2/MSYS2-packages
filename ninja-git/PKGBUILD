# Maintainer: Ivan Shapovalov <intelfx100@gmail.com>
# Contributor: Mika Fischer <mika.fischer@zoopnet.de>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>

pkgname=ninja-git
epoch=2
pkgver=r2337.e234a7bd
pkgrel=1
pkgdesc='Small build system with a focus on speed'
arch=('i686' 'x86_64')
url='https://ninja-build.org/'
license=(Apache)
depends=('gcc-libs')
makedepends=('asciidoc' 'python2' 're2c')
#makedepends+=('emacs-nox')
provides=('ninja')
conflicts=('ninja')
install=ninja-git.install

source=('git+https://github.com/ninja-build/ninja.git')
md5sums=('SKIP')

function pkgver() {
	cd ninja

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd ninja

	python2 ./configure.py --bootstrap
	if [[ "${makedepends[@]}" =~ "emacs-nox" ]]; then
		emacs -Q --batch -f batch-byte-compile misc/ninja-mode.el
	fi
}

package() {
	cd ninja

	install -m755 -D ninja "$pkgdir/usr/bin/ninja"
	install -m644 -D doc/manual.asciidoc "$pkgdir/usr/share/doc/ninja/manual.asciidoc"
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

	install -m644 -D misc/ninja-mode.el "$pkgdir/usr/share/emacs/site-lisp/ninja-mode.el"
	if [[ "${makedepends[@]}" =~ "emacs-nox" ]]; then
		install -m644 -D misc/ninja-mode.elc "$pkgdir/usr/share/emacs/site-lisp/ninja-mode.elc"
	fi
	install -m644 -D misc/ninja.vim "$pkgdir/usr/share/vim/vimfiles/syntax/ninja.vim"

	install -m644 -D misc/bash-completion "$pkgdir/usr/share/bash-completion/completions/ninja"
	install -m644 -D misc/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_ninja"
}

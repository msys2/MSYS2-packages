# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=more-itertools
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
replaces=("python3-${_realname}")
conflicts=("python3-${_realname}")
pkgver=9.0.0
pkgrel=1
pkgdesc='More routines for operating on iterables, beyond itertools'
arch=('any')
url='https://github.com/erikrose/more-itertools'
license=('MIT')
makedepends=('python-build' 'python-installer' 'python-flit-core' 'python-setuptools' 'python-wheel')
depends=('python')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('5a6257e40878ef0520b1803990e3e22303a41b5714006c32a3fd8304b26ea1ab')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  /usr/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  /usr/bin/python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

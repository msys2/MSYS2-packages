# Maintainer: Mateusz Miku≈Ça <mati865@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

pkgname=ccache
pkgver=4.8.3
pkgrel=2
pkgdesc="A compiler cache (mingw-w64)"
arch=('i686' 'x86_64')
url="https://ccache.samba.org/"
license=("spdx:GPL-3.0-or-later")
replaces=("${pkgname}-git")
makedepends=("cmake"
             "ninja"
             "pkgconf"
             "zlib-devel"
             "libzstd-devel"
             'gcc')
depends=("gcc-libs" "zlib" "libzstd")
options=('staticlibs' 'strip')
install="${pkgname}.install"
source=(https://github.com/ccache/ccache/releases/download/v${pkgver}/ccache-${pkgver}.tar.xz{,.asc})
validpgpkeys=("5A939A71A46792CF57866A51996DDA075594ADB8") #Joel Rosdahl <joel@rosdahl.net>
sha256sums=('e47374c810b248cfca3665ee1d86c7c763ffd68d9944bc422d9c1872611f2b11'
            'SKIP')

build() {
  [[ -d build-${CHOST} ]] && rm -rf build-${CHOST}
  mkdir -p build-${CHOST}  && cd build-${CHOST}

  /usr/bin/cmake \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DREDIS_STORAGE_BACKEND=OFF \
    -DUSE_CCACHE=OFF \
    ../${pkgname}-${pkgver}

  cmake --build .
}

package() {
  cd "${srcdir}/build-${CHOST}"
  DESTDIR="${pkgdir}" cmake --build . --target install

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/GPL-3.0.txt ${pkgdir}/usr/share/licenses/${pkgname}/GPL-3.0.txt
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LGPL-3.0.txt ${pkgdir}/usr/share/licenses/${pkgname}/LGPL-3.0.txt
}

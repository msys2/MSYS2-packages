# Maintainer: Mateusz Miku≈Ça <mati865@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

pkgname=ccache
pkgver=3.4.3
pkgrel=1
pkgdesc="A compiler cache (mingw-w64)"
arch=('i686' 'x86_64')
url="https://ccache.samba.org/"
license=("GPL3")
replaces=("${pkgname}-git")
makedepends=("gcc" "pkg-config" "zlib-devel")
depends=("gcc-libs" "zlib")
options=('staticlibs' 'strip')
install="${pkgname}.install"
source=(https://samba.org/ftp/ccache/${pkgname}-${pkgver}.tar.bz2{,.asc}
        "ccache-3.4.2-msys.patch")
validpgpkeys=("5A939A71A46792CF57866A51996DDA075594ADB8") #Joel Rosdahl <joel@rosdahl.net>
sha256sums=('38decec3eed6524a002ffae52b25781843553c28e2d840e7e55e6831c40264d2'
            'SKIP'
            'a889ceffd1a4a98a00e2fb167794d92b548fc79d99d21082389f5fc2c301a09a')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/ccache-3.4.2-msys.patch
}

build() {
  [[ -d build-${CHOST} ]] && rm -rf build-${CHOST}
  mkdir -p build-${CHOST}  && cd build-${CHOST}
  ../${pkgname}-${pkgver}/configure \
      --prefix=/usr \
      --build=${CHOST} \
      --host=${CHOST} \
      --target=${CHOST}
  make
}

check() {
  cd "${srcdir}/build-${CHOST}"

  make check
}

package() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR="${pkgdir}" install
}

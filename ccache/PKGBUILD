# Maintainer: Mateusz Miku≈Ça <mati865@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

pkgname=ccache
pkgver=4.12.3
pkgrel=1
pkgdesc="A compiler cache (mingw-w64)"
arch=('i686' 'x86_64')
url="https://ccache.dev/"
msys2_repository_url="https://github.com/ccache/ccache"
msys2_changelog_url="https://ccache.dev/releasenotes.html"
msys2_references=(
  'anitya: 257'
  'archlinux: ccache'
  'gentoo: dev-util/ccache'
)
license=("spdx:GPL-3.0-or-later")
replaces=("${pkgname}-git")
makedepends=("cmake"
             "ninja"
             "pkgconf"
             "zlib-devel"
             "libzstd-devel"
             "libxxhash-devel"
             'gcc')
depends=("gcc-libs" "zlib" "libzstd" "fmt" "doctest" "libxxhash" "libblake3")
options=('staticlibs' 'strip')
install="${pkgname}.install"
source=(https://github.com/ccache/ccache/releases/download/v${pkgver}/ccache-${pkgver}.tar.xz{,.minisig})
validpgpkeys=("5A939A71A46792CF57866A51996DDA075594ADB8") #Joel Rosdahl <joel@rosdahl.net>
sha256sums=('c8e3ef79531966ecfa05bd1666c483b473df9af00896935cc468cb5ed573c16e'
            'ba3d6bd25713efd3ffa5b8e929c6eb9e0dd6e7e6db1b18464f2d449a5b831da6')

prepare() {
  cd "${pkgname}-${pkgver}"
}

build() {
  [[ -d build-${CHOST} ]] && rm -rf build-${CHOST}
  mkdir -p build-${CHOST}  && cd build-${CHOST}

  unset CI
  /usr/bin/cmake \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DREDIS_STORAGE_BACKEND=OFF \
    -DDEPS=LOCAL \
    ../${pkgname}-${pkgver}

  cmake --build .
}

check() {
  cd "build-${CHOST}"
  /usr/bin/ctest .
}

package() {
  cd "${srcdir}/build-${CHOST}"
  DESTDIR="${pkgdir}" cmake --build . --target install

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/GPL-3.0.txt ${pkgdir}/usr/share/licenses/${pkgname}/GPL-3.0.txt
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LGPL-3.0.txt ${pkgdir}/usr/share/licenses/${pkgname}/LGPL-3.0.txt
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=File-Copy-Recursive
pkgname=perl-${_realname}
pkgver=0.45
pkgrel=1
pkgdesc="extension for recursively copying files and directories"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='https://metacpan.org/release/File-Copy-Recursive'
groups=('perl-modules')
source=("https://www.cpan.org/authors/id/D/DM/DMUEY/${_realname}-${pkgver}.tar.gz")
sha256sums=('d3971cf78a8345e38042b208bb7b39cb695080386af629f4a04ffd6549df1157')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

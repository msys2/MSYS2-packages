pkgname=python-pkgconfig
pkgver=1.5.5
pkgrel=3
pkgdesc='Python module to interface with the pkg-config command line tool'
arch=(any)
url='https://github.com/matze/pkgconfig'
license=('spdx:MIT')
depends=("python")
makedepends=(
  "python-poetry-core"
  "python-build"
  "python-installer"
)
source=(https://pypi.io/packages/source/p/pkgconfig/pkgconfig-$pkgver.tar.gz)
sha256sums=('deb4163ef11f75b520d822d9505c1f462761b4309b1bb713d08689759ea8b899')

build() {
  cd pkgconfig-$pkgver

  export GIT_DIR="$(pwd)"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd pkgconfig-$pkgver

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

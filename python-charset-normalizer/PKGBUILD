# Contributor: Dirk Stolle

_realname=charset-normalizer
pkgname=python-${_realname}
pkgver=3.4.4
pkgrel=1
pkgdesc='A library that helps you read text from an unknown charset encoding'
arch=('any')
url='https://charset-normalizer.readthedocs.io/'
msys2_repository_ulr='https://github.com/jawah/charset_normalizer'
msys2_changelog_url='https://github.com/jawah/charset_normalizer/blob/master/CHANGELOG.md'
msys2_references=(
  'archlinux: python-charset-normalizer'
  'gentoo: dev-python/charset-normalizer'
  'purl: pkg:pypi/charset-normalizer'
)
license=('spdx:MIT')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
)
checkdepends=('python-pytest')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname/-/_}-${pkgver}.tar.gz")
sha256sums=('94537985111c35f28720e43603b8e7b43a6ecfb2ce1d3058bbe955b73404e21a')

build() {
  cp -r "${_realname/-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "python-build-${MSYSTEM}"

  # Test in virtual env., because tests need installed package.
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd "python-build-${MSYSTEM}"

  python -m installer --prefix=${MSYSTEM_PREFIX} --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}

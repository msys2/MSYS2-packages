# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=subversion
pkgver=1.9.4
pkgrel=1
pkgdesc="A Modern Concurrent Version Control System"
arch=('i686' 'x86_64')
url="https://subversion.apache.org/"
license=('APACHE')
groups=('VCS')
depends=('libsqlite' 'file' 'libserf' 'libsasl')
makedepends=('python2' 'perl' 'swig' 'ruby' 'libsqlite-devel' 'libserf-devel' 'libsasl-devel' 'gmp-devel')
optdepends=('bash-completion: for svn bash completion'
            'python2: for some hook scripts'
            'ruby: for some hook scripts')
provides=('svn')
options=('!makeflags' '!libtool' '!emptydirs')
noextract=("subversion-${pkgver}.tar.bz2")
source=(https://archive.apache.org/dist/subversion/subversion-${pkgver}.tar.bz2{,.asc}
        svnclean
        01-make-build-directories.patch
        02-fix-linking.patch
        03-fix-linking-perl.patch
        04-fix-switch-tests.patch
        05-retry-loop.patch
        06-svnadmin-binary-mode.patch
        07-fix-path-tests.patch
        08-ruby-test-fix.patch
        09-wincrypt.patch
        10-perl-vendors.patch
        11-XFAIL-case-sensitive-move-tests.patch
        12-fix-svnlook-test.patch
        13-dso_open.patch
        14-fix_svnauthz_tests.patch
        15-XFAIL_case_sensitive_tree_conflict.patch
        16-fix-swig3.patch
        17-fix-test-link.patch
        18-fix-serf-config.patch
        19-remove-contrib-from-configure.patch
        subversion-1.9.1-msys2.patch
        remove-checking-symlink.patch
        90-use-copy-instead-symlink.patch)
sha256sums=('1267f9e2ab983f260623bee841e6c9cc458bf4bf776238ed5f100983f79e9299'
            'SKIP'
            'b09dd041aba0078c8d50df130ef2f96c3ef8486279a620532fef4fe48ef9961e'
            'cc0367818850e39ccaaeacf6df77303a6ec180017455d7b10c6c68ea024d4e6f'
            'a5d9d9c8d484c8cf71ef782d6f6a5bca1b832cad9b25e74e0bef19b170d89a3d'
            '287470f4098cf103b26f329ca7ab91d0ea338dc10cc63d73ce244bde452ea8e8'
            '43d68aa91140a78f7934e2763507f09a3e2fac0bcc62688d1d0de2e7efaa2379'
            '0fa6a1afb18f019f2dce27592919598c17047ca6ef0187b4cc18a176dfb1aa6f'
            '9c88fc5b48832df9e671e505ec16f6b3da334dd218a202d91c04fc3abe07c77c'
            '8472cb53ff1235a0e0279bac22dd9c00a9fa68df6cc245863c8186dafbe0b644'
            '0da0af269761160244952d7f95ab17c5d4c35b0020044758d164df833b3e7ba9'
            '07e1212a151ff959deb8140c2af3c787827818a8dd57d01a924b89e1b76c082c'
            '494625c681da0d03ad4ebcab2338a3407b1c84e10fcb118307206d8cfed1986b'
            '30aaead019f24a868e1a53235d246e8d62ee186f64b35d54ba80cff405d5be97'
            '6662bf25c897f6550e1dce33e2e198d311120abeca9887f348e3c67159dda7c1'
            'd743c08dd76b73d0ba07bb7c3a2650416239f5533208be9f3da6714601783fc9'
            '94a64c20edd2ae23e5e7eba3ca08b4e7e7c68d0fa47949887607c0ad19118e53'
            '8d5898ac98f5d7ba3554b96eb7cdf6643ed15b547c12d6b1a0fd9fae1a6046de'
            '84b518253247a57ef4df7b3daf265bfdb4cb6ef611d57979c97f7b00eeaecc4f'
            'f32780635df14f6c954d70ed7ff470ffe84edd314c881492cb811252644cae5a'
            'dcec738bb21ebf640522ef36fec4a83c7a26c93090df2df857e3f2825b597954'
            '7bfd5bceb6141d677806222dea4969231d0dfb9da765512672f0666649b9c885'
            'f3d2e7e6ca97c8310fb59ce82430005fcd9e508b4a2a3f4ec7208011bf32a0a8'
            'e6e726b0121a9a2d3ce3942791478135728c13427851f5062554a8540109d634'
            '5268b1136d93691df238e0cc8b7d88739931d64779fc4528b65519f7e19b2dd2')

PYTHON_SITELIB=/usr/lib/python2.7/site-packages

prepare() {
  cd $startdir/
  [ -d $srcdir/$pkgname-$pkgver ] && rm -rf -d $srcdir/$pkgname-$pkgver
  tar -xjvf ${SRCDEST}/${pkgname}-${pkgver}.tar.bz2 -C $srcdir || true

  cd $srcdir/$pkgname-$pkgver

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
    tools/hook-scripts/{,mailer/{,tests/}}*.py \
    tools/client-side/mergeinfo-sanitizer.py

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' gen-make.py build/getversion.py
  sed -i 's|except IOError,|except IOError as|' build/getversion.py

  patch -p1 -i ${srcdir}/01-make-build-directories.patch
  patch -p1 -i ${srcdir}/02-fix-linking.patch
  patch -p1 -i ${srcdir}/03-fix-linking-perl.patch
  patch -p1 -i ${srcdir}/04-fix-switch-tests.patch
  patch -p1 -i ${srcdir}/05-retry-loop.patch
  patch -p1 -i ${srcdir}/06-svnadmin-binary-mode.patch
  patch -p1 -i ${srcdir}/07-fix-path-tests.patch
  patch -p1 -i ${srcdir}/08-ruby-test-fix.patch
  patch -p1 -i ${srcdir}/09-wincrypt.patch
  patch -p1 -i ${srcdir}/10-perl-vendors.patch
  patch -p1 -i ${srcdir}/11-XFAIL-case-sensitive-move-tests.patch
  patch -p1 -i ${srcdir}/12-fix-svnlook-test.patch
  patch -p1 -i ${srcdir}/13-dso_open.patch
  patch -p1 -i ${srcdir}/14-fix_svnauthz_tests.patch
  patch -p1 -i ${srcdir}/15-XFAIL_case_sensitive_tree_conflict.patch
  patch -p1 -i ${srcdir}/16-fix-swig3.patch
  patch -p1 -i ${srcdir}/17-fix-test-link.patch
  patch -p1 -i ${srcdir}/18-fix-serf-config.patch
  patch -p1 -i ${srcdir}/19-remove-contrib-from-configure.patch
  patch -p1 -i ${srcdir}/subversion-1.9.1-msys2.patch
  patch -p1 -i ${srcdir}/remove-checking-symlink.patch
  patch -p1 -i ${srcdir}/90-use-copy-instead-symlink.patch

  PYTHON=/usr/bin/python2 ./autogen.sh
}

build() {
  cd ${pkgname}-${pkgver}
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr \
      --build=${CHOST} \
      --with-apr=/usr \
      --with-apr-util=/usr \
      --with-zlib=/usr \
      --with-serf=/usr \
      --with-sqlite=/usr \
      --without-apxs \
      --without-gnome-keyring \
      --enable-shared --disable-static \
      --with-ruby-sitedir=/usr/lib/ruby/vendor_ruby \
      --disable-mod-activation \
      --enable-disallowing-of-undefined-references \
      --enable-local-library-preloading

  #make LT_LDFLAGS="-L$Fdestdir/usr/lib"
  # make swig_pydir=/usr/lib/python2.7/site-packages/libsvn \
  #  swig_pydir_extra=/usr/lib/python2.7/site-packages/svn swig-py swig-pl swig-rb

  make clean-swig
  make autogen-swig
  make all

  plain "Compiling swig bindings: ruby"
  make swig-rb

  plain "Compiling swig bindings: perl"
  make swig-pl

  plain "Compiling swig bindings: python"
  make swig-py swig_pydir=${PYTHON_SITELIB}/libsvn swig_pydir_extra=${PYTHON_SITELIB}/svn
}

#check() {
#   cd ${pkgname}-${pkgver}
#   export LANG=C LC_ALL=C
#   make check check-swig-pl check-swig-py check-swig-rb CLEANUP=yes # check-javahl
#}

package() {
  cd ${pkgname}-${pkgver}

  make -j1 DESTDIR="${pkgdir}" INSTALLDIRS=vendor \
    swig_pydir=${PYTHON_SITELIB}/libsvn \
    swig_pydir_extra=${PYTHON_SITELIB}/svn \
    install install-tools install-swig-py install-swig-pl install-swig-rb

  install -dm755 "${pkgdir}"/usr/share/subversion
  cp -a tools/hook-scripts "${pkgdir}"/usr/share/subversion/
  rm "${pkgdir}"/usr/share/subversion/hook-scripts/*.in

  ## svnclean
  install -Dm 755 ${srcdir}/svnclean "${pkgdir}"/usr/bin/svnclean

  install -Dm 644 tools/client-side/bash_completion \
     "${pkgdir}"/usr/share/bash-completion/completions/subversion
  for i in svn svnadmin svndumpfilter svnlook svnsync svnversion; do
      ln -sf subversion "${pkgdir}"/usr/share/bash-completion/completions/${i}
  done

  # Remove illegal files
  find ${pkgdir}/usr -type f -name "*::*" | xargs rm -f

  # fix permissons
  find ${pkgdir}/usr -type f \( -name *.dll -o -name *.exe \) | xargs chmod 0755
}

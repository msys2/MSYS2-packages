# $Id$
# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
# shamelessly stolen from Archlinux

pkgname=docbook-sgml31
pkgver=3.1
pkgrel=1
pkgdesc="Legacy docbook-sgml"
arch=('any')
url='https://www.docbook.org/sgml/'
license=('MIT')
depends=('sgml-common')
install=$pkgname.install
source=("https://www.docbook.org/sgml/${pkgver}/docbk31.zip")
sha256sums=('20261d2771b9a052abfa3d8fab1aa62be05791a010281c566f9073bf0e644538')

build() {
  cd "$srcdir"
  # This command removes the ENT definitions from the catalog file.
  # This command replaces the DTDDECL catalog entry, which is not supported by Linux SGML tools, 
  # with the SGMLDECL catalog entry.
  sed -i -e '/ISO 8879/d' \
         -e 's|DTDDECL "-//OASIS//DTD DocBook V3.1//EN"|SGMLDECL|g' \
         docbook.cat
}

package() {
  cd "$srcdir"

  local DTDDIR="usr/share/sgml/docbook-sgml-${pkgver}"

  install -dm755 "$pkgdir/$DTDDIR"
  install -m644 docbook.cat "$pkgdir/$DTDDIR/catalog"
  install -m644 *.dtd *.mod *.dcl "$pkgdir/$DTDDIR"
}


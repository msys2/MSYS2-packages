# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=patiencediff
pkgname="python-${_realname}"
pkgver=0.2.17
pkgrel=1
pkgdesc='Patiencediff Python and C implementations'
arch=('i686' 'x86_64')
url='https://github.com/breezy-team/patiencediff'
msys2_references=(
  "purl: pkg:pypi/patiencediff"
)
license=('spdx:GPL-2.0-or-later')
depends=('python')
makedepends=(
  'python-devel'
  'gcc'
  "python-setuptools"
  "python-setuptools-rust"
  "python-build"
  "python-installer"
)
source=("${_realname}-${pkgver}.tar.gz"::https://github.com/breezy-team/patiencediff/archive/v${pkgver}.tar.gz)
sha256sums=('2f2a818722db078413825b1234c6b7f30761519ee9d8d47dc7e14f26fa50680d')

prepare(){
  cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  local link_arg=$(python -c "import sysconfig; print(sysconfig.get_config_var('LIBPYTHON'))")
  export RUSTFLAGS="-C link-arg=$link_arg"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Ray Donnelly <ray.donnelly@gmail.com>

_realname=gyp
pkgname=${_realname}-git
pkgver=r2051.2b08654
pkgrel=1
pkgdesc="GYP can Generate Your Projects."
url="http://code.google.com/p/gyp/"
arch=('i686' 'x86_64')
provides=("gyp")
conflicts=("gyp")
replaces=("gyp-svn")
makedepends=('subversion')
depends=('python2' 'python2-setuptools')
license=('custom')
source=("git+https://chromium.googlesource.com/external/gyp"
        '0001-msys-ize.patch')
md5sums=('SKIP'
         '1986471501996dbec357fbeddf11ac6f')

pkgver() {
  cd "${srcdir}"/${_realname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/${_realname}
  git am "${srcdir}"/0001-msys-ize.patch
}

package() {
  cd "${srcdir}"/${_realname}
  python2 setup.py install --root="${pkgdir}"
}

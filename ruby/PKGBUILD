# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('ruby' 'ruby-docs')
pkgver=2.3.3
pkgrel=1
arch=('i686' 'x86_64')
url='https://www.ruby-lang.org/en/'
license=('BSD' 'custom')
makedepends=('openssl-devel' 'libffi-devel' 'doxygen' 'libcrypt-devel'  'gmp-devel' 'libyaml-devel' 'libgdbm-devel' 'libiconv-devel' 'libreadline-devel' 'zlib-devel')
options=('!emptydirs' '!makeflags' 'staticlibs')
source=("https://cache.ruby-lang.org/pub/ruby/${pkgver%.*}/ruby-${pkgver//_/-}.tar.bz2"
        1.8.7-cygwin-tcltk.patch
        2.0.0-cygwin-configure.patch
        2.0.0-cygwin-rubygems.patch
        2.2.2-cygwin-compile.patch
        2.0.0-pkgconfig-version.patch
        'gemrc'
        'ruby-2.3.0-msys2.patch')
sha256sums=('882e6146ed26c6e78c02342835f5d46b86de95f0dc4e16543294bc656594cc5b'
            '3453f6b8a77b3a3284b217264769287c29e00e5c3b0e535aeee6a17a4d898258'
            'd8a54a6c57d31411916ae115a798023115dcbca63faf8f3b5ccd91f4bdbd1215'
            '4ae2a314b2942518b48424fc93e5043776bfaf1e84519655af6ba35c3284abff'
            '5fffe4fa469721bfe271650dae142c5a0274c22705e4be01541371d74a5de23d'
            'd74c014e3b5ff848df86c071e8562e699c4488f156c22d3a76d87fab8b9c95d8'
            '4bb7eb2fe66e396ed16b589cdb656831407b39ad4e138d88536754c0448ac614'
            '5e12b081f97e38837e0fcadd034ebd4290200c4c76b794f05ece9d9e9c342df0')

prepare() {
  cd ${srcdir}/ruby-${pkgver//_/-}
  patch -p2 -i ${srcdir}/1.8.7-cygwin-tcltk.patch
  patch -p2 -i ${srcdir}/2.0.0-cygwin-configure.patch
  patch -p2 -i ${srcdir}/2.0.0-cygwin-rubygems.patch
  patch -p2 -i ${srcdir}/2.2.2-cygwin-compile.patch
  patch -p2 -i ${srcdir}/2.0.0-pkgconfig-version.patch
  patch -p1 -i ${srcdir}/ruby-2.3.0-msys2.patch
  autoreconf -fi
}

build() {
  mkdir -p ${srcdir}/build-${CHOST} && cd ${srcdir}/build-${CHOST}

  CFLAGS+=" -fno-strict-aliasing "
  CXXFLAGS+=" -fno-strict-aliasing "
  ../ruby-${pkgver//_/-}/configure \
    --build=${CHOST} \
    --prefix=/usr \
    --exec_prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sharedstatedir=/var/lib \
    --libexecdir=/usr/lib/ruby \
    --enable-shared \
    --disable-rpath \
    --with-dbm-type=gdbm_compat \
    LDSHARED="gcc -shared" \
    DLDFLAGS="-Wl,--export-all-symbols"

  make
}

check() {
  cd ruby-${pkgver//_/-}

  make test
}

package_ruby() {
  pkgdesc='An object-oriented language for quick and easy programming'
  depends=('gcc-libs' 'libopenssl' 'libffi' 'libcrypt'  'gmp' 'libyaml' 'libgdbm' 'libiconv' 'libreadline' 'zlib')
  optdepends=(#'tk: for Ruby/TK'
              'ruby-docs: Ruby documentation')
  provides=('rubygems' 'rake')
  conflicts=('rake')
  backup=('etc/gemrc')
  install='ruby.install'

  cd ${srcdir}/build-${CHOST}

  make DESTDIR="${pkgdir}" install-nodoc

  install -D -m644 ${srcdir}/gemrc "${pkgdir}/etc/gemrc"

  install -D -m644 ${srcdir}/ruby-${pkgver//_/-}/COPYING "${pkgdir}/usr/share/licenses/ruby/LICENSE"
  install -D -m644 ${srcdir}/ruby-${pkgver//_/-}/BSDL "${pkgdir}/usr/share/licenses/ruby/BSDL"
}

package_ruby-docs() {
  pkgdesc='Documentation files for ruby'

  cd ${srcdir}/build-${CHOST}

  make DESTDIR="${pkgdir}" install-doc install-capi

  install -D -m644 ${srcdir}/ruby-${pkgver//_/-}/COPYING "${pkgdir}/usr/share/licenses/ruby-docs/LICENSE"
  install -D -m644 ${srcdir}/ruby-${pkgver//_/-}/BSDL "${pkgdir}/usr/share/licenses/ruby-docs/BSDL"
}

# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgname=breezy
_verbase=3.1.0
_verminor=-4
pkgver=${_verbase}${_verminor//-/.}
pkgrel=1
pkgdesc='A decentralized revision control system with support for Bazaar and Git file formats'
arch=('i686' 'x86_64')
url='https://www.breezy-vcs.org/'
license=('GPL2')
groups=('VCS')
depends=('python-configobj'
         'python-fastimport'
         'python-dulwich'
         'python-patiencediff'
         'python-six')
makedepends=('python-setuptools' 'libcrypt-devel')
provides=(bzr)
conflicts=(bzr)
replaces=(bzr)
source=(#"https://pypi.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        ${pkgname}-${pkgver}.tar.gz::https://github.com/breezy-team/breezy/archive/${_verbase}${_verminor}.tar.gz
        '0002-add-msys2-certs-location.patch')
sha256sums=('3f4fca2fdd1cc09916da70c091f6da31447592073b925e1c5d14c1d5951bf771'
            '8f3a1c151c9ceb8b2ace12dc1c80bd123810e2e77a2c784385d5ad039f0bd3bb')

prepare(){
  cd "${srcdir}/${pkgname}-${_verbase}${_verminor}"

  patch -p1 -i ${srcdir}/0002-add-msys2-certs-location.patch
}

build() {
  cd "${srcdir}/${pkgname}-${_verbase}${_verminor}"

  python setup.py build --parallel "$(nproc)"
}

package() {
  cd "${srcdir}/${pkgname}-${_verbase}${_verminor}"

  python setup.py install --root="${pkgdir}" --install-data=usr/share --optimize=1 --skip-build
  cp "${pkgdir}"/usr/bin/brz "${pkgdir}"/usr/bin/bzr
}

# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgname=breezy
pkgver=3.3.20
pkgrel=1
pkgdesc='A decentralized revision control system with support for Bazaar and Git file formats'
arch=('i686' 'x86_64')
url='https://www.breezy-vcs.org/'
msys2_repository_url="https://github.com/breezy-team/breezy"
msys2_references=(
  "purl: pkg:pypi/breezy"
)
license=('GPL2')
groups=('VCS')
depends=('python-configobj'
         'python-fastimport'
         'python-dulwich'
         'python-patiencediff'
         'python-fastbencode')
makedepends=(
  'python-setuptools'
  'python-build'
  'python-installer'
  'python-devel'
  'python-pip'
  'python-setuptools-rust'
  'python-setuptools-gettext'
  'gcc'
  'cython'
)
provides=(bzr)
conflicts=(bzr)
replaces=(bzr)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/breezy-team/breezy/archive/brz-${pkgver}.tar.gz
        '0002-add-msys2-certs-location.patch')
sha256sums=('32d26d0af0716e3580d7d97dda56ae441ab173f192092bcf762f590162f6bdae'
            '9daea641477f7115345307363713a5f4612282e82cf604500362898bad6e5f7a')

prepare(){
  cd "${srcdir}/${pkgname}-brz-${pkgver}"

  patch -p1 -i ${srcdir}/0002-add-msys2-certs-location.patch
}

build() {
  cd "${srcdir}/${pkgname}-brz-${pkgver}"

  local link_arg=$(python -c "import sysconfig; print(sysconfig.get_config_var('LIBPYTHON'))")
  export RUSTFLAGS="-C link-arg=$link_arg"

  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}/${pkgname}-brz-${pkgver}"

  python -m installer --destdir="$pkgdir" dist/*.whl
  cp "${pkgdir}"/usr/bin/brz "${pkgdir}"/usr/bin/bzr
}

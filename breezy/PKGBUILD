# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgname=breezy
_verbase=3.0.2
_verminor=-5
pkgver=${_verbase}${_verminor//-/.}
pkgrel=1
pkgdesc='A decentralized revision control system with support for Bazaar and Git file formats'
arch=('i686' 'x86_64')
url='https://www.breezy-vcs.org/'
license=('GPL2')
groups=('VCS')
depends=('python-configobj'
         'python-fastimport'
         'python-dulwich'
         'python-patiencediff'
         'python-six')
makedepends=('python3-setuptools' 'libcrypt-devel')
provides=(bzr)
conflicts=(bzr)
replaces=(bzr)
source=(#"https://pypi.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        ${pkgname}-${pkgver}.tar.gz::https://github.com/breezy-team/breezy/archive/${_verbase}${_verminor}.tar.gz
        '0002-add-msys2-certs-location.patch')
sha256sums=('ae9fa0ff78e81898eaa5b29744171d2ba232b223aedd88406324643a316fee10'
            '18be0daab57a10b03643f18a6b148efb2a7129088686590221fd6683d6717c18')

prepare(){
  cd "${srcdir}/${pkgname}-${_verbase}${_verminor}"

  patch -p1 -i ${srcdir}/0002-add-msys2-certs-location.patch
}

build() {
  cd "${srcdir}/${pkgname}-${_verbase}${_verminor}"

  python setup.py build --parallel "$(nproc)"
}

package() {
  cd "${srcdir}/${pkgname}-${_verbase}${_verminor}"

  python setup.py install --root="${pkgdir}" --install-data=usr/share --optimize=1 --skip-build
  cp "${pkgdir}"/usr/bin/brz "${pkgdir}"/usr/bin/bzr
}

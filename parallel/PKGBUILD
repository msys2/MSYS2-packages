# Maintainer: David Mott <mott.david.j@gmail.com>
# Contributor: Niklas Holm <jadedcyborg@gmail.com>

pkgname='parallel'
pkgver=20240522
pkgrel='1'
pkgdesc="A shell tool for executing jobs in parallel"
arch=('any')
url='https://www.gnu.org/software/parallel/'
msys2_references=(
  "cpe: cpe:/a:gnu:parallel"
)
license=('GPL3')
depends=('perl')
makedepends=('autotools')
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,".sig"})
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')
sha256sums=('67ed9fad31bf3e25f09d500e7e8ca7df9e3ac380fe4ebd16c6f014448a346928'
            'SKIP')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # ACLOCAL_PATH is needed to pick up local m4 macro
  ACLOCAL_PATH="${srcdir}/${pkgname}-${pkgver}" autoreconf -vif
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --build="${CHOST}" \
    --host="${CHOST}" \
    --target="${CHOST}" \
    --prefix="/usr"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

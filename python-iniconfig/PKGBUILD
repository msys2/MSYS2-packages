# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_pyname=iniconfig
pkgname=python-iniconfig
pkgver=2.3.0
pkgrel=1
pkgdesc="brain-dead simple config-ini parsing"
url="https://github.com/pytest-dev/iniconfig"
msys2_changelog_url='https://github.com/pytest-dev/iniconfig/blob/main/CHANGELOG'
msys2_references=(
  'archlinux: python-iniconfig'
  'gentoo: dev-python/iniconfig'
  'purl: pkg:pypi/iniconfig'
)
license=('spdx:MIT')
arch=('any')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-hatchling'
  'python-hatch-vcs'
)
source=("$_pyname-$pkgver.tar.gz::https://github.com/pytest-dev/iniconfig/archive/v$pkgver.tar.gz")
sha256sums=('b485a7f6c060b2e758b6e58fd1bf7349a6f99954110ca61ceb59af25cf4ff458')

build() {
  cd "$srcdir/$_pyname-$pkgver"

  export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "$_pyname-$pkgver"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

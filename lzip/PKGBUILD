# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=lzip
pkgver=1.22
pkgrel=2
pkgdesc="A lossless file compressor based on the LZMA algorithm"
arch=('i686' 'x86_64')
url="https://www.nongnu.org/lzip/lzip.html"
license=('GPL3')
depends=('gcc-libs')
makedepends=('autotools' 'gcc')
source=(https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('c3342d42e67139c165b8b128d033b5c96893a13ac5f25933190315214e87a948')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr CXXFLAGS="$CXXFLAGS"
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install{,-man}
}

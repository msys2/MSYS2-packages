# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=lzip
pkgver=1.23
pkgrel=2
pkgdesc="A lossless file compressor based on the LZMA algorithm"
arch=('i686' 'x86_64')
url="https://www.nongnu.org/lzip/lzip.html"
license=('GPL3')
depends=('gcc-libs')
makedepends=('autotools' 'gcc')
source=(https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('4792c047ddf15ef29d55ba8e68a1a21e0cb7692d87ecdf7204419864582f280d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr CXXFLAGS="$CXXFLAGS"
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install{,-man}
}

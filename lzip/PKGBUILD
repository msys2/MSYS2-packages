# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=lzip
pkgver=1.21
pkgrel=1
pkgdesc="A lossless file compressor based on the LZMA algorithm"
arch=('i686' 'x86_64')
url="https://www.nongnu.org/lzip/lzip.html"
license=('GPL3')
depends=('gcc-libs')
install=lzip.install
source=(https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('e48b5039d3164d670791f9c5dbaa832bf2df080cb1fbb4f33aa7b3300b670d8b')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr CXXFLAGS="$CXXFLAGS"
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install{,-man}
}

# Maintainer: Masanori Shirayama <mashir43@gmail.com>

pkgname=tcsh
pkgver=6.24.10
pkgrel=2
epoch=
pkgdesc="C shell with file name completion and command line editing"
arch=("i686" "x86_64")
url="https://www.tcsh.org/"
license=('BSD')
groups=()
depends=('gcc-libs' 'libxcrypt' 'libiconv' 'ncurses')
makedepends=('gcc' 'libxcrypt-devel' 'libiconv-devel' 'ncurses-devel' 'autotools')
provides=('csh')
conflicts=('csh')
install=tcsh.install
source=("https://astron.com/pub/tcsh/tcsh-${pkgver}.tar.gz"{,.asc})
sha256sums=('13475c0fbeb74139d33ed793bf00ffbbb2ac2dc9fb1d44467a410760aba36664'
            'SKIP')
validpgpkeys=('AD87109763D8D8C2C94CBA8065C26E471F45B123') # Kimmo Suominen

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  autoreconf -ivf
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export MSYSTEM=CYGWIN
  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --prefix=/usr \
    --build=${CYGWIN_CHOST} \
    --host=${CYGWIN_CHOST} \
    --target=${CYGWIN_CHOST}

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -k check || true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 ./Copyright ${pkgdir}/usr/share/licenses/tcsh/Copyright

  cd "${pkgdir}/usr/bin"
  cp -p tcsh.exe csh.exe
}

# Maintainer: Masanori Shirayama <mashir43@gmail.com>

pkgname=tcsh
pkgver=6.24.12
pkgrel=1
epoch=
pkgdesc="C shell with file name completion and command line editing"
arch=("i686" "x86_64")
url="https://www.tcsh.org/"
msys2_references=(
  "anitya: 4945"
)
license=('BSD')
groups=()
depends=('gcc-libs' 'libxcrypt' 'libiconv' 'ncurses')
makedepends=('gcc' 'libxcrypt-devel' 'libiconv-devel' 'ncurses-devel' 'autotools')
provides=('csh')
conflicts=('csh')
install=tcsh.install
source=("https://github.com/tcsh-org/tcsh/archive/refs/tags/TCSH${pkgver//./_}.tar.gz")
sha256sums=('349a45d055da1e3ef9296edc6c3ecef3deb35f02835d1a5dec97ac087579f050')
prepare() {
  cd "${srcdir}/${pkgname}-TCSH${pkgver//./_}"

  autoreconf -ivf
}

build() {
  cd "${srcdir}/${pkgname}-TCSH${pkgver//./_}"

  export MSYSTEM=CYGWIN
  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --prefix=/usr \
    --build=${CYGWIN_CHOST} \
    --host=${CYGWIN_CHOST} \
    --target=${CYGWIN_CHOST}

  make
}

check() {
  cd "${srcdir}/${pkgname}-TCSH${pkgver//./_}"
  make -k check || true
}

package() {
  cd "${srcdir}/${pkgname}-TCSH${pkgver//./_}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 ./Copyright ${pkgdir}/usr/share/licenses/tcsh/Copyright

  cd "${pkgdir}/usr/bin"
  cp -p tcsh.exe csh.exe
}

# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

pkgname=patchutils
pkgver=0.4.3
pkgrel=1
pkgdesc="Utilities to work with patches"
arch=('i686' 'x86_64')
url="http://cyberelk.net/tim/software/patchutils/"
license=('GPL')
source=("https://cyberelk.net/tim/data/patchutils/stable/patchutils-${pkgver}.tar.xz"{,.sig}
        '0001-patchutils-0.4.3_interdiff.c-fix-MEMCPY-arguments.patch')
sha256sums=('0efc96a9565fd156fc1064fdcc54c82b6229db0d402827c4c48b02f6ef956445'
            'SKIP'
            'da5eefc0bb7493e63ad940870f35cd8fd896fdbbc4b468f79eac0d6527963bed')
depends=('pcre2')
makedepends=('pcre2-devel' 'autotools' 'gcc' 'xmlto' 'docbook-xsl')
validpgpkeys=('4629AFE960EC20BEC12E3104B7C20D079491EA63')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i '../0001-patchutils-0.4.3_interdiff.c-fix-MEMCPY-arguments.patch'

  autoreconf -vfi
}

build() {
  mkdir -p ${srcdir}/build-${pkgname}-${pkgver}
  cd ${srcdir}/build-${pkgname}-${pkgver}

  ../${pkgname}-${pkgver}/configure -C \
    --with-pcre2

  make
}

check() {
  cd ${srcdir}/build-${pkgname}-${pkgver}
  # patchutils 0.4.3: all tests succeed.
  make check
}

package() {
  cd ${srcdir}/build-${pkgname}-${pkgver}
  make DESTDIR=$pkgdir install

  # license
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}/
  cd ${srcdir}/${pkgname}-${pkgver}/
  cp -v COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}


_realname=reflex
pkgname=${_realname}
pkgver=20260131
pkgrel=1
pkgdesc="A variant of the flex fast lexical scanner"
arch=('i686' 'x86_64')
url="https://invisible-island.net/reflex"
license=('BSD')
makedepends=('autotools' 'gcc')

source=("${_realname}-${pkgver}.tar.gz::https://github.com/ThomasDickey/${_realname}-snapshots/archive/t${pkgver}.tar.gz")
sha256sums=('96da855cbf6a85117e3eb05ba48b8722ae40b9475c8b15071732201a3133e4ef')


prepare() {
  pushd "${_realname}-snapshots-t${pkgver}"
  autoreconf -vfi
  popd
}

build() {
  mkdir -p build-${pkgver}-${MSYSTEM_CHOST}
  pushd build-${pkgver}-${MSYSTEM_CHOST}
  ../${_realname}-snapshots-t${pkgver}/configure -C
  make
  popd
}

check() {
  make -C build-${pkgver}-${MSYSTEM_CHOST} bigcheck
}

package() {
  make -C build-${pkgver}-${MSYSTEM_CHOST} DESTDIR=${pkgdir} install
  install -Dv -t "${pkgdir}/usr/share/licenses/${pkgname}" \
    "${_realname}-snapshots-t${pkgver}/package/debian/copyright"
}
